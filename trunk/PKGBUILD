# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Kevin Azzam <arch@kevin.azz.am>

pkgname=python-hidapi
_pipname=hidapi
pkgver=0.10.1
#_pkgver=${pkgver%.*}.post${pkgver##*.}
_pkgver="$pkgver"
pkgrel=1
arch=('x86_64')
pkgdesc="A Cython interface to the hidapi from signal11/hidapi"
url="https://github.com/trezor/cython-hidapi"
depends=('python' 'hidapi')
makedepends=('cython' 'python-setuptools' 'udev')
license=('custom')
source=("https://pypi.org/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$_pkgver.tar.gz")
sha512sums=('681e3691a4b05b13abc38d305c68e448728612442d0ef12ce54d12eeebee6e18d33566c462a276d18ce5e29fad208e6fcde49bbe4b162eae5cd72ce37dea880b')

build() {
  cd "$_pipname-$_pkgver"

  python setup.py build \
        --without-libusb --with-system-hidapi
}

check() {
  cd "$_pipname-$_pkgver"

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}" python tests.py
}

package_python-hidapi() {
  cd "$_pipname-$_pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build \
        --without-libusb --with-system-hidapi

  install -Dm 755 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  install -Dm 755 LICENSE-bsd.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-bsd.txt
  install -Dm 755 LICENSE-gpl3.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-gpl3.txt
  install -Dm 755 LICENSE-orig.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-orig.txt
}
