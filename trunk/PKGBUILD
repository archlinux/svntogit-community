# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
pkgver=1.3.14
pkgrel=2
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
depends=(python-aws-xray-sdk python-boto python-boto3
         python-cryptography python-dateutil python-docker python-jinja
         python-jsondiff python-jose python-mock python-yaml
         python-pytz python-requests python-responses python-xmltodict
         python-werkzeug python-cfn-lint python-setuptools python-sshpubkeys)
checkdepends=(python-flask python-freezegun python-nose python-parameterized
              python-sure tk)
optdepends=(
  'python-flask: for moto_server'
)
source=("https://github.com/spulec/moto/archive/$pkgver/moto-$pkgver.tar.gz"
        $pkgname-regions.patch::https://github.com/spulec/moto/commit/bd777cad44dc0c0ec979c366fce228429d182e2f.patch
        $pkgname-botocore-1.3.21.patch::https://github.com/spulec/moto/commit/4983f6af91c0ef20c997772168f27f988a7ce0e1.patch
        $pkgname-botocore-1.3.24.patch::https://github.com/spulec/moto/commit/40f241adc212c1c113399534616427d3e2c1d47a.patch
        cfnlint-0.32.1.diff)
sha256sums=('e10412aa495af0f254d3944fd369e75a7c1bcc2f6940a7ea0a0cea34b2f8bf27'
            'a6020fed84da5c8e7298d3c2750ed33c2b60befe3b9d451432b68d2f030bab93'
            '74fe4c7610ad805e0fde46fb65bba7c5bbdcc52718e592bd04dd0a834734b64e'
            'cf4ab97f705bfba537e5e5ac33c820c23c42ba0415ed7e2be670ddb42e6683ff'
            'fcee0eb5c76619ac59382e15d8487c859bda63a8091cbc15cf31acb576752444')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../$pkgname-regions.patch
  patch -Np1 -i ../$pkgname-botocore-1.3.21.patch
  patch -Np1 -i ../$pkgname-botocore-1.3.24.patch
  patch -Np1 -i ../cfnlint-0.32.1.diff

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#,?<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # test_lambda tests moto.awslambda, which requires a running docker.service
  TZ=UTC nosetests -sv ./tests/ --exclude='test_lambda.*'
}

package() {
  cd $_pkgname-$pkgver

  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
