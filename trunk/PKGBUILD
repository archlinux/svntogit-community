# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Co-Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: mortzu

pkgname=ipv6calc
pkgver=4.0.2
pkgrel=1
pkgdesc='Small utility to manipulate, convert and calculate (more than) IPv6 addresses'
url='https://www.deepspace6.net/projects/ipv6calc.html'
license=('GPL2')
arch=('x86_64')
# See https://www.deepspace6.net/projects/ipv6calc.html#idm140634407144416
makedepends=('geoip')
optdepends=('geoip: for country GeoIP support'
            'geoip-database-extra: for city-level GeoIP support'
            'perl: to run the provided scripts')
checkdepends=('perl-digest-sha1' 'perl-uri' 'perl-html-parser' 'perl-proc-processtable')
source=("ftp://ftp.bieringer.de/pub/linux/IPv6/ipv6calc/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "ftp://ftp.bieringer.de/pub/linux/IPv6/ipv6calc/CODE-GPG-KEY-bieringer.de-2013"
        'anti-dos_test.patch')
b2sums=('5ec22bca8e11558bf1eb6ae942e5b2183fd0e31ec71bd556f09b0111b795d1b9303dd7e230f650ce1be5b084625166c5033b833f775f31cd2c7f8f8155106261'
        'SKIP'
        '1483a6681fef1a799baf36bebb7af7608f3233e2b09adb10134c5dfb4349ba3cb3588ce8dd47c2bded1153908694fb324711debc24a3807474cb36695c79cceb'
        '611bbcb7e7babeaf6ace8888230aa15ab5744fd228ec717ee7ff11f1be3dfe03944896ceab07dc36426a3e8c3daaea51c47ed06fc54b7c710cd3ea694f04696e')
validpgpkeys=('AAB38CB797C750C778C608C6DDEB141DF7380F61') # Peter Bieringer (Code Signing Key 2013) <code@bieringer.de>

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/anti-dos_test.patch"

  autoconf
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix="/usr" \
    --libdir="/usr/lib" \
    --datarootdir="/usr/share" \
    --enable-shared \
    --enable-bundled-md5 \
    --enable-geoip \
    --with-geoip-dynamic=yes
  make
}

check() {
  cd "${pkgname}-${pkgver}"

  make test-minimal
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
