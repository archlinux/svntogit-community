# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Lee Watson <aur@revthefox.co.uk>
# Contributor: Tobias Hunger <tobias DOT hunger AT gmail DOT com>
# Contributor: Stefan Tatschner <stefan@sevenbyte.org>


# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-shell
pkgver=13.7.0
pkgrel=1
epoch=2
pkgdesc="Git management software"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby' 'redis' 'git')
makedepends=('go')
options=('!strip')
install=gitlab-shell.install
backup=("etc/webapps/${pkgname}/config.yml"
        "etc/webapps/${pkgname}/secret"
        "etc/webapps/${pkgname}/.gitconfig")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitlab-shell/-/archive/v${pkgver}/gitlab-shell-v${pkgver}.tar.gz"
        "configs.patch"
        "gitlab-shell.gitconfig"
        "gitlab-shell.sysusers")
sha512sums=('1ef6587e2824d802e230d8824c210d0197ebe8b3f3fbe644d0a29411eeb34e6f78f5bca75c3acaadab7f0a14d9f7d45f8868e55bd01d1e3a85cbbcac6d24ea9a'
            '49c5f15eeb985f402296faa991315311596ebf527b1fe715e94852c7048c767ccd69da3b42ddf3ebd45b86838ec72389d68a2204764f432ae5e9ce1651ff1966'
            'cb8003f2ce8013fcd807aae6d66c22c3a767e74d569bd69aaa5a54b84a36135d25b3af57861f858f8f407e089ff69ff5c325bf065c6051ce8fa197fab70470b8'
            '616188911fa7b7c569bc2d5e2024177e67366c48b4ffef1f7ad4dc8e4e9e2cdb421b687f45a1e9bdec8bf2736a157ca9f1e0ba24edae2f073b8c7b5ac42745e8')

_datadir="/usr/share/webapps/${pkgname}"
_etcdir="/etc/webapps/${pkgname}"
_homedir="/var/lib/gitlab"
_logdir="/var/log/gitlab"
_srcdir="gitlab-shell-v${pkgver}"

prepare() {
  cd ${_srcdir}

  patch -p1 < ../configs.patch
  # At this point config file should not contain any references to '/home/git'
}

build() {
  cd ${_srcdir}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make build
}

package() {
  cd ${_srcdir}

  install -dm755 -o root -g root "${pkgdir}${_datadir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}/satellites"
  install -dm700 -o 105 -g 105 "${pkgdir}${_homedir}/.ssh"
  install -dm750 -o 105 -g 105 "${pkgdir}${_etcdir}"
  install -dm770 -o 105 -g root "${pkgdir}${_logdir}"

  # sysusers
  install -Dm644 "${srcdir}/gitlab-shell.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-shell.conf"

  # .gitconfig
  install -Dm644 -o 105 -g 105 "${srcdir}/gitlab-shell.gitconfig" "${pkgdir}${_etcdir}/.gitconfig"
  ln -fs "${_etcdir}/.gitconfig" "${pkgdir}${_homedir}/.gitconfig"

  # .gitlab_shell_secret
  touch "${pkgdir}${_etcdir}/secret"
  chmod 640 "${pkgdir}${_etcdir}/secret"
  chown root:105 "${pkgdir}${_etcdir}/secret"
  ln -fs "${_etcdir}/secret" "${pkgdir}${_datadir}/.gitlab_shell_secret"

  # config.yml
  install -Dm644 config.yml.example "${pkgdir}${_etcdir}/config.yml"
  ln -fs "${_etcdir}/config.yml" "${pkgdir}${_datadir}/config.yml"

  cp -r VERSION bin hooks spec support "${pkgdir}${_datadir}"
  ln -fs "${_datadir}" "${pkgdir}/${_homedir}/"

  # Compatibility for FS#64251
  ln -s gitlab-shell "${pkgdir}"/${_datadir}/bin/gitlab-shell-ruby

  install -dm2770 -o 105 -g 105 "${pkgdir}${_homedir}/repositories"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
