# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxau
pkgname=lib32-$_pkgbasename
pkgver=1.0.10
pkgrel=3
pkgdesc="X11 authorisation library (32-bit)"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('pkgconf' 'xorgproto')
provides=('libXau.so')
options=('debug')
source=(${url}/releases/individual/lib/libXau-${pkgver}.tar.xz)
sha1sums=('fbfef1a4abe2f1d3aa3d8e2252b65f5824627138')
sha256sums=('8be6f292334d2f87e5b919c001e149a9fdc27005d6b3e053862ac6ebbf1a0c0a')

build() {
  cd "${srcdir}/libXau-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXau-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
