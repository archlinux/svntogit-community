# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-notebook
pkgver=4.1.0
pkgrel=2
pkgdesc="The language-agnostic HTML notebook application for Project Jupyter"
arch=('any')
url="https://github.com/jupyter/notebook"
license=('BSD')
depends=('jupyter' 'python-setuptools' 'python-jinja' 'python-tornado' 'python-terminado')
makedepends=('python-setuptools' 'npm' 'git')
optdepends=('mathjax: latex equation rendering'
            'pandoc: notebook export')
md5sums=('763ab54b3fc69f6225b9659b6994e756')

# depends are going to be a little weird
# removed: ipython python-pyzmq python-jsonschema python-mistune python-pygments

source=("https://github.com/jupyter/notebook/archive/$pkgver.tar.gz")

build() {
  cd "$srcdir/notebook-$pkgver"

  # FS45999 disable bundled mathjax
  sed -i 's/^.*MathJax.*$//' bower.json 
  #sed -i '/Trim mathjax/,/^                static_data.append/d' setupbase.py
  sed -i 's/mj(/#mj(/' setupbase.py

  # needs node, downloads a lot of stuff
  python3 setup.py build
}

package() {
  cd "$srcdir/notebook-$pkgver"

  # needs node
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  # FS45999 use system mathjax
  ln -s /usr/share/mathjax \
  "$pkgdir/usr/lib/python3.5/site-packages/notebook/static/components/MathJax"

  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

