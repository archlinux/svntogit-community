# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-netbook
pkgver=1.6.1
pkgrel=1
pkgdesc="A simple window management tool."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libfakekey' 'libmatewnck' 'libunique' 'mate-panel')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('8aa6f4802bbe5ef7aef9d601a4172d1304d6bb86')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
