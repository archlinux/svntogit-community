# $Id$
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=haproxy
pkgver=1.8.9
pkgrel=1

pkgdesc='Reliable, high performance TCP/HTTP load balancer'
url='https://haproxy.org/'
arch=('x86_64')
license=('GPL')

depends=('openssl' 'pcre' 'zlib' 'lua' 'libsystemd')

backup=('etc/haproxy/haproxy.cfg')

install=haproxy.install

source=(https://haproxy.org/download/${pkgver%.*}/src/haproxy-$pkgver.tar.gz
        haproxy.cfg
        haproxy.sysusers)

md5sums=('1466cf8c1c036e376265b86df43efc89'
         '27941f31d25d5629f4bdef6bc4b2555a'
         '2f00e81ee2f3f6e51a0cd9acc6f3ff3e')

prepare() {
  cd haproxy-$pkgver
}

build() {
  cd haproxy-$pkgver

  make CPU=generic TARGET=linux2628 \
    USE_GETADDRINFO=1 \
    USE_OPENSSL=1 \
    USE_PCRE=1 USE_PCRE_JIT=1 \
    USE_ZLIB=1 \
    USE_LUA=1 \
    USE_SYSTEMD=1

  for contrib in halog iprange systemd; do
    make -C contrib/$contrib SBINDIR=/usr/bin
  done
}

package() {
  cd haproxy-$pkgver
  make \
    PREFIX="$pkgdir"/usr \
    SBINDIR="$pkgdir"/usr/bin \
    DOCDIR="$pkgdir"/usr/share/$pkgname \
    install

  for contrib in halog iprange; do
    install -Dm755 contrib/$contrib/$contrib "$pkgdir"/usr/bin/$contrib
  done

  install -Dm644 ../haproxy.sysusers "$pkgdir"/usr/lib/sysusers.d/haproxy.conf
  install -Dm644 ../haproxy.cfg "$pkgdir"/etc/haproxy/haproxy.cfg
  install -Dm644 contrib/systemd/haproxy.service \
    "$pkgdir"/usr/lib/systemd/system/haproxy.service

  install -d "$pkgdir"/usr/share/haproxy/examples/errorfiles
  install -m644 examples/*.cfg "$pkgdir"/usr/share/haproxy/examples/
  install -m644 examples/errorfiles/*.http \
    "$pkgdir"/usr/share/haproxy/examples/errorfiles/

  install -Dm644 examples/haproxy.vim \
    "$pkgdir"/usr/share/vim/vimfiles/syntax/haproxy.vim
}
