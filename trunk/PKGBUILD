# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.256
pkgrel=4
pkgdesc="A free cross-platform lobby client for the Spring RTS project"
arch=('x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('hicolor-icon-theme' 'wxgtk3' 'curl' 'libtorrent-rasterbar' 'boost-libs'
         'alure' 'libsm')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer')
makedepends=('boost' 'cmake' 'git')
source=(git://github.com/springlobby/springlobby.git#tag=${pkgver})
md5sums=('SKIP')

prepare() {
  cd $srcdir/${pkgname}
  git submodule update --init
}

build() {
  cd $srcdir/${pkgname}

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $srcdir/${pkgname}

  make DESTDIR=$pkgdir install
}

# vim: sw=2:ts=2 et:
