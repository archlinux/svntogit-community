# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.12.2
pkgrel=1
pkgdesc='Cairo vector graphics library (32-bit)'
arch=('x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
depends=('lib32-libpng' 'lib32-libxrender' 'lib32-fontconfig'
         'lib32-pixman' 'lib32-glib2' ${_pkgbasename})
makedepends=('gcc-multilib')
source=(http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz
cairo-1.10.0-buggy_gradients.patch)
md5sums=('87649eb75789739d517c743e94879e51'
         '9b323790dab003e228c6955633cb888e')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  autoreconf -vfi

  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --sysconfdir=/etc  --localstatedir=/var \
      --disable-static --enable-tee --disable-xlib-xcb
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
