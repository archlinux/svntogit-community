# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=rkcommon
pkgver=1.10.0
pkgrel=1
pkgdesc="Intel RenderKit common C++/CMake infrastructure"
arch=(x86_64)
url="https://github.com/ospray/rkcommon/"
license=(Apache)
depends=(tbb)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('57a33ce499a7fc5a5aaffa39ec7597115cf69ed4ff773546b5b71ff475ee4730')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  make -C build
}

check() {
  make -C build test
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
