# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Yuval Adam <yuval at y3xz dot com> PGP-Key: 271386AA2EB7672F
# Contributor: Yejun Yang <yejunx AT gmail DOT com>
# Contributor: Biru Ionut <ionut@archlinux.ro>

pkgname=yate
pkgver=5.5.0
pkgrel=3
pkgdesc="Next-generation telephony engine"
arch=('i686' 'x86_64')
url="http://www.yate.ro/"
license=('GPL')
depends=('speex' 'gsm' 'postgresql-libs' 'libmariadbclient' 'alsa-lib' 'openssl-1.0' 'sqlite')
makedepends=('qt4')
optdepends=('qt4')
options=(!makeflags)
source=("http://yate.null.ro/tarballs/yate5/yate-${pkgver}-1.tar.gz"
	"yate.service")
sha256sums=('a9fc472df082e92d578efa3de54689e10ee1a24fdd67a9cc928db7c2e1568676'
            '73eab39a0ba884a4c36ca7c8f1faea4000301a0e795544b74cba78d90de5e302')

build() {
  cd $srcdir/${pkgname}
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-libpq \
              --with-mysql \
              --without-openh323
  make
}

package(){
  cd $srcdir/${pkgname}
  make DESTDIR=$pkgdir install
  install -Dm0644 "$srcdir"/yate.service "$pkgdir"/usr/lib/systemd/system/yate.service
}
