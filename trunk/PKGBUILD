# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lib32-libasyncns
pkgver=0.8+r3+g68cd5af
pkgrel=1
epoch=1
pkgdesc="A C library for executing name service queries asynchronously"
url="https://0pointer.de/lennart/projects/libasyncns/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-glibc libasyncns)
makedepends=(git lynx)
options=(debug)
_commit=68cd5aff1467638c086f1bedcc750e34917168e4  # master
source=("git://git.0pointer.de/libasyncns.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd libasyncns
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libasyncns

  # Fix libdir
  sed -i '/^libdir=/s:/lib:/lib32:' *.pc.in

  NOCONFIGURE=1 ./bootstrap.sh
}

build() {
  cd libasyncns
  export CC="gcc -m32" PKG_CONFIG="i686-pc-linux-gnu-pkg-config"
  ./configure --prefix=/usr --libdir=/usr/lib32
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd libasyncns
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,share}
}
