# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=lib32-atk
pkgver=2.36.0
pkgrel=2
pkgdesc="The interface definitions of accessibility infrastructure (32-bit)"
url="https://git.gnome.org/browse/atk"
arch=(x86_64)
license=(LGPL)
depends=(lib32-glib2 atk)
makedepends=(git gtk-doc meson)
provides=(libatk-1.0.so)
_commit=dbe95f6170ae1f4bb76c755506b4e3dd3990d5aa  # tags/ATK_2_36_0^0
source=("git+https://git.gnome.org/browse/atk#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd atk
  git describe --tags | sed 's/^ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd atk
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson atk build \
    -D introspection=false \
    -D enable_docs=false \
    --libdir=/usr/lib32
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -r "${pkgdir}"/usr/{include,share}
}
