# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mightyjaym <jmambrosino@gmail.com>
# Contributor: moostik <mooostik_at_gmail.com>

pkgname=audex
pkgver=0.72b1
pkgrel=1
pkgdesc="A CDDA extraction tool with a ftp upload function for KDE"
arch=('i686' 'x86_64')
url="http://kde.maniatek.de/audex/"
license=('GPL')
depends=('kdemultimedia-kioslave' 'cdparanoia')
makedepends=('cmake' 'automoc4')
optdepends=('vorbis-tools: OGG encoder'
	'faac: MP4/M4A/AAC encoder'
	'flac: FLAC encoder'
	'lame: MP3 encoder')
install=${pkgname}.install
source=("http://kde.maniatek.de/${pkgname}/files/${pkgname}-${pkgver}.tar.bz2"
	'useless-include.patch'
	'gcc4.5.patch'
	'vorbis1.4.patch')
md5sums=('1953587ebc8e08f5de1eb782ecf92788'
	'1dd969a16dfd2434fac0e7700f954985'
	'30fb51a1861e2ed77a5531fb23b76c1a'
	'418b95d4cdfb99d3ce0a3c5f2c9b83a2')

build() {
  cd ${srcdir}/${pkgname}
  patch -Np1 -i ${srcdir}/useless-include.patch
  patch -Np1 -i ${srcdir}/gcc4.5.patch
  patch -Np1 -i ${srcdir}/vorbis1.4.patch

  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
