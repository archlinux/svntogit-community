# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.5.3
pkgrel=2
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'jsoncpp' 'libunwind')
makedepends=('cmake' 'python' 'python-numpy' 'ninja' 'postgresql-libs')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.bz2"
         https://github.com/PDAL/PDAL/commit/7b4b2d94.patch)
sha512sums=('f67a303711b7d9e9dc14260afb96d299bcd5327ba0cc947143a583f55bd532a248b131e4602fd37d711ad0084e8601420828dacac40c414b95b8e9f9e8175e1b'
            '5e12328f005d7c030561967ab5f067a0474215bdd63387326852e51820274acdd93791aeb835582fe556ba249e2a400a591486f80ab89520de0ed061d8b1c401')

prepare() {
  cd "PDAL-$pkgver-src/"
  patch -p1 -i ../7b4b2d94.patch # Fix build with gdal 3.7
}

build() {
  cd "PDAL-$pkgver-src/"

  cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
      -DWITH_COMPLETION=ON

  ninja -C build
}

package() {
  cd "PDAL-$pkgver-src"

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
