# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=0.1.1
_golibver=0.4.2
_deepinapiver=2.92.1
_dbusfactoryver=2.92.1
pkgrel=2
pkgdesc='deepin file manager backend'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-file-manager"
license=('GPL3')
depends=('deepin-metacity' 'libcanberra' 'poppler-glib' 'gcc-go')
makedepends=('git' 'deepin-dbus-generator')
replaces=('deepin-file-manager-backend')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/deepin-file-manager.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i 's/libmetacity-private/libdeepin-metacity-private/' $pkgname/Makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  go get github.com/howeyc/fsnotify github.com/mattn/go-sqlite3
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/$pkgname"
  make USE_GCCGO=1
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" do-install
}
