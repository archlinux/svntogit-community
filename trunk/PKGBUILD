# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=11.1
pkgrel=1
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=(any)
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=(MPL2)
depends=(python-packaging python-pytest)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest-xdist)
optdepends=(
  'python-pytest-xdist: for recovering from crashes'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('9a572faab4ee1907e829d38979a87c5eb24dc13edfbbd987801b610f67d0a5ce8743f2bdbe883bd247915e6ede6b73fceae9c4994774e74b1b1a192587829e05')
b2sums=('aa1e35659411bc9cc777253982d53b39a969fe1448990008e99903ca3bd2a675f07551ab806a0656b9ed329b519458c02e46dc8fa2f1e0f60be8e3b2457e48d1')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv -k 'not test_rerun_passes_after_temporary_test_crash'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES.rst,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
