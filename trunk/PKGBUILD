# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=fop
pkgver=2.8
pkgrel=2
pkgdesc='XSL-FO to PDF formatter'
arch=(any)
url='https://xmlgraphics.apache.org/fop/'
license=(APACHE)
depends=(java-avalon-framework 'java-batik=1.16' java-commons-io java-commons-logging java-xmlgraphics-commons)
makedepends=(apache-ant java-environment)
source=("https://dlcdn.apache.org/xmlgraphics/$pkgname/source/$pkgname-$pkgver-src.tar.gz"
        fop.conf)
b2sums=('183649f531068b8f7513613c89f9f66a48894ab97a659e110b91853d5dffc1664d3830a8ced9ba221e22ca02fc51aed991ddee0e927cab59b9160a504f6dab02'
        '928ced5f2591d2537829d74ae94c6f59e1be2f12eddf44e2f7fe5fd06606e94a5911396d6ef8596137449524c9d0d13ef8896bc4fb5e58096bd9948708879857')
backup=(etc/fop.conf)

prepare() {
  cd $pkgname-$pkgver/$pkgname
  cp -f "$srcdir/fop.conf" fop_modified.conf
  pacman -Ql java-batik | grep \.jar | sed 's/java-batik /CLASSPATH="$CLASSPATH:/g;s/$/"/g' >> fop_modified.conf
}

build() {
  cd $pkgname-$pkgver
  ant -f $pkgname/build.xml package
}

package() {
  cd $pkgname-$pkgver/$pkgname
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 build/fop.jar "$pkgdir/usr/share/java/fop/fop.jar"
  install -Dm644 fop_modified.conf "$pkgdir/etc/fop.conf"
}
