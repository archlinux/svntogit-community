# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Tomas Schertel<tschertel@gmail.com>

pkgbase=python-logbook
pkgname=(python-logbook python2-logbook)
pkgver=1.1.0
pkgrel=1
pkgdesc="Logging sytem for Python that replaces the standard libraryâ€™s logging module"
arch=('i686' 'x86_64')
url="http://packages.python.org/Logbook/index.html"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'python-six' 'python2-six')
checkdepends=('redis' 'python-pytest' 'python2-pytest' 'python-sqlalchemy' 'python2-sqlalchemy'
              'python-mock' 'python2-mock' 'python-redis' 'python2-redis' 'python-pyzmq'
              'python2-pyzmq' 'python-execnet' 'python2-execnet' 'python-jinja' 'python2-jinja'
              'python-pifpaf')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mitsuhiko/logbook/archive/$pkgver.tar.gz")
sha512sums=('55ec7a026a08baed405e29d7cdcc9d3cc423c9faf13788c2447ac7ccff688de9bd9e332ad78077b1eef8bc8ad9aea572f9c124888f46ecabc2abf22d8ac9086b')

prepare() {
  cp -a logbook-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/logbook-$pkgver
  cython logbook/_speedups.pyx
  python setup.py build

  cd "$srcdir"/logbook-$pkgver-py2
  cython2 logbook/_speedups.pyx
  python2 setup.py build
}

check() {
  # rm -rf redis_tmp
  # mkdir redis_tmp
  # echo "dir $srcdir/redis_tmp" > redis.conf
  # redis-server redis.conf &

  cd "$srcdir"/logbook-$pkgver
  LC_CTYPE=en_US.UTF-8 pifpaf run redis py.test tests

  cd "$srcdir"/logbook-$pkgver-py2
  LC_CTYPE=en_US.UTF-8 pifpaf run redis py.test2 tests

  # redis-cli shutdown
}

package_python-logbook() {
  depends=('python-six')

  cd logbook-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-logbook() {
  depends=('python2-six')

  cd logbook-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
