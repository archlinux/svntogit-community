# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailman
pkgname=mailman3
pkgver=3.3.1
pkgrel=3
pkgdesc="The GNU mailing list manager"
arch=('any')
url="https://www.list.org/"
license=('GPL3')
depends=('gunicorn' 'python-aiosmtpd' 'python-alembic' 'python-atpublic'
'python-authheaders' 'python-authres' 'python-click' 'python-dateutil'
'python-dnspython' 'python-falcon' 'python-flufl.bounce' 'python-flufl.i18n'
'python-flufl-lock' 'python-importlib_resources' 'python-lazr.config'
'python-passlib' 'python-requests' 'python-setuptools' 'python-sqlalchemy'
'python-zope-component' 'python-zope-configuration' 'python-zope-event'
'python-zope-interface')
checkdepends=('python-coverage' 'python-diff-cover' 'python-flufl.testing'
'python-psycopg2' 'python-pymysql' 'python-pytest')
optdepends=('hyperkitty: for local archive and list interaction'
            'lynx: for the default HTML to plaintext conversion'
            'mailman3-hyperkitty: for archiving mails with an instance of hyperkitty'
            'postorius: for local configuration interaction'
            'python-mailmanclient: for programmatically interfacing the mailman REST API locally'
            'python-psycopg2: for PostgreSQL support'
            'python-pymysql: for MySQL support'
            'smtp-server: for using a local mail server')
conflicts=('mailman')
replaces=('mailman-core')
backup=('etc/mailman.cfg')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc}
        "${pkgname}.service"
        "${pkgname}-digests.service"
        "${pkgname}-digests.timer"
        "${pkgname}-gatenews.service"
        "${pkgname}-gatenews.timer"
        "${pkgname}-notify.service"
        "${pkgname}-notify.timer"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha512sums=('31dc5d0fca7f28579a1e174010ea8ac83d17b5e7ace37f3a86fb73110de412af145a9c3d16cf7b446f80e44a3419b51cce7b56b8de93264751cf771b2c084ddc'
            'SKIP'
            '6d1fbb52f72a93c66cc8018d83cbdde1878a3a759743f54252d711c027a732e942d2154a4ef62d011844373c66706dc91fc85757239ddcd07f77782d31b78d60'
            '734e0cdf1198f6609a5e41312c48c5c4e492ba5b9acc3af4cd302a6ed148933396333077932e25aedfc50ff3f68b1d4898137193bdadaf71e23045ec8e96be10'
            '5d7ccba8cf1262ab052078f2188ded15e43e1201302c7c24ce763efef9789ec99d8ea9a19e8fbd9bc5a38f47a162fe5cf4b0ade284894cb57af66350f23507bc'
            'e610060021d6f2ebeb4ffb5b37d448efdd44154ace6f228a316e9712799dc620611953401f705bb76d1046b769b6e8316c9b1d143e535110e383a7762d866669'
            'fc550ef017ad8a22e0c40a8808361831b8c32cd0d34925cdce433ec4a0564e9577d6f9ccdcd38098eb4cf692257c061ff3fbea3b762d274f9b5e2fa88d10d82e'
            'b1f8f3afdfb25f361d0dea59fb00e0ef126dd0557994366c421d473b5e354dc42daafd1b296d5bc3f79c45a085087d40b573c416ae8aba6bf358c06d139d636f'
            'b8538618be8bdd6313d24b844d15c8f25a059c3391cd06db69314b7be19996bfbe913d6c0c9c99d6a19c4f92df32212d31785e92fb32faf5eb792fffe121fcce'
            '074cf58a93204d4b7631c4cc8af62d5720560301c6fefef6baa5d3aa33e72720870757fac7803f7b2835f8f02bb776f9d049df5c7528c743b56dda7da2085b07'
            '6f4b51fd5eb34ac974b3312c34eb1437d9435cfd50f0cb89db02b94ce514bceca2c6dc7cb172b79b2d6a23d68e7ea391ec58dbd9899938c0fe88c03c67c521b9')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <maxking@asynchronous.in>

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # make sure that mailman runs in a FHS compliant way
  echo -e "[mailman]\nlayout: fhs" >> "${_name}.cfg"
  # set /var/lib/mailman instead of /var/tmp/mailman as default var_dir:
  sed -e 's|/var/tmp/mailman|/var/lib/mailman|g' \
      -i src/${_name}/config/schema.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

# # tests are only integration tests and don't work without a previously setup
# # mail server
# check() {
#   cd "$pkgname-$pkgver"
#   pytest -v
# }

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  # config
  install -vDm 640 "${_name}.cfg" -t "${pkgdir}/etc/"
  # state dir
  install -vdm 750 "${pkgdir}/var/lib/mailman"
  # sysusers.d
  install -vDm 644 "../${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # tmpfiles.d
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # services and timer
  install -vDm 644 ../*.{service,timer} -t "${pkgdir}/usr/lib/systemd/system"
}
