# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>
# Contributor: Bernd Prünster (JesusMcCloud) <aur@berndpruenster.org>

pkgname=gnome-latex
pkgver=3.40.0
pkgrel=1
pkgdesc='LaTeX editor for the GNOME desktop'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/GNOME-LaTeX'
license=('GPL3')
depends=('gsettings-desktop-schemas' 'gspell' 'libgee' 'tepl')
makedepends=('appstream-glib' 'autoconf-archive' 'git' 'gtk-doc' 'gobject-introspection' 'intltool'
             'vala' 'yelp-tools')
optdepends=('texlive-core: build LaTeX documents')
_commit=d488276c58b0d126d34e1facc431c153664d980b
source=("git+https://gitlab.gnome.org/swilmet/gnome-latex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

check() {
  make -C $pkgname check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
