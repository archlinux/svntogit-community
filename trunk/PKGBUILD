# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-screensaver-greynetic
epoch=1
pkgver=19.0.1
_codename=Matrix
pkgrel=2
pkgdesc="Greynetic screensaver for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/screensaver.greynetic'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-screensaver')
makedepends=('cmake' 'glm' 'kodi-dev')
options=(!lto debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/screensaver.greynetic/archive/$pkgver-$_codename.tar.gz")
sha512sums=('cf0e6d69088e0d6c2818dd9bc2bfaa628d63824ea0fc73239b2a4f3e8c1e5a91a531e6c9c51ed81e625d40080a1d65eae371017e008fa80bb30910c96a1be8ba')

build() {
    cd "screensaver.greynetic-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'glm')
    cd "screensaver.greynetic-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

