# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cgl
pkgver=0.60.7
pkgrel=1
pkgdesc='COIN-OR Cut Generation Library'
arch=(x86_64)
url='https://github.com/coin-or/Cgl'
license=(EPL)
groups=(coin-or)
depends=(coin-or-clp)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Cgl/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('93b30a80b5d2880c2e72d5877c64bdeaf4d7c1928b3194ea2f88b1aa4517fb1b')

build() {
  cd Cgl-releases-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd Cgl-releases-$pkgver
  make test
}

package() {
  cd Cgl-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
