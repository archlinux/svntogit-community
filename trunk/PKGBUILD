# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.7.0.1
pkgrel=1
pkgdesc="A 2D CAD package based upon Qt"
arch=('i686' 'x86_64')
url="http://www.qcad.org"
license=('GPL3')
depends=('qtwebkit')
makedepends=('glu')
options=('libtool')
source=($pkgname-$pkgver.tar.gz::https://github.com/qcad/qcad/archive/vv${pkgver}.tar.gz
        QCad.desktop)
md5sums=('2838e7c47257621c4ef937f564671819'
         '84f189dd152e7614a1265659dac6222d')

prepare() {
  cd ${srcdir}/qcad-vv$pkgver
#  sed -i '1,1i#include <stdint.h>' src/core/RThread.cpp
#  sed -i '1,1i#include <string.h>' \
#    src/3rdparty/spatialindexnavel/src/storagemanager/*.cc \
#    src/3rdparty/spatialindexnavel/src/mvrtree/*.cc \
#    src/3rdparty/spatialindexnavel/src/tprtree/*.cc \
#    src/3rdparty/spatialindexnavel/src/spatialindex/*.cc
#  (cd src/3rdparty && cp -a qt-labs-qtscriptgenerator-4.8.5 qt-labs-qtscriptgenerator-4.8.6)
#  (cd src/3rdparty/qt-labs-qtscriptgenerator-4.8.6/ && mv qt-labs-qtscriptgenerator-4.8.5.pro qt-labs-qtscriptgenerator-4.8.6.pro)
}

build() {
  cd ${srcdir}/qcad-vv$pkgver
  qmake-qt4 -r
  make all -j1
}

package() {
  cd ${srcdir}/qcad-vv$pkgver

  # remove project files
  find . \( -name '*.pri' -or -name '.pro' -or -name '*.ts' \) -delete
  find . \( -name 'Makefile' -name '.gitignore' \) -delete

  install -dm755 ${pkgdir}/usr/share/{qcad,pixmaps,applications}
  cp -r examples fonts libraries patterns plugins scripts ts ${pkgdir}/usr/share/qcad
  cp debug/* ${pkgdir}/usr/share/qcad

  install -m755 readme.txt ${pkgdir}/usr/share/qcad/readme.txt

  # qtwebkit
  ln -sf /usr/lib/qt4/plugins/designer/libqwebview.so ${pkgdir}/usr/share/qcad/plugins/designer/libqwebview.so
  # qt
  for sofiles in /usr/lib/qt4/plugins/imageformats/*.so
  do
    ln -sf ${sofiles} ${pkgdir}/usr/share/qcad/plugins/imageformats/${sofiles##/*/}
  done
  for sofiles in /usr/lib/qt4/plugins/sqldrivers/*.so
  do
    ln -sf ${sofiles} ${pkgdir}/usr/share/qcad/plugins/sqldrivers/${sofiles##/*/}
  done

  install -Dm644 scripts/qcad_icon.png ${pkgdir}/usr/share/pixmaps/qcad_icon.png
  install -Dm644 $srcdir/QCad.desktop ${pkgdir}/usr/share/applications/QCad.desktop

  install -dm0755 $pkgdir/usr/bin
  echo -e '#!/bin/sh\nLD_LIBRARY_PATH=${LD_LIBRARY_PATH:+$LD_LIBRARY_PATH:}"/usr/share/qcad" exec /usr/share/qcad/qcad-bin "$@"' >$pkgdir/usr/bin/qcad

  chmod 0755 $pkgdir/usr/bin/qcad
}
