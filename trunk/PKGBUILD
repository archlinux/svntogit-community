# Maintainer: Florian Pritz <flo@xinu.at>

_pkgbasename=glew
pkgname=lib32-$_pkgbasename
pkgver=2.2.0
pkgrel=4
pkgdesc="A cross-platform C/C++ extension loading library (32 bit)"
arch=('x86_64')
url="https://github.com/nigels-com/glew"
license=('BSD' 'MIT' 'GPL')
depends=('lib32-libxmu' 'lib32-libxi' 'lib32-glu' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
provides=('libGLEW.so')
source=(https://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tgz
        glew-install.patch)
sha1sums=('22d6e9b9e691080eb6313407dbf51cfebc3345ea'
          'e1bfa1e6ae3c7a553920978111092ea9d3a33c95')
sha256sums=('d4fc82893cfb00109578d0a1a2337fb8ca335b3ceccf97b97e5cc7f08e4353e1'
            'ef864c1e673f6a8fab4d3f5bc3c3eb9596cd225c24765de9dff2064e72f350d5')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i ../glew-install.patch
}


build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  sed -i 's|CC = cc|CC = gcc -m32|' config/Makefile.linux
  sed -i 's|LD = cc|LD = gcc -m32|' config/Makefile.linux
  sed -i 's|lib64|lib32|g' config/Makefile.linux

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" \
    PKGDIR="/usr/lib32/pkgconfig" install

  rm -rf "${pkgdir}"/usr/{include,bin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
