# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pipe-rename
pkgver=1.6.1
pkgrel=1
pkgdesc='Rename your files using your favorite text editor'
arch=('x86_64')
url='https://github.com/marcusbuffett/pipe-rename'
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=(
  "$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
  'LICENSE'
)
sha512sums=('b33c8fe45ff4f0d5adb7304aba1cf445a9978049b75ddb66f8c90b2edb0329fa83b8149ad5c0a203099b65c75f643bce77e9469855ce96de431253c58c6e5703'
            '3a3dbfd7a5a8d02a3a96bb1652bd25cae21d1080b941ecd3bee3f46e98b4a2f30b4a7ecdeb1e55ec2bf8bfefbeb9500287acb964ccb7149a405ee28eebd05889')
b2sums=('ea671503a266a8e4ac2c861eca848801a5a82de0e472f2d3ce1ba96895a769123d92e7c2521233e0df2fc07457e1d2a9b5e6232cb268764b0e59177c4f102740'
        'abec091be86ec71b15a70251d769d4177639881c28d71fb9127d86cf711514e0fe40e80edd645e97df532980baf13380433fd2b497e03b4021d49e87434a4965')

prepare() {
  cd "$pkgname-$pkgver"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --frozen --release
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
