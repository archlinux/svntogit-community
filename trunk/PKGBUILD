# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: kaylyn
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cryfs
pkgver=0.10.3
pkgrel=1
pkgdesc="Cryptographic filesystem for the cloud"
arch=(x86_64)
depends=(fuse2 curl)
makedepends=(cmake python boost)
url="https://www.cryfs.org"
license=(LGPL3)
source=("https://github.com/cryfs/cryfs/releases/download/$pkgver/cryfs-$pkgver.tar.xz"{,.asc})
sha256sums=('051d8d8e6b3751a088effcc4aedd39061be007c34dc1689a93430735193d979f'
            'SKIP')
validpgpkeys=(5D5EC7BC6F1443EC2AF7177A9E6C996C991D25E1) # CryFS Team <messmer@cryfs.org>

prepare() {
  sed -e '/Release/d' -i doc/CMakeLists.txt # Install man page also with dafault build type
}

build() {
  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCRYFS_UPDATE_CHECKS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}
