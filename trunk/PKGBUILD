# $Id$
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libice
pkgname=lib32-$_pkgbasename
pkgver=1.0.9
pkgrel=3
pkgdesc="X11 Inter-Client Exchange library (32-bit)"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
depends=('lib32-glibc' $_pkgbasename)
makedepends=('xtrans>=1.2.5' 'xorg-util-macros')
source=(${url}/releases/individual/lib/libICE-${pkgver}.tar.bz2{,.sig})
license=('custom')
sha1sums=('3c3a857a117ce48a1947a16860056e77cd494fdf'
          'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "${srcdir}/libICE-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32

  make
}

package() {
  cd "${srcdir}/libICE-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
