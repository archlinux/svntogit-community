# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.13.5
pkgrel=1
pkgdesc="An advanced and well-established feature-rich text mode web browser."
arch=("x86_64")
url="http://elinks.or.cz"
license=('GPL')
depends=('bzip2' 'expat>=2.0' 'gpm>=1.20.4' 'openssl' 'lua51' 'libidn'
	 'gc' 'tre' 'zlib')
source=("elinks-$pkgver.tgz::https://github.com/rkd77/elinks/archive/v$pkgver.tar.gz")
md5sums=('76b8a68a0cc29befc117ff314c82f64c')

# todo: make it work with a modern 'js' package
# (upstream has no interest in writing that)

build() {
  cd elinks-$pkgver
  sed -i 's/Terminal=1/Terminal=true/' "contrib/debian/$pkgname.desktop"
  [ -x configure ] || sh autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --disable-smb --without-x --enable-cgi \
              --enable-leds --enable-256-colors --enable-html-highlight \
              --with-zlib
  make
}

package() {
  cd elinks-$pkgver
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/share/locale/locale.alias"

  install -D -m644 "contrib/debian/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
