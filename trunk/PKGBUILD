# Maintainer: Anatol Pomozov

pkgname=aws-c-mqtt
pkgver=0.7.8
pkgrel=1
pkgdesc='C99 implementation of the MQTT 3.1.1 specification'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-mqtt'
license=(Apache)
depends=(aws-c-common aws-c-http)
makedepends=(cmake)
source=(aws-c-mqtt-$pkgver.zip::https://github.com/awslabs/aws-c-mqtt/archive/v$pkgver.zip)
sha256sums=('9935c441765576641f78b95e535442d9fd878db56cb01ef5c5f9622df93de249')

build() {
  cd aws-c-mqtt-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-mqtt-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-mqtt-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
