# $Id$
# Contriburor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mate-applet-dock
_pkgname=mate-dock-applet
pkgver=0.76
pkgrel=2
pkgdesc="Application dock for the MATE panel"
arch=('any')
url="https://github.com/robint99/dock-applet"
license=('GPL3')
depends=('mate-panel' 'python-cairo' 'python-gobject' 'python-pillow' 'python-xdg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/robint99/$_pkgname/archive/$pkgver.tar.gz")
sha256sums=('54a21c016127ae8a113de94972806add311cbe73d995ab4d3192472cde696057')

prepare() {
  cd $_pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --with-gtk3
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
