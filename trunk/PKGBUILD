# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-seedir
pkgver=0.4.2
pkgrel=1
pkgdesc="A Python package for creating, editing, and reading folder tree diagrams"
url="https://github.com/earnestt1234/seedir"
license=('MIT')
arch=('any')
depends=('python-natsort')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('python-emoji: for emoji style')
checkdepends=('python-emoji')
source=("https://github.com/earnestt1234/seedir/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8d16ae1ba020268f9587742cacbc55ecc1d218f36955c625f64829940396c908d0498ee6bf50d560f87b10488dc6daa5698681e10e1699d20d958b3cd58f14c4')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd seedir-$pkgver
  python -m build -nw
}

check() {
  cd seedir-$pkgver
  python -m tests.tests
}

package() {
  cd seedir-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
