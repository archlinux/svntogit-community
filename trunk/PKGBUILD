# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-json-glib
pkgver=1.6.6
pkgrel=2
pkgdesc='JSON library built on GLib'
arch=(x86_64)
url=https://wiki.gnome.org/Projects/JsonGlib
license=(GPL)
depends=(
  json-glib
  lib32-glib2
)
makedepends=(
  git
  meson
)
_tag=7ef38457e12810b1f01da236cf04d4bdc1f551fa
source=(git+https://gitlab.gnome.org/GNOME/json-glib.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd json-glib

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson json-glib build \
    --libdir=/usr/lib32 \
    -D gtk_doc=disabled \
    -D introspection=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libjson-glib-1.0.so)

  meson install -C build --destdir "${pkgdir}"

  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
}

# vim: ts=2 sw=2 et:
