# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: William Grieshaber <me@zee.li>

pkgname=dnscrypt-wrapper
pkgver=0.3
pkgrel=1
pkgdesc="A server-side dnscrypt proxy"
url="https://dnscrypt.org/"
arch=('x86_64' 'i686')
license=('BSD')
depends=('libsodium' 'libevent')
backup=('etc/default/dnscrypt-wrapper')
install=dnscrypt-wrapper.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/cofyc/dnscrypt-wrapper/archive/v$pkgver.tar.gz"
        dnscrypt-wrapper.sysusers)
sha512sums=('3a14b63633f9587594afec26edd81c36fad1044b48d956dd11034d7043f7f0c6490afba7a3502e298a7ca05300690ed5bf6b2dc1869e17022d6a228faba3596f'
            '91dfdf0be4eb0d1f8d29bbfadfef451e09a5eed4464afdb531b67354a1c9357224d46340e4472eb37aaa6c2ba2f1dc685ccaa24dfa7666dd2cb5ac8c0a55b7db')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' debian/dnscrypt-wrapper.service
}

build() {
  cd $pkgname-$pkgver
  make configure
  ./configure
  make
}

check(){
  cd $pkgname-$pkgver/tests
  # TODO
  # make test
}

package() {
  cd $pkgname-$pkgver
  make sbindir="$pkgdir"/usr/bin install

  install -Dm644 debian/dnscrypt-wrapper.service "$pkgdir"/usr/lib/systemd/system/dnscrypt-wrapper.service
  install -Dm644 debian/dnscrypt-wrapper.default "$pkgdir"/etc/default/dnscrypt-wrapper
  install -Dm644 "$srcdir"/dnscrypt-wrapper.sysusers "$pkgdir/usr/lib/sysusers.d/dnscrypt-wrapper.conf"

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  install -Dm644 debian/dnscrypt-wrapper.8 "$pkgdir"/usr/share/man/man8/dnscrypt-wrapper.8
}

# vim:set ts=2 sw=2 et:
