# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch.de>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>

pkgbase=python-tox
pkgname=(python-tox python2-tox)
pkgver=3.15.1
pkgrel=1
pkgdesc='Python virtualenv management and testing tool'
arch=('any')
url='https://tox.readthedocs.io'
license=('GPL2')
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-py' 'python2-py' 'python-virtualenv'
             'python2-virtualenv' 'python-pluggy' 'python2-pluggy' 'python-toml' 'python2-toml'
             'python-filelock' 'python2-filelock' 'python-freezegun' 'python2-freezegun')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-mock'
              'python2-pytest-mock' 'python2-pathlib2' 'python-flaky' 'python2-flaky'
              'python-pathlib2' 'python2-pathlib2')
source=("https://pypi.io/packages/source/t/tox/tox-$pkgver.tar.gz")
sha512sums=('315b9d3de7ab66c1fd774a0f088b522f65150d7b0c3e8ed70ef7051b0b25cda979b8153ee739024356a863e2118dbe9033e24896efcc95820334fe1160d83c32')

prepare() {
  find tox-$pkgver -name "*.pyc" -delete

  cp -a tox-$pkgver{,-py2}

  sed -i 's|pathlib2|pathlib|' tox-$pkgver/tests/integration/*.py
}

build() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/tox-$pkgver
  python setup.py build

  cd "$srcdir"/tox-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points

  (
    cd "$srcdir"/tox-$pkgver
    virtualenv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  )

  (
    cd "$srcdir"/tox-$pkgver-py2
    virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
    . "$srcdir/pyvenv-py2/bin/activate"
    python setup.py install
    python setup.py pytest
  )
}

package_python-tox() {
  depends=('python-py' 'python-virtualenv' 'python-setuptools' 'python-pluggy' 'python-toml'
           'python-filelock')

  cd "$srcdir"/tox-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  ln -s tox "$pkgdir"/usr/bin/tox3
}

package_python2-tox() {
  depends=('python2-py' 'python2-virtualenv' 'python2-setuptools' 'python2-pluggy' 'python2-toml'
           'python2-filelock')

  cd "$srcdir"/tox-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/tox{,2}
  mv "$pkgdir"/usr/bin/{tox-quickstart,tox2-quickstart}
}
