# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.test
pkgver=5.2.0
pkgrel=1
pkgdesc="Testing support by jaraco"
url="https://github.com/jaraco/jaraco.test"
license=('MIT')
arch=('any')
depends=('python-jaraco.context' 'python-jaraco.functools')
makedepends=('python-setuptools-scm' 'python-wheel' 'python-build' 'python-installer')
checkdepends=('python-pytest-enabler' 'python-pytest-mypy')
source=("https://github.com/jaraco/jaraco.test/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('01090dffc8218d869e30b17c6bab9b1ebff336b6696ecf3bc22031a59d124206cd7d447b16d40981a95a3f4789d0717884da03b3155c45161d061d7944448d46')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd jaraco.test-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd jaraco.test-$pkgver
  python -m pytest
}

package() {
  cd jaraco.test-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
