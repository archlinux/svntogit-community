# Maintainer: Christian Hesse <mail@eworm.de>

pkgbase=lib32-libxcrypt
pkgname=(lib32-libxcrypt lib32-libxcrypt-compat)
pkgver=4.4.33
pkgrel=1
pkgdesc='Modern library for one-way hashing of passwords (32-bit)'
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('lib32-glibc' 'libxcrypt')
makedepends=('gcc-multilib')
validpgpkeys=('678CE3FEE430311596DB8C16F52E98007594C21D') # Bj√∂rn 'besser82' Esser
source=("${url}/releases/download/v${pkgver}/libxcrypt-${pkgver}.tar.xz"{,.asc})
sha256sums=('e87acf9c652c573a4713d5582159f98f305d56ed5f754ce64f57d4194d6b3a6f'
            'SKIP')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  mkdir build-libxcrypt build-libxcrypt-compat
  cd "${srcdir}/build-libxcrypt/"
  "${srcdir}/libxcrypt-${pkgver}"/configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make 

  cd "${srcdir}/build-libxcrypt-compat/"
  "${srcdir}/libxcrypt-${pkgver}"/configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=glibc \
    --disable-failure-tokens
  make 
}

package_lib32-libxcrypt() {
  cd build-libxcrypt/

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

package_lib32-libxcrypt-compat() {
  pkgdesc='Modern library for one-way hashing of passwords - legacy API functions (32bit)'
  depends=('lib32-libxcrypt')
  
  cd build-libxcrypt-compat/
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,lib32/{lib*.so,pkgconfig},share}
}
