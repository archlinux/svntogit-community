# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=singletons
pkgname=haskell-singletons
pkgver=2.5.1
pkgrel=26
pkgdesc="A framework for generating singleton types"
url="https://github.com/goldfirere/singletons"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-desugar' 'haskell-syb')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-golden')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('31763ee3f8e7ac0a52894c914d9e48eebdd2d9dcf1a29414e303e9e74c1094781eda44e5311c065f96672624a6cd49442d7275c095373256091c937815847269')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
