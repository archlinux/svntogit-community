# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.230
_portal_pkgver=0.9.0
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://github.com/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-python' 'gtk3' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'python')
optdepends=('flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support'
            'youtube-dl: YouTube playback')
_commit='8f3fec20c99498a48f33df00eb64d5af20f81a38'
source=("https://github.com/gnumdk/lollypop/releases/download/${pkgver}/lollypop-${pkgver}.tar.xz"
        "https://github.com/gnumdk/lollypop-portal/releases/download/${_portal_pkgver}/lollypop-portal-${_portal_pkgver}.tar.xz")
sha256sums=('fe4d33b21affd2b428b1e4555ae60c11741ee67063eabf00b1db18c6e97417b1'
            '67215c1d6ce172c5be5df8dd2171ad29eb47a64f81e7cefc8789f66431a4b3aa')

build() {
  cd lollypop-portal-${_portal_pkgver}

  ./configure \
    --prefix='/usr'
  make

  cd ../lollypop-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd lollypop-portal-${_portal_pkgver}

  make DESTDIR="${pkgdir}" install

  cd ../lollypop-${pkgver}

  make DESTDIR="${pkgdir}" install

  chmod +x "${pkgdir}"/usr/share/{lollypop/lollypop-sp,lollypop-portal/lollypop-portal}
}

# vim: ts=2 sw=2 et:
