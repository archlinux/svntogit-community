# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=cozy-stack
pkgver=1.4.0
pkgrel=1
epoch=1
pkgdesc="Digital home: brings all your web services in the same private space â€“ Stack component"
arch=(x86_64)
url="https://cozy.io"
license=(AGPL3)
backup=(etc/cozy/cozy.yml)
depends=(couchdb imagemagick git)
makedepends=(go-pie)
#optdepends=('cozy-coclyco: X.509 certificates management for instances'
optdepends=('nodejs: required for konnectors'
            'nsjail: run konnectors isolated'
            'smtp-forwarder: to allow sending mail to users')
source=("https://apt.cozy.io/debian/pool/testing/c/${pkgname}/${pkgname}_${pkgver}.orig.tar.xz"
        "cozy.yml"
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('0b95dca2b76d67d82365631e1b2fdadad2e37ed9268fd2bf7f012a6f6f5c2d13'
            'c4ed4b0831308261017fe7d532787ed8aa542ca5ecac65f5770edb29793415ef'
            'ad9b40170e2b07d5aa5ea6d444ad16c96bb39adb5ff579db5cc39cb4e2ec3f91'
            'a6bea52350e85163c3141509a52903223fa0f6e7390b1b1f9336c326a8fff984'
            'fd333c2fd0de859890204554f52a5c64b953664f6cb262b20bb839aa70ed9ecb')

build() {
    cd ${pkgname}
    export GOPATH="${PWD}/vendor"
    go build -v -o ${pkgname} \
             -gcflags "all=-trimpath=${PWD}" \
             -asmflags "all=-trimpath=${PWD}" \
             -ldflags "-X github.com/cozy/cozy-stack/pkg/config.Version=${pkgver} \
                       -X github.com/cozy/cozy-stack/pkg/config.BuildTime=$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +"%Y-%m-%dT%H:%M:%SZ") \
                       -X github.com/cozy/cozy-stack/pkg/config.BuildMode=production \
                       -linkmode external -extldflags ${LDFLAGS} -s -w"
}

package() {
    install -Dm644 cozy.yml -t "${pkgdir}"/etc/cozy/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
    cd ${pkgname}
    install -Dm755 ${pkgname} -t "${pkgdir}"/usr/bin/
    install -Dm644 cozy.example.yaml -t "${pkgdir}"/usr/share/cozy/
    install -Dm755 scripts/konnector-node-run.sh -t "${pkgdir}"/usr/share/cozy/
    install -Dm755 scripts/konnector-nsjail-node12-run.sh "${pkgdir}"/usr/share/cozy/konnector-nsjail-run.sh
}
