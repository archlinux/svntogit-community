# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.9.3
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="https://github.com/zopefoundation/persistent/"
depends=('python-cffi' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("https://github.com/zopefoundation/persistent/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d91f906a4af04bd37558868af7e72569ed9ea5d524af14d73fbe29541b9d6370d72d31ca338a3d207b9fdfc7bf82009824f30b3db147bfe7fbec4d9676eb7508')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=src
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
