# Maintainer: Anatol Pomozov

pkgname=vegeta
pkgver=12.0.0
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(go-pie dep git)
source=(vegeta-$pkgver.zip::https://github.com/tsenart/vegeta/archive/cli/v$pkgver.zip)
sha256sums=('9acfefe0350ced84d5bca7f0a48c043ca5141e4307e1bd62efa9585b10f87559')

prepare() {
  export GOPATH="$srcdir/gopath"
  mkdir -p gopath/src/github.com/tsenart
  cp -r vegeta-cli-v$pkgver/ gopath/src/github.com/tsenart/vegeta

  cd gopath/src/github.com/tsenart/vegeta
  dep ensure -v
}

build() {
  cd gopath/src/github.com/tsenart/vegeta
  #go build -v -X main.Version=$pkgver ./...
  CGO_ENABLED=0 go install -v -ldflags "-X main.Version=$pkgver -X main.Date=`date +'%FT%TZ%z'`" ./...
}

check() {
  cd gopath/src/github.com/tsenart/vegeta
  go test ./...
}

package() {
  install -Dm755 "gopath/bin/vegeta" "$pkgdir/usr/bin/vegeta"
}
