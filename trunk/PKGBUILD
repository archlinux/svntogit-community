# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=soapysdr
_pkgname=SoapySDR
pkgver=0.7.1
_gitver=soapy-sdr-$pkgver
pkgrel=5
pkgdesc='Vendor and platform neutral SDR support library'
arch=('x86_64')
url='https://github.com/pothosware/SoapySDR'
license=('Boost')
depends=('python')
makedepends=('cmake' 'swig' 'doxygen')
optdepends=('soapyairspy: Airspy backend'
            'soapyaudio: Audio devices backend'
            'soapybladerf: BladeRF backend'
            'soapyhackrf: HackRF backend'
            'soapynetsdr: NetSDR backend'
            'soapyosmo: OsmoSDR backend'
            'soapyremote: SoapySDR remote support'
            'soapyrtlsdr: rtl-sdr backend'
            'soapyuhd: UHD backend')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz"
        'python-3.8.patch')
sha512sums=('9b2c4251d6362faecf770df021b74e3b387b99699ae4f7d3f145801e2acc901a3803f7b4d2cd50267013f1add6658703dfa5db6db28b8a645f1f8230117123cd'
            '5815af9e2da0e06f8ade6305f12701beb73c906508d087cb24c9c88e0c53fb9d5d03876c475b33a483e2224d4ad8a96a6cc949786dbd5e759b1e6622b9f18d40')

prepare() {
  cd $_pkgname-$_gitver
  patch -Np1 -i ../python-3.8.patch
}

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
    -DSOAPY_SDR_EXTVER=ARCH \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install
}
