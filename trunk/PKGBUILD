# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.20.0
pkgrel=2
pkgdesc="Small applications for the GNOME Panel"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeApplets"
license=('GPL')
depends=('gnome-panel' 'libgtop' 'libtracker-sparql' 'gucharmap' 'python-gobject' 'cpupower' 'upower')
makedepends=('intltool' 'itstool' 'gnome-settings-daemon')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('cf3782447512a0cad144475783e7d9c3aee3bb210ad0b33bba858f102e6cdd94')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --with-cpufreq-lib=cpupower
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  python3 -m compileall "$pkgdir"/usr/lib/python3.5/site-packages/invest/
}
