# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
pkgname=steam
pkgver=1
pkgrel=1
pkgdesc="Digital distribution client - closed beta - bootstrap package"
arch=("i686" "x86_64")
url="http://steampowered.com/"
license=('custom')
depends=(
      'bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
      'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-liberation'
      )
if [[ "$CARCH" == "x86_64" ]]; then
  depends+=(
  'lib32-sdl' 'lib32-libvorbis' 'lib32-alsa-lib'
  'lib32-libgcrypt' 'lib32-gtk2' 'lib32-nss' 'lib32-openal'
  'lib32-libpng12' 'lib32-libpulse' 'lib32-libcanberra'
  'lib32-glu'
  )
  optdepends=(
  'lib32-ati-dri: for open source ATI driver users'
  'lib32-catalyst-utils: for AMD Catalyst users'
  'lib32-intel-dri: for open source Intel driver users'
  'lib32-nouveau-dri: for Nouveau users'
  'lib32-nvidia-utils-bumblebee: for NVIDIA + Bumblebee users'
  'lib32-nvidia-utils: for NVIDIA proprietary blob users'
  )
else
  depends+=(
  'glu' 'libpng12' 'sdl' 'libpulse' 'alsa-lib' 'libvorbis'
  'libgcrypt' 'nss' 'openal' 'gtk2' 'glu'
  )
fi
# 'libtheora'
install=steam.install

md5sums=('7a076d134ba360337dd6611ac059de87')

source=("http://media.steampowered.com/client/installer/steam.deb")

build() {
  cd "$srcdir"

  bsdtar xf "$srcdir/data.tar.gz"

  # Replace [ ] with [[ ]] in /usr/bin/steam
  sed 's:\[:\[\[:g' -i "$srcdir/usr/bin/steam"
  sed 's:\]:\]\]:g' -i "$srcdir/usr/bin/steam"
}

package() {

  cp -dpr --no-preserve=ownership "$srcdir/usr" "$pkgdir"
  # Install license
  install -Dm644 "$srcdir/usr/share/doc/steam/copyright" "$pkgdir/usr/share/licenses/steam/LICENSE"

}
#vim: set ft=PKGBUILD sw=2 ts=2 et
