# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=mypaint
pkgver=1.2.0
pkgrel=3
pkgdesc="A fast and easy painting application for digital painters, with brush dynamics"
arch=('i686' 'x86_64')
url="http://mypaint.org/"
license=('GPL' 'LGPL')
depends=('pygtk' 'python2-numpy' 'protobuf-python' 'python2' 'json-c'
	 'lcms2' 'python2-gobject' 'gtk3')
makedepends=('scons>=0.97' 'swig>=1.3.39')
source=(https://github.com/mypaint/mypaint/releases/download/v$pkgver/mypaint-$pkgver.tar.xz
	https://github.com/mypaint/mypaint/commit/c03602f3d5456d59fccfc8ad7d41c8c6f1a6d593.patch)
md5sums=('c7c2cfac8e1eec5549e58567ec68daac'
         'e196d8ea9eb9e939118a17c04678a69a')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/c03602f3d5456d59fccfc8ad7d41c8c6f1a6d593.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  scons
}

package() {
  cd $srcdir/$pkgname-$pkgver
  scons prefix="$pkgdir"/usr install
}
