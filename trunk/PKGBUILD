# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hOpenPGP
pkgname=haskell-hopenpgp
pkgver=2.7
pkgrel=27
pkgdesc="Native Haskell implementation of OpenPGP (RFC4880)"
url="http://floss.scru.org/hOpenPGP/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-asn1-encoding' 'haskell-attoparsec'
         'haskell-base16-bytestring' 'haskell-base64-bytestring' 'haskell-bifunctors'
         'haskell-bzlib' 'haskell-binary-conduit' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-cryptonite' 'haskell-crypto-cipher-types' 'haskell-errors' 'haskell-hashable'
         'haskell-incremental-parser' 'haskell-ixset-typed' 'haskell-lens' 'haskell-memory'
         'haskell-monad-loops' 'haskell-nettle' 'haskell-network-uri' 'haskell-newtype'
         'haskell-openpgp-asciiarmor' 'haskell-prettyprinter' 'haskell-resourcet' 'haskell-split'
         'haskell-time-locale-compat' 'haskell-unliftio-core' 'haskell-unordered-containers'
         'haskell-wl-pprint-extras' 'haskell-zlib')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('6432f54853af4562b5c61f0d97fe40dd4a5872f07e07c470ae41e951b11cbe00f491ce469c5d0caa897ba046c44e65fcaff0e4c9db825126b35f7aa15fc6bb40')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
