# $Id$
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
pkgver=3.0.5
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('python-pyaes' 'python-ecdsa' 'python-pbkdf2' 'python-requests' 'python-qrcode'
         'python-protobuf' 'python-dnspython' 'python-jsonrpclib-pelix' 'python-pysocks'
         'python-pyqt5' 'python-pycryptodomex')
makedepends=('python-setuptools')
optdepends=('python-btchip: BTChip hardware wallet support'
            'python-matplotlib: plot transaction history in graphical mode'
            'zbar: QR code reading support')
url="https://electrum.org"
license=(MIT)
source=("https://download.electrum.org/${pkgver}/${pkgname^}-${pkgver}.tar.gz"{,.asc})
sha512sums=('27cfa44ce8eeb5eef8038f1f68b4a42d173daaf7a940ffc9e9b1dad16a8f7f49bd53d5eefdbaf242e5e09aa2f887bce536f17941c2250225cd92c78f62c52e39'
            'SKIP')
validpgpkeys=('6694D8DE7BE8EE5631BED9502BD5824B7F9470E6')

build() {
  cd "${pkgname^}-${pkgver}"

  pyrcc5 icons.qrc -o gui/qt/icons_rc.py
  python setup.py build
}

package() {
  cd "${pkgname^}-${pkgver}"

  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENCE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
