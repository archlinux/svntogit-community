# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-keystoneauth1
pkgname=(python-keystoneauth1 python2-keystoneauth1)
pkgver=3.18.0
pkgrel=3
pkgdesc="Authentication Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/developer/keystoneauth"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-iso8601'
             'python2-iso8601' 'python-os-service-types' 'python2-os-service-types' 'python-requests'
             'python2-requests' 'python-stevedore' 'python2-stevedore')
checkdepends=('python-requests-mock' 'python-testscenarios' 'python-mock' 'python-lxml'
              'python-oslo-config' 'python-betamax' 'python-oslotest' 'python-oslo-utils'
              'python-requests-kerberos' 'python-oauthlib' 'python-pycodestyle')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/keystoneauth/archive/$pkgver.tar.gz")
sha512sums=('cc245b52027df6d555671f27e29ca1c626189182217b2bfe3d3769f076152946d5b80fb21b95419a939f9d7836edca75f3ed85d344fb49c0366a62a53ab668a5')

prepare() {
  sed -i 's/pep8/pycodestyle/g' keystoneauth-$pkgver/keystoneauth1/tests/unit/test_hacking_checks.py

  cp -a keystoneauth-$pkgver{,-py2}

  sed -i 's/assertItemsEqual/assertCountEqual/' keystoneauth-$pkgver/keystoneauth1/tests/unit/extras/kerberos/*.py

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/keystoneauth-$pkgver
  python setup.py build

  cd "$srcdir"/keystoneauth-$pkgver-py2
  python2 setup.py build
}

check() {
  cd keystoneauth-$pkgver
  stestr run
}

package_python-keystoneauth1() {
  depends=('python-pbr' 'python-iso8601' 'python-stevedore' 'python-os-service-types' 'python-requests')
  optdepends=('python-requests-kerberos: for kerberos support'
              'python-lxml: for saml2 support'
              'python-oauthlib: for oauth1 support'
              'python-betamax: for betamax support'
              'python-fixtures: for betamax support'
              'python-mock: for betamax support')

  cd "$srcdir"/keystoneauth-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-keystoneauth1() {
  depends=('python2-pbr' 'python2-iso8601' 'python2-stevedore' 'python2-os-service-types' 'python2-requests')
  optdepends=('python2-requests-kerberos: for kerberos support'
              'python2-lxml: for saml2 support'
              'python2-oauthlib: for oauth1 support'
              'python2-betamax: for betamax support'
              'python2-fixtures: for betamax support'
              'python2-mock: for betamax support')

  cd "$srcdir"/keystoneauth-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
