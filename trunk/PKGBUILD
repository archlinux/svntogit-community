# $Id$
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=openimageio
pkgver=1.1.8
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb')
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa')
optdepends=('qt4: iv image viewer'
            'python2: bindings support')
source=(https://github.com/OpenImageIO/oiio/tarball/Release-$pkgver)
md5sums=('2d816e0ff48f12dfc0491d39a4918456')

build() {
  cd "$srcdir"/$_pkgname*

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DUSE_EXTERNAL_TBB=ON \
      ..
  make
}

package() {
  cd "$srcdir"/$_pkgname*/src/build

  make DESTDIR="$pkgdir" install

  # license
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
