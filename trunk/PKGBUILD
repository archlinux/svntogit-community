# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=1.8
pkgrel=1
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL2)
# NOTE: crc32c is an undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
depends=(python python-crc32c python-cryptography python-pefile python-setuptools)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest)
# no man pages in sdist tarball on pypi.org: https://gitlab.com/kraxel/virt-firmware/-/issues/5
# source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('809b59aa66d6109e7aa42308a065c20ae2eeba454af89022190445df4b660c3b9863eff73d22ccc1a0fe8dffc784808449dbf94c2209ad1dd32a0b046bc3ce51')
b2sums=('b6184fd5e2438294c1da9a9c4c84412ddf2d6afafb8f6f0818e380c31ecae1fb59483e560046944ea8d4e07c6b7badd97f299831e6936bb190194f3b42a5ba9c')

build() {
  cd $pkgname-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-v$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
