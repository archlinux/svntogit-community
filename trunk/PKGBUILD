# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.6.5
_commit=818a7f554108a7b6ef292e7b646258cecb20d613
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-imageformats' 'qt5-multimedia'
         'qt5-svg' 'qt5-x11extras' 'startup-notification')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("git+https://github.com/linuxdeepin/dtkwidget.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd dtkwidget
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DBUILD_DOCS=ON \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DVERSION=$pkgver \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

package() {
  cd dtkwidget
  DESTDIR="$pkgdir" ninja install
}
