# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Johan Rehnberg <cleanrock@gmail.com>

pkgname=naev
pkgver=0.7.0
pkgrel=2
pkgdesc='2D action/rpg space game'
arch=('x86_64')
url="http://sourceforge.net/projects/naev/"
license=('GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libgl' 'libxml2' 'freetype2' 'libpng' 'libzip' 'luajit')
makedepends=('freeglut' 'zip')
source=("https://github.com/naev/naev/archive/naev-${pkgver}.tar.gz")
md5sums=('c07e24c2e87d08fdb73e666fb4e59981')

build() {
  cd "$srcdir/$pkgname-$pkgname-$pkgver"

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./autogen.sh
  ./configure --prefix=/usr --disable-debug --disable-shave --with-ndata-path=/usr/share/naev/ndata
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
