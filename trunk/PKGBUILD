# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel at yahoo dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

_pkgbase=hsa-amd-aqlprofile
pkgname=${_pkgbase}-bin
_pkgver_major=5
_pkgver_minor=5
_pkgver_patch=0
_pkgver_str="${_pkgver_major}$(printf '%02d' $_pkgver_minor $_pkgver_patch)"
_pkgver_magic=63
_pkgver_ubunturel=22.04
pkgver=$_pkgver_major.$_pkgver_minor.$_pkgver_patch
pkgrel=1
pkgdesc='AQLPROFILE library for AMD HSA runtime API extension support'
arch=('x86_64')
url='https://docs.amd.com/'
license=('custom:EULA')
depends=('hsa-rocr')
provides=('hsa-amd-aqlprofile')
conflicts=('hsa-amd-aqlprofile')
source=("${_pkgbase}-${pkgver}.deb::https://repo.radeon.com/rocm/apt/${pkgver:0:3}/pool/main/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}_1.0.0.${_pkgver_str}-${_pkgver_magic}~${_pkgver_ubunturel}_amd64.deb")
sha256sums=('53a4d8a33b363bf70ef383b6e3d54fe2682cf5bc85a8a19990398b6f664fc500')

prepare() {
  tar -xf data.tar.gz
}

package() {
  mv "$srcdir"/opt "$pkgdir"/opt
  rename -- "-$pkgver" '' "$pkgdir/opt/rocm-$pkgver"
  rename -- "${_pkgbase#hsa}" '' "$pkgdir/opt/rocm/$_pkgbase"
  find "$pkgdir" -type d -exec chmod 755 '{}' '+'
  ln -sf "/opt/rocm/hsa/lib/libhsa-amd-aqlprofile64.so" "$pkgdir/opt/rocm/lib/libhsa-amd-aqlprofile64.so.1"
  install -Dm644 "$pkgdir/opt/rocm/share/doc/hsa-amd-aqlprofile/LICENSE.md" "$pkgdir/usr/share/licenses/$_pkgbase/LICENSE"
}
