# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.2.4
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5160a29bd401bce0e5bf3678a4d74b4e8d4af6e2918126e18078cc01da8e8fd8b749ffdb9128df06bef47b5fab68743889183ceb7917397d12877417a8ec6bee')
b2sums=('6d97fcaa045f49121dda5d2b3cb9b8b7f1e7a343ebc8c558bf261829a4f6cd0a45ff7fa245ebeeba62da00e5ff5bdf78f02d9a17ae46f930c239b4a3b2685be0')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 kube-linter -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
