# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=2.0.8
pkgrel=5
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'libibus' 'libcanberra-pulse' 'librsvg' 'nss' 'pulseaudio-alsa' 'upower' 'libnotify')
makedepends=('intltool' 'docbook-xsl' 'gnome-common')
options=('!emptydirs')
install=cinnamon-settings-daemon.install
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
        keyboard.patch
        systemd-support.patch
        cinnamon-settings-daemon-2.0.6-upower_deprecated.patch
        remove_obsolete_battery_recall_code.patch
        automount-plugin.patch
        dpms.patch)
sha256sums=('6889f021acab93d146c78fe8aa540a83a12260232e1ea5f697037a7c94c877fd'
            '3c05b978cb414c8fc1530f211bab58627a64ac9c156630c9d486f5926a85109c'
            '28c002628bc97e8b4a2aa7e8a8141bcc8388b250a6c3ebc09a640e110ae815a9'
            '291855ec208974a0a59ce24707820dcb9ff04868421f185b42d86d1dc52eb0d6'
            '4c1d917a7a70e16a5307f767c1494fa59cbc5168d9293ad66b56e17a74e8baf1'
            '4fc2b3a354b53d41d92d7f95d175a6a18382330353a0579d29c2d33b9400a837'
            '194cc704320a2d6be636191e96f62514f2b5f97655c200991b5690fbf3006026')

prepare() {
  cd $pkgname-$pkgver

  # Fix crasher
  patch -Np1 -i ../keyboard.patch

  # Add systemd support for power and media-keys plugins
  patch -Np1 -i ../systemd-support.patch

  # Port to upower 0.99
  patch -Np1 -i ../cinnamon-settings-daemon-2.0.6-upower_deprecated.patch
  patch -Np1 -i ../remove_obsolete_battery_recall_code.patch

  # Turn automount helper into a plugin
  patch -Np1 -i ../automount-plugin.patch

  # Fix DPMS issue with Xorg 1.14
  patch -Np1 -i ../dpms.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
