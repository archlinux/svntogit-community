# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

_pkgbasename=libtiff
pkgname=lib32-$_pkgbasename
pkgver=4.0.8
pkgrel=1
pkgdesc="Library for manipulation of TIFF images (32-bit)"
arch=('x86_64')
url="http://www.simplesystems.org/libtiff/"
license=('custom')
depends=('lib32-gcc-libs' 'lib32-libjpeg' 'lib32-zlib' 'lib32-xz' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz CVE-2016-10095.patch)
sha512sums=('5d010ec4ce37aca733f7ab7db9f432987b0cd21664bd9d99452a146833c40f0d1e7309d1870b0395e947964134d5cfeb1366181e761fe353ad585803ff3d6be6'
            'a707225f3fdf400409ff1d26dae9301d4babf177001e018b676f2cdbd4b900d011c07dc39832cb7778ee2418d9d19fd48eb816fdce5c2dd30adb789eb729fd67')

prepare() {
  cd tiff-$pkgver
  patch -p1 -i ../CVE-2016-10095.patch # Fix several security issues
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/tiff-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32 --includedir=/usr/include/libtiff32
  make
}

check() {
cd "${srcdir}/tiff-${pkgver}"
make check
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
