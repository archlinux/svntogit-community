# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.13.0
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=(x86_64)
url="https://amsynth.github.io/"
license=(GPL2)
groups=(dssi-plugins lv2-plugins pro-audio vst-plugins)
depends=(cairo gcc-libs glibc)
makedepends=(atk autoconf-archive dssi fontconfig freetype2 gdk-pixbuf2 glib2
gtk2 harfbuzz intltool jack ladspa liblo lv2 pandoc)
checkdepends=(lv2lint)
optdepends=(
  'alsa-lib: for standalone application'
  'dssi-host: for DSSI plugin'
  'jack: for standalone application'
  'liblo: for standalone application and DSSI plugin'
  'new-session-manager: for session management with the standalone application'
  'lv2-host: for LV2 plugin'
  'vst-host: for VST plugin'
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/amsynth/amsynth/archive/release-$pkgver.tar.gz
)
sha512sums=('33f31892c2f1d843a2a3a8df6ab8fcbd628dbc3ddb05b7413f259cb820a664957b8701528373b433d29c5c0155bb38f03f0820c62432b47252b1f02dcf40ca06')
b2sums=('1b33c91ddbeaeac26326a502d3dec5e33aa95937c98c4568770e10d8a4d6ed6fe9652d37fce7dae5d1376e3be16cf4582decccd9ae173052899e7b230e07b1eb')

prepare() {
  cd $pkgname-release-$pkgver
  autoreconf -fiv
  intltoolize -f
}

build() {
  cd $pkgname-release-$pkgver
  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack \
              --with-dssi \
              --with-lv2 \
              --with-vst \
              --with-pandoc
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-release-$pkgver
  cp .libs/${pkgname}_lv2*.so data/$pkgname.lv2/
  lv2lint -Mpack -I data/$pkgname.lv2 "http://code.google.com/p/amsynth/amsynth"
}

package() {
  depends+=(libatk-1.0.so libfontconfig.so libfreetype.so libgdk_pixbuf-2.0.so
  libgdk-x11-2.0.so libgio-2.0.so libglib-2.0.so libgobject-2.0.so
  libgtk-x11-2.0.so libharfbuzz.so libpangocairo-1.0.so libpangoft2-1.0.so
  libpango-1.0.so)

  make DESTDIR="$pkgdir/" install -C $pkgname-release-$pkgver
  install -vDm 644 $pkgname-release-$pkgver/{AUTHORS,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
