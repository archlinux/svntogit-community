# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-static
pkgname=haskell-yesod-static
pkgver=1.6.0
pkgrel=84
pkgdesc="Static file serving subsite for Yesod Web Framework."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-blaze-builder' 'haskell-byteable' 'haskell-conduit' 'haskell-cryptonite'
         'haskell-cryptonite-conduit' 'haskell-memory' 'haskell-css-text' 'haskell-data-default'
         'haskell-exceptions' 'haskell-file-embed' 'haskell-hashable' 'haskell-hjsmin'
         'haskell-http-types' 'haskell-mime-types' 'haskell-old-time' 'haskell-resourcet'
         'haskell-unix-compat' 'haskell-unordered-containers' 'haskell-wai' 'haskell-wai-app-static'
         'haskell-yesod-core')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('85352eb6ec0f4d7682a0d000f158446ed0370d7a63f9cca8976e5972d10ca427e45e8307c1169bfbcc25ba7fa62e1906651a9e8c7d6937ac4ea6f25412b5cea5')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
