# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=comm
pkgname=python-$_pyname
pkgver=0.1.2
pkgrel=1
pkgdesc='Python Comm implementation for the Jupyter kernel protocol'
arch=(any)
url='https://pypi.org/project/comm/'
license=(BSD)
depends=(python-traitlets)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest)
source=(https://github.com/ipython/$_pyname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f6ce2ae0a4aefaa6a7a071e68988c17b089b0af40633e1aafd276b5190649878')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  PYTHONPATH="$PWD" \
  pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
