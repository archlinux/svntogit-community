# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=2.92.2
_commit=32804cad060510fc0e61da2106dc5f14fcc8c56b
#_golibver=0.4.3
_golibrev=902e89b94c9d657f523bcdabad399dc789dcf86e
#_dbusfactoryver=2.92.2
_dbusfactoryrev=bb43480b94f666f1eb4b139d7e9710e13ee5aee6
pkgrel=1
pkgdesc='Go-lang bingdings for dde-daemon'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill' 'gcc-go' 'poppler-glib' 'deepin-metacity' 'xcur2png')
conflicts=('dde-api')
replaces=('dde-api')
groups=('deepin')
makedepends=('git' 'deepin-dbus-generator' 'bzr')
source=(#"git+https://github.com/linuxdeepin/dde-api.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/dde-api.git#commit=$_commit"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        #"git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_dbusfactoryrev")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/lib
  cp -a go-lib/* build/src/pkg.deepin.io/lib

  cd "$srcdir/dde-api"
  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2
}

build(){
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/dde-api"
  make USE_GCCGO=1
}

package() {
  cd dde-api
  make DESTDIR="${pkgdir}" install-binary

  mv "$pkgdir"/{lib/systemd,usr/lib/}
  rmdir "$pkgdir/lib"
}
