# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.8.23
pkgrel=1
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm'
arch=('x86_64')
url='https://www.nongnu.org/duplicity/'
license=('GPL')
depends=('gnupg' 'librsync' 'python-paramiko' 'python-fasteners' 'python-future')
makedepends=('python-setuptools-scm')
optdepends=('lftp: backend for ftp, ftps, fish'
            'ncftp: alternative backend for ftp'
            'python-boto: Amazon S3 and Google Cloud Storage backend'
            'python-dropbox: Dropbox backend'
            'python-gobject: GIO backend'
            'gvfs: GIO backend'
            'python-pydrive2: Google Drive backend'
            'python-requests-oauthlib: OneDrive backend'
            'rsync: rsync backend')
source=("https://launchpad.net/$pkgname/0.8-series/$pkgver/+download/$pkgname-$pkgver.tar.gz"{,.sig})
b2sums=('63042486e6e17d2d93af72f617990c879763d5921e230302f406b4f9e7c57a67284130f9f57849b9ffc8638614b55f0f8abdf6bb20201155b33b72bef952ec9f'
        'SKIP')
validpgpkeys=('9D95920CED4A8D5F8B086A9F8B6F8FF4E654E600'
              '9946FB5BE18E34E016DAE992330B3C2A96A9EA9C')

package() {
	cd "${pkgname}-${pkgver}"
	python setup.py install --root="$pkgdir" --optimize=1

  # move documentation to the correct directory
  cd "$pkgdir/usr/share/doc"
  mv "$pkgname-$pkgver" "$pkgname"
}
