# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=('python-argh' 'python2-argh')
pkgver=0.26.2
pkgrel=1
pkgdesc="An unobtrusive argparse wrapper with natural syntax"
arch=(any)
url="https://pypi.python.org/pypi/argh"
license=('LGPL3')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock'
              'python-iocapture' 'python2-iocapture')
source=("git+https://github.com/neithere/argh.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a argh{,-py2}
}

build() {
  cd "$srcdir"/argh
  python setup.py build

  cd "$srcdir"/argh-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/argh
  py.test

  cd "$srcdir"/argh-py2
  LC_CTYPE=en_US.UTF-8 py.test2
}

package_python-argh() {
  depends=('python')

  cd argh
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-argh() {
  depends=('python2')

  cd argh-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
