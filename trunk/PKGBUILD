# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=knative-client
_pkgname=client
pkgver=1.0.0
pkgrel=1
pkgdesc="knative client"
arch=('x86_64')
url="https://github.com/knative/client"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/knative/${_pkgname}/archive/refs/tags/knative-v${pkgver}.tar.gz")
sha512sums=('fce77769b91f2a5fb0c21e1b70f37f542661cef4dd6b523335d1f6d68388d41674fc3987056f8a3054457d70e501a9eb450fda1efe4783a563a7399d9c8154f7')
b2sums=('38bba50fbdd371e88f6b21fb83cb22f0fdd1c140f5af767af1052b934d7aad6b4cfcb53a9144c71382ab9b443df0f08396c512498c5c8bbf730a77713944ac8f')

build() {
  cd "client-knative-v${pkgver}/cmd/kn"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build -v
}

check() {
  cd "client-knative-v${pkgver}"
  go test -v ./...
}

package() {
  cd "client-knative-v${pkgver}/cmd/kn"
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./kn completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_kn"
  ./kn completion bash > "${pkgdir}/usr/share/bash-completion/completions/kn"
  install -Dm755 kn "${pkgdir}/usr/bin/kn"
}
