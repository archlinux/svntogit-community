# Maintainer: David Runge <dvzrv@archlinux.org>

_name=rcssmin
pkgname=python-rcssmin
pkgver=1.1.0
pkgrel=1
pkgdesc="Fast CSS minifier for Python"
arch=(x86_64)
url="https://github.com/ndparker/rcssmin"
license=(Apache)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9dc7fac0fef73c389baf8fc132e58599471517a2b77b13fe6bfabd30246b46bf859f7307ff1606d6634bdcdc472bdde0882fe7282018a34fba7b8fc80f6e9279')
b2sums=('162ec087b791a4ee0c23994cea431e01f76c199c1d2118a75c9e49ebfc0d4f46fc4088126032851446acf2bd7b76b8f17095896100338636f4df8de75612f55d')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_python_version}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
