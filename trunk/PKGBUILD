# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=wallutils
pkgver=5.8.0
pkgrel=1
pkgdesc='Utilities for handling resolutions, wallpapers and timed wallpapers'
arch=(x86_64)
url='https://github.com/xyproto/wallutils'
license=(MIT)
depends=(wayland libx11)
makedepends=(git go libxcursor libxmu upx xbitmaps xorgproto)
optdepends=('feh: for setting the wallpaper for some window managers for X')
source=("git+$url#tag=$pkgver")
options=(!strip)
sha256sums=('SKIP')

prepare() {
  sed -i 's/go build/go build -mod=vendor -buildmode=pie -gcflags "all=-trimpath=$$PWD" -asmflags "all=-trimpath=$$PWD" -ldflags "-s -w -extldflags $$LDFLAGS"/g' $pkgname/Makefile
}

build() {
  make -C $pkgname
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname install
  find "$pkgdir/usr/bin/" -executable -type f -exec \
    upx -q --no-progress {} >/dev/null \;
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
