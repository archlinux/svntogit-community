# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=yabridge
pkgname=(yabridge yabridgectl)
_commit=669b42b31bc40c1135431d8f4f64e7f3fde588d0  # refs/tags/5.0.2
_vst3sdk_commit=ac1d0ea4b7b8540708d5f78d51bfecb7e75dddd9  # refs/tags/v3.7.5_build_44-patched-2
pkgver=5.0.2
pkgrel=1
pkgdesc="A modern and transparent way to use Windows VST2 and VST3 plugins on Linux"
arch=(x86_64)
url="https://github.com/robbert-vdh/yabridge"
license=(GPL3)
makedepends=(asio bitsery clap cmake function2 ghc-filesystem git lib32-libxcb libxcb meson rust tomlplusplus vst3sdk wine)
# if LTO is enabled the cmake detection will explode because of wine
# NOTE: debug package includes garbage because of rust
options=(debug !lto)
source=(
  git+https://github.com/robbert-vdh/$pkgbase.git#tag=$_commit
  $pkgbase-vst3sdk::git+https://github.com/robbert-vdh/vst3sdk.git#commit=$_vst3sdk_commit
  $pkgbase-vst3_base::git+https://github.com/steinbergmedia/vst3_base.git
  $pkgbase-vst3_pluginterfaces::git+https://github.com/steinbergmedia/vst3_pluginterfaces.git
  $pkgbase-vst3_public_sdk::git+https://github.com/steinbergmedia/vst3_public_sdk.git
)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  # NOTE: use upstream's specific version of vst3sdk to get around compilation errors
  (
    cd $pkgbase-vst3sdk
    git submodule init
    git config submodule.base.url "$srcdir/$pkgbase-vst3_base"
    git config submodule.pluginterfaces.url "$srcdir/$pkgbase-vst3_pluginterfaces"
    git config submodule.publik.sdk.url "$srcdir/$pkgbase-vst3_public_sdk"
    git -c protocol.file.allow=always submodule update
  )

  (
    cd $pkgbase
    # support clap >= 1.1.3
    git cherry-pick -n 5150332d20a50f6c9082ac6a9ae1873eb7ee5a9f
  )
  # remove all wraps as they are ignored anyways
  rm -rv $pkgname/subprojects/*

  mkdir -vp $pkgname/subprojects/vst3
  # put system-installed vst3sdk in place and add upstream meson.build file from https://github.com/robbert-vdh/vst3sdk/
  # NOTE: vst3sdk > 3.7.5 currently not compatible
  # cp -av /usr/share/vst3sdk/* $pkgname/subprojects/vst3/
  # cp -v $pkgbase-*-vst3sdk_meson.build $pkgname/subprojects/vst3/meson.build

  cp -av $pkgbase-vst3sdk/* $pkgname/subprojects/vst3/

  cd $pkgname/tools/yabridgectl
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  # NOTE: we need to add -mwindows to cpp_link_args as we are otherwise overriding it
  local _options=(
    --cross-file=$pkgname/cross-wine.conf
    -Dbitbridge=true
    -Dsystem-asio=true
    -Db_lto=false
    -Db_pie=false
    -Dbuild.cpp_link_args="$LDFLAGS"
    -Dcpp_link_args="$LDFLAGS -mwindows"
  )

  arch-meson $pkgbase build "${_options[@]}"
  ninja -C build

  cd $pkgname/tools/yabridgectl
  cargo build --frozen --release --all-features
}

package_yabridge() {
  groups=(pro-audio)
  depends=(lib32-libxcb libxcb wine)
  optdepends=(
    'libnotify: for notification integration'
    'yabridgectl: for optional setup and management utility'
  )

  # project has no install target
  install -vDm 755 build/$pkgname-host{,-32}.exe{,.so} -t "$pkgdir/usr/bin"
  install -vDm 755 build/lib$pkgname-{,chainloader-}{vst2,vst3}.so -t "$pkgdir/usr/lib/"
}

package_yabridgectl() {
  depends=(gcc-libs glibc)
  pkgdesc="Optional utility to help set up and manage yabridge"

  install -vDm 755 $pkgbase/tools/$pkgname/target/release/$pkgname -t "$pkgdir/usr/bin"
}
