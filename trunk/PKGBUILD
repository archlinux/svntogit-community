# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=btrbk
pkgver=0.32.6
pkgrel=1
pkgdesc="Tool for creating snapshots and remote backups of btrfs subvolumes"
arch=(any)
url="https://github.com/digint/btrbk"
license=(GPL3)
depends=(btrfs-progs perl)
makedepends=(asciidoctor)
optdepends=(
  'bash: for contrib scripts'
  'mbuffer: for rate limiting and progress bars'
  'openssh: for transfering backups to/from remote hosts'
  'python: for contrib scripts'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('707def45a38325586d3ce8731c9dc957dcb416f724ef91dc027fc2ba727708c6892cad6bec5143066463d772d5aa92d0661521a6e13c5871f05eee11540c9c25')
b2sums=('c8b39f65e67ee1380faaaad7eb88c21bdfbba562d554dad750cc0c26354d6ce703f75d5033d19599ce3d506d67ecdeeb8f2b2af5fd5cdaa1d521055bde26ba1d')

build() {
  make -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
