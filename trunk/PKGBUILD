# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>
# Contributor: Gary van der Merwe <g a r y v d m at gmail dot com>

pkgbase=('python-requests-ntlm')
pkgname=('python-requests-ntlm' 'python2-requests-ntlm')
_module='requests_ntlm'
pkgver=1.1.0
pkgrel=2
pkgdesc="Allows for HTTP NTLM authentication using the requests library."
url="https://github.com/requests/requests-ntlm"
makedepends=(python-setuptools python2-setuptools)
license=('custom:ISC License (ISCL)')
arch=(any)
source=("https://files.pythonhosted.org/packages/source/r/requests_ntlm/requests_ntlm-${pkgver}.tar.gz")
sha512sums=('9a74ecd6e4ed5c5e4381d2ee3fb9ff233352b49baa5fae4e0ecc30aaad12fc7a5c1b9dd936f35d4a3815ae7f6ec5bb8581e84128d63ff0e961181408daddb807')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-requests-ntlm() {
    depends+=('python' 'python-requests' 'python-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-requests-ntlm/LICENSE"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-requests-ntlm() {
    depends+=('python2' 'python2-requests' 'python2-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}-python2"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python2-requests-ntlm/LICENSE"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
