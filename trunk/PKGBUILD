# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=proxmark3
pkgver=4.15864
pkgrel=1
pkgdesc='Software for the the Proxmark3, an RFID swiss-army tool'
arch=('x86_64')
url='https://github.com/RfidResearchGroup/proxmark3'
license=('GPL2')
depends=('qt5-base' 'bluez-libs' 'jansson')
makedepends=('arm-none-eabi-gcc' 'arm-none-eabi-newlib' 'lua52' 'python')
optdepends=('lua52: lua scripts'
            'python: some scripts')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('dddda0cdd3fb94fa51baff0c85768ff72c7194319f5554917c330ab505d42fdf0bd41ab3147362e25d14361bff6206067327c6f4da3619654658fd86177c6bee')

prepare() {
  cp -r $pkgname-$pkgver{,-rdv4}
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC SKIPQT=0 all

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 common bootrom recovery fullimage
}

package() {
  export PREFIX=/usr
  export DESTDIR="$pkgdir"
  export UDEV_PREFIX=/usr/lib/udev/rules.d
  export INSTALLTOOLSRELPATH=bin

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 {bootrom,recovery,fullimage}/install

  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC install
}

