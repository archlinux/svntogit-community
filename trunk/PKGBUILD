# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=2.5.3
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('7b6b606ae08786f5837543bd8b25f1e86b3bd695e73f5ebf4b3487dae4a4390805060e09cfd1f5389255ceb1afb8b81860021bddb6436b80739e48012f304124')
b2sums=('8ffe54a46d14285d90daa60c147c53e0c0daf8e3f7d715c834303997050a0453f6b63940cba847dd29056bfabcbeed1b1278a45bcfe1a60b1d3d0d3d8d61c2b9')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
