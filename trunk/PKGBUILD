# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=shadowsocks-v2ray-plugin
pkgver=5.0.8
pkgrel=1
pkgdesc='A SIP003 plugin based on v2ray for shadowsocks'
arch=('x86_64')
url='https://github.com/teddysun/v2ray-plugin'
license=('GPL')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
source=("https://github.com/teddysun/v2ray-plugin/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d0273a3cf9c8f422abcc1ca710ad45435ad1d8be15f7ac2bdaa82f8ae43e396a5f41907436bf8bbbe8a59999de792ad2e3a191ef68c846542b552dc9b2f86266')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd v2ray-plugin-$pkgver
  go build
}

package() {
  cd v2ray-plugin-$pkgver
  install -Dm755 v2ray-plugin "$pkgdir"/usr/bin/v2ray-plugin
}
