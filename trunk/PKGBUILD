# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeremie Dimino <jeremie@dimino.org>
# Contributor: bender02 at gmx dot com

pkgname=lrzsz
pkgver=0.12.20
pkgrel=5
pkgdesc="xmodem, ymodem and zmodem file transfer protocols"
url="http://www.ohse.de/uwe/software/lrzsz.html"
license=('GPL2')
arch=('x86_64')
source=(http://www.ohse.de/uwe/releases/$pkgname-$pkgver.tar.gz
	lrzsz.patch)
md5sums=('b5ce6a74abc9b9eb2af94dffdfd372a4'
         '332f6e4af9fe0266efecf9d334c4d3db')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -Np1 -i ../lrzsz.patch
  sed -i 's|${CC-cc} -E|${CC-cc} -O2 -E|g' configure
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  # Rename file so they can be used with minicom
  ./configure --prefix=/usr --mandir=/usr/share/man \
	--program-transform-name=s/l//
  make -j1
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
}
