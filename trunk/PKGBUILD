# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Steven Honeyman <stevenhoneyman at gmail com>
# Contributor: nbvcxz (b4283)

pkgname=l3afpad
pkgver=0.8.18.1.11
pkgrel=2
pkgdesc="Simple plain text editor for GTK+ 3"
arch=('x86_64')
url="https://github.com/stevenhoneyman/l3afpad"
license=('GPL2')
depends=('gtk3')
makedepends=('intltool')
source=(https://github.com/stevenhoneyman/l3afpad/archive/v$pkgver.tar.gz)
md5sums=('2c209503116b53e732c37f8d8d4c40fd')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
