# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-notebook
pkgver=6.0.3
pkgrel=1
pkgdesc="The language-agnostic HTML notebook application for Project Jupyter"
arch=('any')
url="https://github.com/jupyter/notebook"
license=('BSD')
depends=('python-setuptools' 'python-jinja' 'python-tornado' 'python-terminado'
         'python-traitlets' 'jupyter-nbformat' 'python-jupyter_core'
         'python-jupyter_client' 'mathjax2' 'python-send2trash'
         'jupyter-nbconvert' 'python-prometheus_client')
# Much of their official dependency tree appears inverted and circular?
makedepends=('python-setuptools' 'npm' 'git')
optdepends=('pandoc: notebook export')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/notebook/archive/$pkgver.tar.gz")
md5sums=('1e5962b6e1fea5c34711d28c6ba841cb')

# todo: report bug that mathjax and nbconvert is no longer optional

build() {
  cd "$srcdir/notebook-$pkgver"

  # FS45999 disable bundled mathjax
  sed -i 's/^.*MathJax.*$//' bower.json 
  #sed -i '/Trim mathjax/,/^                static_data.append/d' setupbase.py
  sed -i 's/  mj(/  #mj(/' setupbase.py

  # needs node, downloads a lot of stuff
  python3 setup.py build
}

package() {
  cd "$srcdir/notebook-$pkgver"

  # needs node
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  # FS45999 use system mathjax
  ln -s /usr/share/mathjax2 \
  "$pkgdir/usr/lib/python3.8/site-packages/notebook/static/components/MathJax"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

