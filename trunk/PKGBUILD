# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=colorcet
pkgname=python-${_pkg}
pkgver=2.0.2
pkgrel=3
pkgdesc="Set of useful perceptually uniform colormaps for plotting scientific data"
arch=(any)
url="https://colorcet.holoviz.org/"
license=(BSD)
depends=(python-param python-pyct)
makedepends=(python-setuptools)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('514813790a74b578c3eaff76b2102274c2ba8b0239c9504586df685223007dee')

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build
}

package() {
    cd ${_pkg}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
