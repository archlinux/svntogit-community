# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=monad-logger
pkgname=haskell-monad-logger
pkgver=0.3.28.3
pkgrel=1
pkgdesc="A class of monads which can log messages."
url="https://github.com/kazu-yamamoto/logger"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-exceptions'
         'haskell-fast-logger' 'haskell-lifted-base' 'haskell-monad-control' 'haskell-monad-loops'
         'haskell-mtl' 'haskell-resourcet' 'haskell-stm' 'haskell-stm-chans' 'haskell-text'
         'haskell-transformers-base' 'haskell-transformers-compat' 'haskell-unliftio-core')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('011b538545abee79fc9b70ec2d9d90de964df74b9eb4fa24f9d51877c38fd7dd29f9fd9cc4b2c588d39ebda191a058956d15b16c79c5946b7d2067888d629d9d')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -ftemplate_haskell
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
