# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgbase=himalaya
pkgname=('himalaya' 'vim-himalaya')
pkgver=0.5.1
pkgrel=1
pkgdesc="A CLI email client"
arch=('x86_64')
url="https://github.com/soywod/himalaya"
license=('BSD')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('8e45dff561d11ce7c3df9882d00345862dfca8ece112eb2134f59d9b3a550b3d06faa16697f451704d4f4fc56cb48783910de7fefb9f3a6e8d95fa9b6eb5cecc')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package_himalaya() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "assets/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
}

package_vim-himalaya() {
  depends=('vim-plugin-runtime' 'himalaya')
  pkgdesc+=' - Vim UI plugin'

  cd "$pkgbase-$pkgver/vim/"
  find autoload doc ftplugin lua plugin syntax \
    -type f -exec install -Dm 644 '{}' "$pkgdir/usr/share/vim/vimfiles/{}" \;
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 ../LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
