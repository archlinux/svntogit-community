# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.6.0
pkgrel=2
pkgdesc='An engine for running Transport Tycoon Deluxe.'
arch=('i686' 'x86_64')
url='http://www.openttd.org'
license=('GPL')
depends=('libpng' 'sdl' 'icu' 'fontconfig' 'lzo' 'hicolor-icon-theme' 'desktop-file-utils' 'xz')
optdepends=('openttd-opengfx: free graphics' 
            'openttd-opensfx: free soundset')
source=("http://binaries.openttd.org/releases/${pkgver}/${pkgname}-${pkgver}-source.tar.xz"
        openttd-1.5.3-gcc6.patch)
sha256sums=('4c12e6b516ffdee20a03ebad80dad85d137130002d6d3e123a568376fe4b4eb2'
            '3e9652e1401150c287b71595afbf268dc4379908519d7939ec4e46548026b7e9')

prepare() {
  cd ${pkgname}-${pkgver}

  # patch from fedora
  patch -Np1 -i ../openttd-1.5.3-gcc6.patch
}

build() {
  cd ${pkgname}-${pkgver} 

 ./configure \
    --prefix-dir=/usr \
    --binary-name=${pkgname} \
    --binary-dir=bin \
    --data-dir=share/${pkgname} \
    --install-dir=${pkgdir} \
    --doc-dir=share/doc/${pkgname} \
    --menu-name="OpenTTD" \
    --personal-dir=.${pkgname}    

  make
}

package() {
  cd ${pkgname}-${pkgver} 

  make install
}
