# $Id$
# Maintainer: Jaroslav Lichtblau <setlemodry@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pork
pkgver=0.99.8.1
pkgrel=16
pkgdesc="Console-based AOL Instant Messenger & IRC client"
arch=('x86_64')
url="http://dev.ojnk.net/"
license=('GPL')
depends=('ncurses' 'perl')
source=(https://downloads.sourceforge.net/sourceforge/ojnk/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('f0d4cad21017fd75b3c200342c54dd56d9ff7bc38d1e9b3c8ba686ba53d585ed'
            'SKIP')
validpgpkeys=('7A4533F19780BF32793C1820DAD25608075C69BC') # Ryan McCabe (Odin) <odin@numb.org>

build() {
  cd $pkgname-$pkgver

  CFLAGS+=' -fgnu89-inline'

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
