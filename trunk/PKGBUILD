# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gloobus-preview
pkgver=0.4.5.290
_pkgver=0.4.5-ubuntu11~ppa290
pkgrel=1
pkgdesc="Gnome application designed to enable a full screen preview of any kind of file"
arch=('i686' 'x86_64')
url="http://gloobus.net/"
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'gtksourceview3' 'poppler-glib' 'python2-dbus' 'python2-gconf' 'taglib' 'xdg-utils')
makedepends=('boost' 'bzr' 'djvulibre' 'libspectre')
optdepends=('djvulibre: Preview DjVu documents'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'libspectre: Preview PostScript documents'
            'p7zip: View content of 7z archives'
            'unoconv: Preview LibreOffice compatible documents'
            'unrar: View content of RAR archives'
            'unzip: View content of .ZIP archives')
options=('!libtool')
install=$pkgname.install
source=(https://launchpad.net/~gloobus-dev/+archive/$pkgname/+files/${pkgname}_$_pkgver.orig.tar.xz
        gloobus-sushi::bzr+http://bazaar.launchpad.net/~gloobus-dev/gloobus-sushi/gloobus-sushi
        fix-gtksource-include.patch
        fix-unoconv.patch
        use-xdg-open.patch)
md5sums=('27b3db2043ecaf47fa928940bbb4f32a'
         'SKIP'
         'ca15a3e31cec01d3d4fc5a4e1720f692'
         '03379c173e67ad1b6804bc609336451d'
         'd6d9e2b3e001f0ea8440cccdf90480af')

prepare() {
  cd "$srcdir/$pkgname-$_pkgver"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/gloobus-preview-configuration

  # Fix build
  patch -Np1 -i "$srcdir/fix-gtksource-include.patch"
  sed -i 's/-Werror//' configure.ac src/Makefile.am

  # Fix unoconv support
  patch -Np1 -i "$srcdir/fix-unoconv.patch"

  # Use xdg-open
  patch -Np1 -i "$srcdir/use-xdg-open.patch"

  ### Prepare gloobus-sushi
  cd "$srcdir/gloobus-sushi"

  # Python2 fix
  sed -i 's/python /python2 /' src/gloobus-sushi.in
}


build() {
  cd "$srcdir/$pkgname-$_pkgver"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static
  make

  ### Build gloobus-sushi
  cd "$srcdir/gloobus-sushi"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install

  ### Install gloobus-sushi
  cd "$srcdir/gloobus-sushi"
  make DESTDIR="$pkgdir" install

  # Rename service file to avoid conflict with sushi
  mv "$pkgdir"/usr/share/dbus-1/services/{org.gnome.Sushi.service,net.gloobus.Sushi.service}
}
