# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=9.0
pkgrel=1
pkgdesc='Very fast network logon cracker which support many different services'
url='https://github.com/vanhauser-thc/thc-hydra'
arch=('x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'mariadb-libs' 'libidn'
         'libfbclient' 'afpfs-ng' 'zlib' 'openssl' 'ncurses' 'pcre')
optdepends=('gtk2: graphical front-end (xhydra)')
makedepends=('gtk2')
source=(https://github.com/vanhauser-thc/thc-hydra/archive/v${pkgver}/thc-hydra-v${pkgver}.tar.gz)
sha256sums=('56672e253c128abaa6fb19e77f6f59ba6a93762a9ba435505a009ef6d58e8d0e')
sha512sums=('58067bd2696e82645bb959fe5537f2876e1d8c170a15fe6a9ac65bbdc33cfb75b47fd1a969c314a6c6d91b2b6c0ce4ba087680bbd67a5a38925a2a8b5c5ad338')

build() {
  cd thc-hydra-${pkgver}
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --fhs
  make
}

package() {
  cd thc-hydra-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
