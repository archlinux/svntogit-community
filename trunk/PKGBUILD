# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgname=python-blessed
pkgver=1.19.1
pkgrel=1
pkgdesc='A thin, practical wrapper around terminal styling, screen positioning, and keyboard input'
arch=(any)
url=https://github.com/jquast/blessed
license=(GPL)
depends=(
  python-six
  python-wcwidth
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-tox
)
_tag=7078fadc7502cda371f848a369fecbe0e47e4ee4
source=(git+https://github.com/jquast/blessed.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd blessed

  git describe --tags
}

build() {
  cd blessed

  python setup.py build
}

check() {
  cd blessed

  tox -e py310
}

package() {
  cd blessed

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
