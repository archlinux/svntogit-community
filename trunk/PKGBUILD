# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michal Kaliszka <desmont@gmail.com>
# Contributor: Zsolt Varadi <sysop_xxl@fibermail.hu>

pkgname=tea
pkgver=41.1.0
pkgrel=2
pkgdesc="A Qt-based text editor for Linux and *BSD. With an ultimate small size TEA provides you hundreds of functions."
arch=('i686' 'x86_64')
url="http://semiletov.org/tea/"
license=('GPL')
depends=('qt4' 'aspell' 'hunspell')
install=tea.install
source=(http://semiletov.org/tea/dloads/tea-$pkgver.tar.bz2
        tea.desktop)
md5sums=('8a41da9884f98b8ca6f57296243f8fbc'
         '377ace3363124f4c086de0babb820761')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
#  sed -i 's|i + j|(int)(i + j)|' textproc.cpp
  sed -i 's|/usr/local|/usr|g' src.pro
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake-qt4
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}/tea.desktop" "${pkgdir}/usr/share/applications/tea.desktop"
  install -Dm644 icons/tea_icon_v2.png "${pkgdir}/usr/share/pixmaps/tea.png"
}
