# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

pkgname=uranium
pkgver=4.4.0
pkgrel=4
pkgdesc="A Python framework for building Desktop applications."
url="https://github.com/Ultimaker/Uranium"
arch=('any')
license=('LGPL')
depends=('python' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'python-pyqt5' 'python-numpy' 'arcus' 'python-shapely')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz"
        "fix-crash.patch::https://github.com/Ultimaker/Uranium/commit/78fa9ab2dd8ab117a512e0ae3ce9d8cb93dc89d4.patch")
sha256sums=('6906804add202b19931e01844f418256168e560265b217e725a4d9d0acf21800'
            '6318fac94328cf2e20ca80f8470637cb0a76429b1d022b94d52f0bcae3fe8e71')

prepare() {
  cd Uranium-${pkgver}
  sed -i 's,/dist-packages,.${PYTHON_VERSION_MINOR}/site-packages,g' CMakeLists.txt

  patch -Np1 -i $srcdir/fix-crash.patch
}

build() {
  cd Uranium-${pkgver}
  mkdir -p build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd Uranium-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
