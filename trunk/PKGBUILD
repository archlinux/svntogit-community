# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-session-manager
pkgver=1.27.0
pkgrel=1
pkgdesc="The MATE Session Handler"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('dbus-glib' 'gettext' 'gtk3' 'libsm' 'mate-desktop')
makedepends=('xtrans' 'systemd' 'python')
optdepends=('gnome-keyring: keyring support'
            'xdg-user-dirs-gtk: manage user directories')
groups=('mate')
conflicts=('mate-session-manager-gtk3')
replaces=('mate-session-manager-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('987aa024cd37116e330a309fe52def038e5f1d26af1b566675399dda6911aae1')

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
	            --prefix=/usr \
	            --libexecdir=/usr/lib/${pkgname} \
	            --sysconfdir=/etc \
	            --localstatedir=/var \
	            --disable-upower
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
