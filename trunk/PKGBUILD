# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: quininer <quininer@live.com>
pkgbase='alacritty'
pkgname=('alacritty' 'alacritty-terminfo')
pkgdesc="A cross-platform, GPU-accelerated terminal emulator"
_pkgver=0.3.2
pkgver=$_pkgver
pkgrel=2
arch=('x86_64')
url="https://github.com/jwilm/alacritty"
license=('Apache')
makedepends=('rust' 'cargo' 'cmake' 'fontconfig' 'ncurses' 'desktop-file-utils' 'gdb')
source=("${pkgbase}-${_pkgver}.tar.gz::https://github.com/jwilm/${pkgbase}/archive/v${_pkgver}.tar.gz"
        "pr2367.patch"
        "fix-transparency-${_pkgver}.patch")
sha256sums=('e2bc5323d505d9d487b2fdfc29f82a77e18b17f92de3988742950471808272f7'
            '56d01cf49d68110af0218183e8867554d47953507ae1b570a576a83b452e367d'
            'c1d6de5d791b905c6b71f1898dac321db47fe145bbbbf06676823938e20ae399')

prepare() {
  cd $pkgbase-$_pkgver
  # https://github.com/jwilm/alacritty/issues/2254
  patch -Np1 -i ../fix-transparency-${_pkgver}.patch
  # https://github.com/jwilm/alacritty/pull/2367
  patch -Np1 -i ../pr2367.patch
}
build(){
  cd $pkgbase-$_pkgver
  env CARGO_INCREMENTAL=0 cargo build --release --locked
}

check(){
  cd $pkgbase-$_pkgver
  env CARGO_INCREMENTAL=0 cargo test --release
}

package_alacritty() {
  depends=('freetype2' 'fontconfig' 'xclip' 'libxi' 'libxcursor')
  optdepends=('alacritty-terminfo: terminfo for alacritty')
  
  cd $pkgbase-$_pkgver
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "extra/linux/alacritty.desktop"
  
  install -D -m755 "target/release/alacritty" "$pkgdir/usr/bin/alacritty"
  install -D -m644 "extra/alacritty.man" "$pkgdir/usr/share/man/man1/alacritty.1"
  install -D -m644 "extra/completions/alacritty.bash" "$pkgdir/usr/share/bash-completion/completions/alacritty"
  install -D -m644 "extra/completions/_alacritty" "$pkgdir/usr/share/zsh/site-functions/_alacritty"
  install -D -m644 "extra/completions/alacritty.fish" "$pkgdir/usr/share/fish/completions/alacritty.fish"
  install -D -m644 "extra/logo/alacritty-term.svg" "$pkgdir/usr/share/pixmaps/Alacritty.svg"
}

package_alacritty-terminfo() {
  pkgdesc="Terminfo files for the alacritty terminal emulator"
  depends=('ncurses')
  
  cd $pkgbase-$_pkgver
  
  install -dm 755 "$pkgdir/usr/share/terminfo/a/"
  tic -o "$pkgdir/usr/share/terminfo" -xe alacritty,alacritty-direct extra/alacritty.info
}
