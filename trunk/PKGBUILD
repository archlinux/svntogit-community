# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.8.0
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=(any)
url="https://github.com/python/importlib_resources"
license=(Apache)
depends=(python-zipp)
makedepends=(python-build python-installer python-setuptools-scm python-toml python-wheel)
checkdepends=(python-pytest python-tests)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('5c20a46a1832c7aaddfaa561a3986ea02dae59b94c06861757b6e5912f36624d564c65e184612af6e5930927a3172dc132ceb9846b0585de851d019c68cc8955')
b2sums=('ce6668520297cd7e06b944146277065cd1a0f5ae3f9e3bce24005f0f4a1181a41fa7f91f6b9ee9c0ef4caf7ca65c8efff748957c5c1a45c56778b30568970b36')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
