# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Michael Migliore <mcmigliore+aur@gmail.com>
# Contributor: Mathieu Wespthal <mathieu.westphal+aur@gmail.com>
pkgname=f3d
pkgver=2.0.0
pkgrel=1
pkgdesc='A fast and minimalist 3D viewer'
arch=('x86_64')
url="https://github.com/${pkgname}-app/${pkgname}"
license=('BSD')
depends=('vtk' 'glew' 'pugixml' 'netcdf' 'ospray' 'assimp' 'opencascade' 'alembic' 'python' 'fmt' 'nlohmann-json')
makedepends=('cmake' 'help2man' 'openmp' 'pybind11' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname-app/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('5b335de78a9f68903d7023d947090d4b36fa15b9e165749906a82153e0f56d05')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DF3D_INSTALL_DEFAULT_CONFIGURATION_FILE=ON \
    -DF3D_GENERATE_MAN=ON \
    -DF3D_INSTALL_MIME_TYPES_FILE=ON \
    -DF3D_INSTALL_THUMBNAILER_FILES=ON \
    -DF3D_PYTHON_BINDINGS=ON \
    -DF3D_INSTALL_SDK=ON \
    -DF3D_MODULE_OCCT=ON \
    -DF3D_MODULE_ASSIMP=ON \
    -DF3D_MODULE_ALEMBIC=ON \
    -DF3D_MODULE_RAYTRACING=ON \
    -DF3D_MODULE_EXTERNAL_RENDERING=ON
  ninja -C build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
