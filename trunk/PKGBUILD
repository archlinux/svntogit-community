# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: kevku <kevku@gmx.com>

pkgname=kodi-addon-inputstream-rtmp
pkgver=20.3.0
_codename=Nexus
pkgrel=2
pkgdesc="RTMP input stream add-on for Kodi"
arch=('x86_64')
url="https://github.com/xbmc/inputstream.rtmp"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-inputstream')
makedepends=('cmake' 'rtmpdump' 'p8-platform' 'kodi-dev')
options=(!lto debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/inputstream.rtmp/archive/$pkgver-$_codename.tar.gz")
sha512sums=('11c90ac7a14a71b7b2425ef15e8fc52f6fca08a5b2831ac3a5f0ae506c46ac4728e1ad185025364c0ef83ed3b7c3f00570f38616057d92d4e751290f53ad578e')

build() {
    cd "inputstream.rtmp-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'rtmpdump' 'p8-platform')
    cd "inputstream.rtmp-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
