# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-os-service-types
pkgname=(python-os-service-types python2-os-service-types)
pkgver=1.7.0
pkgrel=4
pkgdesc="Python library for consuming OpenStack sevice-types-authority data"
arch=('any')
url="https://pypi.python.org/pypi/os-service-types/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr')
checkdepends=('python-oslotest' 'python-requests-mock')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/os-service-types/archive/$pkgver.tar.gz")
sha512sums=('df5848d648a9181dabd33fa734b556ea3799e8b06d0d4ba9f440c66bf7e942b5d65a5ffbd00fadfc61705a702fce3aa2b5d601a5ebde23a1da7259a712c4c0d5')

export PBR_VERSION=$pkgver

prepare() {
  echo "global-include *.json" >> os-service-types-$pkgver/MANIFEST.in

  cp -a os-service-types-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/os-service-types-$pkgver
  python setup.py build

  cd "$srcdir"/os-service-types-$pkgver-py2
  python2 setup.py build
}

check() {
  cd os-service-types-$pkgver
  stestr run
}

package_python-os-service-types() {
  depends=('python-pbr')

  cd "$srcdir"/os-service-types-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-os-service-types() {
  depends=('python2-pbr')

  cd "$srcdir"/os-service-types-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
