# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgbase=python-gmpy2
pkgname=(python2-gmpy2 python-gmpy2)
pkgver=2.1.0b1
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(x86_64)
url="https://github.com/aleaxit/gmpy"
license=(LGPL2.1)
makedepends=(python2-setuptools python-setuptools libmpc git)
source=("$pkgname-$pkgver.tar.gz::https://github.com/aleaxit/gmpy/archive/gmpy2-$pkgver.tar.gz")
sha256sums=('888d3c34a91c2ac068ac5033431c765e04f125e85920bd69d8739fbb62f4279d')

prepare() {
  cp -r gmpy-gmpy2-$pkgver{,-py2}  
}

build() {
  cd gmpy-gmpy2-$pkgver
  python setup.py build

  cd ../gmpy-gmpy2-$pkgver-py2
  python2 setup.py build
}

package_python2-gmpy2() {
  depends=(python2 libmpc)

  cd gmpy-gmpy2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

package_python-gmpy2() {
  depends=(python libmpc)
  
  cd gmpy-gmpy2-$pkgver 
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

