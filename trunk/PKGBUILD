# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=sqlalchemy-migrate
pkgname=python-sqlalchemy-migrate
pkgver=0.13.0
pkgrel=10
pkgdesc="Database schema migration for SQLAlchemy"
arch=('any')
url="https://pypi.python.org/pypi/sqlalchemy-migrate/$pkgver"
license=('Apache')
depends=('python-decorator' 'python-pbr' 'python-six' 'python-sqlalchemy'
'python-sqlparse' 'python-tempita')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-mock' 'python-testrepository' 'python-scripttest' 'python-ibm-db-sa')
optdepends=('python-ibm-db-sa: for IBM data server support')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://opendev.org/x/sqlalchemy-migrate/archive/$pkgver.tar.gz")
sha512sums=('3ae1140cb17ea24db7423a8cb62d667620333c8ed559f6bfd6eecfb35f829c9f9c4918f41f8f723d89bcb3c51d6b803e5cbdc30cb485c8cdfc140caaf1d981f0')

export PBR_VERSION=$pkgver

prepare() {
  cd $_name
  # Perhaps related to new testtools
  sed -i -e '/self.setUp/d' -e '/self.tearDown/d' migrate/tests/versioning/test_schema.py
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  export PATH="$PWD/test_dir/usr/bin:$PATH"
  python -m unittest discover -vs migrate/tests/
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  # remove tests
  rm -frv "$pkgdir/$site_packages/migrate/tests/"
}

# vim:set ts=2 sw=2 et:
