# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=1.2
pkgrel=1
pkgdesc="Qt5 Configuration Utility"
arch=(x86_64)
url="https://qt5ct.sourceforge.io/"
license=(BSD)
depends=(qt5-svg)
makedepends=(cmake qt5-tools)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2
        qt5ct-datadir.patch)
sha256sums=('1de3f06e6d5d0746bf19a949e56cc04b53bdbc18294f38cb20759f35006b872e'
            '58422b0fff7de20ba45744d0a7610da5685142daf5db12414d3fed8fd91b152f')

prepare() {
  patch -d $pkgname-$pkgver -p1 < qt5ct-datadir.patch # Fix data install dir
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

