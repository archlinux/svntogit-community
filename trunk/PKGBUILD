# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Julien Nicoulaud <julien.nicoulaud at gmail dot com>

pkgname=zsh-syntax-highlighting
pkgver=0.6.0
pkgrel=1
pkgdesc="Fish shell like syntax highlighting for Zsh"
arch=('any')
url="https://github.com/zsh-users/zsh-syntax-highlighting"
license=('BSD')
depends=('zsh>=4.3.9')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/zsh-users/${pkgname}/tarball/${pkgver}")
sha512sums=('a375e6f22b0b4158eafbb245c21d6265c2b0298e82a524ccd2468fe6bcb5d2b71927b11329fc60ec8d479db309d8d0e05f108e089b0a996bef8e5d3f624791e6')
changelog=ChangeLog

build() {
  cd "zsh-users-zsh-syntax-highlighting-"*
  make
}

package() {
  cd "zsh-users-zsh-syntax-highlighting-"*
  make PREFIX="/usr" SHARE_DIR="${pkgdir}/usr/share/zsh/plugins/${pkgname}" DESTDIR="${pkgdir}" install
  # create symlink for using with oh-my-zsh
  ln -s "zsh-syntax-highlighting.zsh" \
       "${pkgdir}/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh"

  # licence
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/${pkgname}/COPYING.md" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
