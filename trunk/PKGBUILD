# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.2
pkgrel=1
pkgdesc="A tool for monitoring webpages for updates"
arch=('any')
url="http://thp.io/2008/urlwatch/"
license=('BSD')
depends=('python-keyring' 'python-minidb' 'python-requests' 'python-yaml')
source=("http://thp.io/2008/urlwatch/$pkgname-$pkgver.tar.gz"
        "fix-install.patch")
md5sums=('9a3af570ad0c696987eb0b4dc39a930d'
         '6878aaa0c73e039fb48a2eb50b427617')

prepare() {
  cd $pkgname-$pkgver
  # Fix installation
  patch -RNp1 -i ../fix-install.patch
}

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
