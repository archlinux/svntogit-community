# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Firegem <mrfiregem [at] protonmail [dot] ch>

pkgname=taplo-cli
pkgver=0.6.10
pkgrel=1
pkgdesc='TOML toolkit written in Rust'
arch=('x86_64')
url=https://taplo.tamasfe.dev/cli/introduction.html
license=('MIT')
depends=('gcc-libs' 'openssl')
makedepends=('cargo')
provides=("${pkgname%-cli}")
source=("$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
        "$pkgname-$pkgver-LICENSE.md::https://raw.githubusercontent.com/tamasfe/taplo/release-$pkgname-$pkgver/LICENSE.md")
b2sums=('c8840c70eea44d40835add65825082fbea3813dc6be5edcffc77c8b40f5b3a5c1468ac0324e19cf85c138bff798bcf23a7110b4f3aa031fd7e5342a8417b5584'
        '2b1282c2e1b54a67fd56894ae96a1447d954b52fecdf0730d8c3fe7a95ccf79245e738588a9bda7262eb0ef2507cdeded228346358894d4bbfc540ec90069964')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked --offline --all-features
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/${pkgname%-cli}
  install -Dm644  ../$pkgname-$pkgver-LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
