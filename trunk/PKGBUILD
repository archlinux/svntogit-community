# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.10.2
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL2)
groups=(pd-externals pro-audio)
depends=(gcc-libs glibc lua pd)
source=($pkgname-$pkgver.tar.gz::https://github.com/agraef/$pkgname/archive/$pkgver.tar.gz)
sha512sums=('0f61159bbb67c48ce379e4c1f7f1223004cc43065417e08a26d9ae40dd745ad01cc6c01921703a7118873055abc782eff0479713e187a61bd8edf2a98df72a30')
b2sums=('605e01a7d76a9d01819c6133cb9a2976729ed233e02da71d48b63708b6c4bc3bc5fb2aa67ec1732ff00d7240033d78ef4505bf15e0e7e8f8a50568d374927ab4')

prepare() {
  # not installing license and README (to wrong location)
  sed -e '/COPYING /d;/README /d' -i $pkgname-$pkgver/Makefile
}

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" prefix=/usr install
  # docs
  install -vDm 644 README -t "$pkgdir/usr/share/doc/$pkgname/"
  mv -v "$pkgdir/usr/lib/pd/extra/pdlua/doc/"* "$pkgdir/usr/share/doc/$pkgname/"
  rmdir -v "$pkgdir/usr/lib/pd/extra/pdlua/doc/"
}
