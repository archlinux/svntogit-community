# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Ivy Foster <iff@archlinux.org>
# Contributor: Abdelhakim Qbaich <abdelhakim@qbaich.com>
# Contributor: korjjj <korjjj+aur@gmail.com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Adam Ehlers Nyholm Thomsen <adament@gmail.com>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=3.2.1
pkgrel=12
pkgdesc='Double-entry accounting system with a command-line reporting interface'
arch=(i686 x86_64)
url='https://ledger-cli.org'
license=(BSD)
depends=(boost-libs libedit mpfr)
makedepends=(boost cmake python)
optdepends=(
	'emacs: emacs interface'
	'python: python library'
)
source=("ledger-$pkgver.tar.gz::https://github.com/ledger/ledger/archive/v$pkgver.tar.gz"
        $pkgname-boost-1.77-part1.patch::https://github.com/ledger/ledger/commit/2a0543398b5c.patch
        $pkgname-boost-1.77-part2.patch::https://github.com/ledger/ledger/commit/ba8ec3243531.patch)
sha256sums=('92bf09bc385b171987f456fe3ee9fa998ed5e40b97b3acdd562b663aa364384a'
            'fde41098cedff9d12814db899cc9aa814d88ce6492a25fdfd581694112e310c6'
            '22fc19f574499694a7720c7d3366af1669e0eb914a21e74d20364a0055dcfce8')

prepare() {
	cd "ledger-$pkgver"

	# https://github.com/ledger/ledger/issues/2075
	patch -Np1 -i ../$pkgname-boost-1.77-part1.patch
	patch -Np1 -i ../$pkgname-boost-1.77-part2.patch
}

build() {
	cd "ledger-$pkgver"

	cmake . \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DCMAKE_INSTALL_LIBDIR:PATH=lib \
		-DUSE_PYTHON:BOOL=TRUE
	make
}

check() {
	make -C "ledger-$pkgver" check
}

package() {
	cd "ledger-$pkgver"

	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/ledger/LICENSE.md"
}
