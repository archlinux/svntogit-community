# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.11
pkgrel=5
pkgdesc='Basic, good-looking task manager for WMs'
arch=('i686' 'x86_64')
url="http://code.google.com/p/tint2/"
license=('GPL2')
conflicts=('ttm-svn' 'tint')
replaces=('tint')
depends=('gtk2' 'imlib2')
optdepends=('pygtk: for using tint2conf')
makedepends=('pygtk' 'cmake')
provides=('tint')
source=("http://$pkgname.googlecode.com/files/$pkgname-${pkgver}.tar.bz2"
        'add-power-now-support.patch')
md5sums=('6fc5731e7425125fa84a2add5cef4bff'
         '6cfcad028f1bd2f69812167f5395f890')

build() {
  cd "$srcdir/$pkgname-${pkgver}"

  # add support for power_now battery attribute (fixes FS#21954)
  patch -Np0 -i ../add-power-now-support.patch

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' src/tint2conf/tintwizard.py
  sed -i 's_python _python2 _' src/tint2conf/main.c

  [ -d build ] || mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  make 
}

package() {
  cd "$srcdir/$pkgname-${pkgver}/build"
  make DESTDIR="$pkgdir" install
}
