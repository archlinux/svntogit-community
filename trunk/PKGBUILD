# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# Contributor: devmotion <nospam-archlinux.org@devmotion.de>
# Contributor: Valentin Churavy <v.churavy@gmail.com>

pkgname=julia
epoch=2
pkgver=1.8.3
pkgrel=3
arch=(x86_64)
pkgdesc='High-level, high-performance, dynamic programming language'
url='https://julialang.org/'
license=(MIT)
depends=(cblas fftw hicolor-icon-theme libgit2 libunwind libutf8proc openblas
         suitesparse mbedtls openlibm pcre2 llvm-libs p7zip libblastrampoline)
makedepends=(cmake gcc-fortran python llvm patchelf libwhich)
optdepends=('gnuplot: If using the Gaston Package from julia')
replaces=(julia-docs)
source=(https://github.com/JuliaLang/julia/releases/download/v$pkgver/$pkgname-$pkgver-full.tar.gz{,.asc}
        ed9851b0.patch
        074d761f.patch
        julia-llvm-14.patch
        julia-libunwind-1.6.patch
        julia-system-cblas.patch
        julia-hardcoded-libs.patch
        make-install-no-build.patch
        julia-suitesparse-version-mismatch.patch)
backup=(etc/julia/startup.jl)
sha256sums=('52b6895a9d4ad2fe36db261ee8c4c8cc9212b837a12f93002faaf537a2151f50'
            'SKIP'
            'ce0de27461197ade425cacdd3a642a6b2579e27c7ecc4a457e2b1bde2d76492a'
            '5d4093de84f836e16ec7f83736494e82804ab2fa37bc5594a5b99b1708dce322'
            '0d9e21126820a20083e0251c61129134166cc1c7b6637fbda1d3e6ee7614cce0'
            'cea321f7e16381dd795ee20d7bd5eda64f9b453c01d13e960962eb2723907e24'
            '65f24275edb8357ded3c53fd8a10c3d1ed13fa3d34a3869df0c04da10dba6c9d'
            '929a52a5503b6d1a21774c170ece0981cb7da1c1e7a5b7a2892cfb1251689b55'
            'f2dc0b7164868dc56d3f8744c4a1596e964c66265023751c95e1976bb15bd5f8'
            '52c481d691940bbc4714d54cfde20fb1973350ba4a4c2386941a32fc007fdd85')
validpgpkeys=('3673DF529D9049477F76B37566E3C7DC03D6E495') # Julia (Binary signing key) <buildbot@julialang.org>
options=(!lto)

prepare() {
  cd $pkgname-$pkgver

# Fixes for LLVM 14
  patch -p1 -i ../ed9851b0.patch
  patch -p1 -i ../074d761f.patch
  patch -p1 -i ../julia-llvm-14.patch
# libunwind 1.6 compatibility
  patch -p1 -i ../julia-libunwind-1.6.patch
# Add and use option to build with system cblas
  patch -p1 -i ../julia-system-cblas.patch
# Don't hardcode library names
  patch -p1 -i ../julia-hardcoded-libs.patch
# Don't build again in install
  patch -p1 -i ../make-install-no-build.patch
# Fix test failures due to using system blas
  sed -e 's|0.22314355f0 + 3.1415927f0im|0.22314355f0 - 3.1415927f0im|' -i stdlib/LinearAlgebra/test/lu.jl
  sed -e 's|\$Int|Int32|' -i stdlib/LinearAlgebra/test/lu.jl
# Fix warnings with suitesparse 6
  cd stdlib/srccache
  tar -xzf SuiteSparse-f63732c1c6adecb277d8f2981cc8c1883c321bcc.tar.gz
  patch -d JuliaSparse-SuiteSparse.jl-f63732c -p1 < "$srcdir"/julia-suitesparse-version-mismatch.patch
  rm SuiteSparse-f63732c1c6adecb277d8f2981cc8c1883c321bcc.tar.gz
  tar -czf SuiteSparse-f63732c1c6adecb277d8f2981cc8c1883c321bcc.tar.gz JuliaSparse-SuiteSparse.jl-f63732c
  _suitesparse_md5=$(md5sum SuiteSparse-f63732c1c6adecb277d8f2981cc8c1883c321bcc.tar.gz | cut -d ' ' -f 1)
  _suitesparse_sha512=$(sha512sum SuiteSparse-f63732c1c6adecb277d8f2981cc8c1883c321bcc.tar.gz | cut -d ' ' -f 1)
  sed -e "s|baeb73b8ac38dd04174ed04fa1ea8cef|${_suitesparse_md5}|" \
      -e "s|a95e6ebafe948f419a65a9630b01cda380f3ce19499afe57e212a75dd43aa7a09ddd038e90d1215ae55566a676e392e696565d2d7a96853ec4fca7f73762b268|${_suitesparse_sha512}|" \
      -i ../../deps/checksums/suitesparse
}

_buildopts="prefix=/usr \
    bindir=/usr/bin \
    sysconfdir=/etc \
    libexecdir=/usr/lib \
    USE_BINARYBUILDER=0 \
    USE_SYSTEM_CSL=1 \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_LIBBLASTRAMPOLINE=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_LIBSUITESPARSE=1 \
    USE_SYSTEM_LIBWHICH=1 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=1 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_LIBSSH2=1 \
    USE_SYSTEM_MBEDTLS=1 \
    USE_SYSTEM_CURL=1 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_P7ZIP=1 \
    USE_SYSTEM_OPENLIBM=1 \
    MARCH=x86-64"

build() {
  cd $pkgname-$pkgver
  make release VERBOSE=1 JLDFLAGS=${LDFLAGS} $_buildopts
}

check() {
  cd $pkgname-$pkgver/test
  ln -s /etc/ssl/cert.pem ../usr/share/julia

  # TODO: Remove SuiteSparse from skip list in 1.9 (it is downloaded at build time from a separate repo, not easily patchable)
  ../julia --check-bounds=yes --startup-file=no ./runtests.jl \
    --skip Sockets \
    --skip nghttp2_jll \
    --skip GMP_jll \
    --skip LibCURL \
    --skip LibSSH2_jll \
    --skip MbedTLS_jll \
    --skip MPFR_jll \
    --skip SuiteSparse \
    --skip SuiteSparse_jll \
    --skip PCRE2_jll \
    --skip LibGit2_jll \
    --skip Zlib_jll
  find ../stdlib \( -name \*.cov -o -name \*.mem \) -delete
  rm -fr ../stdlib/Artifacts/test/artifacts
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install $_buildopts

  ln -s /usr/lib/libopenblas.so "$pkgdir"/usr/lib/julia/libopenblas64_.so # Needed by some 3rd party packages
  ln -sf /etc/ssl/cert.pem "$pkgdir"/usr/share/julia # Needed by some packages

  rm "$pkgdir"/usr/lib/julia/libccalltest.so.debug # Remove debug testing library
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
