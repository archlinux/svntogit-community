# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=44.0
pkgrel=1
pkgdesc='A trivial-to-use personal backup tool to protect against accidental data loss'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/DejaDup'
license=('GPL')
depends=(
  'duplicity'
  'gvfs'
  'libsecret'
  'org.freedesktop.secrets'
  'python-gobject'
  'python-pydrive2'
  'libadwaita'
)
makedepends=(
  'appstream-glib'
  'itstool'
  'meson'
  'vala'
)
source=("https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4c9b760d30a7d34f27f84be0efb5b8b13d3687bc8339bc030ea0cc3d96be8fc4daa9ae55be7f8aa4376ed5a3641527cc54ea9b153606b5a8b6835ad6a3509195')
b2sums=('03e99f0ab2da3972fdd379f850477a93688f9a6de2f371fe40c658c0d417191382b0988b01d3a5ecee2ce06e02081c534fc1d030b8bf789b6c7c6f32a68c9ed4')
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry <mike@mterry.name>

prepare() {
  find "$pkgname-$pkgver" -name meson.build | xargs sed -e 's|C.UTF-8|C|' -i
}

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
