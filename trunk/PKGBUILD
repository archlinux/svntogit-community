# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maximilian Stein <theoddbird@posteo.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libsoup
pkgver=2.74.2
pkgrel=1
pkgdesc='HTTP client/server library for GNOME'
url=https://wiki.gnome.org/Projects/libsoup
arch=(x86_64)
license=(LGPL)
depends=(
  lib32-brotli
  lib32-glib-networking
  lib32-krb5
  lib32-libxml2
  lib32-sqlite
  lib32-libpsl
  libsoup
)
makedepends=(
  git
  meson
  samba
)
checkdepends=(
  apache
  php-apache
)
optdepends=('samba: Windows Domain SSO')
_commit=2add3502a7de754f4aed61571f6cb45094e8f443  # tags/2.74.2^0
source=(git+https://gitlab.gnome.org/GNOME/libsoup.git#tag=${_commit})
sha256sums=('SKIP')

pkgver() {
  cd libsoup
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  arch-meson libsoup build \
    --libdir=/usr/lib32 \
    -D introspection=disabled \
    -D sysprof=disabled \
    -D vapi=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libbrotlidec.so
    libgssapi_krb5.so
    libg{lib,object,io}-2.0.so
    libpsl.so
  )
  provides+=(libsoup{,-gnome}-2.4.so)

  meson install -C build --destdir "${pkgdir}"
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim:set sw=2 et:
