# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Streetwalrus <streetwalrus@codewalr.us>

pkgname=shotgun
pkgver=2.2.1
pkgrel=1
pkgdesc='Minimal X screenshot utility'
arch=('x86_64')
url=https://github.com/neXromancers/shotgun
license=('MPL2')
depends=('gcc-libs' 'libxrandr')
makedepends=('git' 'rust')
optdepends=('hacksaw: for area/window selection'
            'slop: for area/window selection')
source=("shotgun-$pkgver.tar.gz::https://static.crates.io/crates/shotgun/shotgun-$pkgver.crate")
sha512sums=('2d51e401718c4a13b114a9e563a1dff13a1817c06e1b6ce046cb0ca324d5386a12b06406a9b6545532b43f2727189e1f7c4c7cd9ca650794a6f769076c1669d0')

prepare() {
  cd shotgun-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd shotgun-$pkgver
  cargo build --release --locked --offline
}

package() {
  cd shotgun-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/shotgun
}

# vim:set ts=2 sw=2 et:
