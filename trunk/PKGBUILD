# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgbase=python-pyicu
pkgname=('python-pyicu' 'python2-pyicu')
pkgver=2.0.2
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'icu')
checkdepends=('python-fonttools' 'python2-fonttools' 'python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('b3715a0f88227a41b56988ee98e795fe398867c2788b2929e0d4f83bfe9519ff01648eb23cfe8d1d9073930a4d7220c0ad5ebcf2652d8fa3ec43e4c4a4bcf091')

prepare() {
  cp -a PyICU-$pkgver{,-py2}
}

build() {
  cd "$srcdir/"PyICU-$pkgver
  python setup.py build

  cd "$srcdir/"PyICU-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir/"PyICU-$pkgver
  python setup.py test

  cd "$srcdir/"PyICU-$pkgver-py2
  python2 setup.py test || true # https://github.com/ovalhub/pyicu/issues/61
}

package_python-pyicu() {
  depends=('python' 'icu')

  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyicu() {
  depends=('python2' 'icu')

  cd PyICU-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
