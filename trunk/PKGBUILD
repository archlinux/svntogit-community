# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Patrik Ilg <p.ilg@arcor.de>

pkgname=gnujump
pkgver=1.0.8
pkgrel=6
pkgdesc="A clone of the simple yet addictive game Xjump"
arch=('x86_64')
url="http://gnujump.es.gnu.org"
license=('GPL3')
depends=('mesa' 'sdl_image' 'sdl_mixer')
makedepends=('glu')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz
        https://sources.archlinux.org/other/community/$pkgname/$pkgname.png
        $pkgname.desktop)
sha256sums=('13c3fe5f430eb0c010560c7e438123a573ca61a55c6708aa750cfbf56bf25e17'
            '74db0c3e9921d7d1374e116ea045bd1c63b30b501dd656d84bce67767d0d7f2c'
            'd29d273a015fea06ea5b811e88aa572423e0c3418225a52cf04f2c01d6a2b3ed')

build() {
  cd $pkgname-$pkgver

  LDFLAGS="-lm"

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
