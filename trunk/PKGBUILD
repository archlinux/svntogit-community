# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: robertfoster
pkgname=rnnoise
pkgver=0.4.1
pkgrel=1
pkgdesc="A noise suppression library based on a recurrent neural network"
arch=(x86_64)
url="https://gitlab.xiph.org/xiph/rnnoise"
license=('BSD')
_commit=1cbdbcf1283499bbb2230a6b0f126eb9b236defd
depends=('glibc')
source=("${pkgname}-${_commit}.tar.gz::https://gitlab.xiph.org/xiph/rnnoise/-/archive/${_commit}/rnnoise-${_commit}.tar.gz")
sha512sums=('7d16f7d68a0a28ecdaaea098df5482d55adf7948292cab8ed78ae5cc6652ebdfc48e623f193ee356fcf527e54883e9d3938195f2fd52c20dff895fd2515617f3')

build() {
  cd "rnnoise-${_commit}"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "rnnoise-${_commit}"
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
