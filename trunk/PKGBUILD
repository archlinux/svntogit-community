# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=check-jsonschema
pkgver=0.23.0
pkgrel=1
pkgdesc="A JSON Schema CLI and pre-commit hook built on jsonschema"
arch=(any)
url="https://github.com/python-jsonschema/check-jsonschema"
license=(Apache)
depends=(
  python
  python-click
  python-jsonschema
  python-requests
  python-ruamel-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-xdist
  python-responses
)
source=(
  # no tests in sdist tarballs
  # https://github.com/python-jsonschema/check-jsonschema/issues/258
  # https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz
  $url/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('4ebb3f421396631430b546305a0c589cf553ee06925a8ead865111398f89b7396a0a03fedd535af04237e5de0aa54e9bbd5b384fdba830f1304d47c2474a0d85')
b2sums=('7e20a740dc163cc57175b27fc5bbef7d79954da7794a241ed0723f609e5890fe7cd3a501ecc3e6bfa33a446fc26577bdee3ba87e20b91795e45e2109235da08e')

check() {
  # we don't support pinning
  sed -e 's/==/>=/g' -i $pkgname-$pkgver/setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
