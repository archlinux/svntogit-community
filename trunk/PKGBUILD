# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=staticcheck
pkgver=2023.1
pkgrel=1
pkgdesc="The advanced Go linter"
arch=('x86_64')
url="https://staticcheck.io"
license=("MIT")
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dominikh/go-tools/archive/$pkgver.tar.gz")
sha512sums=('ebf8711fbd9208d88974e666304b4506de121b5beeca92a2be5731e85080c82e1dc24f7c7ef28a0ecba2fa7e059ff774f87dd9e7a15d3f85309503b7e84acd44')
b2sums=('f89aa9a2790db737da1b1a24dc0ba5bc82df4c202e562bb45844484b6c8e67c9d3056f3005119a59befffb2806be1f888552e4a9b556df1a6a9e7548a9d2aa6f')

prepare(){
	cd "go-tools-$pkgver"
	mkdir -p bin
}

build(){
	cd "go-tools-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags ${LDFLAGS}" \
    -o bin\
    ./cmd/staticcheck
}

check(){
	cd "go-tools-$pkgver"
	GOROOT="/usr/lib/go" go test -v ./...
}

package(){
	cd "go-tools-$pkgver"
	install -p -Dm755 bin/staticcheck "${pkgdir}/usr/bin/staticcheck"
	install -Dm644 "LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
	install -Dm644 "LICENSE-THIRD-PARTY"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE-THIRD-PARTY"
}
