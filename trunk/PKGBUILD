# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=linuxconsole
pkgver=1.5.1
pkgrel=1
pkgdesc="Set of utilities for joysticks and serial devices"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/linuxconsole/"
license=('GPL')
makedepends=('sdl')
provides=('joystick' 'inputattach')
replaces=('joystick' 'inputattach')
optdepends=('sdl: for ffmvforce utility')
source=(http://prdownloads.sourceforge.net/linuxconsole/linuxconsoletools-$pkgver.tar.bz2
	'inputattach.conf.d'
	'inputattach.service'
	'inputattachctl')
md5sums=('86b5c4e8fb6c91c5b7e9298f7dfe5b3e'
         '7ca903e54829764c8241233af5069216'
         'c19b9ed38b243191cf5378b87a42a4cc'
         '7dd0690a9d2fe5e1052900650906e273')

prepare() {
  cd "${srcdir}"/linuxconsoletools-$pkgver
  sed -i 's|lib/udev|usr/lib/udev|g' utils/Makefile
}

build() {
  cd "${srcdir}"/linuxconsoletools-$pkgver
  make PREFIX=/usr
}

package() {
  cd "${srcdir}"/linuxconsoletools-$pkgver
  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm755 $srcdir/inputattachctl $pkgdir/usr/bin/inputattachctl
  install -Dm644 $srcdir/inputattach.conf.d "$pkgdir/etc/conf.d/inputattach"
  install -Dm644 $srcdir/inputattach.service $pkgdir/usr/lib/systemd/system/inputattach.service
}
