# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: robertfoster

pkgname=libpam-google-authenticator
pkgver=1.05
pkgrel=1
pkgdesc='PAM module for google authenticator app'
arch=('x86_64')
url="https://github.com/google/google-authenticator-libpam"
license=('Apache')
depends=('pam')
optdepends=('qrencode: scannable QR codes for google auth phone app')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/google-authenticator-libpam/archive/$pkgver.tar.gz")
sha512sums=('d97b26c6181dbce0612628484db37b1bf61e984fb2fb3d4974d04038e564404aa17415368dba524f0d17d96ec8d57ae4129b27f0c672d849d16ef03941d87996')

build() {
  cd google-authenticator-libpam-$pkgver
  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd google-authenticator-libpam-$pkgver
  make DESTDIR="$pkgdir" install
  libtool --finish "$pkgdir"/usr/lib/security
}
