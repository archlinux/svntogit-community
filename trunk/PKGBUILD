# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rofimoji
pkgver=4.3.0
pkgrel=2
pkgdesc='Character picker for rofi'
arch=('any')
url=https://github.com/fdw/rofimoji
license=('MIT')
depends=('emoji-font' 'python-configargparse' 'python-setuptools' 'python-xdg'
         'rofi' 'xclip' 'xdotool')
makedepends=('python-pip' 'python-wheel')
source=("$url/archive/$pkgver/rofimoji-$pkgver.tar.gz")
sha512sums=('d43d2e8c609baa1e9b875ccf9446d0c4f4262e1932de4a2f8a0f80719b6d7e73317725d86e934f044a2d0ba8369b6ae938b5525a925b0dde658c76dfd94a512b')

build() {
  cd rofimoji-$pkgver
  python setup.py bdist_wheel
}

package() {
  cd rofimoji-$pkgver
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl
}

# vim:set ts=2 sw=2 et:
