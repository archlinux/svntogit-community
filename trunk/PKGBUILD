# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=woob
pkgver=3.5
pkgrel=1
pkgdesc="Core library and modules for Web outside of Browsers"
arch=(any)
url="https://woob.tech/"
license=(LGPL3)
depends=(
    python-lxml
    python-cssselect
    python-requests
    python-dateutil
    python-yaml
    python-html2text
    python-six
    python-unidecode
    python-pillow
    python-babel
    python-simplejson
    python-pycountry
)
optdepends=(
    'python-colorama: CLI usage'
    'python-prettytable: CLI output formatting'
    'python-feedparser: required by some modules'
    'python-jose: required by some modules'
    'python-pycryptodome: required by some modules'
    'python-google-api-python-client: multiple Google services like YouTube'
)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-prettytable)
replaces=(weboob)
provides=(weboob)
conflicts=(weboob)
#source=(https://files.pythonhosted.org/packages/source/w/${pkgname}/${pkgname}-${pkgver}.tar.gz)
source=(https://gitlab.com/woob/woob/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a3927c5421a5f7af6fc21c79f8e0c7456685125eae88aede0aecb9773176d0b4')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  pytest -vv --color=yes
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
