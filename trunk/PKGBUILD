# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=5.1.0
_commit=03cce883da2666d3f5f7f0792aeb124dfc6d0bcf
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-pbr' 'python-msgpack' 'python-oslo-utils' 'python-pytz')
makedepends=('git' 'python-setuptools')
checkdepends=('python-oslotest')
source=("git+https://github.com/openstack/oslo.serialization.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd oslo.serialization
  python setup.py build
}

check() {
  cd oslo.serialization
  stestr run
}

package() {
  cd oslo.serialization
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
