# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=camlp4
_ocaml_ver=4.13.1
pkgver=${_ocaml_ver%.*}+1
pkgrel=1
pkgdesc='Caml preprocessor and pretty-printer'
arch=('x86_64')
license=('GPL2')
url='https://github.com/ocaml/camlp4/releases'
depends=("ocaml>=${_ocaml_ver}")
makedepends=('git' 'ocamlbuild' 'ocaml-compiler-libs')
options=('!makeflags' '!emptydirs' 'staticlibs')
source=("https://github.com/ocaml/camlp4/archive/$pkgver.tar.gz")
sha256sums=('1854eee6007af5ea746e82535bde052d465d9fefda29a2080e84e2bcb2c89ed9')

build() {
  cd "${srcdir}/$pkgname-${pkgver/+/-}"
  ./configure
  make all camlp4/META
}

package() {
  cd "${srcdir}/$pkgname-${pkgver/+/-}"
  make \
    BINDIR="$pkgdir/usr/bin" \
    LIBDIR="$pkgdir/usr/lib/ocaml" \
    PKGDIR="$pkgdir/usr/lib/ocaml" \
    install install-META
}

# vim:set ts=2 sw=2 et:
