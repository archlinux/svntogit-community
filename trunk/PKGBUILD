# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=2.0.16
pkgrel=1
pkgdesc='KDE media player'
license=(GPL)
arch=(x86_64)
url="https://kaffeine.kde.org"
depends=(kio libxss vlc)
makedepends=(extra-cmake-modules kidletime kdoctools)
source=("https://download.kde.org/stable/$pkgname/$pkgname-${pkgver}-2.tar.xz"{,.sig})
sha256sums=('8659189d907c2512321dd2c0d0c47176229c344970d633e33f29ce82432deee5'
            'SKIP')
validpgpkeys=(F909AE68FC11DF09C1755C00085F3EBD8EE4E115) # Mauro Carvalho Chehab <mchehab@s-opensource.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-${pkgver}-2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
