# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: <gmail.com: Jesse Jaara>
# Contributor: Gabriel Laskar <gabriel@lse.epita.fr>

pkgbase=python-pandocfilters
pkgname=(python-pandocfilters python2-pandocfilters)
pkgver=1.4.2
pkgrel=2
pkgdesc="A python module for writing pandoc filters"
arch=('any')
url="https://pypi.python.org/pypi/pandocfilters"
license=('BSD')
depends=('python')
makedepends=('python2')
source=("https://pypi.io/packages/source/p/pandocfilters/pandocfilters-${pkgver}.tar.gz")
md5sums=('dc391791ef54c7de1572d7b46b63361f')

prepare() {
  cd "$srcdir"
  cp -r "pandocfilters-$pkgver" "py2-pandocfilters-$pkgver"
}

package_python-pandocfilters() {
  cd "$srcdir/pandocfilters-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pandocfilters() {
  depends=('python2')
  cd "$srcdir/py2-pandocfilters-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
