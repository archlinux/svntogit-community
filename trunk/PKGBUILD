# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: J. W. Birdsong <jwbirdsong AT gmail DOT com>

pkgname=luakit
pkgver=2012.09.13.r1
_pkgver=2012.09.13-r1
pkgrel=5
pkgdesc='Fast, small, webkit based browser framework extensible by Lua'
arch=('i686' 'x86_64')
url='http://mason-larobina.github.com/luakit/'
license=('GPL3')
depends=('webkitgtk2' 'lua51-filesystem' 'libunique' 'desktop-file-utils' 'luajit')
makedepends=('git' 'help2man')
options=(!makeflags)
install='luakit.install'
source=($pkgname-$pkgver.tar.gz::https://github.com/mason-larobina/luakit/tarball/$_pkgver)
md5sums=('77b02e6d19ba021409f1778fddd94a42')

build() {
  cd mason-larobina-luakit-*

  # use luajit to run script needed at build
  sed -i '1s,lua,luajit,' build-utils/gentokens.lua

  make USE_LUAJIT=1 PREFIX=/usr all
}

package() {
  cd mason-larobina-luakit-*
  make PREFIX=/usr DESTDIR=$pkgdir install 

  chmod -x $pkgdir/usr/share/pixmaps/luakit.png # FS#32938
}
