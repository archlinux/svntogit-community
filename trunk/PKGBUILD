# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-wayland-protocols
pkgver=1.6.0.deepin.1.2
_tag=1.6.0-deepin.1.2
pkgrel=1
pkgdesc='deepin Specific Protocols for Wayland'
url="https://github.com/linuxdeepin/deepin-wayland-protocols"
arch=(any)
license=(LGPL)
makedepends=(extra-cmake-modules ninja qt5-base)
source=("https://github.com/linuxdeepin/deepin-wayland-protocols/archive/$_tag/$pkgname-$_tag.tar.gz")
sha256sums=('5a870dedc0f67338a977c52e13b472887958d666d787ebaee261d9aff1def124')

build() {
  cmake -GNinja -B build -S $pkgname-$_tag
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
