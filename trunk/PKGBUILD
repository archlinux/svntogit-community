# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.6.3.2
pkgrel=4
pkgdesc='Deepin Image Viewer'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'libexif' 'libraw' 'qt5-svg' 'udisks2-qt5')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz"
        deepin-image-viewer-qt5.15.patch)
sha512sums=('1a819d6cebce1fecd40c8a701a7cf9346777771ae08e87709ce4caa9220101b6badc60e55a03a4b66b7cc1ddd0840481b3966882abf8db4cf55f8f245e28d9bb'
            'e802fa93972bf3d32c3672603e41bc662866aaf7e458b0cd7eeef786e22348c57161d3292754df5357beae6bb3b475bd45dfc6912c969200e4bf079c00b77763')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../deepin-image-viewer-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
