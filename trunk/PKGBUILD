# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=python-rtmidi
pkgver=1.4.9
pkgrel=3
pkgdesc='Python bindings for the MIDI I/O library RtMidi'
arch=('x86_64')
url='https://github.com/SpotlightKid/python-rtmidi'
license=('MIT')
depends=('python' 'rtmidi')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'cython'
  'alsa-lib'
  'jack'
)
_commit='0a5aa06d46485cb2dab836bd20205f60efb95eed'
source=(
  "$pkgname::git+$url#commit=$_commit"
  'unbundle-rtmidi.patch'
)
b2sums=('SKIP'
        'b27c438fa93c00fd458fd8083302838ad0ca96c09c89f0993c6fdc09681f8eccddca53445925c1b437860faa488115d2bc33076a202a5dd730f44cef298c9831')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # use system rtmidi
  patch -p1 -i "$srcdir/unbundle-rtmidi.patch"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  depends+=('libasound.so' 'libjack.so')
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
