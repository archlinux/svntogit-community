# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.43.1
pkgrel=2
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=(x86_64)
url="https://guitarix.org"
license=(GPL3)
groups=(ladspa-plugins lv2-plugins pro-audio)
depends=(atkmm bluez-libs cairo cairomm gcc-libs glibc glibmm gtkmm3 libsigc++
libx11 pangomm ttf-roboto)
makedepends=(avahi boost curl gdk-pixbuf2 eigen faust fftw glib2 gperf gtk3
intltool jack ladspa liblo liblrdf lilv lv2 pango sassc waf zita-convolver
zita-resampler)
optdepends=('new-session-manager: for session management')
provides=(guitarix2 libgxw.so libgxwmm.so ladspa-host lv2-host)
conflicts=(guitarix2)
replaces=(guitarix2)
source=("${pkgname}-${pkgver}.tar.xz::https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}2-${pkgver}.tar.xz")
sha512sums=('30e56d863308cea18bb4af6f71d3e8d852662b4252c998507a1c3de9a2d97fe1c4ecb525bd35ef8e176b5cb5138806553cba157c1aa6cb14e48fe0bbea8a7def')
b2sums=('bfa72a20f5fe1bdd08162e283f7074f6bcd6b4d6ccb1771f0351e60802ebf030f8f6627d2480ccd2e5972215847edd62bff38c338723fd0bf2504b9b2cee5101')

build() {
  cd "${pkgname}-${pkgver}"
  export LINKFLAGS="$LDFLAGS"
  waf configure --prefix=/usr \
                --enable-nls \
                --ladspa \
                --new-ladspa \
                --shared-lib \
                --lib-dev \
                --ldflags="${LDFLAGS}"
  waf build -vv
}

package() {
  depends+=(libavahi-common.so libavahi-gobject.so libboost_iostreams.so
  libcurl.so libfftw3f.so libgdk-3.so libgdk_pixbuf-2.0.so libgio-2.0.so
  libglib-2.0.so libgobject-2.0.so libjack.so liblilv-0.so liblo.so liblrdf.so
  libpangocairo-1.0.so libpango-1.0.so libsndfile.so libzita-convolver.so
  libzita-resampler.so)

  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
