# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-term-table
pkgver=0.012
pkgrel=2
pkgdesc="Format a header and rows into a table"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Term-Table"
depends=('perl-importer')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Term-Table-$pkgver.tar.gz")
sha512sums=('913541de3bdf694d44aca82ae7e87144d24f062824df345dba55865c1d535c3830de9fa3df27c6f566f5ac994c24a654bebf72b6fdbc173c7754a89ebec8ea6a')

build() {
  cd Term-Table-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Term-Table-$pkgver
  make test || warning "Expected failure due to Test2::Tools::Tiny being too old"
}

package() {
  cd Term-Table-$pkgver
  make DESTDIR="$pkgdir" install
}
