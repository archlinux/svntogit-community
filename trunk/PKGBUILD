# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=yesod-test
pkgname=haskell-yesod-test
pkgver=1.5.9.1
pkgrel=97
pkgdesc="Integration testing for WAI/Yesod Applications"
url="http://www.yesodweb.com"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-attoparsec' 'haskell-blaze-builder'
         'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-case-insensitive' 'haskell-cookie'
         'haskell-hspec-core' 'haskell-html-conduit' 'haskell-http-types' 'haskell-monad-control'
         'haskell-network' 'haskell-persistent' 'haskell-pretty-show' 'haskell-text' 'haskell-wai'
         'haskell-wai-extra' 'haskell-xml-conduit' 'haskell-xml-types' 'haskell-yesod-core')
makedepends=('ghc' 'haskell-yesod-form' 'haskell-lifted-base' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9b33af1df7cdc526e4be9339f2ff84f1b2f748d5516f1a851fcd537df2500961a3ac07c74db6311d499cb233ddb802e425efc16f10c1dedb16239fac8b767bed')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
