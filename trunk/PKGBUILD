# $Id$
# Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Daniel Micay <danielmicay@gmail.com>
_pkgname='gvm'
pkgname='openvas-manager'
pkgver=7.0.3
pkgrel=1
pkgdesc='A layer between the OpenVAS Scanner and various client applications'
arch=('x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('libxslt' 'openvas-libraries' 'rsync' 'sqlite3')
makedepends=('cmake' 'doxygen' 'xmltoman')
optdepends=('python2: SPLIT_PART_SIZE feature via xml_split script')
groups=('openvas')
install="${pkgname}.install"
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/greenbone/gvm/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/greenbone/gvm/releases/download/v${pkgver}/openvas-manager-${pkgver}.tar.gz.sig")
sha512sums=('c382b8791a66cc73c1f0da190a11a4addd22afee93abfe96718aba6c5e4488c3ccf33e17cb0aaf8dc376efe2b0264fef4195d5292d97387d2a24d515e3a4d325'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

prepare() {
  cd "${_pkgname}-${pkgver}"
  sed -i '1c#!/usr/bin/python2' tools/extra/xml_split
}

build() {
  cd "${_pkgname}-${pkgver}"
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
