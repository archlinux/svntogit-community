# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=gpg-tui
pkgver=0.6.2
pkgrel=1
pkgdesc="A terminal user interface for GnuPG"
arch=('x86_64')
url="https://github.com/orhun/gpg-tui"
license=('MIT')
depends=('libxcb' 'gpgme')
makedepends=('rust' 'python')
optdepends=('xplr: file selection support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('3c5ea4cb89ffa9bc600be62816472cabde1b121f32b212609fa8b878a873c384469151328b4130ce72f1167f968a0754f2f99cec87bc9ead9605b4cf74e3ca45')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir completions/
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked
  OUT_DIR=completions/ cargo run --release --bin completions
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 "completions/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
