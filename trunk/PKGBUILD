# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com?>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=gnuradio-osmosdr
_pkgname=gr-osmosdr
pkgver=0.1.4
pkgrel=27
pkgdesc="Source block for Funcube Dongle, RTL-SDR, USRP, OsmoSDR, BladeRF, HackRF and AirSpy devices"
url="http://sdr.osmocom.org/trac/"
arch=('x86_64')
license=('GPL')
depends=('gnuradio' 'gnuradio-iqbal' 'rtl-sdr' 'gnuradio-fcdproplus' 'hackrf' 'bladerf' 'airspy')
optdepends=('python2-opengl: osmocom_fft')
makedepends=('git' 'cmake' 'boost' 'python2-cheetah' 'swig')
conflicts=('gr-osmosdr-git')
replaces=('gr-osmosdr')
source=("git://git.osmocom.org/gr-osmosdr#tag=v$pkgver")
md5sums=('SKIP')

# add support for miri, osmo?

prepare() {
  cd "$srcdir/$_pkgname"
  sed -i 's/python$/python2/' apps/osmocom_siggen_base.py
}

build() {
  cd "$srcdir/$_pkgname"
  mkdir build
  cd build
  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$_pkgname/build/"
  make DESTDIR="${pkgdir}" install
}
