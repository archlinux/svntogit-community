# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=pingus
pkgver=0.7.6
pkgrel=22
pkgdesc="A Lemmings clone, i.e. a level-based puzzle game."
arch=('x86_64')
url="http://pingus.seul.org"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'libgl' 'boost-libs>=1.49')
makedepends=('scons' 'boost>=1.49' 'mesa' 'glu')
source=("$pkgname-$pkgver.tgz::https://github.com/Pingus/pingus/archive/v$pkgver.tar.gz"
        'pingus.desktop'
        'pingus-0.7.6-gcc470-udl.patch'
        'missing-header.patch')
sha1sums=('c888c277995bed84052b62d0eacbd77d0346a276'
          '579a1144f161ce89e6e024cea37210149b89c0c0'
          '143dd9969e8d100d29ed4f5b2039dde627afeb06'
          '9e1402d43ac5e2423c573b3b3afada3a20709dd5')

build() {
  cd ${pkgname}-${pkgver}

  # fedora patch to fix error due to new standards
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=51282
  patch -Np0 -i ${srcdir}/pingus-0.7.6-gcc470-udl.patch
  # manpage must be in man6, not man1
  sed -i "s#/man1/#/man6/#g" Makefile
  # missing header
  patch -Np1 -i ../missing-header.patch

  scons prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR="${pkgdir}" PREFIX="/usr"

  install -D -m644 "${srcdir}/pingus.desktop" "${pkgdir}/usr/share/applications/pingus.desktop"
}
