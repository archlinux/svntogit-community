# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.13
pkgrel=1
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=('x86_64')
depends=('python-lxml' 'xmlsec')
makedepends=('python-pkgconfig' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis' 'python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mehcode/python-xmlsec/archive/$pkgver.tar.gz")
sha512sums=('a79c8bddf4e7eef8fc1ccf7c9045b7486a05e9192481b2ce351a20c2511a1992fc91d28be8feb9ad3dd4372ba630521bdf79152001775e17813bad4af4c4e34c')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd python-xmlsec-$pkgver
  python setup.py build
}

check() {
  cd python-xmlsec-$pkgver
  # https://github.com/mehcode/python-xmlsec/issues/84
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310" pytest \
    --deselect 'tests/test_doc_examples.py::test_doc_example[encrypt.py]' \
    --deselect 'tests/test_doc_examples.py::test_doc_example[sign.py]' \
    --deselect 'tests/test_doc_examples.py::test_doc_example[verify.py]'
}

package() {
  cd python-xmlsec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
