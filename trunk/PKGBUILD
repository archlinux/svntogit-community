# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-persistent
pkgname=haskell-yesod-persistent
pkgver=1.6.0.2
pkgrel=56
pkgdesc="Some helpers for using Persistent from Yesod."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-blaze-builder" "haskell-conduit" "haskell-persistent"
         "haskell-persistent-template" "haskell-resource-pool" "haskell-resourcet"
         "haskell-yesod-core")
makedepends=('ghc' 'haskell-hspec' 'haskell-wai-extra' 'haskell-persistent-sqlite')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8014d1bcb5101bfad61911377e4122b07f41b4e98bd20c8f7a205d151caa73dbe4b1cc526f1f3d4bb2323920b290f080c77649e0f368ca017cd50389a7f7120c')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
