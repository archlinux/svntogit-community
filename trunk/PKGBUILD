# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-subtitles
pkgver=1.3
pkgrel=3
pkgdesc="Video subtitle editor for GNOME"
arch=('x86_64')
url="http://gnomesubtitles.org/"
license=('GPL')
depends=('gst-plugins-base' 'gconf-sharp' 'gtkspell')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('b0570134af6626d4e6c018c5f7774964')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make -j1
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
