# $Id$
# Maintainer: Jiachen Yang <farseerfc@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc
pkgver=4.1
_pkgver=$pkgver
pkgrel=2
pkgdesc='A presenter console with multi-monitor support for PDF files'
arch=('x86_64')
url='https://pdfpc.github.io/'
license=('GPL')
depends=('gtk3' 'poppler-glib' 'libgee' 'gstreamer' 'gst-plugins-base')
optdepends=('gst-plugins-good: more codecs for video playback support'
            'gst-libav: more codecs for video playback support')
makedepends=('cmake' 'vala')
source=("$pkgname-$_pkgver.tar.gz::https://github.com/pdfpc/pdfpc/archive/v$_pkgver.tar.gz"
"pr317.patch::https://github.com/pdfpc/pdfpc/commit/e120f098db8ab3b79f76d33640ac413053a61d71.patch")
#"pr277.patch::https://github.com/pdfpc/pdfpc/commit/202abb5d2f9095689fbdf295c796ea57bcd0e934.patch")

sha256sums=('aee684cc4b6cd8f2f83196ebd42608b9917104c9b1572985797daf1ae68e5454'
            '090f4dbeb1ca8fc3775c243af1173479fd31e40b25b40d63b136b34dc8a86712')
            #'376f33bdd919a15564ebec25e48e97aadd42cf386ade5b5362ddaddc773ac1fa')

prepare() {
    cd "$srcdir/$pkgname-$_pkgver"
    patch -p1 <../pr317.patch
}

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    cmake  -DCMAKE_INSTALL_PREFIX="/usr/" -DSYSCONFDIR="/etc" .
    make
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=4 sw=4 et:
