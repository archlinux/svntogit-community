# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=phonegap
pkgver=7.1.1
pkgrel=1
pkgdesc="PhoneGap command-line interface and node.js library"
arch=('x86_64')
url="https://github.com/phonegap/phonegap-cli"
license=('Apache')
depends=('npm' 'acorn' 'semver' 'cordova' 'browserify')
makedepends=('python2')
install=${pkgname}.install
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('0b91e5a28e22b1e5fc48f0a012aa8fed3ed3da810b7d3497a948125f06c14c1aacbe75389b93971dcefdd9b6055139cee2befd63425b3e45ed45fb037023c972')

package() {
  PYTHON=python2 npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver cordova npm browserify; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/syntax-error/node_modules
  for dep in acorn; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/cordova-lib/node_modules
  for dep in semver; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/npm-package-arg/node_modules
  for dep in semver; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/normalize-package-data/node_modules
  for dep in semver; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/init-package-json/node_modules
  for dep in semver; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/semver-diff/node_modules
  for dep in semver; do
    rm -r $dep;
  done
}

# vim:set ts=2 sw=2 et:
