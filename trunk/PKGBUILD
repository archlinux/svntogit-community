# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
pkgname=packer
replaces=('packer-io')
pkgver=1.4.4
pkgrel=2
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL')
makedepends=('git' 'go-pie' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('c4d2ca049b442fbbc65214ae75f5c85dc87b09eac8711891c4678ff3b2a676d00a051c08448a7a99b7a0977d4a04ad8bc559fd09885b520e6a8eb2dfc33ba50a')
changelog=CHANGELOG.md

prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p "src/github.com/hashicorp"
  mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
  sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}
build() {
  cd "src/github.com/hashicorp/${pkgname}"
  go build -o packer-binary
}

package() {
  cd "src/github.com/hashicorp/${pkgname}"
  install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
  install -Dm644 contrib/zsh-completion/_packer "${pkgdir}"/usr/share/zsh/site-functions/_packer
}

# vim:set et sw=2 ts=2 tw=79:
