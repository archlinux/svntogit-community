# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-sayura
pkgver=5.0.7
pkgrel=1
pkgdesc="Sinhala Transe IME engine for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-sayura"
license=('GPL')
depends=('fcitx5')
makedepends=('extra-cmake-modules')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('37c7365caf63e20f47275dfd4bb18ad5c97aa3006e018cc6aa444beac081dc64e9bd26317e89ec3cd53aaad355bb014e7662a296a01d647869cc396a1c63d0a9'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
