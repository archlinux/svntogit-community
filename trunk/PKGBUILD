# Maintainer: christian rebischke <chris.rebischke@archlinux.org>

pkgname=gopass-jsonapi
pkgver=1.11.1
pkgrel=2
pkgdesc="jsonapi for gopass used for bridging to browsers"
arch=('x86_64')
url="https://github.com/gopasspw/gopass-jsonapi"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass-jsonapi/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('231585e4e6de51a7f98218d80cc805d71e4b61e5ec1eff7dfeab8ed2cc2a031ee21c58f2cf9e5e081dc24e7b90ffe7bd59ac7522fee1a3eeff187534c7eb0cc1')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  sed -i 's/install-completion//g' Makefile
}

build(){
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
