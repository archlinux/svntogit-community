# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=padthv1
pkgver=0.9.28
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=(x86_64)
url="https://padthv1.sourceforge.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base qt6-svg)
makedepends=(alsa-lib cmake fftw jack liblo lv2 qt6-tools)
optdepends=(
  'alsa-lib: for standalone application'
  'jack: for standalone application'
  'liblo: for standalone application'
  'new-session-manager: for session management using the standalone application'
  'qt6-wayland: for native wayland support'
)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('2269d96633723e8cca9447b732b9590a5d4f95b88ccad5424931a8de027964df0681b009a71fefdec8f3885856bdba293f9ccdd03f108c9508f54976ee11f7ff')
b2sums=('5864a3caaf02a226c454a128c1851f6a1e9e2f0625095faad314deb0e0b4b47cd17e8de81e03b8d0a61d3625c67747b59998cd963928071ba83f29afe065609a')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfftw3.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
