# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>

pkgname=fdkaac
pkgver=0.6.2
pkgrel=2
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=('i686' 'x86_64')
url='https://github.com/nu774/fdkaac'
license=('custom')
depends=('libfdk-aac.so')
source=("fdkaac-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('de758d6e651e81e9be89d2972612fc5b96cb70321234c3339f35483b636458ad')

prepare() {
  cd fdkaac-${pkgver}

  autoreconf -fi
}

build() {
  cd fdkaac-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-rpath
  make
}

package() {
  cd fdkaac-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/fdkaac
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/fdkaac/
}

# vim: ts=2 sw=2 et:
