# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Wellington <wellingtonwallace@gmail.com>
# Contributor: robertfoster

pkgname=easyeffects
pkgver=6.2.5
pkgrel=1
pkgdesc='Audio Effects for Pipewire applications'
arch=('x86_64')
url='https://github.com/wwmm/easyeffects'
license=('GPL3')
depends=('calf' 'gtk4' 'libebur128' 'libbs2b.so'
         'librubberband.so' 'libsamplerate.so' 'libsigc-3.0.so' 'libsndfile.so'
         'liblilv-0.so' 'lsp-plugins' 'libpipewire-0.3.so' 'rnnoise' 'rubberband'
         'tbb' 'zita-convolver' 'libadwaita-1.so' 'libfmt.so')
makedepends=('appstream-glib' 'itstool' 'lv2' 'meson' 'nlohmann-json' 'zam-plugins')
optdepends=(
  'zam-plugins: maximizer'
  'mda.lv2: bass loudness plugin'
)
replaces=('pulseeffects')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('260b864e5928191dd5574e23a5f51f4df0dcc552c44ecf2d0c1166d589fc1b4320b9da786de88c8837dcb28527163051dc80b5a5a33a9cdde2466405c73248f2')

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja install -C build
}
