# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.8.15
_pkgver=0.8.15
pkgrel=1
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm.'
arch=('x86_64')
url='https://www.nongnu.org/duplicity/'
license=('GPL')
depends=('gnupg' 'librsync' 'python-paramiko' 'python-fasteners' 'python-future')
makedepends=('python-setuptools')
optdepends=('lftp: backend for ftp, ftps, fish'
            'ncftp: alternative backend for ftp'
            'python-boto: Amazon S3 and Google Cloud Storage backend'
            'python-dropbox: Dropbox backend'
            'python-gobject: GIO backend'
            'gvfs: GIO backend'
            'python-pydrive: Google Drive backend'
            'python-requests-oauthlib: OneDrive backend'
            'rsync: rsync backend')
source=("https://launchpad.net/$pkgname/0.8-series/$pkgver/+download/$pkgname-$_pkgver.tar.gz"{,.sig})
md5sums=('1c642ff2e0f661a4404c39a9c5b09ef4'
         'SKIP')
validpgpkeys=('9D95920CED4A8D5F8B086A9F8B6F8FF4E654E600'
              '9946FB5BE18E34E016DAE992330B3C2A96A9EA9C')

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	python setup.py install --root="$pkgdir" --optimize=1
}
