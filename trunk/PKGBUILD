# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-gcc
pkgver=7.2.0
pkgrel=2
_snapshot=7-20171109
_islver=0.18
pkgdesc='The GNU AVR Compiler Collection'
arch=(i686 x86_64)
license=(GPL LGPL FDL custom)
url='http://gcc.gnu.org/'
depends=(avr-binutils gcc-libs libmpc)
optdepends=('avr-libc: Standard C library for Atmel AVR development')
options=(!emptydirs !strip)
source=(#ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        ftp://gcc.gnu.org/pub/gcc/snapshots/${_snapshot}/gcc-${_snapshot}.tar.xz
        http://isl.gforge.inria.fr/isl-${_islver}.tar.bz2
        Revert-eeb6872bf.patch
       )
sha1sums=('8fa17d2a8ebc84a9f8f8d8c90a62975dfc7dd157'
          'bbffc5a2b05e4f0c97e882f96c448504491dc4ed'
          'f93cd532288a58d76c9dcdf654c7e6028c6f411f')

if [ -n "${_snapshot}" ]; then
  _basedir=gcc-${_snapshot}
else
  _basedir=gcc-${pkgver}
fi

prepare() {
    cd ${_basedir}

    # https://gcc.gnu.org/bugzilla/show_bug.cgi?id=80717
    patch -p1 < ../Revert-eeb6872bf.patch
}

build() {
    cd ${srcdir}/${_basedir} 

    # link isl for in-tree build
    ln -s ../isl-${_islver} isl

    # https://bugs.archlinux.org/task/34629
    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    echo ${pkgver} > gcc/BASE-VER

    cd ${srcdir}
    mkdir gcc-build && cd gcc-build

    export CFLAGS_FOR_TARGET='-O2 -pipe'
    export CXXFLAGS_FOR_TARGET='-O2 -pipe'

    # --disable-linker-build-id   https://bugs.archlinux.org/task/34902
    # --disable-__cxa_atexit   https://bugs.archlinux.org/task/50848
    ${srcdir}/${_basedir}/configure \
                --disable-install-libiberty \
                --disable-libssp \
                --disable-libstdcxx-pch \
                --disable-libunwind-exceptions \
                --disable-linker-build-id \
                --disable-nls \
                --disable-werror \
                --disable-__cxa_atexit \
                --enable-checking=release \
                --enable-clocale=gnu \
                --enable-gnu-unique-object \
                --enable-gold \
                --enable-languages=c,c++ \
                --enable-ld=default \
                --enable-lto \
                --enable-plugin \
                --enable-shared \
                --infodir=/usr/share/info \
                --libdir=/usr/lib \
                --libexecdir=/usr/lib \
                --mandir=/usr/share/man \
                --prefix=/usr \
                --target=avr \
                --with-as=/usr/bin/avr-as \
                --with-gnu-as \
                --with-gnu-ld \
                --with-ld=/usr/bin/avr-ld \
                --with-plugin-ld=ld.gold \
                --with-system-zlib \
                --with-isl \
                --enable-gnu-indirect-function \
                --enable-libstdcxx \
                --enable-cxx-flags='-fno-exceptions -fno-rtti' \
                --disable-libstdcxx-verbose \
                --disable-vtable-verify \
                --disable-libstdcxx-time \
                --disable-libstdcxx-threads \
                --disable-threads \
                --enable-libstdcxx-allocator=malloc \
                --disable-libstdcxx-filesystem-ts

    make
}

package() {
    cd ${srcdir}/gcc-build

    make -j1 DESTDIR=${pkgdir} install

    # Strip debug symbols from libraries; without this, the package size balloons to ~500MB.
    find ${pkgdir}/usr/lib -type f -name "*.a" \
        -exec /usr/bin/avr-strip --strip-debug '{}' \;

    # Install Runtime Library Exception
    install -Dm644 ${srcdir}/${_basedir}/COPYING.RUNTIME \
        ${pkgdir}/usr/share/licenses/avr-gcc/RUNTIME.LIBRARY.EXCEPTION

    rm -r ${pkgdir}/usr/share/man/man7
    rm -r ${pkgdir}/usr/share/info
    rm -r ${pkgdir}/usr/share/gcc-7.2.0
    rm ${pkgdir}/usr/lib/libcc1.*
}
