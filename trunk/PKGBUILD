# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=2.0.3
pkgrel=1
pkgdesc="An XML Schema validator and decoder"
url="https://github.com/sissaschool/xmlschema"
license=('MIT')
arch=('any')
depends=('python-elementpath')
makedepends=('python-setuptools')
checkdepends=('python-lxml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/xmlschema/archive/v$pkgver.tar.gz")
sha512sums=('b8cee1a8ce14002bc680728c2785fbe96be78cb3367e912d75cc96ad2c988d5df67740370cf968dc2eae83a878ddcf0d95323cd5a4a0eddba53fb7362367a396')

prepare() {
  sed -i 's/~=/>=/' xmlschema-$pkgver/setup.py
}

build() {
  cd xmlschema-$pkgver
  python setup.py build
}

check() {
  cd xmlschema-$pkgver
  PYTHONPATH="$PWD/build/lib" python -m unittest
}

package() {
  cd xmlschema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
