# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=granite
pkgver=5.2.4
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=(x86_64)
url=https://github.com/elementary/granite
license=(LGPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libgee
  pango
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
provides=(libgranite.so)
source=(git+https://github.com/elementary/granite.git#tag=${pkgver})
sha256sums=(SKIP)

build() {
  arch-meson granite build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
