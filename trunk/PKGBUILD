# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lucas Hermann Negri <kkndrox@gmail.com>

pkgbase=luaexpat
pkgname=(lua-expat lua51-expat)
pkgver=1.2.0
pkgrel=3
pkgdesc='SAX XML parser based on the Expat library'
arch=('i686' 'x86_64')
url='http://matthewwild.co.uk/projects/luaexpat/'
license=('MIT')
makedepends=('gcc' 'lua' 'lua51' 'expat')
replaces=(luaexpat)
conflicts=(luaexpat)
source=("http://matthewwild.co.uk/projects/luaexpat/luaexpat-$pkgver.tar.gz"
	    'config.51'
	    'config.52'
	    'lua52.patch')
md5sums=('03efe50c7f30a34580701e6527d7bfee'
         '64abbf5b602e0e31b68db2925a5e6b2f'
         '60ae0633ac6f829cc3359f3533252eb0'
         '21da93cbf0d3cef80d00e2380b7c2679')

build() {
  # 5.1
  msg2 "Building $pkgbase with lua 5.1"
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-51
  cd $pkgbase-$pkgver-51
  cp -L "$srcdir/config.51" config
  make LUA_INC=/usr/include/lua5.1
  # 5.2
  msg2 "Building $pkgbase with lua 5.2"
  cd "$srcdir/$pkgbase-$pkgver"
  patch -p1 < "$srcdir/lua52.patch"
  cp -L "$srcdir/config.52" config
  make
}

package_lua-expat() {
  depends=('lua' 'expat')
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  mv usr "$pkgdir"
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua51-expat() {
  depends=('lua51' 'expat')
  cd $pkgbase-$pkgver-51
  make DESTDIR="$pkgdir" install
  mv usr "$pkgdir"
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
