# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-localhost
pkgver=1.1.10
_commit=4d85ada0ce511f8427ad2eba1f6f174f5bf84ef3
pkgrel=1
pkgdesc='Manage a local certificate authority for self-signed localhost development servers'
arch=(any)
url='https://github.com/socketry/localhost'
license=(MIT)
depends=(ruby)
makedepends=(git ruby-covered ruby-sus ruby-sus-fixtures-async)
checkdepends=(ruby-bake-modernize ruby-bake-test ruby-bake-test-external ruby-async-io ruby-async-process)
options=(!emptydirs)
source=(git+https://github.com/socketry/localhost.git#commit=$_commit)
sha256sums=('SKIP')

prepare() {
  cd localhost
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i localhost.gemspec
  sed -i '/bake-gem/d;/utopia-project/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd localhost
  gem build localhost.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    localhost-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd localhost
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd localhost
  cp -a tmp_install/* "$pkgdir"/
}
