# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-json-logger
pkgname=('python-json-logger' 'python2-json-logger')
pkgver=0.1.11
pkgrel=3
pkgdesc='A python library adding a json log formatter'
arch=('any')
license=('BSD')
url='https://github.com/madzak/python-json-logger'
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-v$pkgver.tar.gz::https://github.com/madzak/python-json-logger/archive/v$pkgver.tar.gz")
sha512sums=('de0e66fd99d5b763c1fe2574e0e2cfc8c1053b88adbbc9b7bf8a48027c1c14c141c1389a303f9dcc43a18671085abaef8650150ecd4a7c281a2a966de5f0f136')

prepare() {
  cp -a python-json-logger-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-json-logger-$pkgver
  python setup.py build

  cd "$srcdir"/python-json-logger-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-json-logger-$pkgver
  python setup.py test

  cd "$srcdir"/python-json-logger-$pkgver-py2
  python2 setup.py test
}

package_python-json-logger() {
  depends=('python-setuptools')

  cd python-json-logger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-json-logger() {
  depends=('python2-setuptools')

  cd python-json-logger-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
