# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=ali
pkgver=0.7.2
pkgrel=1
pkgdesc="Generate HTTP load and plot the results in real-time"
arch=('x86_64')
url="https://github.com/nakabonne/ali"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('8f20135ce212f508352b39afe6db8499242116a28f9654d908ab25c5b690c5b455c31d0d8eeb91e3251cf979cdad8805349c525dffee37f2e600f9f844d9c456')

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o "$pkgname" .
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
