# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-kiwisolver
pkgver=1.1.0
pkgrel=4
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('PerlArtistic')
arch=('x86_64')
depends=('python-setuptools')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/nucleic/kiwi/archive/$pkgver.tar.gz")
sha512sums=('e26a951fdba2fb221effe7ed785010d5795695b30e19fb67a30dc2ac883ee21b1daec967ad89734309c101513cd17bb5971118591270f402b8d46098178aff25')

build() {
  cd "$srcdir"/kiwi-$pkgver/
  python setup.py build
}

check() {
  cd "$srcdir"/kiwi-$pkgver/
  python setup.py pytest
}

package() {
  cd kiwi-$pkgver/
  python3 setup.py install --root="$pkgdir" --optimize=1
}
