# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
pkgver=0.4.0
pkgrel=1
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-jsonschema')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-isodate' 'python-rfc3339-validator' 'python-strict-rfc3339')
optdepends=(
  'python-isodate: validate datetime with isodate'
  'python-rfc3339-validator: validate datetime with rfc3339_validator'
  'python-strict-rfc3339: validate datetime with strict_rfc3339'
)
source=("https://github.com/p1c2u/openapi-schema-validator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ce61c112c528bac022e3feb959bfe5adbac000ec0b4956f678bf61f66efdbf3a5cf912a4dfa1512858b497fdadaf533532f5a580f28f6ceea831e6b5ac98d85e')

prepare() {
  cd openapi-schema-validator-$pkgver
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd openapi-schema-validator-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd openapi-schema-validator-$pkgver
  python -m pytest
}

package() {
  cd openapi-schema-validator-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
