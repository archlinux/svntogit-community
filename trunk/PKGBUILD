# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Grayhatter
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Madotsuki <madotsuki at cock dot li>

pkgname=utox
_pkgname=uTox
pkgver=0.12.2
pkgrel=1
_signature="grayhatter"
pkgdesc='Lightweight Tox client'
arch=('i686' 'x86_64')
url="https://github.com/uTox/uTox"
license=('GPL3')
depends=('fontconfig'
         'libfilteraudio'
         'libxext'
         'libxrender'
         'openal'
         'toxcore'
         'v4l-utils')
optdepends=('gtk3: GTK file picker'
            'libdbus: Notification support')
makedepends=('toxcore>=0.1.0' 'cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/uTox/uTox/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgname}/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-v${pkgver}.${_signature}.tar.gz.asc")
sha512sums=('be66fc7a6c3ca814e1987a066e99226844fb96247ec03db85234888abc502589c09571ef40ebd1b53c9dd343781aa46a864e7a5765e5110b2d9920bca15f4fdf'
            'SKIP')
validpgpkeys=("15D3B9A6B3951DF9854FCA93E786548AE0A0B56B"  # RobinLindén<mail+gitlab@robinlinden.eu>
              "4C4E8E7E23A85F2474DC2F1A0558CBEA7BE857DD") # Grayhatter

build() {
  cd "$_pkgname-${pkgver}"
  CFLAGS="-Wl,-z,noexecstack" cmake . -DCMAKE_BUILD_TYPE=Release -DENABLE_ASAN=OFF -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$_pkgname-${pkgver}"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
}
