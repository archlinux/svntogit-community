# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=streamlink
pkgver=1.7.0
pkgrel=2
pkgdesc='CLI program that launches streams from various streaming services in a custom video player (livestreamer fork)'
arch=('any')
url='https://streamlink.github.io/'
license=('BSD')
depends=("python-"{pycryptodome,isodate,pycountry,requests,pysocks,websocket-client} 'rtmpdump')
checkdepends=("python-"{freezegun,mock,pytest,requests-mock})
makedepends=("python-"{recommonmark,setuptools,sphinx})
optdepends=('ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('E2B794C7C2C37162E5E2A097E3DB9E282E390FA0') # Streamlink Signing Key <streamlink@protonmail.com>
sha256sums=('f87a62a47212d94769929bd040d9c186b461643bdbda06f839b99ec9efefb87a'
            'SKIP')
sha512sums=('2921905290488b7ad955bda5921a7725fdc0984f5084f833779bae19188d370d2bad08e3ff9714254e0595e3cf1f6244ecfa145ddf94458211048335840d82c5'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  export STREAMLINK_USE_PYCOUNTRY="true"
  python setup.py build
  python setup.py build_sphinx -b man
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASSEED=0
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 build/sphinx/man/streamlink.1 \
    "$pkgdir/usr/share/man/man1/streamlink.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
