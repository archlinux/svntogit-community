# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel2008 at yahoo dot com>
pkgname=rocprofiler
pkgver=5.5.0
pkgrel=1
pkgdesc="ROC profiler library. Profiling with perf-counters and derived metrics."
arch=('x86_64')
url='https://docs.amd.com/bundle/ROCProfiler-User-Guide-v5.1/page/ROCProfiler_Profiling_API.html'
license=('MIT')
depends=('hip' 'python-lxml' 'hsa-amd-aqlprofile')
makedepends=('cmake' 'rocm-llvm' 'hsa-rocr' 'rocm-dbgapi' 'gtest'
             'python-cppheaderparser' 'python-yaml' 'python-barectf')
_git='https://github.com/ROCm-Developer-Tools/rocprofiler'
_roctracer='https://github.com/ROCm-Developer-Tools/roctracer'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/refs/tags/rocm-$pkgver.tar.gz"
        "$pkgname-roctracer-$pkgver.tar.gz::$_roctracer/archive/refs/tags/rocm-$pkgver.tar.gz"
        "rocprofiler-stdint.patch")
sha256sums=('d9dd38c42b4b12d4149f1cc3fca1af5bec69c72f455653a8f4fd8195b3b95703'
            'fe9ad95628fa96639db6fc33f78d334c814c7161b4a754598f5a4a7852625777'
            '4fcbbe649d7a92f85240476401ff8617076caefe1bf91c6e08356b6bf25f6953')
options=(!lto)
_dirname="$(basename "$_git")-$(basename "${source[0]}" ".tar.gz")"
_dirtracer="$(basename "$_roctracer")-$(basename "${source[1]}" ".tar.gz")"

prepare() {
    cd "$_dirname"
    patch -Np1 -i "$srcdir/rocprofiler-stdint.patch"
}

build() {
  HIP_CLANG_PATH=/opt/rocm/llvm/bin \
  cmake \
    -Wno-dev \
    -B build \
    -S "$_dirname" \
    -DCMAKE_INSTALL_PREFIX=/opt/rocm \
    -DHIP_ROOT_DIR=/opt/rocm/hip \
    -DCMAKE_BUILD_TYPE=None \
    -DPROF_API_HEADER_PATH="$srcdir/$_dirtracer/inc/ext"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_dirname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
