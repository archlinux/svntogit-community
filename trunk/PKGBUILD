# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Eric Gillingham <sysrq@sysrq.tk>

pkgname=libdiscid
pkgver=0.6.4
pkgrel=1
pkgdesc="A Library for creating MusicBrainz DiscIDs"
arch=('x86_64')
url="https://musicbrainz.org/doc/libdiscid"
license=('LGPL')
makedepends=('cmake')
source=("$pkgname-$pkgver"::"https://github.com/metabrainz/libdiscid/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('c581baeabaf6e410398005645d66acaaa41ff2cab272c267a2d6839e41486ebb')

build() {
    cmake -B build -S "$pkgname-$pkgver" \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
    cmake --build build
}

check() {
    make -C build check
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
