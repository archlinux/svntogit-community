# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.3.12
pkgrel=2
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'deepin-qt5dxcb-plugin')
makedepends=('xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz")
sha512sums=('d897c6b19caabfc776196153ac478c3eacb63a599cc520d258cbbb8268b7c0745545d855389ded5c0082fac2a00d0498fa4e4da4aca7345fa7560dc3862b552c')

build() {
  cd qt5integration-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
