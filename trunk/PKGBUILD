# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-xattr
pkgname=('python-xattr' 'python2-xattr')
pkgver=0.9.1
pkgrel=2
pkgdesc='Python wrapper for extended filesystem attributes'
arch=('i686' 'x86_64')
license=('MIT')
url='http://github.com/xattr/xattr'
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi' 'git')
source=("git+https://github.com/xattr/xattr.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a xattr{,-py2}
}

build() {
  cd "$srcdir"/xattr
  python setup.py build

  cd "$srcdir"/xattr-py2
  python2 setup.py build
}

check() { (
  # Build env doesn't have permission for those xattrs
  cd "$srcdir"/xattr
  python setup.py test

  cd "$srcdir"/xattr-py2
  python2 setup.py test
  ) || warning "Tests failed"
}

package_python-xattr() {
  depends=('python-cffi')

  cd xattr
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-xattr() {
  depends=('python2-cffi')

  cd xattr-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt

  mv "$pkgdir"/usr/bin/xattr{,2}
}

# vim:set ts=2 sw=2 et:
