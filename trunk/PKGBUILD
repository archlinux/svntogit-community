# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: catwell <catwell@archlinux.us>

pkgname=lout
pkgver=3.42.1
pkgrel=1
pkgdesc="A lightweight document formatting system"
arch=('x86_64')
url="https://github.com/william8000/lout"
license=('GPL')
depends=('glibc' 'zlib')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('ac13d2ddad9904cd5bc2255c292a4738c55617d47b05ebd26a5a3131095748764cd050fdd79710945c8071eac779c9b5babd37b95d13464e558a6feee758e86d')
b2sums=('454f02e883904e3ff39785e1d04140e78d8bc45244181665e217924283666cfaa7936f2507da811da142b329c493978de5b2b2b8d2d2cbffd84d6b6c61dc8bdd')

prepare() {
  cd "$pkgname-$pkgver"

  # simplify paths
  sed -i \
    -e 's:^MANDIR.*:MANDIR  = $(PREFIX)/share/man/man1:' \
    -e 's:^LIBDIR.*:LIBDIR = $(PREFIX)/lib/lout:' \
    -e 's:^LOUTLIBDIR.*:LOUTLIBDIR = $(LIBDIR):' \
    makefile

  # TODO generate documentation
  #-e 's:^LOUTDOCDIR.*:LOUTDOCDIR = $(PREFIX)/share/doc/lout:' \
}

build() {
  cd "$pkgname-$pkgver"

  make PDF_COMPRESSION=1 ZLIB=-lz PREFIX=/usr all
}

package() {
  cd "$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install installman
}

