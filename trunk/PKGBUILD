# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>

pkgname=supertuxkart
pkgver=0.7.3
pkgrel=2
pkgdesc='Kart racing game featuring Tux and his friends'
arch=('i686' 'x86_64')
url="http://supertuxkart.sourceforge.net/"
license=('GPL2')
depends=('openal' 'libvorbis' 'libgl' 'fribidi' 'curl')
makedepends=('cmake' 'subversion' 'mesa')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.tar.bz2)
md5sums=('502664b2ec9ad5ab88b1882fef4c074d')

build() {
  cd "$srcdir"

  svn co -r 3900 https://irrlicht.svn.sourceforge.net/svnroot/irrlicht/trunk irrlicht

  cd irrlicht/source/Irrlicht
  NDEBUG=1 make

  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i "s|share/games|share|g" CMakeLists.txt

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
    -DIRRLICHT_DIR="$srcdir/irrlicht" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="-lpthread -lm -ldl" \
    ..

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  cd build
  make DESTDIR=${pkgdir} install

  # fix executable location...
  install -Dm755 ${pkgdir}/usr/games/supertuxkart ${pkgdir}/usr/bin/supertuxkart
  rm -r ${pkgdir}/usr/games

  sed -i "s#usr/games/supertuxkart#usr/bin/supertuxkart#" ${pkgdir}/usr/share/applications/supertuxkart.desktop
}

# vim:set ts=2 sw=2 et:
