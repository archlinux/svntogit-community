# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gupnp-tools
pkgver=0.12.0
pkgrel=1
pkgdesc="Utilities and demos to work with UPnP"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64)
license=(GPL)
depends=(gtksourceview4 gupnp gupnp-av)
makedepends=(meson git)
_commit=8886f95134d95e49d859512fc0c2c62ed63a72b2  # tags/gupnp-tools-0.12.0^0
source=("git+https://gitlab.gnome.org/GNOME/gupnp-tools.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gupnp-tools-//;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
