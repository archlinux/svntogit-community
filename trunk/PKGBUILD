# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=5.4.1
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('accountsservice' 'cinnamon-desktop' 'libgnomekbd'
         'python-cairo' 'python-gobject' 'python-setproctitle' 'python-xapp' 'xapp' 'xdotool')
optdepends=('cinnamon-translations: i18n')
makedepends=('gobject-introspection' 'meson' 'samurai')
backup=('etc/pam.d/cinnamon-screensaver')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('2f8ab0b0633ccc03a5350a0629c3434604f8ad8e8c46f62f48ffd4150c30c865d9de1f21ad9a022e9954ed1c73431d7518d563771e6321d39af3e4cfa5a626b6')
b2sums=('292fc4ecd1a357c40fd62b0f04fd0a8aac30d7dbdf6a7267e1f9121568ddcd7946bc3529d5940ff0dbc0f5541b34d066e54fd4e79403f1971d6456254fa8028c')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/cinnamon-screensaver \
          --buildtype=plain \
          ..
    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
