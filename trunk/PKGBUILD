# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Arnau Sanchez <tokland {at} gmail {dot} com>

pkgbase=python-pyquery
pkgname=('python-pyquery' 'python2-pyquery')
pkgver=1.4.1
pkgrel=1
pkgdesc="A jquery-like library for python."
arch=('any')
url="https://github.com/gawel/pyquery/"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-lxml' 'python2-lxml'
             'python-cssselect' 'python2-cssselect')
checkdepends=('python-requests' 'python2-requests' 'python-webob' 'python2-webob' 'python-nose'
              'python2-nose' 'python-coverage' 'python2-coverage' 'python-webtest' 'python2-webtest'
              'python2-beautifulsoup4' 'python2-restkit')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gawel/pyquery/archive/$pkgver.tar.gz")
sha512sums=('25fe43155ecfcafb34863cee0a4690f51f23690b866a9e985f605f9995093e6698dbe867e6a98978f6f389735666fe17328d60c59904456ee1b35ff450eae463')

prepare() {
  cp -a pyquery-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyquery-$pkgver
  python setup.py build

  cd "$srcdir"/pyquery-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyquery-$pkgver
  nosetests3

  cd "$srcdir"/pyquery-$pkgver-py2
  nosetests2
}

package_python-pyquery() {
  depends=('python-lxml' 'python-cssselect')
  optdepends=('python-webob: query wsgi app')

  cd pyquery-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyquery() {
  depends=('python2-lxml' 'python2-cssselect')
  optdepends=('python2-webob: to query wsgi app'
              'python2-restkit: to get url directly with a HostProxy app')

  cd pyquery-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
