# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=hey
pkgver=0.1.4
pkgrel=1
pkgdesc='HTTP load generator'
url="https://github.com/rakyll/hey"
arch=(x86_64)
license=('GPL')
depends=('glibc')
makedepends=('go')
source=($pkgname-$pkgver.tar.gz::https://github.com/rakyll/hey/archive/v$pkgver.tar.gz)
sha512sums=('f71ad13c401e9c9c45e1176924de49eb33df9b180dc10907997d1e500a4e5c6e28dd0293cef5d3fe8fabf0bfbade353938327bf3fce894c271209ec86e265ead')


build() {
  cd $pkgname-$pkgver
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags \"${LDFLAGS}\"" \
    .
}


package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
}
