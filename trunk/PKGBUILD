# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: RaphaÃ«l Doursenaud <rdoursenaud at free dot fr>

pkgbase=python-mysql-connector
pkgname=('python-mysql-connector' 'python2-mysql-connector')
_pkgname=mysql-connector-python
pkgver=8.0.18
pkgrel=2
pkgdesc="A standardized database driver for Python platforms and development"
arch=('any')
url="https://dev.mysql.com/downloads/connector/python/"
license=('GPL')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://cdn.mysql.com/Downloads/Connector-Python/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('cb441a6388de8609b56e4d9885ae2491d207c7b1e3bb387341d55853d383eff6e268e34feb32b42797c7b210d35f9d6dbc302ea4c9ed2441a76b91ea66d504b2')

prepare() {
  cp -r "${_pkgname}-${pkgver}" "${_pkgname}-${pkgver}-py2"
  # change python to python2
  find "${_pkgname}-${pkgver}-py2" -name '*.py' -type f -exec \
      sed -i 's|env python|env python2|' {} \;
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py build
}

package_python-mysql-connector() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-mysql-connector() {
  depends=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
