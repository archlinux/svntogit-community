# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>
# Contributor: Lukasz Fidosz <virhilo@gmail.com>

pkgname=menumaker
pkgver=0.99.7
pkgrel=8
pkgdesc='Heuristics-driven menu generator for Deskmenu, FluxBox, IceWM, OpenBox, WindowMaker and XFCE'
arch=('any')
url="http://menumaker.sourceforge.net/"
license=('BSD')
depends=('python2')
options=('!makeflags')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('a54cb7ec32db1bc8e04218e0be727b1e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --infodir=/usr/share/info \
              --with-python=/usr/bin/python2
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license
  rm -rf ${pkgdir}/usr/share/info
}
