# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=orderedmultidict
pkgbase=python-orderedmultidict
pkgname=('python-orderedmultidict' 'python2-orderedmultidict')
pkgver=0.7.11
pkgrel=1
pkgdesc="Ordered multivalue dictionary that retains method parity with dict."
url="https://github.com/gruns/orderedmultidict"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pycodestyle' 'python2-pycodestyle')
license=('custom')
arch=('any')
source=("https://pypi.io/packages/source/o/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0aefb0b308709e66e6f4d3358dc6db53563054a78ba70a49e4e89db20350d36295b863c0feff8232eecff0fe68e0b62b8ef032e1c596dae5c5873bd6d345e75c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test

  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-orderedmultidict() {
  depends=('python-six')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {API,README}.md
}

package_python2-orderedmultidict() {
  depends=('python2-six')
  cd "${pkgname[0]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {API,README}.md
}
