# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Contributor: Alexander Diana <alexander@rouk.org>

pkgname=python-bcrypt
pkgver=3.2.0
pkgrel=1
pkgdesc="Modern password hashing for your software and your servers"
arch=('x86_64')
url="https://github.com/pyca/bcrypt"
license=('Apache')
depends=('python-cffi' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyca/bcrypt/archive/$pkgver.tar.gz")
sha512sums=('7dcca87acfc5562cf735a440dc236a4af79a2a033e63b5ca9203129cbcb90d6f3f2c127c2a73f2b254f2316f9f6e91a23cddd3227411c02d63f5f44e806556aa')

build() {
  cd bcrypt-$pkgver
  python setup.py build
}

check() {
  cd bcrypt-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8" pytest
}

package() {
  cd bcrypt-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
}
