# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgname=atk
pkgname=lib32-$_pkgname
pkgver=2.26.1
pkgrel=1
pkgdesc="The interface definitions of accessibility infrastructure (32-bit)"
url='https://developer.gnome.org/atk/'
arch=(x86_64)
license=(LGPL)
depends=(lib32-glib2 $_pkgbasename)
makedepends=(gcc-multilib git gtk-doc meson)
_commit=6ce1698e8bff43d72c1f00d60f16e19d3b057899 # tags/ATK_2_26_1^0
source=("git+https://git.gnome.org/browse/atk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $_pkgname
}

check() {
  cd build
  meson test
}
 
build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd build
  arch-meson --libdir=/usr/lib32 --libexecdir=/usr/lib32 ../$_pkgname \
    -Ddisable_introspection=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  rm -rf "${pkgdir}"/usr/{include,share}
}
