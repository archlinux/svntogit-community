# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gramps
epoch=2
pkgver=4.2.5
pkgrel=2
pkgdesc="A genealogy program, which helps you track your family tree"
arch=('any')
url="http://gramps-project.org/"
license=('GPL')
depends=('goocanvas' 'gtk3' 'python-gobject' 'librsvg' 'xdg-utils' 
	 'python-bsddb' 'desktop-file-utils'
	 'shared-mime-info' 'python-cairo')
makedepends=('intltool')
optdepends=('graphviz: enable creation of graphs (required for tree reports)'
	    'gtkspell3: enable spell checking in the notes'
	    'rcs: manage multiple revisions of your family trees'
	    'python-pillow: crop and convert images to JPG'
	    'libgexiv2: manage Exif metadata embedded in your media'
	    'python-pyicu: improves localised sorting'
	    'osm-gps-map: Geography feature'
	    'ttf-freefont: more font support in the reports')
#	    'python-osmgpsmap: show maps in the geography view'
#	    'webkitgtk: allows websites to be opened in Gramps'
source=("$pkgname-$pkgver.tar.gz::https://github.com/gramps-project/gramps/archive/v$pkgver.tar.gz")
sha256sums=('1de3007215b7d0b779ec8f086e14e7a75f2aa811ee37c6d0cff06bfc9458743d')

build() {
  cd "$srcdir/gramps-$pkgver"
  python setup.py build
}

package() {
_pyver=`pacman -Q python | cut -f2 -d' ' | cut -f1,2 -d.`
  cd "$srcdir/gramps-$pkgver"
  python setup.py install --root="$pkgdir"
  echo -n '/usr/share' >"$pkgdir"/usr/lib/python${_pyver}/site-packages/gramps/gen/utils/resource-path
}
