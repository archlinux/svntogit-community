# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cabal-install-parsers
pkgname=haskell-cabal-install-parsers
pkgver=0.4
pkgrel=95
pkgdesc="Utilities to work with cabal-install files"
url="https://haskell-ci.rtfd.org/"
license=("GPL" "BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base16-bytestring' 'haskell-binary-instances'
         'haskell-cryptohash-sha256' 'haskell-lukko' 'haskell-network-uri' 'haskell-tar')
makedepends=('ghc' 'uusi' 'haskell-ansi-terminal' 'haskell-tasty' 'haskell-tasty-golden'
             'haskell-tasty-hunit' 'haskell-tree-diff')
checkdepends=('cabal-install')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        $_hkgname-tree-diff-0.2.patch::https://github.com/haskell-CI/haskell-ci/pull/500.patch)
sha512sums=('ed521221e822b471ed297d8291fb1360d785197b0d36de7a4e1dd2bf44b5c2840281fc423e2618dd70dc38354b156984e05f7469f3915321a181a349e94a1a27'
            '57809a0e379595b84534c56eaf08d0a1c34e52e3bfbc6aa631785746cb0c67eb00fcea241ceef26c0dc0801fa26cfb429a4a04b4515c3f2433035b63f9201420')

prepare() {
  cd $_hkgname-$pkgver
  patch -p2 -i ../$_hkgname-tree-diff-0.2.patch
  uusi -u tasty $_hkgname.cabal
  gen-setup
}

build() {
  cd $_hkgname-$pkgver    

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  cabal update  # Generate cabal config for tests
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
