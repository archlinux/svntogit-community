# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=binocle
pkgver=0.2.0
pkgrel=1
pkgdesc="A graphical tool to visualize binary data"
arch=('x86_64')
url="https://github.com/sharkdp/binocle"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('8d2c2a54e1b59065350b0bc2cfded16c7822f5012e48df5f5162d330beddbf298b6c363357dc9a1625c8ae814af82ef57180680374d626ab61e0f55c6fb8766c')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
