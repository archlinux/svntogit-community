# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor : Vassilis Palassopoulos <palasso [at] gmail [dot] com>

pkgname=rkward
pkgver=0.7.5
pkgrel=1
pkgdesc='An easy to use and easily extensible IDE/GUI for R'
url='https://rkward.kde.org/'
arch=(x86_64)
license=(GPL)
depends=(qt5-webengine qt5-script ktexteditor r)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/rkward/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('eb7d385b0b9e694fd639c858340ac14cdc7c932ec5e06b61adffbf77d12cd8c8')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
