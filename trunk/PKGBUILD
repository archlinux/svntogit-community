# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Zamprogno <stefano dot zamprogno at gmail dot com>
# Contributor: Chuck Yang <Chuck.Yang@gmail.com>

pkgname=lshw
pkgver=B.02.18
pkgrel=3
pkgdesc="A small tool to provide detailed information on the hardware configuration of the machine."
url="https://ezix.org/project/wiki/HardwareLiSter"
license=('GPL')
arch=('x86_64')
depends=('gcc-libs' 'hwids')
optdepends=('gtk2')
makedepends=('gcc' 'gtk2' 'sqlite')
source=(https://ezix.org/software/files/lshw-$pkgver.tar.gz
	https://ezix.org/src/pkg/lshw/commit/fbdc6ab15f7eea0ddcd63da355356ef156dd0d96.patch)
sha256sums=('ae22ef11c934364be4fd2a0a1a7aadf4495a0251ec6979da280d342a89ca3c2f'
            '8fe3bf1f8afee4538d1ce3e26ceecfe609c78ef08e3682eba83ce29bf53af64c')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 <$srcdir/fbdc6ab15f7eea0ddcd63da355356ef156dd0d96.patch
  sed -i 's|/usr/bin/gtk-lshw|/usr/sbin/gtk-lshw|' src/gui/integration/gtk-lshw.desktop
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make SBINDIR=/usr/bin
  make SBINDIR=/usr/bin gui
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir"/ SBINDIR=/usr/bin install
  make DESTDIR="$pkgdir"/ SBINDIR=/usr/bin install-gui
  install -Dm0644 src/gui/integration/gtk-lshw.desktop "$pkgdir"/usr/share/applications/gtk-lshw.desktop
  install -Dm0644 src/gui/integration/gtk-lshw.pam "$pkgdir"/usr/share/doc/$pkgname/gtk-lshw.pam
  install -Dm0644 src/gui/integration/console.apps "$pkgdir"/usr/share/doc/$pkgname/console.apps
  rm -f "$pkgdir"/usr/share/lshw/{pci,usb}.ids
}
