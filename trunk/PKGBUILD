# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

_pypiname=dnslib
pkgbase=python-$_pypiname
#pkgname=(python-$_pypiname python2-$_pypiname) # Python 3 version not released yet
pkgname=(python2-$_pypiname)
pkgver=0.8.3
pkgrel=2
pkgdesc="Simple library to encode/decode DNS wire-format packets"
arch=('any')
url=("https://bitbucket.org/paulc/dnslib")
license=('custom:BSD')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/d/$_pypiname/$_pypiname-${pkgver}.tar.gz")
md5sums=("0a09a407ae5239e8d53e3115510e7107")

prepare() {
  cp -r ${_pypiname}-${pkgver} python2-${_pypiname}-${pkgver}

  cd python2-${_pypiname}-${pkgver}/dnslib
  sed -i -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python2|" server/udp_server.py server/udp_proxy.py server/gevent_server.py server/circuits_server.py
}

package_python-dnslib() {
  depends=("python")

  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
}

package_python2-dnslib() {
  depends=("python2")

  cd python2-${_pypiname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
