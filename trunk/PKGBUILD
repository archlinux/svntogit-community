# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=debootstrap
pkgver=1.0.81
pkgrel=1
pkgdesc="Bootstrap a basic Debian system"
arch=('any')
license=('GPL')
url="https://tracker.debian.org/pkg/debootstrap"
depends=('wget' 'binutils')
makedepends=('git' 'makedev')
source=("git+https://anonscm.debian.org/git/d-i/debootstrap.git#tag=$pkgver"
        arch-detect.patch)
optdepends=('gnupg: check release signatures'
            'debian-archive-keyring: check release signatures for Debian'
            'ubuntu-keyring: check release signatures for Ubuntu')
md5sums=('SKIP'
         '60043aaeb9afc1c2d7d2a2d9888eca35')

prepare() {
  cd debootstrap

  sed -i 's/sbin/bin/g' Makefile

  # Debian defaults
  sed -i 's|export PATH|export PATH="/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin"|' debootstrap

  # Detect Architecture
  patch -p1 -i ../arch-detect.patch

  # Workaround permission errors
  # MAKEDEV is not happy with the instructions here
  sed -e 's/install -o root -g root/install/' \
      -e '/chown 0:0 dev/d' \
      -e 's/(cd dev && $(MAKEDEV) std ptmx fd consoleonly)/$(MAKEDEV) -d dev std ptmx fd consoleonly/' \
      -i Makefile
}

build() {
  cd debootstrap
  fakeroot make
}

package() {
  cd debootstrap
  make DESTDIR="$pkgdir" install

  install -Dm644 debootstrap.8 "$pkgdir"/usr/share/man/man8/debootstrap.8
}
