# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=1.25
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache)
depends=(android-tools ffmpeg sdl2)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('9be58de258d2e048fc87ef642ba668ad7427faea51d6bd06d48d1ca61a5c55bbce2b758b445d21ea3052ac63809769043e57a28502c53fae98079056029ded1f'
        '90789c8b259f9a63820652f8884b51abe1040201f37788542796fbff6445bc13259e2aaf635c99c6ccdb515d8fd4b0b3bcd54d8972d134086f05d149a5073d41')

build() {
  mkdir -p build
  arch-meson build $pkgname-$pkgver \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-v$pkgver \
    --buildtype release
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-$pkgver/LICENSE -t \
    "$pkgdir/usr/share/licenses/$pkgname"
}
