# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kirigaya <kirigaya@mkacg.com>

pkgname=deepin-topbar
pkgver=0.7.0
pkgrel=1
pkgdesc="This is topbar for Deepin"
arch=('x86_64')
url="https://github.com/kirigayakazushin/deepin-topbar"
license=('GPL')
depends=('deepin-network-utils' 'deepin-qt5integration' 'deepin-qt-dbus-factory' 'libdbusmenu-qt5'
         'procps-ng')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kirigayakazushin/deepin-topbar/archive/$pkgver.tar.gz")
sha512sums=('30d0b0df25c5ceef626b079c1113d3915532e74c2392f964d5438b374aa5e3d2adedbc9cc3106f8aa23c822539310260f0d5a7d28392181c6b30bfb669dbbb9f')

build() {
  cd $pkgname-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
