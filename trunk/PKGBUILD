# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aniso8601
pkgver=8.1.0
pkgrel=1
pkgdesc="A library for parsing ISO 8601 strings."
url="https://bitbucket.org/nielsenb/aniso8601"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-dateutil' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/nielsenb/aniso8601/get/v$pkgver.tar.gz")
sha512sums=('12aa2dfca3a4138b56e37e007d1bd02a63eb02b0defa654a2070bb83f42187d6f7b771cba4d6659c88bb1a3a5b44f18ff2f380377a35951ff37725404c8e70bd')

prepare() {
  mv nielsenb-aniso8601-* aniso8601-$pkgver
}

build() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py pytest
}

package() {
  cd aniso8601-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
