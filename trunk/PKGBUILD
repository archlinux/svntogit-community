# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lib32-freeglut
pkgver=3.4.0
pkgrel=1
pkgdesc="Provides functionality for small OpenGL programs (32-bit)"
url="https://freeglut.sourceforge.net/"
arch=(x86_64)
license=(MIT)
depends=(lib32-libxi lib32-libxrandr lib32-libgl freeglut)
makedepends=(lib32-mesa lib32-glu lib32-libxxf86vm cmake ninja git)
replaces=(lib32-glut)
provides=(lib32-glut)
conflicts=(lib32-glut)
options=(debug)
_commit=63c0e78d84ecbfcf8accd8bac04404427899053b  # tags/v3.4.0
source=("git+https://github.com/FreeGLUTProject/freeglut#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd freeglut
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd freeglut
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='i686-pc-linux-gnu-pkg-config'

  cmake -S freeglut -B build -G Ninja \
    -DFREEGLUT_BUILD_STATIC_LIBS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir/usr/include"

  ln -s glut.pc "$pkgdir/usr/lib32/pkgconfig/freeglut.pc"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" freeglut/COPYING
}

# vim:set sw=2 sts=-1 et:
