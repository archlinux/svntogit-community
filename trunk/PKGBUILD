# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=ipython
pkgname=(ipython ipython2)
pkgver=4.2.0
pkgrel=2
pkgdesc="An enhanced Interactive Python shell."
arch=('any')
url="http://ipython.org"
license=('BSD')
depends=('python' 'python-decorator' 'python-pexpect' 'sqlite' 'python-setuptools' 'python-path')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=("python-nose: for IPython's test suite")
install=ipython.install

# some of these will update rapidly, others never
# so _vars will be phased in as new updates are released
_traitv=4.2.1
_psharev=0.7.2

source=("ipython-$pkgver.tgz::https://github.com/ipython/ipython/archive/$pkgver.tar.gz"
        "https://pypi.python.org/packages/source/t/traitlets/traitlets-$_traitv.tar.gz"
        "https://pypi.python.org/packages/source/i/ipython_genutils/ipython_genutils-0.1.0.tar.gz"
        "https://pypi.python.org/packages/source/p/pickleshare/pickleshare-$_psharev.tar.gz"
        "https://pypi.python.org/packages/source/s/simplegeneric/simplegeneric-0.8.1.zip"
        "http://www.packal.org/sites/default/files/public/styles/icon_large/public/workflow-files/nkeimipynbworkflow/icon/icon.png")
md5sums=('9432d4758961501bf5f33adde08a9e6e'
         'f9cc1ad00a793a65d7bc88d69ee65920'
         '9a8afbe0978adbcbfcb3b35b2d015a56'
         '29d74cde0255546b6b2e1b48a0b31a54'
         'f9c1fab00fd981be588fc32759f474e3'
         '2901d65f1b1fe354e72850085cd1c072')

# With 4.0 many previously bundled external libraries are no longer bundled
# Most are written/maintained/used only by the ipython project
# Since there are so many and they are so small, they get re-bundled here
# Break them out if any non-ipython package ends up needing these

# traitlets (depends on python-decorator)
# ipython_genutils
# "Pretend this doesnâ€™t exist. Nobody should use it."
# "Vestigial IPython utilities: DO NOT USE"
# (still required for now though, but drop at first chance)
# pickleshare (depends on path.py)
# simplegeneric (so old that it only comes in .zip and still refers to the cheeseshop)
# shutil_get_terminal_size ?

# confirm that an update does not break sage?

prepare() {
  cd "$srcdir"
  cp -r ipython-$pkgver        ipython2-$pkgver
  cp -r ipython_genutils-0.1.0 ipython2_genutils-0.1.0
  cp -r pickleshare-$_psharev  python2-pickleshare-$_psharev
  cp -r traitlets-$_traitv     python2-traitlets-$_traitv
  cp -r simplegeneric-0.8.1    python2-simplegeneric-0.8.1

  # python 3.5 does not need a 3.3 backport hack
  cd "$srcdir/ipython-$pkgver"
  sed -i 's|backports.shutil_get_terminal_size|shutil|' IPython/utils/terminal.py
}

build() {
  cd "$srcdir"
}

package_ipython() {
  cd "$srcdir/ipython-$pkgver"

  # see https://github.com/ipython/ipython/issues/2057
  #export LC_ALL=en_US.UTF-8
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  # TEMPORARY!
  cd "$srcdir/ipython_genutils-0.1.0"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/pickleshare-$_psharev"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/traitlets-$_traitv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/simplegeneric-0.8.1"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipython-$pkgver"
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython/LICENSE"

  cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  # FS#45120
  sed -i 's/gnome-netstatus-idle/ipython/' *.desktop
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython.desktop"
  #install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"
  # FS#47046
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/ipython.png"

  #cd $srcdir/ipython-$pkgver/IPython/qt/console/resources/icon/
  #install -Dm644 IPythonConsole.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/ipython.svg"
}

package_ipython2() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('python2' 'python2-decorator' 'python2-pexpect' 'sqlite'
    'python2-setuptools' 'python2-path' 'python2-pathlib'
    'python2-backports.shutil_get_terminal_size')
  optdepends=("wxpython: needed for ipython2 --gui=wx"
              "python2-nose: for IPython's test suite")
  # "wxpython: needed for ipython2 --gui=wx"   does this still work?

  cd "$srcdir/ipython2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  # TEMPORARY!
  cd "$srcdir/ipython2_genutils-0.1.0"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-pickleshare-$_psharev"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-traitlets-$_traitv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-simplegeneric-0.8.1"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipython2-$pkgver"
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython2/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/2.1}
  done
  find "$pkgdir/usr/bin/" -type f -regex '.*[^2]$' -delete

  cd "$srcdir/ipython2-$pkgver/examples/IPython Kernel/"
  sed -i 's/ython/ython2/g' *.desktop
  sed -i 's/gnome-netstatus-idle/ipython2/' *.desktop
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython2.desktop"
  #install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython2-qtconsole.desktop"
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/ipython2.png"

  #cd "$pkgdir/usr/share/man/man1/IPython/qt/console/resources/icon/"
  #install -Dm644 IPythonConsole.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/ipython2.svg"
}

