# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=u-msgpack-python
pkgbase='python-u-msgpack'
pkgname=('python-u-msgpack' 'python2-u-msgpack')
pkgver=2.7.0
pkgrel=3
arch=('any')
url="https://github.com/vsergeev/${_pkgname}"
license=('MIT')
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('996e4c4454771f0ff0fd2a7566b1a159d305d3611cd755addf444e3533e2bc54')
b2sums=('f4e4447635fc20ef5246bbc90ee9cc0b93eaa9d13a0384423145531bb8bbb21dc9d1ddadc075a4a5e456dd8cc758fed3add4b2b43f83c0115c78a0095030ff70')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    pytest
    pytest2
}

package_python-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python"
    depends=('python')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python2"
    depends=('python2')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
