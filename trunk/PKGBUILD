# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

pkgname=python-black
pkgver=20.8b1
pkgrel=6
pkgdesc='Uncompromising Python code formatter'
arch=('any')
url=https://github.com/psf/black
license=('MIT')
depends=('python' 'python-appdirs' 'python-click' 'python-mypy_extensions'
         'python-pathspec' 'python-regex' 'python-toml' 'python-typed-ast')
makedepends=('python-setuptools-scm')
checkdepends=('python-aiohttp' 'python-aiohttp-cors')
optdepends=('python-aiohttp: for the blackd HTTP server'
            'python-aiohttp-cors: for the blackd HTTP server'
            'python-colorama: for colored diffs')
source=("https://files.pythonhosted.org/packages/source/b/black/black-$pkgver.tar.gz"
        'https://github.com/psf/black/pull/1873/commits/794bb3be687d22bb0a7c5cc6c9e992de7fcfcf7b.patch')
sha256sums=('1c02557aa099101b9d21496f8a914e9ed2222ef70336404eeeac8edba836fbea'
            'ee2ff73e2cb890debd5ecf3b1072d29024c7de9b45faf8f023f6214755dccaea')
b2sums=('f5c4755c5e07c63954041fb437b6dc54ae1aa062e58e0bff2d421b617974b7fdff44a6630beccbed6670b8f07880fafe0fec7c9ce7b170af078a2b15d230e21e'
        '7f8974d7ba6003316abb0437e63970fddb32d1155c8c3831915e559f694061bd73a56ad8957d56ebce346a3d98befc8644b17ca2e4f86405da4c6233ca2abd30')

prepare() {
  cd "black-$pkgver"
  # Only require typing-extensions if Python < 3.8
  patch --forward --strip=1 --input=../794bb3be687d22bb0a7c5cc6c9e992de7fcfcf7b.patch
}

build() {
  cd "black-$pkgver"
  python setup.py build
}

check() {
  cd "black-$pkgver"
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" python -m unittest tests/test_black.py
}

package() {
  cd "black-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
