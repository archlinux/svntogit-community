# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
epoch=1
pkgver=1.4.28
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
url="https://www.remmina.org/"
arch=('x86_64')
license=('GPL')
depends=('avahi' 'libgcrypt' 'libssh' 'vte3' 'libsodium' 'libappindicator-gtk3' 'libsoup3')
makedepends=('cmake' 'freerdp' 'libvncserver' 'spice-gtk' 'spice-protocol'
             'harfbuzz' 'xorgproto' 'gobject-introspection' 'kwallet'
             'webkit2gtk-4.1' 'gtk-vnc' 'ninja')
optdepends=('freerdp: RDP plugin'
            'libsecret: Secret plugin'
            'libvncserver: VNC plugin'
            'spice-gtk: Spice plugin'
#            'pyhoca-cli: X2Go plugin'
            'webkit2gtk-4.1: WWW plugin'
            'gtk-vnc: GVNC plugin'
            'kwallet: kwallet plugin'
            'gnome-terminal: external tools')
replaces=('remmina-plugins')
provides=('remmina-plugins')
options=('debug')
source=("$pkgname-$pkgver.tar.bz2::https://gitlab.com/Remmina/Remmina/-/archive/v${pkgver/rc/-rc}/Remmina-v${pkgver/rc/-rc}.tar.bz2"
        '0001-Multiple-changes-to-build-and-run-with-libsoup-3.0.patch')
sha512sums=('a144f90a9c300f0c5dcd9ab01d1ab323874ef3373dc95d8c62eb36821ef73318d0e0334431fa7cdddb5bef8bfc3838bb609f5c8179d36b19950f203653601482'
            'bcdf834a2506e1ad11b313956cc68726711086fb70a4b2f59c1082f4dc2274f0fa77d2fad94b55afe35ef384f498cc600c129f3945c9ae28bc9d9f277c1419c4')

prepare() {
  cd Remmina-v${pkgver/rc/-rc}

  # We require libsoup 3.0 because of spice-gtk
#  patch -Np1 -i ../0001-Multiple-changes-to-build-and-run-with-libsoup-3.0.patch
}

build() {
  cmake -S Remmina-v${pkgver/rc/-rc} -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=ON \
    -DWITH_NEWS=OFF \
    -DWITH_KF5WALLET=ON \
    -DWITH_X2GO=ON \
    -DWITH_GVNC=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
