# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Pavel Borzenkov <pavel@voidptr.ru>
# Contributor: aksr <aksr at t-com dot me>

pkgname=criu
pkgver=3.15
pkgrel=2
pkgdesc="Utilities to checkpoint and restore processes in userspace"
url="http://criu.org"
license=("GPL2")
arch=("x86_64")
source=("$pkgname-$pkgver.tar.gz::https://github.com/checkpoint-restore/criu/archive/v$pkgver.tar.gz")
depends=('libbsd' 'libnet' 'libnl' 'protobuf-c' 'python-protobuf' 'gnutls' 'nftables')
makedepends=('xmlto' 'asciidoc')
options=("!buildflags")
b2sums=('2479f117e66713632ec1095185dfa3394f8bd08cd3aba205aada6d1cac0c4b8c200d8bb0b95855d3c7caee1ba0696fe3f46c1c166dbbed037cd2ecdf1315f201')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" \
    PREFIX=/usr \
    SBINDIR=/usr/bin \
    LIBDIR=/usr/lib \
    LIBEXECDIR=/usr/lib \
    install

  # remove empty directory
  rmdir "$pkgdir/usr/include/compel/common/asm"
}
