# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.1.0
_rev=5674
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator'
arch=('i686' 'x86_64')
url='http://www.pcsx2.net'
license=('GPL')
makedepends=('cmake' 'sparsehash')
if [[ $CARCH == "i686" ]]; then
  depends=('glew' 'libaio' 'libcanberra' 'libjpeg-turbo' 'portaudio' 'sdl2' 'soundtouch' 'wxgtk2.8')
elif [[ $CARCH == "x86_64" ]]; then
  makedepends+=('gcc-multilib' 'lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo' 'lib32-mesa-libgl' 'lib32-portaudio' 'lib32-sdl2' 'lib32-soundtouch' 'lib32-wxgtk2.8')
fi
options=('!emptydirs')
source=("http://pcsx2.googlecode.com/files/${pkgname}-${pkgver}-r${_rev}-lnx-sources.tar.bz2"
        'zzogl-mesa10.patch')
sha256sums=('1353db351784dca47244c4b184e14c279218fb6933fba53b9d6a6a81182c2663'
            'e9b3a70ada333c78d94733a1b71f3a1f005a8dabb480c3fbfaf1f91f44f4c5fe')

prepare() {
  cd ${pkgname}.snapshot-${_rev}

  patch -Np1 -i ../zzogl-mesa10.patch
}

build() {
  cd ${pkgname}.snapshot-${_rev}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  if [[ $CARCH == "i686" ]]; then
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release' -D{GLSL_API,PACKAGE_MODE,REBUILD_SHADER,SDL2_API,XDG_STD}=TRUE -DPLUGIN_DIR='/usr/lib/pcsx2' -DGAMEINDEX_DIR='/usr/share/pcsx2' -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'
  elif [[ $CARCH == "x86_64" ]]; then
    export CC='gcc -m32'
    export CXX='g++ -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release' -D{GLSL_API,PACKAGE_MODE,REBUILD_SHADER,SDL2_API,XDG_STD}=TRUE -DPLUGIN_DIR='/usr/lib32/pcsx2' -DGAMEINDEX_DIR='/usr/share/pcsx2' -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config32-2.8' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc32-2.8' -DCMAKE_LIBRARY_PATH='/usr/lib32'
  fi

  make
}

package() {
  cd ${pkgname}.snapshot-${_rev}/build

  if [[ $CARCH == "x86_64" ]]; then
    depends=('lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo' 'lib32-portaudio' 'lib32-sdl2' 'lib32-soundtouch' 'lib32-wxgtk2.8')
  fi

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
