# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Neil Lin <neil@vip.url.com.tw>
# Contributor: Cravix < dr dot neemous at gmail dot com >

pkgname=stardict
pkgver=3.0.6
pkgrel=6
pkgdesc='International dictionary software'
arch=('x86_64')
url='http://stardict-4.sourceforge.net/'
license=('GPL')
depends=('gtk2' 'libsigc++')
makedepends=('enchant' 'espeak' 'libmariadbclient' 'gnome-common' 'gnome-doc-utils')
optdepends=('enchant: spell checker plugin'
            'espeak: text to speech plugin')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/project/stardict-4/$pkgver/$pkgname-$pkgver.tar.bz2"
        'gcc46.patch'
        'glib2.patch')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../gcc46.patch
  patch -p1 -i ../glib2.patch

  sed -i 's/, enchant/, enchant-2/' dict/configure.ac
  sed -i '/AM_GCONF_SOURCE_2/d' dict/configure.ac

  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  CXXFLAGS+=" -std=gnu++11"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --disable-gnome-support \
    --disable-gucharmap \
    --disable-festival \
    --disable-updateinfo
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i {dict,lib,tools}/libtool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

md5sums=('93371b35482e6380e8bd4ba5d256b864'
         '52ff47441c503de07add4b7688510bc5'
         'b2f216897c9053c7e4666c3d78705cf1')
