# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=cowfortune
pkgver=0.1.2
pkgrel=7
pkgdesc='Configurable fortune cookie proclaiming cow (and a few other creatures)'
url='https://github.com/anthraxx/cowfortune'
arch=('any')
license=('MIT')
depends=('cowsay' 'fortune-mod')
optdepends=('lolcat: rainbow coloring support')
backup=('etc/cowfortune/config'
        'etc/cowfortune/whitelist'
        'etc/cowfortune/blacklist')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/anthraxx/cowfortune/archive/${pkgver}.tar.gz)
sha512sums=('42ab237ab571bc758e678fc66d1c4eb9694807e5886ee23056ccd738805a31c1de2c07c35466e4cc6c8cf4d7fcdd94289ddf207e5de374ccd60e37161d72469b')

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
