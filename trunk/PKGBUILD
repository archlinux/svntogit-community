# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=1.6.1
pkgrel=6
pkgdesc="Minimalistic plugin API for video effects"
arch=('x86_64')
url="https://frei0r.dyne.org/"
license=('GPL')
depends=('cairo' 'gavl')
makedepends=('doxygen' 'opencv')
optdepends=('opencv: for facebl0r and facedetect plugins')
source=(https://files.dyne.org/frei0r/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('6113D89CA825C5CEDD02C87273B35DA54ACB7D10') # Denis Roio (Jaromil)
sha256sums=('e0c24630961195d9bd65aa8d43732469e8248e8918faa942cfb881769d11515e'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
