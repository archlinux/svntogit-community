# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=tree-sitter
pkgver=0.18.3
pkgrel=1
pkgdesc='An incremental parsing system for programming tools'
arch=('x86_64')
url='https://github.com/tree-sitter/tree-sitter'
license=('MIT')
provides=('libtree-sitter.so')
makedepends=('git' 'cargo' 'npm' 'emscripten')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('b669a499f826eb51fe51cbf93fd9bee6cd17ec3d628a2b7c4359c6b8d60390ce9d348a9d7470d5faa82c937d8afb720b596ce5e7b553943345cf1f79d74a2d79')

build() {
  cd $pkgname-$pkgver

  make

  ./script/build-wasm

  cd cli

  cargo build --release --locked --all-features
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 755 target/release/$pkgname -t "$pkgdir"/usr/bin

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

