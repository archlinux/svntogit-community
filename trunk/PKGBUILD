# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tidal
pkgname=haskell-tidal
pkgver=1.9.0
pkgrel=6
pkgdesc="A domain specific language for live coding pattern."
arch=(x86_64)
url="https://tidalcycles.org/"
license=(GPL3)
groups=(pro-audio)
depends=(ghc ghc-libs glibc haskell-clock haskell-colour haskell-hosc
haskell-mwc-random haskell-network haskell-primitive haskell-random
haskell-tidal-link haskell-vector supercollider)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=("https://hackage.haskell.org/packages/archive/$_name/$pkgver/$_name-$pkgver.tar.gz")
sha512sums=('5ed1d40fc0865e46d50fb144fe749724196fa1c2cdd1411a04b8c44941291322eb767f60c2ebba2f3bbd197a9282bc03b5613bbac08e4fcff323733c0f5d3422')
b2sums=('752ec131aa445e209f90e923ea65cab10f911fc5b9139a990f86e35d732da90aa2dd50ec27d2c37928314308180a6db59a7b1ebc23d03ebb62f9c9f6dbba8d41')

build() {
  cd $_name-$pkgver
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
