# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libimagequant
pkgver=2.13.0
pkgrel=1
pkgdesc="Library for high-quality conversion of RGBA images to 8-bit indexed-color (palette) images"
arch=('x86_64')
url="https://pngquant.org/lib/"
license=('BSD')
depends=('gcc-libs')
source=("https://github.com/ImageOptim/libimagequant/archive/$pkgver/libimagequant-$pkgver.tar.gz")
sha256sums=('1dcd874f97758472d5282ea1241f50959a6e643b8b5ad07dad100b7a85633efd')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    sed -r 's/^install:.*/install:/;/install.*STATICLIB/d' -i Makefile
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-openmp
    make shared imagequant.pc
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
