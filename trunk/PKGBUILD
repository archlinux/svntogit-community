# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Alexander RÃ¸dseth
# Contributor: Andrea Scarpino
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>
# Contributor: Dominic Tubach

pkgbase=sword
pkgname=(sword python-sword python2-sword)
_mainver=1.7
pkgver=1.7.4
pkgrel=12
pkgdesc="Library for Bible study programs"
arch=('x86_64')
url="http://www.crosswire.org/sword/"
license=('GPL')
makedepends=('curl' 'clucene' 'swig' 'cmake' 'python' 'python2')
source=("http://www.crosswire.org/ftpmirror/pub/$pkgbase/source/v$_mainver/$pkgbase-$pkgver.tar.gz")
md5sums=('1677f02a86cbf07713d4e1d4c39791e6')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  mkdir build
}

build() {
  cd "$srcdir/$pkgbase-$pkgver/build"

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CXXFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  #./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc
  cmake .. -DLIBSWORD_LIBRARY_TYPE="Shared" -DSYSCONF_INSTALL_DIR='/etc' \
           -DSWORD_BUILD_TESTS="Yes" -DSWORD_BINDINGS="Python" -DCMAKE_INSTALL_PREFIX=/usr
  make

  cp -a bindings/swig/python bindings/swig/python2
  cd bindings/swig/python2
  python2 setup.py build
}

package_sword() {
  depends=('curl' 'clucene' 'swig')
  backup=('etc/sword.conf')

  cd "$srcdir/$pkgbase-$pkgver/build"
  make DESTDIR="$pkgdir" install

  # Move it into a separated package
  rm -r "$pkgdir/usr/lib/python3.6"
}

package_python-sword() {
  depends=('sword' 'python')

  cd "$srcdir/$pkgbase-$pkgver/build/bindings/swig/python"
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python2-sword() {
  depends=('sword' 'python2')

  cd "$srcdir/$pkgbase-$pkgver/build/bindings/swig/python2"
  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
