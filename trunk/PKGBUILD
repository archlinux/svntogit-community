# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=buildah
pkgver=1.10.1
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache)
depends=(ostree runc skopeo)
makedepends=(go-pie git bash-bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man runc skopeo)
_commit=8c1c2c5819e14dc15ea1cd11faa7d0f74d607f61  # tags/v1.10.1
source=(git://github.com/containers/buildah.git#commit=$_commit)
md5sums=('SKIP')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^[vV]//;s/-/+/g'
}

prepare() {
  mkdir -p src/github.com/containers
  cp -r $pkgname src/github.com/containers
}

build() {
  export GOPATH="${srcdir}"
  cd src/github.com/containers/buildah
  make PREFIX=/usr BUILDTAGS="containers_image_ostree_stub ostree seccomp"
}

package() {
  cd src/github.com/containers/buildah
  make DESTDIR="$pkgdir" PREFIX=usr install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
