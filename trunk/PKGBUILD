# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fabien Dubosson <fabien.dubosson@gmail.com>
# Contributor: Konstantin Gizdov <arch@kge.com>

pkgname=xxhash
pkgver=0.7.0
pkgrel=1
pkgdesc='Extremely fast non-cryptographic hash algorithm'
arch=(x86_64)
url=https://cyan4973.github.io/xxHash/
license=(
  GPL2
  BSD
)
makedepends=(git)
provides=(libxxhash.so)
source=(git+https://github.com/Cyan4973/xxHash.git#tag=v${pkgver})
sha256sums=('SKIP')

build() {
  make PREFIX='/usr' -C xxHash
}

package() {
  make PREFIX='/usr' DESTDIR="${pkgdir}" -C xxHash install
  install -Dm 644 xxHash/LICENSE -t "${pkgdir}"/usr/share/licenses/xxhash
}

# vim:set ts=4 sw=4 et:
