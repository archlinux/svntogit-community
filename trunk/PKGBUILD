# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=easy-pdk-programmer-software
pkgname=easy-pdk
pkgver=1.3
pkgrel=2
pkgdesc='Easy PDK programmer for PADAUK microcontroller'
arch=('x86_64')
url='https://github.com/free-pdk/easy-pdk-programmer-software'
license=('GPL')
makedepends=('arm-none-eabi-gcc' 'arm-none-eabi-newlib' 'dfu-util')
optdepends=('dfu-util: flash the programmer')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('d9c21ddc258018a45da2864b03b8f507a71b4086251867cad1cf157fa651ae211dded6717ee4920d8d417712c72e5dc47445271c4000e43f2dfcffa2e93fc1ab')

build() {
  cd $_pkgname-$pkgver

  make

  cd Firmware/source

  make
}

package() {
  cd $_pkgname-$pkgver

  install -Dm 755 easypdkprog "$pkgdir"/usr/bin/easypdkprog

  install -Dm 644 Linux_udevrules/70-stm32vcp.rules "$pkgdir"/usr/lib/udev/rules.d/70-stm32vcp.rules

  install -Dm 644 Firmware/source/build/EASYPDKPROG.bin "$pkgdir"/usr/share/$pkgname/EASYPDKPROG.bin
  install -Dm 644 Firmware/source/build/EASYPDKPROG.dfu "$pkgdir"/usr/share/$pkgname/EASYPDKPROG.dfu
}

