# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi-fast
pkgver=2.15
pkgrel=2
pkgdesc='CGI Interface for Fast CGI'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' 'perl-cgi>=4' 'perl-fcgi>=0.67')
url=https://metacpan.org/release/CGI-Fast
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI-Fast-$pkgver.tar.gz")
md5sums=(f9dac0982ccce4a9b89433b378b1573d)
sha512sums=(b984cbcf5d1d96dd9fcb58b704ae643e127fcdbe37b4d7e7560e5e1219abb849d72c4ff5a2eb45d7f076a126543b06fda282aabf2c3788b8a87f075c081d30c1)
_ddir="CGI-Fast-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
