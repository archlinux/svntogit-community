# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: David <david@pako.st>

pkgname=chicken
pkgver=5.1.0
pkgrel=1
pkgdesc='Feature rich R5RS Scheme compiler and interpreter'
arch=(x86_64)
url='https://call-cc.org/'
license=(BSD)
depends=(tk)
makedepends=(chicken)
options=(staticlibs)
source=("https://code.call-cc.org/releases/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('5c1101a8d8faabfd500ad69101e0c7c8bd826c68970f89c270640470e7b84b4b')

build() {
  make -j1 -C $pkgname-$pkgver \
    PLATFORM=linux \
    CSC_PROGRAM=chicken-csc \
    CSI_PROGRAM=chicken-csi \
    PREFIX=/usr
}

package() {
  cd $pkgname-$pkgver

  # Chicken had csc and csi first, but then mono introduced a conflict and
  # does not seem to want to change this. OpenBSD renamed csc and csi to
  # chicken-csc and chicken-csi 2018-12-12.
  make -j1 \
    PLATFORM=linux \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    CSC_PROGRAM=chicken-csc \
    CSI_PROGRAM=chicken-csi \
    install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: code.call-cc.org/releases/current/NEWS
# vim: ts=2 sw=2 et:
