# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Chaiwat Suttipongsakul <cwt114@gmail.com>

pkgname=lib32-libthai
pkgver=0.1.29
pkgrel=2
pkgdesc='Thai language support routines'
url='https://linux.thai.net/projects/libthai'
arch=('x86_64')
license=('LGPL')
depends=('lib32-libdatrie')
makedepends=('autoconf-archive' 'git')
provides=('libthai.so')
source=("git+https://github.com/tlwg/libthai.git#tag=v${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd libthai

  ./autogen.sh
}

build() {
  cd libthai

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static \
    --disable-doxygen-doc
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libthai install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim:set sw=2 sts=-1 et:
