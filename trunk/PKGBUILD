# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=flask-security
pkgname=python-flask-security-too
pkgver=4.1.5
pkgrel=3
pkgdesc='Quick and simple security for Flask applications'
url='https://github.com/jwag956/flask-security'
arch=('any')
license=('MIT')
depends=('python' 'python-flask' 'python-flask-login' 'python-flask-mail' 'python-flask-principal'
         'python-flask-wtf' 'python-flask-babelex' 'python-itsdangerous' 'python-passlib'
         'python-email-validator')
makedepends=('python-flask' 'python-flask-login' 'python-flask-mail' 'python-flask-principal'
             'python-flask-wtf' 'python-flask-babelex' 'python-itsdangerous' 'python-passlib'
             'python-sphinx' 'python-setuptools' 'python-pallets-sphinx-themes' 'python-sphinx-issues')
checkdepends=('python-pytest' 'python-pytest-runner' 'python-pytest-cov' 'python-peewee'
              'python-sqlalchemy' 'python-flask-sqlalchemy' 'python-bcrypt' 'python-pony'
              'python-flask-babel' 'python-zxcvbn' 'python-cryptography' 'python-pyqrcode' 'python-bleach'
              'python-phonenumbers' 'python-argon2_cffi')
provides=('python-flask-security')
conflicts=('python-flask-security')
source=(https://github.com/jwag956/flask-security/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('cf0a4ea6d8ad519ae5055898d2b7eb1bea8b3ceb3fffd2a79b47b92c6ccb72b692ed9483088fbea3987ff6fa3aaf60fcc388b7553a7e3e6ba8edfce63c714ad3')
b2sums=('18a1711e99794293fec17cca90ab9fcc7437c4882d7f14fb8c5abfddf137724a37ecf968cebd3801ef509f5b34f9671428405477894166d9c4350e1fd4a59054')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -e 's/ --translations//' \
      -e 's/ --pep8//' \
      -e 's/ --black//' \
      -e 's/ --flake8//' \
      -e 's/ --flakes//' \
      -i pytest.ini
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  python setup.py compile_catalog
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd ${_pkgname}-${pkgver}
  PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst CHANGES.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-security.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 docs/_build/man/flask-security.1 "${pkgdir}/usr/share/man/man1/python-flask-security.1"
}

# vim: ts=2 sw=2 et:
