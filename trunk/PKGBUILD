# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=nemo-qt-components
pkgver=2.0.15
pkgrel=3
pkgdesc="Qt Quick Components"
arch=('x86_64')
url="https://github.com/nemomobile/qt-components"
license=('BSD' 'FDL')
depends=('qt5-declarative' 'mlite')
makedepends=('git')
source=("git+https://github.com/nemomobile/qt-components.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd qt-components

  # Skip quick1 test during installation
  sed -i 's/bump_qml_version_installed.commands = test -d.*$/bump_qml_version_installed.commands = true/' qml.pri
}

build() {
  cd qt-components

  ./configure -meego -nomake tests
  make
}

package() {
  cd qt-components

  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 header.BSD "$pkgdir/usr/share/licenses/$pkgname/header.BSD"
}
