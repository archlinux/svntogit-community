# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger
pkgver=1.3.1
pkgrel=1
pkgdesc="Command-line interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-ansi-terminal" "haskell-base-compat" "haskell-file-embed" "haskell-here"
         "haskell-pretty-show" "haskell-temporary" "haskell-tabular" "haskell-hledger-lib"
         "haskell-unordered-containers" "haskell-cmdargs" "haskell-csv" "haskell-data-default"
         "haskell-hashable" "haskell-hunit" "haskell-mtl" "haskell-old-time" "haskell-megaparsec"
         "haskell-regex-tdfa" "haskell-safe" "haskell-split" "haskell-text" "haskell-utf8-string"
         "haskell-wizards" "haskell-shakespeare")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('71b1e4f75c582ada81487ac8a69a120d7c7d43510692600c833f546839faf4dbb2dc1525736688e421308e80ef5276eb95ce328f51453bb272aed2cd841fc070')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-oldtime -fterminfo -fthreaded
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
