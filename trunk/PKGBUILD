# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=blueman
pkgver=2.0.6
pkgrel=1
pkgdesc="GTK+ Bluetooth Manager"
arch=('x86_64')
url="https://github.com/blueman-project/blueman"
license=('GPL')
depends=('bluez' 'bluez-libs' 'gtk3' 'libnotify' 'python-cairo' 'python-dbus' 'python-gobject')
makedepends=('cython' 'intltool')
optdepends=('dnsmasq: Network Access Point (NAP) support'
            'networkmanager: Dial Up Networking (DUN) and Personal Area Networking (PAN) support'
            'net-tools: Network Access Point (NAP) support'
            'pulseaudio-bluetooth: audio devices support')
source=(https://github.com/blueman-project/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('728d7f21906a03644f7f19bce42535a71370d7e9d5702b834b2faf29b5a3812d')

prepare() {
  cd $pkgname-$pkgver

  # Fix path for bluetoothd
  sed -i 's|/usr/sbin/bluetoothd|/usr/lib/bluetooth/bluetoothd|' apps/blueman-report

  # Fix non-standard icon names
  sed -i 's/gtk-find/edit-find/' data/ui/device-list-widget.ui
  sed -i 's/mouse/input-mouse/' blueman/services/Input.py
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/$pkgname \
    --disable-schemas-compile \
    --with-dhcp-config='/etc/dhcpd.conf'
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
