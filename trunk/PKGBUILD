# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.4.5
pkgrel=1
pkgdesc='Device Tree Compiler'
url='http://www.devicetree.org/Device_Tree_Compiler'
arch=(x86_64)
license=(GPL2)
makedepends=(swig python2)
source=(https://www.kernel.org/pub/software/utils/dtc/dtc-$pkgver.tar.xz
        fix32bit.patch::https://github.com/dgibson/dtc/commit/f8872e29ce06d78d3db71b3ab26a7465fc8a9586.patch)
sha256sums=('042c7164806af34069d13ede59d85b8156d09f179f721c516dc37712d3a0f621'
            'e92bfc787b65e2610528d5c499835c083c1672455fc7998d202b7c986d5c5f1d')

prepare() {
  cd dtc-$pkgver
  sed -i 's/-Werror//' Makefile
  sed -i 's/python\b/python2/' tests/run_tests.sh
  patch -p1 < ../fix32bit.patch
}

build() {
  cd dtc-$pkgver
  make
}

check() {
  cd dtc-$pkgver
  make check
}

package() {
  cd dtc-$pkgver
  make DESTDIR="$pkgdir" SETUP_PREFIX="$pkgdir/usr" PREFIX=/usr install
}

