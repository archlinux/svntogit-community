#$Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Doug Newgard <scimmia at archlinux dot info>

pkgname=notepadqq
pkgver=1.3.6
pkgrel=1
pkgdesc='Notepad++-like text editor for Linux'
arch=('x86_64')
url='https://notepadqq.altervista.org/'
license=('GPL3')
depends=('hicolor-icon-theme' 'qt5-svg' 'qt5-webkit')
makedepends=('git' 'qt5-tools')
options=('!emptydirs')
_commit=ab074d30e02d49e0fe6957c1523e7fed239aff7d  # tags/v1.2.0
source=("git+https://github.com/notepadqq/notepadqq.git#commit=$_commit"
        "git+https://github.com/notepadqq/CodeMirror.git")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname
  git config submodule.src/editor/libs/codemirror.url "$srcdir/CodeMirror"
  git submodule update --init

  # Fix Qt5 version detection
  git cherry-pick -n c340237e3f795c1bbf53259ed812b97f6dfee737
}

build() {
  cd $pkgname
  qmake-qt5 PREFIX=/usr LRELEASE=/usr/bin/lrelease notepadqq.pro
  make
}

package() {
  cd $pkgname
  make INSTALL_ROOT="$pkgdir" install
}
