# Maintainer: Anatol Pomozov

pkgname=aws-c-io
pkgver=0.13.11
pkgrel=1
pkgdesc='AWS SDK module to handle all IO and TLS work for application protocols'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-io'
license=(Apache)
depends=(aws-c-common aws-c-cal s2n-tls)
makedepends=(cmake)
source=(aws-c-io-$pkgver.zip::https://github.com/awslabs/aws-c-io/archive/v$pkgver.zip
        fix_tests.patch::https://github.com/awslabs/aws-c-io/commit/837600295122d22fbb0acc73157a5fde3c57b198.patch)
sha256sums=('50c5ca1730071ca3c60776263497a940e16ff101380054e86eb6a7e55b506e06'
            'cfd0c4b4eb9c28caf3b4855c5dcaff4cc88776252cf97c5822032d37311100fa')

prepare() {
  cd aws-c-io-$pkgver

  patch -p1 < ../fix_tests.patch
}

build() {
  cd aws-c-io-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-io-$pkgver
  cmake --build build --target test --verbose
}

package() {
  cd aws-c-io-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
