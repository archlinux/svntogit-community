# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>

pkgname=kodi-addon-imagedecoder-raw
pkgver=20.1.0
_codename=Nexus
pkgrel=2
pkgdesc="Kodi raw imagedecoder addon"
arch=('x86_64')
url="https://github.com/xbmc/imagedecoder.raw"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-imagedecoder')
makedepends=('cmake' 'kodi-dev' 'libraw')
options=(!lto debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/imagedecoder.raw/archive/refs/tags/$pkgver-$_codename.tar.gz")
sha512sums=('eb775332de8fd94b5479d534cd1bd576b1fdfcd2478d9341c58491ad4b7f0a2533d31a72f145b1206683f3c382e172d10b36cbe425b52014e1ff0d91dda31105')

build() {
    cd "imagedecoder.raw-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'libraw')
    cd "imagedecoder.raw-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
