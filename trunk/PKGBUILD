# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.18.1
pkgrel=2
pkgdesc="Small applications for the GNOME Panel"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeApplets"
license=('GPL')
depends=('gnome-panel' 'libgtop' 'libtracker-sparql' 'gucharmap' 'python-gobject' 'cpupower' 'upower')
makedepends=('intltool' 'itstool' 'gnome-settings-daemon')
install=$pkgname.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        0001-stickynotes-don-t-ignore-font-type.patch)
sha256sums=('e09eb18f55be851da4c75c7d97df40130625ea548b2aa8d9db21e17bb06eefd5'
            '7b6120c5d8daebe71fceb1f6993bed1580c82ed28d75d6ca44f1f87add46325a')

prepare() {
  cd $pkgname-$pkgver
  # stickynotes: don't ignore font type
  patch -Np1 -i ../0001-stickynotes-don-t-ignore-font-type.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --with-cpufreq-lib=cpupower
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
