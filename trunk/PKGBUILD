# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.8.0
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=('x86_64')
url="https://amsynth.github.io/"
license=('GPL')
groups=('dssi-plugins' 'lv2-plugins')
depends=('gtk2' 'jack' 'liblo')
makedepends=('dssi' 'ladspa' 'intltool')
source=("https://github.com/amsynth/amsynth/releases/download/release-$pkgver/amsynth-$pkgver.tar.bz2")
md5sums=('54eaabc55d90ece5675b3cb3c2dc732d')

build() {
  cd $pkgname-$pkgver

  # enable required C++11 support
  export CXXFLAGS+=' -std=c++11'

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
