# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.1.0
pkgrel=2
pkgdesc="A general-purpose subtitle editor with ASS/SSA support"
arch=('i686' 'x86_64')
url="http://www.aegisub.org"
license=('GPL' 'BSD')
depends=('boost-libs' 'desktop-file-utils' 'ffms2' 'fftw' 'hunspell' 'lua51' 'wxgtk')
makedepends=('boost' 'intltool' 'mesa')
install="${pkgname}.install"
source=("http://ftp.aegisub.org/pub/releases/${pkgname}-${pkgver}.tar.xz"
        'aegisub-wx-stl.patch')
sha256sums=('3df061456bbab6bd955d7c5b63336be47392eafa21148ff6ed5727eaa6a29987'
            '1306b2caf2e192597a99931b4519023ea63b684f330e0838b1152d52b279f6c1')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../aegisub-wx-stl.patch
}

build() {
  cd ${pkgname}-${pkgver}/${pkgname}

  ./configure --prefix='/usr' --without-{portaudio,openal,oss}
  make
}

package() {
  cd ${pkgname}-${pkgver}/${pkgname}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
