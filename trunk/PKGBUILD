# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgbase=gap
pkgname=('gap' 'gap-data' 'gap-doc' 'gap-packages')
pkgver=4.7.7
pkgrel=1
pkgdesc="Groups, Algorithms, Programming: a system for computational discrete algebra"
arch=('i686' 'x86_64')
url="http://www.gap-system.org/"
license=('GPL')
source=("http://www.gap-system.org/pub/gap/gap47/tar.bz2/gap4r7p7_2015_02_13-15_29.tar.bz2")
sha256sums=('3a80bd46def5ea5387b283dc51fedcfcd6b3e4ec766f199df36d6ee30161c7c0')

build() {
  cd gap4r7
  ./configure --prefix=/usr --with-gmp=system
  make
}

package_gap() {
  depends=('gmp')
  optdepends=('gap-packages: extra packages' 'gap-data: additional databases' 'gap-doc: documentation')
  replaces=('gap-math')
  conflicts=('gap-math')
  cd gap4r7
  
  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r bin etc grp lib tst CITATION "$pkgdir"/usr/lib/gap
  
  mkdir -p "$pkgdir"/usr/lib/gap/pkg
  cp -r pkg/GAPDoc-* "$pkgdir"/usr/lib/gap/pkg

  mkdir -p "$pkgdir"/usr/bin
  ln -s /usr/lib/gap/bin/gap.sh "$pkgdir"/usr/bin/gap

# fix location
  sed -e 's|/build/gap/src/gap4r7|/usr/lib/gap|' -i "$pkgdir"/usr/lib/gap/bin/gap.sh  
}

package_gap-data() {
  depends=('gap')
  pkgdesc="Additional databases for GAP"
  cd gap4r7

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r prim small trans "$pkgdir"/usr/lib/gap
}

package_gap-doc() {
  depends=('gap')
  pkgdesc="Documentation for GAP"
  cd gap4r7

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r doc "$pkgdir"/usr/lib/gap
}

package_gap-packages() {
  depends=('gap')
  pkgdesc="Extra packages for GAP"
  cd gap4r7

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r pkg "$pkgdir"/usr/lib/gap

# GAPDoc is in main package
  rm -r "$pkgdir"/usr/lib/gap/pkg/GAPDoc-*
}
