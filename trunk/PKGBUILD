# $Id$
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=tarsnap
pkgver=1.0.37
pkgrel=2
pkgdesc='An online encrypted snapshotted backup service'
arch=('i686' 'x86_64')
url='http://www.tarsnap.com/'
license=('custom')
depends=('bzip2' 'openssl-1.0' 'xz')
makedepends=('e2fsprogs')
source=("https://www.tarsnap.com/download/${pkgname}-autoconf-${pkgver}.tgz")
validpgpkeys=('38DAFD2CEEB3E78D8627F5486D0D5B8046B64BEB')
md5sums=('755492b15a2ab53286182c3ba5f05c94')
sha256sums=('fa999413651b3bd994547a10ffe3127b4a85a88b1b9a253f2de798888718dbfa')

build() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"
  export CFLAGS="$CFLASG -I/usr/include/openssl-1.0"
  export LDFLAGS="$LDFLAGS -L/usr/lib/openssl-1.0 -lssl -lcrypto"

  ./configure --prefix=/usr --sysconfdir="/etc/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
