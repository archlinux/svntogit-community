# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Andreas Zwinkau
# Contributor: rabyte <rabyte__gmail>
# Contributor: Stefan Husmann < stefan-husmann@t-online.de>

pkgname=ttf-liberation
pkgver=2.1.3
pkgrel=1
pkgdesc='Font family which aims at metric compatibility with Arial, Times New Roman, and Courier New'
arch=('any')
url=https://github.com/liberationfonts/liberation-fonts
license=('custom:OFL')
makedepends=('fontforge' 'python-fonttools')
provides=('ttf-font')
source=("$url/archive/$pkgver/liberation-fonts-$pkgver.tar.gz")
b2sums=('f2bd1652aea043e06ba5ae17a5284162024c9033c5749e768068402565e595674a8fa6beb98ffdb09422daa61d2b00cdd620c3fe8c35e9e738152fa05b578532')

build() {
  cd liberation-fonts-$pkgver
  make build
}

package() {
  cd liberation-fonts-$pkgver
  install -Dm644 -t "$pkgdir/usr/share/fonts/${pkgname#ttf-}" \
    liberation-fonts-ttf-$pkgver/*.ttf
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
