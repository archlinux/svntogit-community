# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgname=('handbrake' 'handbrake-cli')
pkgver=0.9.5
pkgrel=2
arch=('i686' 'x86_64')
url="http://handbrake.fr/"
license=('GPL')
makedepends=('intltool' 'python2' 'yasm' 'wget' 'bzip2' 'gcc-libs' 'libnotify'
             'gstreamer0.10-base' 'gtk2' 'dbus-glib')
source=(http://downloads.sourceforge.net/project/handbrake/$pkgver/HandBrake-$pkgver.tar.bz2
        dbus-glib.patch
        libnotify-0.7.patch)
md5sums=('e17d3663fc36a985fe43e188695e3196'
         'f8c2a3b258b2c30ef4abb28cee80f8d4'
         'd56bf38d8e1a1d373e5f3b69522958b1')

build() {
  cd "$srcdir/HandBrake-$pkgver"

  patch -Np1 -i "$srcdir/dbus-glib.patch"
  patch -Np1 -i "$srcdir/libnotify-0.7.patch"

  # Use Python 2
  sed -i 's/python /python2 /' gtk/src/Makefile.am

  ./configure --prefix=/usr --force --disable-gtk-update-checks
  cd build
  make
}

package_handbrake() {
  pkgdesc="Multithreaded video transcoder"
  depends=('bzip2' 'gcc-libs' 'gstreamer0.10-base' 'libnotify'
           'hicolor-icon-theme')
  install=$pkgname.install

  cd "$srcdir/HandBrake-$pkgver/build"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/bin/HandBrakeCLI"
}

package_handbrake-cli() {
  pkgdesc="Multithreaded video transcoder (CLI)"
  depends=('bzip2' 'gcc-libs' 'zlib')

  cd "$srcdir/HandBrake-$pkgver/build"
  install -D HandBrakeCLI "$pkgdir/usr/bin/HandBrakeCLI"
}

# vim:set ts=2 sw=2 et:
