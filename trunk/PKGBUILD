# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sherlock Holo <sherlockya(at)gmail.com>
# Contributor: Ariel AxionL <i [at] axionl [dot] me>

pkgname=dns-over-https
pkgver=2.1.2
pkgrel=1
pkgdesc="Client and server software to query DNS over HTTPS, using Google DNS-over-HTTPS protocol"
url="https://github.com/m13253/dns-over-https"
arch=('x86_64')
license=('MIT')
backup=('etc/dns-over-https/doh-client.conf'
        'etc/dns-over-https/doh-server.conf')
provides=('dns-over-https-client' 'dns-over-https-server')
makedepends=('go-pie' 'golang-golang-x-crypto' 'golang-golang-x-net' 'golang-golang-x-sys' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/m13253/dns-over-https/archive/v$pkgver.tar.gz")
sha256sums=('b94c57adc888d0bc773137d9f9b01bb459ed8832a2129e0096a3b0ccf7053615')

build() {
  mkdir -p $srcdir/go/src
  export GOPATH="$srcdir/go:/usr/share/gocode"

  cd $srcdir/$pkgname-$pkgver
  sed -i 's/\/local//g' systemd/doh-{client,server}.service

  go get github.com/BurntSushi/toml github.com/gorilla/handlers github.com/miekg/dns
  
  go build -gcflags "all=-trimpath=$PWD" -asmflags "all=-trimpath=$PWD" -ldflags "-w -s -extldflags $LDFLAGS" -v -o client ./doh-client
  go build -gcflags "all=-trimpath=$PWD" -asmflags "all=-trimpath=$PWD" -ldflags "-w -s -extldflags $LDFLAGS" -v -o server ./doh-server
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 client "$pkgdir"/usr/bin/doh-client
  install -Dm644 doh-client/doh-client.conf "$pkgdir"/etc/dns-over-https/doh-client.conf
  install -Dm644 systemd/doh-client.service "$pkgdir"/usr/lib/systemd/system/doh-client.service
  install -Dm755 NetworkManager/dispatcher.d/doh-client "$pkgdir"/etc/NetworkManager/dispatcher.d/doh-client

  install -Dm755 server "$pkgdir"/usr/bin/doh-server
  install -Dm644 doh-server/doh-server.conf "$pkgdir"/etc/dns-over-https/doh-server.conf
  install -Dm644 systemd/doh-server.service "$pkgdir"/usr/lib/systemd/system/doh-server.service
  install -Dm755 NetworkManager/dispatcher.d/doh-server "$pkgdir"/etc/NetworkManager/dispatcher.d/doh-server

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
