# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Alois Nespor <info@aloisnespor.info>
# Contributor: speps <speps dot aur dot archlinux dot org>

pkgbase=rssguard
pkgname=(rssguard{,-nowebengine})
pkgver=4.2.6
pkgrel=1
pkgdesc='Simple (yet powerful) Qt feed reader'
arch=('x86_64')
url='https://github.com/martinrotter/rssguard'
license=('GPL3')
makedepends=('cmake' 'qt6-tools' 'qt6-multimedia' 'qt6-webengine' 'qt6-5compat')
optdepends=('clang: Support for beautification of message filter scripts')
source=("https://github.com/martinrotter/$pkgbase/archive/$pkgver/$pkgbase-$pkgver.tar.gz")
sha256sums=('282623a33d6a85c68f91800d021472ba858e50c301fb62d96b26c03755e0cf69')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT6=ON
  cmake --build build

  cmake -B build-nowebengine -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT6=ON \
    -DUSE_WEBENGINE=OFF
  cmake --build build-nowebengine
}

package_rssguard() {
  depends=('qt6-webengine' 'qt6-multimedia' 'qt6-5compat')

  DESTDIR="$pkgdir" cmake --install build
}

package_rssguard-nowebengine() {
  pkgdesc+=' without WebEngine support'
  depends=('hicolor-icon-theme' 'qt6-declarative' 'qt6-multimedia' 'qt6-5compat')
  conflicts=("$pkgbase")
  provides=("$pkgbase=$pkgver")

  DESTDIR="$pkgdir" cmake --install build-nowebengine

  # Ignore package by AppStream to avoid duplicated IDs
  rm -r "$pkgdir/usr/share/metainfo/"
  echo 'X-AppStream-Ignore=true' >> "$pkgdir/usr/share/applications/com.github.rssguard.desktop"
}
