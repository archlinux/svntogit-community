# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Keogh <davekeogh@archlinux.us>

pkgname=pycharm-community-edition
pkgver=2020.2.3
_build=202.7660.27
_gitcommit=4682003011bb42ffdb872d081e79d300bb393d17
pkgrel=4
pkgdesc='Python IDE for Professional Developers'
arch=(x86_64)
url=https://www.jetbrains.com/pycharm/
license=(APACHE)
depends=(
  giflib
  glib2
  java-runtime=11
  java11-openjfx
  python
  sh
  ttf-font
  libdbusmenu-glib
)
optdepends=(
  'ipython2: IPython integration for Python 2'
  'ipython: IPython integration for Python 3'
  'python2: Python 2 support'
)
makedepends=(
  ant
  git
  java-environment=8
  java8-openjfx
  python2-setuptools
  python-setuptools
)
source=(
  "git+https://github.com/JetBrains/intellij-community.git#commit=${_gitcommit}"
  pycharm.desktop
  pycharm.sh
)
sha256sums=('SKIP'
            'f727119ec7c8b96820e80712b1ee342de6a94ff72402e8f2d390cbff702f9b21'
            '7b5360b4977e934931acab775d04915417a5da9f3ea1dd44355525b0016cf65d')

prepare() {
  cd intellij-community

  echo ${_build} > build.txt
  sed '/def targetOs =/c def targetOs = "linux"' -i build/dependencies/setupJbre.gradle
  sed '/String targetOS/c   String targetOS = OS_LINUX' -i platform/build-scripts/groovy/org/jetbrains/intellij/build/BuildOptions.groovy
  sed -E 's|(<sysproperty key="jna.nosys")|<sysproperty key="intellij.build.target.os" value="linux" />\1|' -i build.xml
  sed -E 's|(<sysproperty key="java.awt.headless")|<sysproperty key="intellij.build.target.os" value="linux" />\1|' -i python/build.xml
  sed "s/-Xmx612m -XX:MaxPermSize=152m/-Xmx2048m -XX:MaxPermSize=512m/" -i python/build.xml
  sed 's|../build/plugins-autoupload-whitelist.txt|plugins-autoupload-whitelist.txt|' -i platform/build-scripts/groovy/org/jetbrains/intellij/build/impl/DistributionJARsBuilder.groovy
  touch plugins-autoupload-whitelist.txt
}

build() {
  cd intellij-community/python

  unset _JAVA_OPTIONS
  export JAVA_HOME=/usr/lib/jvm/java-11-openjdk
  export PATH="${JAVA_HOME}/bin:${PATH}"
  export JDK_16_x64=/usr/lib/jvm/java-8-openjdk
  export JDK_18_x64=/usr/lib/jvm/java-8-openjdk

  ant -Dintellij.build.target.os=linux build
  tar -xf ../out/pycharm-ce/artifacts/pycharmPC-${_build}-no-jbr.tar.gz -C "${srcdir}"

  cd ../../pycharm-community-${pkgver}

  python2 plugins/python-ce/helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
  python3 plugins/python-ce/helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
}

package() {
  cd pycharm-community-${pkgver}

  # workaround FS#40934
  sed -i 's/lcd/on/' bin/*.vmoptions

  rm -rf bin/fsnotifier{,-arm} lib/libpty/linux/x86

  install -dm 755 "${pkgdir}"/usr/share/{licenses,pixmaps,pycharm}
  cp -dr --no-preserve='ownership' bin lib plugins "${pkgdir}"/usr/share/pycharm/
  cp -dr --no-preserve='ownership' license "${pkgdir}"/usr/share/licenses/pycharm/
  ln -s /usr/share/pycharm/bin/pycharm.png "${pkgdir}"/usr/share/pixmaps/
  install -Dm 644 ../pycharm.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 755 ../pycharm.sh "${pkgdir}"/usr/bin/pycharm
}

# vim: ts=2 sw=2 et:
