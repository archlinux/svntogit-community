# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sergio A. Morales <sergiomorales@archlinux.cl>
# Contributor: soeren <nonick@posteo.de>

pkgname=gummi
epoch=1
pkgver=0.6.6
pkgrel=3
pkgdesc='Simple LaTex editor for GTK users'
arch=('x86_64')
url='http://gummi.midnightcoding.org/'
license=('MIT')
depends=('texlive-core' 'gtkspell' 'gtksourceview2' 'poppler-glib')
makedepends=('glib2' 'gtk2' 'pango' 'poppler' 'intltool' 'setconf' 'git')
source=("git://github.com/alexandervdm/gummi#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  # TODO: Simplify
  setconf autogen.sh AM_VERSION ''
  ./autogen.sh
  setconf Makefile.in PACKAGE_VERSION "'"$pkgver"'"
  setconf data/misc/Makefile.in PACKAGE_VERSION "'"$pkgver"'"
  echo $pkgver > dev/latest
  autoreconf
  setconf configure PACKAGE_VERSION "'"$pkgver"'"
  setconf configure PACKAGE_STRING "'""Gummi $pkgver""'"
  setconf configure PACKAGE_BUGREPORT "'https://bugs.archlinux.org/'"
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
