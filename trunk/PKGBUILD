# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.3.1
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG and JPEG file types, both lossless and lossy compression mode'
arch=('any')
url='https://github.com/Huluti/Curtail'
license=('GPL3')
depends=('gtk3' 'jpegoptim' 'optipng' 'pngquant' 'python-gobject')
makedepends=('appstream-glib' 'meson')
source=("https://github.com/Huluti/Curtail/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('bdf9a5863d42f04c45fc8d2fa41c962361d177eebfe916b42d436fe2e30e3ed0')

build() {
  arch-meson Curtail-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir "$pkgdir"
}
