# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Erik Johnson <palehose at gmail dot com>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-hglib
pkgver=2.6.1
pkgrel=3
arch=('any')
url="https://pypi.python.org/pypi/python-hglib"
license=('MIT')
pkgdesc="A library with a fast, convenient interface to Mercurial. It uses Mercurial's command server for communication with hg."
depends=('python' 'mercurial')
checkdepends=('python-nose')
source=(https://files.pythonhosted.org/packages/source/${pkgname:0:1}/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        hglib-fix-obsolete-markers.patch::https://www.mercurial-scm.org/repo/python-hglib/raw-rev/12e6aaef0f6e)
validpgpkeys=('2BCCE14F5C6725AA2EA8AEB7B9C9DC824AA5BDD5') # Augie Fackler <raf@durin42.com> v2.5
# v2.4 validpgpkeys=('FAD61395F642FC2B33C84B9A205781AC682A2D72') # Matthew Mackall <mpm@selenic.com>
sha256sums=('7c1fa0cb4d332dd6ec8409b04787ceba4623e97fb378656f7cab0b996c6ca3b2'
            'SKIP'
            'ea0d32a061746ebe621e070ddb4653bbb4f755503ed63489c100870e82b7f553')

prepare() {
  cd ${pkgname}-${pkgver}
  # pass tests with recent mercurial versions
  patch -p1 -i ../hglib-fix-obsolete-markers.patch
}

check() {
  cd ${pkgname}-${pkgver}
  python test.py
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
