# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=unagi-chan
pkgname=haskell-unagi-chan
pkgver=0.4.1.3
pkgrel=25
pkgdesc="Fast concurrent queues with a Chan-like API, and more"
url="https://github.com/jberryman/unagi-chan"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-atomic-primops' 'haskell-primitive')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b7eadbef75a789fa028a47a56fa6b923902e8032f6f49ab110ef7479d8f6a04a496272947e491270191099914d072c4100623de932d810f32300f785dbc3c8e4')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/jberryman/unagi-chan/issues/25
    runhaskell Setup test || :
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
