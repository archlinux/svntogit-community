# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=guvcview
pkgver=2.0.4
pkgrel=1
pkgdesc="A video viewer and capturer for the linux uvc driver"
arch=('i686' 'x86_64')
url="http://guvcview.sourceforge.net/"
license=('GPL')
depends=('portaudio' 'ffmpeg' 'gtk3' 'sdl2' 'gsl')
makedepends=('pkg-config' 'intltool')
optdepends=('pulseaudio: for PulseAudio support')
options=('!docs' '!buildflags')
source=("http://downloads.sourceforge.net/project/${pkgname}/source/${pkgname}-src-${pkgver}.tar.gz" guvcview-ffmpeg3.patch)
md5sums=('a6d900166ac2bba251a2c09cb602f1fe'
         '782b5f90f051b5c6217587a1fed7e6b1')

prepare() {
  cd $pkgname-src-$pkgver
# Fix build against ffmpeg 3.0
  patch -p1 -i ../guvcview-ffmpeg3.patch
}

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  ./configure --prefix=/usr \
              --disable-debian-menu
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
