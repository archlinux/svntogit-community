# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.6.4
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop gobject-introspection-runtime libcanberra libinput
         libsm libxkbcommon-x11 startup-notification zenity graphene pipewire)
makedepends=(meson intltool gobject-introspection)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bd0fc4542257bcfd7683e3d7694a122b99dc770da54cd12267ecdb2965c0c88d')
b2sums=('25658e858939cd6046ccfeee6fefba3f62b96555703259eb4d4706eebee43466832b161a8be7b29e360fa3cc26fb0cdceb9f10f26f79177268e61eec11ab6857')

build() {
  arch-meson build ${pkgname}-${pkgver}
  meson compile -C build
}

package() {
  meson install --destdir="${pkgdir}" -C build
}
