# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=clanlib
pkgver=3.0.1
pkgrel=1
pkgdesc="A multi-platform game development library."
arch=('i686' 'x86_64')
url="http://clanlib.org/"
license=('zlib')
depends=('alsa-lib' 'libjpeg' 'libmikmod' 'libpng' 'libvorbis' 'libxi' 'libxmu' 'sdl_gfx' 'freetype2' 'pcre' 'sqlite' 'libgl')
makedepends=('doxygen' 'graphviz' 'mesa')
source=(http://www.clanlib.org/download/releases-3.0/ClanLib-${pkgver}.tgz)
md5sums=('29f2ce542019c6d5d8793cdf4811e4f9')

build() { 
	cd ${srcdir}/ClanLib-${pkgver}

  ./autogen.sh
	./configure --prefix=/usr

	make
	make html
}

package() {
	cd ${srcdir}/ClanLib-${pkgver}

	make DESTDIR=${pkgdir} install
	make DESTDIR=${pkgdir} install-html

	install -d ${pkgdir}/usr/share/licenses/${pkgname}
	install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}

# vim: sw=2:ts=2 et:
