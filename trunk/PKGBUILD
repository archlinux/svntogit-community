# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=pretty-show
pkgname=haskell-pretty-show
pkgver=1.7
pkgrel=2
pkgdesc='Tools for working with derived `Show` instances and generic inspection of values.'
url="http://wiki.github.com/yav/pretty-show"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-lexer')
makedepends=('ghc' 'happy')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('3bd43ccc466117912f525fafa24ecd06913e191652847804fe58f7031235501ac927a15681ec4cf9f2450fea8e00005598ce408fa95cebe39ad21f19d1e99202')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
