# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Platon Pronko <platon7pronko@gmail.com>

pkgname=python-lsp-server
pkgver=1.7.1
pkgrel=2
pkgdesc="Fork of the python-language-server project, maintained by the Spyder IDE team and the community"
arch=(any)
url="https://github.com/python-lsp/python-lsp-server"
license=(MIT)
depends=(python-jedi python-lsp-jsonrpc python-pluggy python-ujson python-setuptools python-websockets python-docstring-to-markdown)
makedepends=(python-build python-installer python-setuptools-scm python-wheel)
optdepends=(
    'python-mccabe: complexity checking'
    'python-pycodestyle: linter - style checking'
    'python-pydocstyle: linter - docstring style checking'
    'python-pyflakes: linter to detect various errors'
    'python-pylint: code linting'
    'python-rope: for completions and renaming'
    'autopep8: code formatting'
    'flake8: error checking'
    'yapf: code formatting (preferred)'
    'python-whatthepatch: required for yapf support'
)
checkdepends=(
    python-pytest
    python-flaky
    python-mccabe
    python-pycodestyle
    python-pydocstyle
    python-pyflakes
    python-pylint
    python-rope
    autopep8
    flake8
    yapf
    python-whatthepatch
    python-matplotlib
    python-numpy
    python-pandas
    python-pyqt5
    python-appdirs
)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ce913f16.patch)
sha256sums=('67473bb301f35434b5fa8b21fc5ed5fac27dc8a8446ccec8bae456af52a0aef6'
            '05cce14f51081854b33df34a22f0ecf07060c3195412cce49238f36d3cf1b05f')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|autopep8>=1.6.0,<1.7.0|autopep8>=1.6.0|' -i pyproject.toml
  sed 's|flake8>=5.0.0,<7|flake8>=5.0.0|' -i pyproject.toml
  sed 's|mccabe>=0.7.0,<0.8.0|mccabe>=0.7.0|' -i pyproject.toml
  sed 's|pycodestyle>=2.9.0,<2.11.0|pycodestyle>=2.9.0|' -i pyproject.toml
  sed 's|pyflakes>=2.5.0,<3.1.0|pyflakes>=2.5.0|' -i pyproject.toml
  sed 's|pydocstyle>=6.2.0,<6.3.0|pydocstyle|' -i pyproject.toml
  patch -Rp1 -i ../ce913f16.patch # Revert pydocstyle bump until package is updated
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build -wn
}

check() {
  cd ${pkgname}-${pkgver}
  # Disable coverage
  sed -i '/--cov/d' pyproject.toml
  pytest -vv --color=yes
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer -d="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
