# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pig Monkey <pm@pig-monkey.com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>
# Contributor: Javier Ti√° <javier dot tia at gmail dot com>

pkgname=visidata
pkgver=2.10.1
pkgrel=1
pkgdesc='Terminal spreadsheet multitool for discovering and arranging data'
arch=(any)
url=https://www.visidata.org
license=(GPL3)
depends=(python
         python-dateutil
         python-setuptools)
optdepends=('python-dnslib: pcap support'
            'python-fonttools: ttf/otf support'
            'python-h5py: hdf5 support'
            'python-lxml: html/xml support'
            'python-openpyxl: xlsx support'
            'python-pandas: dta (Stata) support'
            'python-pdfminer: pdf support'
            'python-psycopg2: postgres support'
            'python-pyaml: yaml/yml support'
            'python-pypng: png support'
            'python-requests: http support'
            'python-tabulate: tabulate saver support'
            'python-urllib3: remote zip support'
            'python-vobject: vcf support'
            'python-wcwidth: tabulate saver with unicode support'
            'python-xlrd: xls support')
# optdepends+=( # AUR only optdepends
#             'python-dpkt: pcap support'
#             'python-mapbox-vector-tile: mbtiles support'
#             'python-namestand: graphviz support'
#             'python-pyshp: shapefiles support')
makedepends=(python-{build,installer,wheel})
checkdepends=(python-pytest)
_archive="$pkgname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$_archive.tar.gz")
sha256sums=('cd0850b42072ac5c2b5ee1a79b14212e654a22fe1cf448bd62f10cac9dc025e3')

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	# Testing commands assumes reading an rc file not present in chroot build env
	pytest -k 'not test_baseCommands'
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
