# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: John D Jones III <j[nospace]n[nospace]b[nospace]e[nospace]k[nospace]1972 -_AT_- the domain name google offers a mail service at ending in dot com>
# Generator  : CPANPLUS::Dist::Arch 1.25

pkgname=perl-crypt-openssl-dsa
pkgver=0.20
pkgrel=3
pkgdesc="Digital Signature Algorithm using OpenSSL"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('openssl')
url='https://search.cpan.org/dist/Crypt-OpenSSL-DSA'
source=("https://search.cpan.org/CPAN/authors/id/K/KM/KMX/Crypt-OpenSSL-DSA-$pkgver.tar.gz")
sha512sums=('d9441c63af6eb9c33d040e3734f8b047e27c864c936ddc660323c2794b5cfee76fa37f9f232b3f6c61345bfbc7e5928b2265ecb975189d62af2afa0d07c911e1')

build() {
  cd Crypt-OpenSSL-DSA-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Crypt-OpenSSL-DSA-$pkgver
  make test
}

package() {
  cd Crypt-OpenSSL-DSA-$pkgver
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
