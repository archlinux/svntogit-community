# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Current Maintainer:  Patrick Palka <patrick@parcs.ath.cx>
# Previous Maintainer:  Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>
# Development: http://closure.ath.cx/aur-dev

pkgname=cgit
pkgver=0.9.0.2
_gitver=1.7.4
pkgrel=3
pkgdesc="A web interface for git written in plain C"
arch=('i686' 'x86_64')
url="http://hjemli.net/git/cgit"
license=('GPL')
depends=('openssl')
makedepends=('git' 'zlib' 'curl' 'asciidoc')
install=cgit.install
#	"http://www.kernel.org/pub/software/scm/git/git-${_gitver}.tar.bz2"
source=("http://hjemli.net/git/cgit/snapshot/$pkgname-$pkgver.tar.bz2"
	"http://arch.p5n.pp.ru/~sergej/dl/2011/git-${_gitver}.tar.bz2"
	"apache.example.conf")
md5sums=('5b92b07e0107ff24a783adfb8c0cd1f2'
         'fa24dc1ef0b663327ea0fe1981365b39'
         'bd5696bea0654bfcf98f249b1a36737d')

build() {
  cd "$pkgname-$pkgver"
  _cgit_gitver=`grep 'GIT_VER = ' Makefile | cut -f3 -d\ `
  [ "x${_gitver}" == "x${_cgit_gitver}" ] || { msg "Bad git version"; return 1; }
  rm -rf git/*
  mv ../git-${_gitver}/* git/
  make
  make doc-man
}

package() {
  cd "$pkgname-$pkgver"
  make CGIT_SCRIPT_PATH=/usr/share/webapps/cgit DESTDIR="$pkgdir" install install-man
  install -vDm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -vd "$pkgdir/var/cache/$pkgname"
  install -vDm0644 $srcdir/apache.example.conf $pkgdir/etc/webapps/cgit/apache.example.conf
  mkdir -p $pkgdir/usr/lib/cgit
  mv $pkgdir/usr/share/webapps/cgit/cgit.cgi $pkgdir/usr/lib/cgit
  ln -sf ../../../lib/cgit/cgit.cgi $pkgdir/usr/share/webapps/cgit/cgit.cgi
}
