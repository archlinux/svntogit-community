# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-edit
pkgver=0.7.0
pkgrel=3
pkgdesc='Managing cargo dependencies from the command line'
url='https://github.com/killercup/cargo-edit/releases'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('cargo' 'libgit2')
source=(https://github.com/killercup/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('462712406e4cc2cf2baf3cbc95c89a306f1683ebe184611b77d20a1d1176f98cc7cd855c0a727e303b19ecfd739b03d714f16779feb4b339692176aa7996e2ad')
b2sums=('9fae3bc32ee1747be4e7c5c23987fe36b09648884422a3f19e801dd8e448e8253899069b33a3b1cf235141866d8331037844294dc0860b4ac126a3ac849cfa25')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  # tests depend on target/debug/
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 \
    target/release/cargo-add \
    target/release/cargo-rm \
    target/release/cargo-upgrade \
    -t "${pkgdir}/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
