# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Daniele Paolella <dp@mcrservice.it>

pkgname=supervisor
pkgver=3.0a10
pkgrel=1
pkgdesc='A system for controlling process state under UNIX'
arch=('any')
url='http://supervisord.org'
license=('custom')
depends=('python2-distribute' 'python2-meld3')
backup=('etc/supervisord.conf')
options=('emptydirs')
source=("http://pypi.python.org/packages/source/s/$pkgname/$pkgname-$pkgver.tar.gz" 'supervisord' 'supervisord.conf')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	rm -rf "src/supervisor/medusa/"{debian,demo,docs,test}
	find -type f -name "*.py" -exec sed -i 's|/usr/bin/env python|/usr/bin/env python2|' {} \;
	python2 setup.py install --prefix=/usr --root="$pkgdir"
	install -D -m600 "$srcdir/supervisord.conf" "$pkgdir/etc/supervisord.conf"
	install -D "$srcdir/supervisord" "$pkgdir/etc/rc.d/supervisord"
	install -d "$pkgdir/usr/share/licenses/$pkgname"
	install -d "$pkgdir/var/log/$pkgname"
	install -d "$pkgdir/etc/supervisor.d"
	mv "$pkgdir/usr/doc/COPYRIGHT.txt" "$pkgdir/usr/share/licenses/$pkgname"
	mv "$pkgdir/usr/doc/LICENSES.txt" "$pkgdir/usr/share/licenses/$pkgname"
	mkdir -p "$pkgdir/usr/share/doc"
	mv "$pkgdir/usr/doc" "$pkgdir/usr/share/doc/$pkgname"
}

md5sums=('99c6fbd45bade87301296b7a597fb68e'
         '38931759dc31fc1feaccccaa32059f38'
         '5e459a36bb3133faef77d4a1204df541')
sha256sums=('63620d0431bafff6f346fb4df5d9a7745588b5dee1ca5fec2ddff2298107d69f'
            '44ef896edfd9b22d4752287bd635225584257e3f869f48772564e12903060207'
            '0f26c02fbad753171e77fd0df13cf2817a69cc1ea3289b1f1cda5d1ee6a6902a')
