# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Benzo <mb_benzo@yahoo.es>

pkgname=osmo
pkgver=0.2.12
pkgrel=1
pkgdesc="A handy personal organizer"
_svnver=937
arch=('i686' 'x86_64')
url="http://clayo.org/osmo/"
license=('GPL')
depends=('gtkspell' 'hicolor-icon-theme' 'libical' 'libnotify' 'webkitgtk2' 'libgringotts>=1.2.1' 'libxml2')
makedepends=('pkgconfig' 'libtar')
optdepends=('sox: to play sounds')
install=osmo.install
source=(ftp://ftp.archlinux.org/other/community/osmo/osmo-${_svnver}.tar.xz
        osmo-0.2.10-configure.patch)
md5sums=('ddcd0b23a24d8f1ec5abc904742233bf'
         '122fc15441b6215081772a00aa532394')

prepare() {
  cd osmo-pim
  patch -Np1 -i ../osmo-0.2.10-configure.patch
  autoreconf -fi
}

build() {
  cd osmo-pim
  ./configure --prefix=/usr
  make 
}

package() {
  cd osmo-pim
  make DESTDIR="$pkgdir" install
}
