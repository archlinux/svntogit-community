# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-3-0
_patchver=1
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=1
pkgdesc='Computer Algebra System for polynomial computations'
arch=(x86_64)
url='https://www.singular.uni-kl.de/'
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen)
source=(ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz
#source=(ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz
         doc-$pkgver.tbz2::ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/doc.tbz2)
sha256sums=('9ecf2228c9238d23456b8ffbe60b1b8d20f6f45b36e7af0ab2945e80d7c86e9b'
            '75c5d0760b0591edcd4c10c8514a38545e9555fbbe923e6e8d3e4aa7ce8f0fd1')
options=(!zipman)

build() {
  cd singular-${_majver//-/.}p${_patchver}
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --docdir=/usr/share/doc \
    --enable-bigintm-module \
    --enable-bigintm-module \
    --enable-Order-module \
    --enable-python-module \
    --enable-gfanlib-module \
    --enable-polymake-module
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd singular-${_majver//-/.}p${_patchver}
  make check
}
  
package() {
  cd singular-${_majver//-/.}p${_patchver}
  make DESTDIR="$pkgdir" install
}
