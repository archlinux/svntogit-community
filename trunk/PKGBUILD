# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgbase=python-fastpbkdf2
pkgname=(python-fastpbkdf2 python2-fastpbkdf2)
pkgver=0.2
pkgrel=3
pkgdesc="A faster implementation of PBKDF2"
arch=('x86_64')
license=('Apache')
url="https://github.com/Ayrx/python-fastpbkdf2"
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner') 
source=("git+https://github.com/Ayrx/python-fastpbkdf2.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a python-fastpbkdf2{,-py2}
}

build() {
  cd "$srcdir"/python-fastpbkdf2
  python setup.py build

  cd "$srcdir"/python-fastpbkdf2-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-fastpbkdf2
  python setup.py pytest

  cd "$srcdir"/python-fastpbkdf2-py2
  python2 setup.py pytest
}

package_python-fastpbkdf2() {
  depends=('python-cffi' 'python-six')

  cd python-fastpbkdf2
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-fastpbkdf2() {
  depends=('python2-cffi' 'python2-six')

  cd python-fastpbkdf2-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
