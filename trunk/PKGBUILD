# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=4.8.1
pkgrel=2
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('x86_64')
url="http://kmymoney2.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime' 'shared-mime-info' 'aqbanking'
         'libofx' 'libalkimia' 'boost-libs' 'libical')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl' 'boost' 'doxygen' 'patch')
replaces=('kmymoney2')
changelog=$pkgname.changelog
source=(http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.7z)
sha256sums=('4435acabb3c9771dd39471651767c857087b5fe4f5cd43132c19cdc92aa6cbab')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  mkdir build
  cd build

  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver/build

  make DESTDIR="${pkgdir}" install
}
