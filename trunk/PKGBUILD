# Maintainer: Caleb Maclennana <caleb@alerque.com>

pkgname=python-pytest-param-files
_pyname=${pkgname#python-}
pkgver=0.3.4
pkgrel=3
pkgdesc='Create pytest parametrize decorators from external files '
arch=(any)
url="https://github.com/chrisjsewell/$_pyname"
license=(MIT)
depends=(python
         python-pytest)
makedepends=(python-{build,installer,wheel}
             python-flit-core)
# _archive="$_pyname-$pkgver"
# source=("$url/archive/v$pkgver/$_archive.tar.gz")
_archive="${_pyname//-/_}-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('6c0c324f9d91bd16a5d63ab5bb78bd2d19ca5c6f2b68b6acbc4ae61fb1b6893d')

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	# pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
