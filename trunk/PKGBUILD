# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fulcio
pkgver=0.5.2
pkgrel=1
pkgdesc="free Root-CA for code signing certs - issuing certificates based on an OIDC email address."
arch=('x86_64')
url="https://github.com/sigstore/fulcio"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/fulcio/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('232c2d2017e4b1ecb61b6f94a2dd1211d95f790a2605063234e0f5a135d8baaf3c07bc2bb11e5397b11956f92a69b55d58cd119d98b233641bbc9b9faa11d676')
b2sums=('80ea3ac7561d336e90aefcfcc8e9806fdef853c66ace318d53d63a18bf4599b96786517f415cf3e628484e915d4c2b8ef92fedba3f3fa5f7fb065aa3af71b0b2')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/fulcio "${pkgdir}/usr/bin/${pkgname}"
}
