# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-pyppmd
_pyname=${pkgname#python-}
pkgver=0.18.3
pkgrel=1
pkgdesc='PPM compression/decompression library'
arch=(x86_64)
url="http://github.com/miurahr/$_pyname"
license=(LGPL)
depends=(python)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
checkdepends=(python-pytest-benchmark python-py-cpuinfo python-hypothesis)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/$_archive.tar.gz")
sha256sums=('6c74ce28213044e33a595279019e8d71512d5d8188826d106df9083f2b3006c4')

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	local _pyver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
	export PYTHONPATH="$PWD/build/lib.linux-$CARCH-$_pyver"
	python -m pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
