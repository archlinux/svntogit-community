# Maintainer:
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-media
pkgver=1.18.2
pkgrel=1
url="https://mate-desktop.org"
pkgdesc="MATE Media Tools"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libcanberra' 'libmatemixer' 'mate-desktop')
makedepends=('intltool')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
groups=('mate-extra')
conflicts=('mate-media-gtk3')
replaces=('mate-media-gtk3')
sha256sums=('6101df5a694251e7032ef69f164cb197bd123c8fbf65ad28cca5229ab73c631f')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
