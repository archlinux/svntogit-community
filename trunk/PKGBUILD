# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-osi
pkgver=0.107.0
pkgrel=2
pkgdesc="COIN-OR Open Solver Interface"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/Osi"
license=('EPL')
groups=('coin-or')
depends=('coin-or-coinutils')
makedepends=('gcc-fortran' 'lapack' 'glpk')
source=("http://www.coin-or.org/download/source/Osi/Osi-${pkgver}.tgz")
sha1sums=('cb50dbb9e39b1f4788423a273d32babd6a1c43d0')


build() {
  cd Osi-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr --enable-dependency-linking \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/"
  make
}

package() {
  cd Osi-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
