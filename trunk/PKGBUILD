# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Trương Xuân Tính <xuantinh at gmail dot com>

pkgname=ibus-unikey
pkgver=0.6.1
pkgrel=7
pkgdesc='IBus module for Vietnamese Keyboard'
arch=('x86_64')
license=('GPL')
url='https://code.google.com/archive/p/ibus-unikey/'
depends=('ibus')
makedepends=('intltool')
source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/ibus-unikey/${pkgname}-${pkgver}.tar.gz"
        ibus-unikey-gcc6.patch)
sha256sums=('989bbea706d2d8d7b86630b1f2b4f63d8ed5167a1cc2b8fafd7f2198ec78d734'
            '928b12d470994072bb4b08472b365b340a24e07e83c2020905de3ecaf61a1dee')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../ibus-unikey-gcc6.patch # Fix build with GCC 6 (Gentoo)
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --with-gtk-version=3

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
