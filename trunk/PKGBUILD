# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>
pkgname=e3
pkgver=2.8
pkgrel=3
pkgdesc='Editor written in assembly. Tiny, with no library dependencies'
arch=('x86_64' 'i686')
url="http://sites.google.com/site/e3editor/"
license=('GPL2')
makedepends=('nasm')
options=('!strip')
source=("http://sites.google.com/site/e3editor/Home/$pkgname-$pkgver.tgz")
sha256sums=('035737d0cc82b287386fdff8682b2c23ef620d7ef97dff7a1b1fe1777e4c4fb7')
  
build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  sed -i 's:$(BINDIR)/e3 :e3 :' Makefile
  make clean
  if [ "$CARCH" == "i686" ]; then
    make EXMODE=_ 32
  else
    make EXMODE=_ 64
  fi
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" MANDIR="$pkgdir/usr/share/man/man1" install
  install -Dm644 COPYING.GPL "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
