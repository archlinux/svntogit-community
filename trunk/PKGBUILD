# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jon Bergli Heier <snakebite@jvnv.net>

pkgname=python-pyxattr
_pkgname=pyxattr
pkgver=0.7.1
pkgrel=3
pkgdesc='A Python extension module that allows you to manipulate the extended attributes.'
arch=('x86_64')
url='https://pyxattr.k1024.org/'
license=('GPL')
makedepends=('python' 'python-pytest' 'python-setuptools')
depends=('python')
source=("https://pyxattr.k1024.org/downloads/pyxattr-$pkgver.tar.gz"{,.asc})
md5sums=('f3341e703489452afaf68b336be5d32c'
         'SKIP')
validpgpkeys=('CB94E3AA3B1755D61EBB19A5F66E3E419F84F4DE') # Iustin Pop <iustin@k1024.org>

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py build
}

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py test
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py install --root="${pkgdir}"
}
