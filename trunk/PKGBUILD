# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgbase=python-redis
pkgname=('python-redis' 'python2-redis')
pkgver=3.3.8
pkgrel=1
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="https://github.com/andymccurdy/redis-py"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock' 'pifpaf' 'redis')
source=("https://pypi.io/packages/source/r/redis/redis-$pkgver.tar.gz")
sha512sums=('7bdaa0815be3a0304bdca5d791b3f26a34cc8fb696581ff3144b486c5c40c476218cc6cf12868e08364b96eaa71f2dffb86de9c945ac3ffe5f6c9fe914a2ecd8')

prepare() {
  cp -a redis-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/redis-$pkgver
  python setup.py build

  cd "$srcdir"/redis-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/redis-$pkgver
  pifpaf run redis py.test || warning "https://github.com/andymccurdy/redis-py/issues/1073"

  cd "$srcdir"/redis-$pkgver-py2
  pifpaf run redis py.test2 || warning "https://github.com/andymccurdy/redis-py/issues/1073"
}

package_python-redis() {
  depends=('python')

  cd redis-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-redis() {
  depends=('python2')

  cd redis-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
