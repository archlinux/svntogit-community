# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.3.3
pkgrel=2
pkgdesc="Free, open source software for digital DJ'ing."
arch=(x86_64)
url="https://www.mixxx.org"
license=(GPL2)
groups=(pro-audio)
# TODO: package libshout-idjc
depends=(gcc-libs glibc hidapi lame libglvnd libmad libmodplug libx11 openssl
opusfile qt5-base qt5-script qt5-svg qt5-x11extras qtkeychain soundtouch speex
sqlite taglib upower wavpack)
makedepends=(chromaprint cmake faad2 ffmpeg fftw flac glib2 glu gperftools
libid3tag libogg libsndfile libusb libvorbis lilv lv2 qt5-tools portaudio
portmidi protobuf rubberband vamp-plugin-sdk)
checkdepends=(xorg-server-xvfb)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/${pkgname}dj/$pkgname/archive/$pkgver.tar.gz)
sha512sums=('6bd3809802f2f66af7d840db6ce006d7e46a824cf460ea9ae1bcff81f17483debd2ef47fd68d0da82671afaf01b6c3bc2522e02b41d992e08bc98f4d3d68120f')
b2sums=('1b3d863341afbdce86c61c4689494a334a689d02aaf5a700aa74d3291843f028d77ef7d62e545d05377ffb1f9a5077529c3a77a2b4a5e8ce20823728a8fe8d80')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  cd $pkgname-$pkgver
  xvfb-run ../build/$pkgname-test
}

package() {
  depends+=(libFLAC.so libavcodec.so libavformat.so libavutil.so
  libchromaprint.so libfftw3.so libglib-2.0.so libid3tag.so libgobject-2.0.so
  liblilv-0.so libogg.so libportaudio.so libportmidi.so libprotobuf-lite.so
  librubberband.so libsndfile.so libusb-1.0.so libvorbis.so libvorbisenc.so
  libvorbisfile.so)

  make DESTDIR="$pkgdir" install -C build
}
