# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=netCDF4
_mpi=openmpi
pkgname=python-${_pkg,,}-${_mpi}
pkgver=1.6.0
pkgrel=1
pkgdesc="Python/NumPy interface to the netCDF C library (${_mpi} version)"
arch=(x86_64)
url="https://unidata.github.io/${_pkg,,}-python"
license=(MIT)
depends=(python-numpy python-cftime python-mpi4py "netcdf-${_mpi}")
makedepends=(cython python-setuptools)
checkdepends=(openssh)
conflicts=(python-netcdf4)
provides=(python-netcdf4)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz
        ${pkgname}-fix-netcdf-filters-detection.patch::https://github.com/Unidata/netcdf4-python/pull/1173.patch)
sha256sums=('95efa373d9a3e1cd0df7193e76e6680d9eca28e60097ca8139afea8a4346ba63'
            'c407affa80e49547dca938ef1cb4764dedb9abd8f32a3e402d66d29aa7137f4c')

prepare() {
  # Compressor filters are not packaged currently
  patch -p1 -d ${_pkg}-${pkgver} < ${pkgname}-fix-netcdf-filters-detection.patch || true
}

build() {
  cd ${_pkg}-${pkgver}
  USE_NCCONFIG=1 python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}/test
  export OMPI_MCA_opal_warn_on_missing_libcuda=0
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="../build/lib.linux-${CARCH}-${python_version}" python -B ./run_all.py
}

package() {
  cd ${_pkg}-${pkgver}
  USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
