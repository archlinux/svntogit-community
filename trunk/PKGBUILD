# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=('python-pyinotify' 'python2-pyinotify')
pkgver=0.9.6
pkgrel=4
pkgdesc='Python module used for monitoring filesystems events on Linux platforms with inotify.'
arch=('any')
url="http://github.com/seb-m/pyinotify"
license=('custom:MIT')
makedepends=('python' 'python2')
source=("https://github.com/seb-m/pyinotify/archive/${pkgver}.tar.gz")
md5sums=('33f4e599c22a3ac31b684335b4a535a0')

build() {
  cp -r $srcdir/pyinotify-$pkgver  $srcdir/pyinotify2-$pkgver

  cd $srcdir/pyinotify2-$pkgver

  python2 setup.py build

  cd $srcdir/pyinotify-$pkgver

  python3 setup.py build
}

package_python-pyinotify() {
  depends=('python')

  cd $srcdir/pyinotify-$pkgver

  python3 setup.py install --root=${pkgdir} -O1

  install -d ${pkgdir}/usr/share/licenses/python-pyinotify
  install -m644 COPYING \
    ${pkgdir}/usr/share/licenses/python-pyinotify
}

package_python2-pyinotify() {
  depends=('python2')
  replaces=('pyinotify')

  cd $srcdir/pyinotify2-$pkgver

  sed \
    -e 's_#!/usr/bin/env python_&2_' \
  -i python2/pyinotify.py

  python2 setup.py install --root=${pkgdir} -O1

  install -d ${pkgdir}/usr/share/licenses/python2-pyinotify
  install -m644 COPYING \
    ${pkgdir}/usr/share/licenses/python2-pyinotify
}
