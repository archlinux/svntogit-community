# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=1.2.1
pkgrel=1
pkgdesc='A Modern Linker'
arch=('x86_64')
url="https://github.com/rui314/mold"
license=('AGPL3')
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib' 'tbb') # xxhash is bundled
makedepends=('git')
checkdepends=('clang' 'libdwarf')
options=('debug')
_commit='c8d8f86a52084c96e2663d9f692c51e98c04cc2f'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # use /usr/lib instead of /usr/libexec
  sed -i "s/libexec/lib/" Makefile
}

build() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1
}

check() {
  cd "$pkgname"

  # temporarily remove failing tests
  for failing_test in exception; do
    rm -vf "test/elf/$failing_test.sh"
  done

  make \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1 \
    check
}

package() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1 \
    DESTDIR="$pkgdir" \
    install

  ln -vsf /usr/bin/mold "$pkgdir/usr/lib/mold/ld"
}
