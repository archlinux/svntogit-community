# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=mtxclient
pkgver=0.9.1
pkgrel=2
pkgdesc="Client API library for the Matrix protocol"
arch=('x86_64')
url="https://github.com/Nheko-Reborn/mtxclient"
license=('MIT')
depends=(gcc-libs glibc)
makedepends=(cmake coeurl fmt libolm meson nlohmann-json openssl re2 spdlog git)
provides=(libmatrix_client.so)
source=(git+https://github.com/Nheko-Reborn/mtxclient.git#tag=v${pkgver})
sha256sums=('SKIP')
# validpgpkeys=('D58B462425A6A37125C6FEDB9206AE1B231E05BB') # Nicolas Werner @deepbluev7 https://nheko.im/deepbluev7.gpg
#                                                           # Nicolas Werner <nicolas.werner@hotmail.de>
#                                                           # Nicolas Werner <nicolas@nekodev.net>
#                                                           # Nicolas Werner <nicolas.werner@gmx.net>

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "$pkgname" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DBUILD_LIB_EXAMPLES=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_LIB_TESTS=OFF \
    -DBUILD_LIB_EXAMPLES=OFF \
    -Wno-dev
  cmake --build build --verbose
}

# check() {
#   # Tests require a synapse docker instance running and a commit so disable for now
#   # git cherry-pick --no-commit 6daf21627c36704e28245e52ad525508ec5be51f
#   ninja test -C build
# }

package() {
  depends+=(
    coeurl libcoeurl.so
    fmt libfmt.so
    libolm libolm.so
    openssl libcrypto.so
    re2 libre2.so
    spdlog libspdlog.so
  )
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ft=sh sw=2 sts=2 et:
