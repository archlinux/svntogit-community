# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
epoch=1
pkgver=0.9.3.2
pkgrel=1
pkgdesc='A BibTeX editor for KDE'
arch=(x86_64)
url='https://apps.kde.org/kbibtex/'
license=(GPL)
depends=(poppler-qt5 qt5-webengine qt5-xmlpatterns ktexteditor)
optdepends=('okular: Document preview')
makedepends=(extra-cmake-modules kdoctools qoauth)
source=(https://download.kde.org/stable/KBibTeX/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0733c24ca322327cf3d92fa36e4e196c4b9dc895f911a4c3558e7db5efc0c457'
            'SKIP')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
