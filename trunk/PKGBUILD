# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=1.0.4
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-tool-kit')
makedepends=('git' 'qt5-tools')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/dde-calendar.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd dde-calendar
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-calendar
  make INSTALL_ROOT="$pkgdir" install
}
