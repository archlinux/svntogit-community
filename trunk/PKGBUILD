# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Christoph 'delmonico' Neuroth <delmonico@gmx.net>

pkgname=librsync
pkgver=0.9.7
pkgrel=5
pkgdesc="A free software library that implements the rsync remote-delta algorithm (rdiff)"
arch=('i686' 'x86_64')
url="http://librsync.sourceforge.net/"
license=('GPL')
depends=('popt' 'zlib' 'bzip2')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/librsync/$pkgname-$pkgver.tar.gz
        lfs-overflow.patch)
md5sums=('24cdb6b78f45e0e83766903fd4f6bc84'
         'e18536482c0a32e7004c46eac6a53b2d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Patch for large files (https://bugzilla.redhat.com/show_bug.cgi?id=207940)
  patch -Np1 -i "$srcdir/lfs-overflow.patch" || return 1

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install || return 1
}

# vim:set ts=2 sw=2 et:
