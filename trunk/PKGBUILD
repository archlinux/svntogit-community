# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-config
pkgname=(python-oslo-config python2-oslo-config)
pkgver=3.24.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-netaddr' 'python2-netaddr'
             'python-stevedore' 'python2-stevedore' 'python-debtcollector' 'python2-debtcollector'
             'python-oslo-i18n' 'python2-oslo-i18n' 'python-rfc3986' 'python2-rfc3986')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-sphinx' 'python2-sphinx')
options=('!emptydirs')
source=("git+https://git.openstack.org/openstack/oslo.config#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  sed -i '/argparse/d' oslo.config/requirements.txt

  cp -a oslo.config{,-py2}
  find oslo.config-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir"/oslo.config
  python setup.py build

  cd "$srcdir"/oslo.config-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.config
  python setup.py testr

  cd "$srcdir"/oslo.config-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-config() {
  depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
           'python-oslo-i18n' 'python-rfc3986')

  cd "$srcdir"/oslo.config
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-config() {
  depends=('python2-six' 'python2-netaddr' 'python2-stevedore' 'python2-debtcollector'
           'python2-oslo-i18n' 'python2-rfc3986')

  cd "$srcdir"/oslo.config-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv $pkgdir/usr/bin/oslo-config-generator $pkgdir/usr/bin/oslo-config-generator2
}

# vim:set ts=2 sw=2 et:
