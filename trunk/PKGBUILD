# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=20130518
_pkgver=${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}
_pkgbzr=4017
pkgrel=3
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('glu' 'wxgtk2.8' 'hicolor-icon-theme' 'desktop-file-utils' 'libsm')
makedepends=('cmake' 'zlib' 'mesa')
optdepends=('kicad-docs-bzr: for documentation'
            'kicad-library-bzr: for footprints')
install=kicad.install
source=(http://iut-tice.ujf-grenoble.fr/cao/${pkgname}-sources-stable_${_pkgver}_BZR${_pkgbzr}.zip
        kicad-boost-polygon-declare-gtlsort-earlier.patch
        wxgtk3.0.patch)
md5sums=('9e6aab0f2ad01a17c8ec96cd9162dc79'
         'a2c39704238946e74a5ed0c38326345f'
         'ec3e7581c814ca2b76becc7b30a8529f')

#source=(https://launchpad.net/ubuntu/trusty/+source/kicad/0.${pkgver}+bzr${_pkgbzr}-2/+files/kicad_0.${pkgver}+bzr${_pkgbzr}.orig.tar.gz)
# patches from https://launchpad.net/ubuntu/trusty/+source/kicad/0.20130727+bzr4024-2/+files/kicad_0.20130727+bzr4024-2.debian.tar.gz ?

build() {
  cd "$srcdir/stable_${_pkgver}_BZR${_pkgbzr}"

  # kicad-boost-polygon-declare-gtlsort-earlier.patch
  #sed -i '20i #include "detail/polygon_sort_adaptor.hpp"' include/boost/polygon/polygon.hpp
  patch -p0 < "$srcdir/kicad-boost-polygon-declare-gtlsort-earlier.patch"
  #patch -p1 < "$srcdir/wxgtk3.0.patch"
  #sed -i 's/wxStandardPaths/&::Get/' common/edaappl.cpp

  # harmless build blocking error
  # see https://lists.launchpad.net/kicad-developers/msg07841.html
  # for proper fix
  sed -i 's|^#.*error "You must use|//&|' include/common.h

  # FS#31559
  sed -i 's/-O2/-O1/' CMakeLists.txt

  # Fix build with GCC 5.1 and C++11 ABI
  CXXFLAGS+=' -D_GLIBCXX_USE_CXX11_ABI=0'
  CXXFLAGS+=' -Wno-deprecated-declarations -Wno-unused-local-typedefs'

  # build and install kicad
  mkdir -p build/Release
  cd build/Release
  # STABLE_VERSION gets removed, eventually
  # gets ported to wxgtk3, eventually
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' \
              -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc32-2.8'
  make
}

package() {
  cd "$srcdir/stable_${_pkgver}_BZR${_pkgbzr}/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"
}


