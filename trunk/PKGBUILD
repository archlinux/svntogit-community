# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Y7n05h < echo WTduMDVoQHk3bjA1aC5kZXY= | base64 -d >

pkgname=pwru
pkgver=0.0.9
pkgrel=2
pkgdesc="Packet, where are you? -- eBPF-based Linux kernel networking debugger"
arch=("x86_64")
url="https://github.com/cilium/pwru"
license=("GPL2")
depends=("glibc")
makedepends=("clang" "llvm" "go")
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('46f92e5b8eeea8fb60518acc172489701c64ef135d438b979dfddfab13b73957')

prepare() {
	cd "$pkgname-$pkgver"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOPATH="${srcdir}"
	export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
	go generate "main_amd64.go"
}

build() {
	cd "$pkgname-$pkgver"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOPATH="${srcdir}"
	export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
	go build \
		-ldflags "-compressdwarf=false -linkmode external -X \"github.com/cilium/pwru/internal/pwru.Version=${pkgver}\"" \
		-o ${pkgname}
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 ${pkgname} -t "$pkgdir/usr/bin"
}
