# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mikael Eriksson <mikael_eriksson@miffe.org>
# Originally by: Denis Smirnov <detanator@gmail.com>

pkgname=intel-gpu-tools
pkgver=1.22
pkgrel=3
pkgdesc="Tools for development and testing of the Intel DRM driver"
arch=(x86_64)
license=(MIT)
url='http://cgit.freedesktop.org/xorg/app/intel-gpu-tools/'
depends=(libdrm libpciaccess cairo python xorg-xrandr procps-ng kmod libxv libunwind peg systemd)
makedepends=(swig gtk-doc xorg-util-macros dri2proto meson)
source=(https://xorg.freedesktop.org/releases/individual/app/$pkgname-$pkgver.tar.xz{,.sig})
md5sums=('965c591b23a132084113c2a0604f537a'
         'SKIP')
validpgpkeys=('775965B85650195ACE77E18D7370055DB74C2475')

prepare() {
  mkdir -p build
}

build() {
  cd build
  meson ../$pkgname-$pkgver \
    --prefix=/usr \
    --libexecdir=/usr/lib

  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  cd ../$pkgname-$pkgver
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
