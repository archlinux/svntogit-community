# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.5.0
pkgrel=1
pkgdesc="Creates virtual Ethernet networks of almost unlimited size."
arch=('x86_64')
url="https://www.zerotier.com/index.html"
license=('GPL3')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('ruby-ronn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/$pkgver.tar.gz"
        "zerotier-one-1.4.6-gcc10-fix.patch::https://github.com/zerotier/ZeroTierOne/commit/cce4fa719d447c55d93458b25fa92717a2d61a60.patch")
sha512sums=('9c579016153872afd51410d2648670537309c67b523fb180e9a17530d3e812a039b0ffa96f3035f7c8a69e19e1eb3f8315693206219cec1494118f7accc2fa31'
            '3929bf88a1df1001ac36f4b6dec0d70be45039dfd9418b16b64249ed5566c9997f17ff6d563ea00b509f5e6d8b1b8de95d20cd7b7c972d201e4fa78844395bd8')

prepare() {
  cd ZeroTierOne-$pkgver
  sed -i 's/sbin/bin/' make-linux.mk debian/zerotier-one.service

  # Fix compilation with GCC 10 (https://github.com/zerotier/ZeroTierOne/pull/1232)
  patch -Np1 -i "$srcdir/zerotier-one-1.4.6-gcc10-fix.patch"
}

build() {
  cd ZeroTierOne-$pkgver
  make
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
