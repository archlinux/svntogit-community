# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
pkgver=0.26.2
pkgrel=2
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-python-lint'
license=('custom:MIT No Attribution')
depends=(python python-yaml python-six python-aws-sam-translator
         python-jsonpatch python-jsonschema python-setuptools)
checkdepends=(python-pytest python-mock)
source=("https://github.com/aws-cloudformation/cfn-python-lint/archive/v$pkgver/cfn-python-lint-$pkgver.tar.gz"
        $pkgname-nodejs.patch::https://github.com/aws-cloudformation/cfn-python-lint/commit/86203fe84cf5077312e808f5469518d7dfb631f3.patch)
sha256sums=('fff4b0a6f867308b5fa27b7e6cbd4e6bfa161b6a9edefc03835698c7c266a263'
            'd676e910887c79fad0b0da2b4c27c6f5538e81f72cfde1a79733393f3cd8fde8')

prepare() {
  cd cfn-python-lint-$pkgver
  patch -Np1 -i ../$pkgname-nodejs.patch
}

build() {
  cd cfn-python-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-python-lint-$pkgver

  site_packages_path=$(python -c 'import site; print(site.getsitepackages()[0])')

  # Tests in test/integration need the cfn-lint binary
  python setup.py install --root="$srcdir"/tmp_install

  export PYTHONPATH="$srcdir"/tmp_install$site_packages_path
  export PATH="$PATH:$srcdir/tmp_install/usr/bin"
  pytest -v test
}

package() {
  cd cfn-python-lint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
