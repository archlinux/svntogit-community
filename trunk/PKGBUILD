# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=10.11
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('glibc' 'gcc-libs')
makedepends=('cmake' 'python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('7cbb77d1eca0aa5b589a26bf65085576372a3ebe9c82dcbf154affddd19490d3b88ed2eedc5b197962e30849f2151d8e35649e207c4b2540822272014d000211'
            'cd2e6dd9d89044a8e4e125d035ebb933593bb6868f77607136c6897ed77606c71584ba3f5587e64b3f9a18277ba799d82cc12c91b146b952ee02beabe04d2fa5')
b2sums=('404b27063e4fa831c835923bd31d5db3fb4f0ed912eb2c4799d17fd880a9313f4e871f67200b5b96bc0614d2ce25c1ac3618500483403b0cd5f38a7971c11a12'
        '6f8ab6ca6e7b367a901c083c2390c1fd175cc0aee9b86b6280a7883797525172ad5f6e3b4293ca288009adc550cd1646a54b74bdbaae40759fa3be551d16de50')

build() {
  cmake \
    -S . \
    -B build \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX='/usr'

  cmake --build build

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
