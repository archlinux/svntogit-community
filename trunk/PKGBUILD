# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
pkgver=2.7.1
pkgrel=2

pkgdesc="cli interface for Github"
url="https://hub.github.com"
arch=('x86_64')
license=('MIT')

depends=('git')
makedepends=('go-pie' 'ruby-ronn')

source=("hub-$pkgver.tar.gz::https://github.com/github/hub/archive/v$pkgver.tar.gz")
sha256sums=('83fac8b47b2f56da1e7366688df4bd0eeb5fa20bde5d3de39792ea1cc1d15cd4')

prepare() {
  mkdir -p src/github.com/github
  cp -r hub-$pkgver src/github.com/github/hub

  sed -i 's@.man-pages.stamp: bin/ronn@.man-pages.stamp:@;s@bin/ronn@ronn@' src/github.com/github/hub/Makefile
}

build() {
  cd src/github.com/github/hub

  GOPATH="$srcdir" make
  make man-pages
}

# Fails with weird 'loadinternal' errors, needs more investigation.
#check() {
#  cd src/github.com/github/hub
#
#  GOPATH="$srcdir" make test
#}

package() {
  cd src/github.com/github/hub

  make PREFIX="$pkgdir"/usr install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 etc/hub.bash_completion.sh "$pkgdir"/usr/share/bash-completion/completions/hub
  install -Dm644 etc/hub.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_hub
  install -Dm644 etc/hub.fish_completion "$pkgdir"/usr/share/fish/vendor_completions.d/hub.fish
}
