# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libinsane
pkgver=1.0.9
pkgrel=1
pkgdesc='Library to access scanners'
arch=('x86_64')
url='https://gitlab.gnome.org/World/OpenPaperwork/libinsane'
license=('LGPL3')
depends=('sane')
makedepends=('gobject-introspection' 'meson' 'vala')
checkdepends=('cunit')
source=("https://gitlab.gnome.org/World/OpenPaperwork/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('cbc70ed0ebee92472f0d3655f8635e4a9c6ce3c57322fb1637fe732389937dc5')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
