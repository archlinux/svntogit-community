# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org> 
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Sï¿½ren Holm <sgh@sgh.dk> 

pkgname=vym
pkgver=1.12.7
pkgrel=1
pkgdesc="A mindmapping tool"
arch=('i686' 'x86_64')
url="http://www.insilmaril.de/vym/"
license=('GPL')
depends=('qt' 'unzip' 'zip')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2 \
        $pkgname.desktop)
md5sums=('f0187669c3381499983a1918dc5495df'
         'ef7cfe93f97803159da7c9a136d58b47')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  qmake PREFIX=/usr || return 1
  make || return 1
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

# install files
  make INSTALL_ROOT=${pkgdir} install  || return 1

# correct doc location
  install -d ${pkgdir}/usr/share/doc/$pkgname || return 1
  install -m644 doc/* ${pkgdir}/usr/share/doc/$pkgname || return 1
  rm -rf ${pkgdir}/usr/share/doc/packages || return 1

# .desktop and icon file
  install -D -m644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop || return 1
  install -D -m644 ${pkgdir}/usr/share/vym/icons/$pkgname.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png || return 1
}
