# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-sniffio
pkgver=1.3.0
pkgrel=3
pkgdesc='Sniff out which async library your code is running under'
arch=(any)
url=https://github.com/python-trio/sniffio
license=(MIT)
depends=(python)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-curio
  python-pytest
  python-pytest-cov
)
_tag=cb8a03d45371efb20156ec895003a9bd988ac89b
source=(git+https://github.com/python-trio/sniffio.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd sniffio
  git describe --tags | sed 's/^v//'
}

build() {
  cd sniffio
  python setup.py build
}

check() {
  cd sniffio
  pytest
}

package() {
  cd sniffio
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-sniffio/
}

# vim: ts=2 sw=2 et:
