# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=glom
pkgver=1.31.6
pkgrel=8
pkgdesc="An easy-to-use database designer and user interface"
arch=('x86_64')
url="https://www.glom.org/"
license=('GPL')
depends=('gettext' 'boost-libs' 'libgdamm' 'libxml++' 'python-gobject' 'libepc'
         'goocanvasmm' 'gtksourceviewmm' 'evince' 'postgresql' 'libgda-postgres')
makedepends=('intltool' 'python-sphinx' 'boost' 'itstool' 'libgda-mysql')
changelog=$pkgname.changelog
source=(https://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('6c5bb163e66d487f979763c3ca3437b56322581872429f1fc3af95f11a2eace0')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --with-postgres-utils=/usr/bin \
    --disable-update-mime-database \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
