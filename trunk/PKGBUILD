# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pastedeploy
pkgname=('python-pastedeploy' 'python2-pastedeploy')
pkgver=2.1.0
pkgrel=1
pkgdesc="Load, configure, and compose WSGI applications and servers"
arch=('any')
license=('MIT')
url="http://pythonpaste.org/deploy/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/pastedeploy/archive/$pkgver.tar.gz")
sha512sums=('2c639b5ad07faee013ff3fe37d0e3c6ca7b56ed0960dbcaf133c05d51a7f29d6a2f35118bd8faea4d9aca90438c43d8046a340833878e4b925f32142df169c97')

prepare() {
  cp -a pastedeploy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pastedeploy-$pkgver
  python setup.py build

  cd "$srcdir"/pastedeploy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pastedeploy-$pkgver
  python setup.py pytest

  cd "$srcdir"/pastedeploy-$pkgver-py2
  python2 setup.py pytest
}

package_python-pastedeploy() {
  depends=('python-setuptools')

  cd pastedeploy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-pastedeploy() {
  depends=('python2-setuptools')

  cd pastedeploy-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
