# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=psi-plugins
pkgver=1.3
pkgrel=1
pkgdesc="Additional plugins for Psi+ (built with Qt 5.x)"
arch=('x86_64')
url="http://psi-plus.com"
license=('GPL2')
depends=('qt5-base' 'qt5-webkit' 'qt5-multimedia' 'psi' 'qt5-webengine')
conflicts=('psi-plus-plugins-git' 'psi-plus-plugins-qt5-git' 'psi-plus-plugins')
replaces=('psi-plus-plugins')
makedepends=('libotr' 'tidyhtml' 'git')
optdepends=('libotr: for OTR plugin'
            'tidyhtml: for OTR plugin')
source=("psi-plus-plugins::git://github.com/psi-im/plugins.git#tag=$pkgver"
        'psiplugin.pri')
sha256sums=('SKIP'
            '2be7b26400faecf69d552b89ed8470cb461ba1c8c56e3e8646d166feb0de56c6')

prepare() {
    cp -v psiplugin.pri psi-plus-plugins/
}

build() {
    cd psi-plus-plugins
    # Kill broken plugins
    rm -rf generic/juickplugin
    for plugin in generic/*; do
        if [ -d "$srcdir/psi-plus-plugins/$plugin" ]; then
            cd "$srcdir/psi-plus-plugins/$plugin"
            qmake-qt5 PREFIX="/usr" QMAKE_STRIP=
            make
        fi
    done
}

package() {
    cd psi-plus-plugins
    for plugin in generic/* unix/*; do
        if [ -d "$srcdir/psi-plus-plugins/$plugin" ]; then
            cd "$srcdir/psi-plus-plugins/$plugin"
            make INSTALL_ROOT="$pkgdir" install
        fi
    done
}
