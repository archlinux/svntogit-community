# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=2.1
pkgrel=1
pkgdesc='Lightweight NTP client and server'
arch=('i686' 'x86_64')
url="http://chrony.tuxfamily.org/"
options=(strip emptydirs)
license=('GPL')
depends=('nss' 'readline' 'libcap' 'libedit')
backup=('etc/chrony.conf')
source=(http://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        service)
install=${pkgname}.install
sha256sums=('f4da5f5071e77314b49281c48c35a03d07d76cbc7ec19e787b4414a86023b68b'
            'dbd30435d5f51a92ecef779efa0eabaf56d09a35a18ddd30644d5418beada212')
sha256sums=('72da3f3c442693e6130d4d5f8c7dcfbe9908b4b3b79afbb2c70360c7919fcd5d'
            'dbd30435d5f51a92ecef779efa0eabaf56d09a35a18ddd30644d5418beada212')
sha256sums=('72da3f3c442693e6130d4d5f8c7dcfbe9908b4b3b79afbb2c70360c7919fcd5d'
            'dbd30435d5f51a92ecef779efa0eabaf56d09a35a18ddd30644d5418beada212')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|pool.ntp.org|arch.pool.ntp.org|g' examples/chrony.conf.example*
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-docs

  install -Dm0644 "$srcdir/$pkgname-$pkgver/examples/chrony.conf.example3" "$pkgdir/etc/chrony.conf"

  echo -e "\n# Enable kernel RTC sync\nrtcsync" >> "$pkgdir/etc/chrony.conf"

  sed -i '/^! rtconutc$/ s/^! //' "$pkgdir/etc/chrony.conf"

  mv "$pkgdir/usr/sbin/chronyd" "$pkgdir/usr/bin"
  rmdir "$pkgdir/usr/sbin"

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/chrony.service"

  cp -a examples $pkgdir/usr/share/doc/chrony/examples

  chown 183:183 "$pkgdir/var/lib/chrony"
}
