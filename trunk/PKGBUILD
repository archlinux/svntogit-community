# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Niels Abspoel <aboe76 (at) Gmail.com>

_gemname=pg
pkgname=ruby-pg
pkgver=1.1.4
pkgrel=3
pkgdesc='Pg is the Ruby interface to the Postgresql RDBMS'
url='https://bitbucket.org/ged/ruby-pg'
arch=('x86_64')
license=('custom:BSDL')
depends=('ruby' 'ruby-rspec-core' 'postgresql-libs')
options=('!emptydirs')
source=(https://github.com/ged/ruby-pg/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a8092df4ed85e99b4bb40d30164b34e08b134f3d3ee4b618a8bd7974db379e6a')
sha512sums=('936696c47eb4761104a451335c73e0bc13c11b8098c62f62b7973ba51e4a1f4f692f32322cbd5044b442d7acced15eefed026fde24268c4f3bde662b49eadb3e')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|"ChangeLog".freeze, ||' -i ${_gemname}.gemspec
  sed 's|"lib/pg/deprecated_constants.rb".freeze, ||' -i ${_gemname}.gemspec
  sed "s|^  s.version = .*$|  s.version = '${pkgver}'|" -i ${_gemname}.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
