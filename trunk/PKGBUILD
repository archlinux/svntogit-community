# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=5.6.4
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('LGPL3')
depends=('dtkcommon' 'deepin-desktop-base' 'python' 'gsettings-qt' 'lshw' 'icu' 'uchardet')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtkcore/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9cc7fdfdfdb16e884e6b009f6b4334c688b2cd57517a18481c62dfd41f20d6ad6ee71a6d5b0bdf48e1e5c3b8d7a25e03588155b973fb56507852da4d7c0a78d6')

prepare() {
  cd dtkcore-$pkgver
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtkcore-$pkgver
  cmake -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DBUILD_DOCS=ON \
      -DBUILD_EXAMPLES=ON \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDVERSION=$pkgver
  ninja
}

check() {
  cd dtkcore-$pkgver
  ninja check || echo "Tests failed"
}

package() {
  cd dtkcore-$pkgver
  DESTDIR="$pkgdir" ninja install
}
