# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=persistent-qq
pkgname=haskell-persistent-qq
pkgver=2.9.0
pkgrel=11
pkgdesc="Provides a quasi-quoter for raw SQL for persistent"
url="https://github.com/yesod-web/persistent"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-src-meta' 'haskell-persistent')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0bd5a00d9bc97128e3eaf556af46347e924b4b20713b0526ed79cc55bef5350f2c03c5e30fbccfb3d62a980ee92e52b34d1626504a4a70ec16caf95f4b494cd5')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
