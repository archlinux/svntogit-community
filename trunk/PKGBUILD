# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-positional
pkgname=(python-positional python2-positional)
pkgver=1.1.1
pkgrel=2
pkgdesc="Library to enforce positional or key-word arguments"
arch=('any')
url="https://github.com/morganfainberg/positional"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-wrapt' 'python2-wrapt')
checkdepends=('python-testrepository' 'python2-testrepository')
source=("git+https://github.com/morganfainberg/positional.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a positional{,-py2}
}

build() {
  cd "$srcdir"/positional
  python setup.py build

  cd "$srcdir"/positional-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/positional
  python setup.py testr

  cd "$srcdir"/positional-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-positional() {
  depends=('python-pbr' 'python-wrapt')

  cd "$srcdir"/positional
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-positional() {
  depends=('python2-pbr' 'python2-wrapt')

  cd "$srcdir"/positional-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
