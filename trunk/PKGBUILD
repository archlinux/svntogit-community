# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.12.0
pkgrel=1
pkgdesc='A new Unix shell'
arch=('x86_64')
url='https://www.oilshell.org'
license=('Apache')
depends=('readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
options=('!lto' '!strip')
source=("$url/download/$pkgname-$pkgver.tar.xz")
sha512sums=('4e78d08cdf32bc3cbf49d09784f7edb1fdd7e974834cba3024d3b709fca3ecf7b29425d48a90fe7e442516dc2c459c4554c25b8691897ad7ffdd7f2a1b06368a')
b2sums=('c9be75456fe02219c38e289914288bd43617db6d3d772298090423d122b2faf9dc06e180f8444c16eb74479ed9f9d93e903fbae268412d19b3c4b6660b3ce919')

build() {
	cd "$pkgname-$pkgver"

	./configure \
    --prefix=/usr \
    --with-readline

	make
}

package() {
	cd "$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}
