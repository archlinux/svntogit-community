# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-yabause
pkgver=2876
pkgrel=1
pkgdesc='Sega Saturn core'
arch=('x86_64')
url='https://github.com/libretro/yabause'
license=('GPL2')
groups=('libretro')
depends=('glibc')
makedepends=('git')
_commit='c8f4c5732c0ebb02c8f3aae757c6575a25d41896'
source=("libretro-yabause::git+https://github.com/libretro/yabause.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/yabause_libretro.info')
sha256sums=('SKIP'
            'f1a02cc5a96b87551d423c337a9f7958a70aafc1b2e95d74f3bcb0bf80e2a56b')

pkgver() {
  cd libretro-yabause

  git rev-list --count HEAD
}

build() {
  cd libretro-yabause/libretro

  make
}

package() {
  cd libretro-yabause/libretro

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 yabause_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../../yabause_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
