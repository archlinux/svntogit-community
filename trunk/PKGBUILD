# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tidal
pkgname=haskell-tidal
pkgver=1.9.3
pkgrel=6
pkgdesc="A domain specific language for live coding pattern."
arch=(x86_64)
url="https://tidalcycles.org/"
license=(GPL3)
groups=(pro-audio)
depends=(ghc ghc-libs glibc haskell-clock haskell-colour haskell-hosc
haskell-mwc-random haskell-network haskell-primitive haskell-random
haskell-tidal-link haskell-vector supercollider)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=("https://hackage.haskell.org/packages/archive/$_name/$pkgver/$_name-$pkgver.tar.gz")
sha512sums=('4ff1b8b1cc1318dd8965a6761da92619c82bfe90d296857ab07efe0ffea5cca23511fada95a19cf8b42a26eed0bbc53c27d260d39dcf7a8bfbfb066c2d9e1707')
b2sums=('350c62fa5b6f8e2ea9a69f6b730ffa25c68788f49141cca833f42a0a79b2d0e2ec6eaab424c1deb2043d7f1efea5e69440406c278f9e3614416246716d222972')

build() {
  cd $_name-$pkgver
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
