# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.8.27
pkgrel=6
pkgdesc="Produce PDF or Postscript from HTML documents including TOCs and Indices"
arch=(i686 x86_64)
url="http://www.htmldoc.org"
license=('GPL')
depends=('libjpeg' 'libpng' 'openssl' 'fltk' 'gcc-libs' 'libxft' 'libxpm')
source=(ftp://ftp.easysw.com/pub/${pkgname}/${pkgver}/${pkgname}-$pkgver-source.tar.bz2)
md5sums=('35589e7b8fe9c54e11be87cd5aec4dcc')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver}

  make install \
	prefix=${pkgdir}/usr \
	bindir=$pkgdir/usr/bin \
	datadir=$pkgdir/usr/share \
	datarootdir=$pkgdir/usr/share \
	docdir=$pkgdir/usr/share/doc/htmldoc \
	exec_prefix=$pkgdir/usr \
	mandir=$pkgdir/usr/share/man \
	prefix=$pkgdir/usr \
	sysconfdir=$pkgdir/etc

  # freedesktop stuff
  install -Dm 644 desktop/htmldoc-128.png \
    $pkgdir/usr/share/pixmaps/htmldoc.png
  echo "MimeType=application/vnd.htmldoc-book;" >> desktop/htmldoc.desktop
  sed -i 's|X-Red-Hat.*$||' desktop/htmldoc.desktop
  install -Dm 644 desktop/htmldoc.desktop \
    $pkgdir/usr/share/applications/htmldoc.desktop
}
