# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=concurrent-supply
pkgname=haskell-concurrent-supply
pkgver=0.1.8
pkgrel=184
pkgdesc="A fast concurrent unique identifier supply with a pure API"
url="https://github.com/ekmett/concurrent-supply"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('23983e1fba2cc1331088c4f30be950a055e254ce443edef55e2f61ce208925665021768351072721b4c604ef1a8fb588c5e3390cc24ecc28ea91002c39590363')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *1.3/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
