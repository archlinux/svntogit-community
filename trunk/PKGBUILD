# Maintainer: Kyle Keen <keenerd@gmail.com>

# cms2 had a manpage, where did it go?
# need to hack up the pythonpath and get the tests to run

pkgname=cryptominisat5
pkgver=5.6.8
pkgrel=4
pkgdesc="A modern, multi-threaded, feature-rich, simplifying SAT solver"
arch=('x86_64')
#url="https://www.msoos.org/2016/07/cryptominisat-5-0-0-released/"
url="https://github.com/msoos/cryptominisat/"
license=('MIT')
depends=('zlib' 'gcc-libs' 'boost-libs' 'intel-tbb')
makedepends=('python2' 'python' 'boost' 'cmake' 'vim' 'help2man')
# vim for xxd
optdepends=('python2: python2 module'
            'python: python module')
source=("cms5-$pkgver.tgz::https://github.com/msoos/cryptominisat/archive/$pkgver.tar.gz")
md5sums=('ca1bf853e568c19968daa5464ab86843')

# many fancy features requiring makedeps
# intel-tbb, python2, python3, m4ri, libmysqlclient, valgrind

prepare() {
  cd cryptominisat-$pkgver
  sed -i 's/python$/python2/' python/Makefile
  sed -i 's/\(CRYPTOMINISAT4_EXECUTABLE\).*/\1 cryptominisat5\)/' *.cmake.in
}

build() {
  cd cryptominisat-$pkgver
  mkdir -p build
  cd build
  # options to play with:
  # -DUSE_TBB -DUSE_ZLIB -DUSE_MYSQL
  # NOMYSQL NOSTATS NOM4RI ENABLE_TESTING
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DNOM4RI=on -DNOMYSQL=on ../
  make

  # no way to build both the py2 and py3 modules
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DNOM4RI=on -DNOMYSQL=on -DFORCE_PYTHON2=on ../
  make

  cd pycryptosat
  python2 setup.py build
  python3 setup.py build
}

check() {
  cd "$srcdir/cryptominisat-$pkgver/build/pycryptosat"
  _TESTPATH="$PYTHONPATH:$PWD"

  # smoke test
  ln -sf pycryptosat.so libcryptominisat5.so.5.6
  PYTHONPATH=$_TESTPATH python2 -c "from pycryptosat import Solver"
  PYTHONPATH=$_TESTPATH python3 -c "from pycryptosat import Solver"

  # skip all the cmake stuff
  PYTHONPATH=$_TESTPATH python2 tests/test_pycryptosat.py
  PYTHONPATH=$_TESTPATH python3 tests/test_pycryptosat.py
}

package() {
  cd cryptominisat-$pkgver
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cd build
  make install DESTDIR="$pkgdir"
  # todo, tweak the build tools to respect DESTDIR
  cd pycryptosat
  python2 setup.py install --record files.txt --root="${pkgdir}"
  python3 setup.py install --record files.txt --root="${pkgdir}"
}

