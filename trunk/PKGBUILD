# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=blosc
pkgver=1.12.0
pkgrel=1
pkgdesc='A blocking, shuffling and loss-less compression library.'
arch=('i686' 'x86_64')
url='http://blosc.org/'
license=('BSD')
depends=('lz4' 'snappy' 'zlib' 'zstd')
makedepends=('cmake')
options=(staticlibs)
source=("https://github.com/Blosc/c-blosc/archive/v${pkgver}.tar.gz")
md5sums=('47d6f7da5b66ef1472d3739f23bade17')

build() {
  cd "${srcdir}"
  rm -rf build
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DPREFER_EXTERNAL_LZ4="ON" \
        -DBUILD_TESTS="OFF" \
        ../c-blosc-${pkgver}

  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir"/c-blosc-${pkgver}/LICENSES/BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
