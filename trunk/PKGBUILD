# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=scribus
pkgver=1.4.6
pkgrel=2
pkgdesc='Desktop publishing software'
url='http://www.scribus.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake' 'boost' 'mesa')
optdepends=('tk: scripts based on tkinter')
depends=('qt4' 'cairo' 'lcms2' 'libcups' 'libxml2' 'hyphen'
         'ghostscript' 'python2' 'podofo' 'boost-libs'
         'desktop-file-utils' 'shared-mime-info')
validpgpkeys=('5086B8D68E70FDDF4C40045AEF7B95E7F60166DA')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha1sums=('f04a2eecfb531af4beb99645f85165be161b00a3' 'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed \
		-e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
		-e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
		-i scribus/plugins/scriptplugin/{samples,scripts}/*
	install -d build
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DLIB_SUFFIX=""
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../scribus.desktop "${pkgdir}/usr/share/applications/scribus.desktop"
	install -Dm644 ../resources/icons/AppIcon.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/scribus.png"
}
