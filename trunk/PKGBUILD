# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Sven Klomp <mail@klomp.eu>

pkgname=python-gnupg
pkgdesc="A wrapper for the Gnu Privacy Guard (GPG or GnuPG)"
pkgver=0.4.8
pkgrel=1
arch=(any)
url="https://docs.red-dove.com/python-gnupg/"
license=(BSD)
depends=(gnupg python)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('fcaa121a356595328a26e8184280f859dfc2771b9692dcf28db2d9b0fdddc77d4c1238665a8e419758ec17320616e7741aacd3638e37d449b86fe5555beef1f6'
            'SKIP')
b2sums=('d9ab2fe3ea933a22e731fd70e153e60cc3b158f15e3f8537440c355037092ff7545d29e8d2eab88efb38dcc6e3776c31f537a9c06467b473e93cbc9f0e2ef189'
        'SKIP')
validpgpkeys=('CA749061914EAC138E66EADB9147B477339A9B86') # Vinay Sajip (CODE SIGNING KEY) <vinay_sajip@yahoo.co.uk>

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export NO_EXTERNAL_TESTS='true'
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
