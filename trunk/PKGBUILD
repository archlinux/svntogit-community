# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hannes Rist <hrist@phreeknet.org>

pkgname=mg
pkgver=20220614
pkgrel=1
pkgdesc="Micro GNU/emacs"
arch=('x86_64')
url="https://github.com/hboetes/mg"
depends=('ncurses' 'libbsd')
license=('custom')
#source=("https://homepage.boetes.org/software/mg/mg-$pkgver.tar.gz")
source=("mg-$pkgver.tgz::https://github.com/hboetes/mg/archive/$pkgver.tar.gz")
sha256sums=('d3bc16baba82457c4f60ecd258762ee2ed848064f79729fe92e29bfd181af121')

build() {
  cd mg-$pkgver
  make prefix=/usr mandir=/usr/share/man
}

package() {
  cd mg-$pkgver

  install -d "$pkgdir/usr/bin"
  make install INSTALL=install prefix=/usr mandir=/usr/share/man DESTDIR="$pkgdir" BINDIR=/usr/bin

  install -Dm644 README -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 tutorial -t "$pkgdir/usr/share/doc/$pkgname"
}
