# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=markdown-unlit
pkgname=haskell-markdown-unlit
pkgver=0.5.1
pkgrel=70
pkgdesc="Literate Haskell support for Markdown"
url="https://github.com/sol/markdown-unlit"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-discover' 'haskell-silently'
             'haskell-stringbuilder' 'haskell-temporary')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('a9018d201c81af673eb40398a47a2010b7deb15546b2c67aca59329f8132d42b29d9f3c50b3388777cfb4540bd5ef7d95f2a9beee1c6463239b81372198240b8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
