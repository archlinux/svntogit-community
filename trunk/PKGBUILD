# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gabriele Musco <gabmus@disroot.org>

pkgname=gfeeds
pkgver=0.16.2
pkgrel=4
pkgdesc='RSS/Atom feed reader for GNOME'
arch=('any')
url='https://gfeeds.gabmus.org/'
license=('GPL3')
depends=('libhandy' 'python-beautifulsoup4' 'python-cssselect' 'python-dateutil'
         'python-feedparser' 'python-gobject' 'python-html5lib' 'python-listparser' 'python-lxml'
         'python-pillow' 'python-pygments' 'python-pytz' 'python-readability-lxml'
         'python-requests' 'webkit2gtk')
makedepends=('meson' 'gobject-introspection')
source=("https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('f061e89a1962a5ac544ea98239c2503af8da817ded0a5bfd8094ddf43e9dd00e')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
