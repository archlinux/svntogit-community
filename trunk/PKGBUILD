# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=roc-toolkit
pkgver=0.2.1
pkgrel=3
pkgdesc="Real-time audio streaming over the network"
arch=(x86_64)
url="https://github.com/roc-streaming/roc-toolkit/"
license=(MPL2 custom:CC0)
depends=(glibc libunwind libuv)
makedepends=(alsa-lib cpputest gengetopt libpulse openfec ragel scons sox speexdsp)
optdepends=(
  'libpulse: for roc-conv, roc-receive and roc-send'
  'sox: for roc-conv, roc-receive and roc-send'
)
provides=(libroc.so)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  # add support for DESTDIR: https://github.com/roc-streaming/roc-toolkit/pull/505
  $pkgname-0.2.1-destdir.patch
  # remove unnecessary library dependencies from pkg-config integration: https://github.com/roc-streaming/roc-toolkit/pull/507
  $pkgname-0.2.1-pkgconfig_deps.patch
)
sha512sums=('482d11d35bdb2b298f368d4d0c2c223f48a166c57880c0d19df503923ec36a1515ed2d32d110966ceb0de4771584adf2ccb50a1ae5fbe9e5c47c42d17d9cc37c'
            '76ca9f78fe4eff47fac18c8b66aadb72f88f9a04fbf073f36cecaa73cd595ad397ccf59d5ced057ace1494b9d22954dcc0029086235d0a98b45fa35e52921cc4'
            '59ab91620a1c1a013f5f3da51d78d0e56df10fb5c3e21e156c0a86929652f7f9921798b2b8ada1ab8056109a9c30712e9dfa00f41697b3ca6d81e5ff92d0dfdf')
b2sums=('0d84c616006f3944a13c63ec3bee0a17d909464049d7b3ab2f45c27cabe064ab1967721b29e2c8e060a0e22f6d51eb4420c1b2c3f2840d3bc4f51bd4f71e6db7'
        'f61dc0e0c21c4951fba7448bcfacd665e66920ff54378db557e28ff82283168242fe8fc24073b6ccc77ece6bf0cc39f798c44a1b159c318079046e40f32433bb'
        '7cff5118b8773f3266d345f410308e110bd9c4615b230ce6586def53ea0028c6c417ba66bde61be472ac5ae7988eae25ffb858ce9d2b3431794c155d2149752d')

prepare() {
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.2.1-destdir.patch
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.2.1-pkgconfig_deps.patch
}

build() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons "${scons_options[@]}"
}

check() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons test "${scons_options[@]}"
}

package() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
  )
  depends+=(
    openfec libopenfec.so
    speexdsp libspeexdsp.so
  )

  cd $pkgname-$pkgver
  scons DESTDIR="$pkgdir/" "${scons_options[@]}" install

  install -vDm 644 3rdparty/hedley/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/hedley.COPYING"
}
