# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=8.4.0
pkgrel=1
pkgdesc='Prolog environment'
arch=(x86_64)
url='https://www.swi-prolog.org/'
license=(GPL LGPL)
depends=(libarchive libxcrypt libyaml qt5-base util-linux-libs)
makedepends=(cmake gperftools jdk-openjdk junit libjpeg libxft libxinerama libxpm ninja python-protobuf unixodbc)
optdepends=('java-runtime: for using Prolog from Java'
            'jdk-openjdk: for using Prolog from Java'
            'libjpeg: for using the pl2xpce library'
            'libxft: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'unixodbc: for using the odbc4pl library')
source=("$pkgname-$pkgver.tar.gz::https://www.swi-prolog.org/download/stable/src/swipl-$pkgver.tar.gz")
b2sums=('ba0a62616432a3a958dd3ad90b56bfb644c5a99fdea5936cabf32224beac0a405622cd4f754415203b61545a1a238a227eea44e16ee455ee1270c47e3455a728')

build() {
  cmake -B build -S "swipl-$pkgver" \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -ffile-prefix-map=$PWD= -w" \
    -D LIBEDIT_LIBRARIES=/usr/lib/libedit.so.0 \
    -D LIBEDIT_INCLUDE_DIR=/usr/include \
    -G Ninja
  "$srcdir/swipl-$pkgver/scripts/pgo-compile.sh"
  ninja -C build
}

check() {
  # skip the MQI test
  sed -i '/mqi/d' build/CTestTestfile.cmake
  ctest -j 12 --test-dir build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: www.swi-prolog.org/download/stable
