# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapp
pkgver=2.4.2
pkgrel=2
pkgdesc="Common library for X-Apps project"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(libdbusmenu-gtk3 libgnomekbd)
optdepends=('python: for mate-desktop status applet')
makedepends=(meson samurai gobject-introspection python-gobject vala)
provides=(xapps)
conflicts=(xapps)
replaces=(xapps)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/linuxmint/xapp/commit/3ef9861d.patch)
sha256sums=('b01c5037f0da773bc822f32c98c7281fd1cf5b9bc9c0e7cbf8eb2c38d0661b4d'
            '3d5220a3ee594af038eab9f04f89dd4344993e3e0f40448b89b2c7a846c5469f')
b2sums=('184f8459c5fa8165054a6cc3f2aea6b01ab0c73ecb0f5a79a7ba78908e9a149b3b08c691fa1b0a0077d238dc144dd9068b0b49228d530e9684670974bbbe2a69'
        '524cf1a3d9f920a058611f420d36f25426beaa951c37c67505fd408f8d51d2098e7bdee4ddc9a2d6e55fb00878072389d82698ee74ba73d416b65faab9898170')

prepare() {
  patch -d ${pkgname}-${pkgver} -p1 < 3ef9861d.patch # Fix crashes with glib 2.76
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install

  # rm useless scripts
  rm -r "${pkgdir}"/usr/bin

  # byte-compile python modules since meson does not implement autotoolsâ€™ py-compile.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m compileall -o 0 -o 1 -o 2 --hardlink-dupes -s "${pkgdir}" "${pkgdir}"${site_packages}
}
