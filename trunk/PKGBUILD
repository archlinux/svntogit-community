# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: foxbunny <bg.branko@gmail.com>

_pkgname=DisplayCAL
pkgname=displaycal
pkgver=3.9.4
pkgrel=1
pkgdesc="Open Source Display Calibration and Characterization powered by Argyll CMS (Formerly known as dispcalGUI)"
arch=(x86_64)
url="https://github.com/eoyilmaz/displaycal-py3"
license=(GPL3)
depends=(argyllcms dbus dbus-python glib2 python-certifi python-numpy
         python-pillow python-send2trash python-wxpython python-zeroconf)
makedepends=(libxrandr libxxf86vm python-build python-installer
             python-setuptools python-wheel)
optdepends=('gnome-color-manager: profile installation for GNOME'
            'colord-kde: profile installation for KDE Plasma')
source=(${url}/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('fd0fe74043b076fd949eaa084b9180b33153cec7344f86627fecf930e3f9f592')

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  # https://github.com/eoyilmaz/displaycal-py3/issues/117
  rm -r "${pkgdir}"/usr/lib/python3.10/site-packages/etc/
  install -d "${pkgdir}"/etc/xdg/autostart
  mv "${pkgdir}"/{usr/lib/python3.10/site-packages/build/.config,etc/xdg}/autostart/z-displaycal-apply-profiles.desktop
  rmdir "${pkgdir}"/usr/lib/python3.10/site-packages/build/{.config{/autostart,},}
}
