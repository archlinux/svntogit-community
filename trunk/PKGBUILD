# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Philipp Sieweck

pkgname=cvc4
pkgver=1.5
pkgrel=1
pkgdesc="An automatic theorem prover"
arch=('x86_64')
url="http://cvc4.cs.nyu.edu/web/"
depends=('boost' 'libantlr3c' 'cln')
optdepends=('readline: for an improved interaction support')
license=('GPL' 'BSD' 'MIT' 'custom:HPND')
source=("http://cvc4.cs.stanford.edu/downloads/builds/src/cvc4-$pkgver.tar.gz" cvc4-gcc7.patch)
sha256sums=('5d6b4f8ee8420f85e3f804181341cedf6ea32342c48f355a5be87754152b14e9'
            'be2a4a9c843da030b12215bebbd159f2b5acaa1465eb9d1aab2f10c350f4defa')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../cvc4-gcc7.patch # Fix build with GCC 7
}

build() {
  cd $pkgname-$pkgver
#  export CXXFLAGS+=" -std=gnu++98"
  ./configure --prefix=/usr --enable-optimization --disable-doxygen-doc --with-cln --enable-gpl
  make
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir"/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/cvc4/LICENSE
}
