# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-config
pkgver=1.2.0
pkgrel=2
pkgdesc="LXQt system configuration."
arch=("x86_64")
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
license=("LGPL2.1")
depends=("glibc" "gcc-libs"
         "qt5-base" "qt5-svg" "qt5-x11extras" "libkscreen"
         "libQt5Xdg.so" "liblxqt" "lxqt-themes"
         "libx11" "libxcb" "libxcursor" "libxfixes" "libxi" "xf86-input-libinput"
         "libudev.so" "zlib")
makedepends=("lxqt-build-tools")
optdepends=(
	'xsettingsd: help GTK applications apply themes on the fly'
)
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	"$pkgname-pr915.patch"::"https://github.com/lxqt/lxqt-config/commit/6add4e4f0040693e7c4242fbae48c9d32007686c.patch"
)
sha256sums=('3e1c4edd6c3099aa9a15623d43ea81da6a0a7cf5bd894b1e2873dc1bac2a1640'
            'SKIP'
            '3acb08d235e5e029b3c5c0286d4da63d0f188c6df0c097866eb57d4f7fdc8b07')
validpgpkeys=(
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # https://github.com/tsujan.gpg
)

prepare() {
	cd $pkgname-$pkgver
	# Backport https://github.com/lxqt/lxqt-config/pull/915 (merged)
	patch -Np1 -i ../$pkgname-pr915.patch
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
