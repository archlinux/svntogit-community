# Maintainer: Florian "Bluewind" Pritz <flo@xssn.at>

pkgname=fb-client
pkgver=2.3.0
pkgrel=1
pkgdesc="Client for paste.xinu.at"
arch=("any")
url="https://github.com/Bluewind/fb"
license=('GPL3')
depends=('python' 'python-pycurl' 'python-xdg')
optdepends=(
  'xclip: automatically copy the URL into the clipboard on X11'
  'wl-clipboard: automatically copy the URL into the clipboard on wayland'
)
source=(https://paste.xinu.at/data/client/fb-$pkgver.tar.gz{,.sig})
sha256sums=('1164eca06eeacb4210d462c4baf1c4004272a6197d873d61166e7793539d1983'
            'SKIP')
validpgpkeys=("CFA6AF15E5C74149FC1D8C086D1655C14CE1C13E")

build() {
  cd "$srcdir/fb-$pkgver"

  make
}

package() {
  cd "$srcdir/fb-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
