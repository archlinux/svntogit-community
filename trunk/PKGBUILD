# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kevin Dodd <jesin00@gmail.com>
# Contributor: Tianjiao Yin <ytj000@gmail.com>

pkgname=mathjax
pkgver=2.5.1
pkgrel=2
pkgdesc='An open source JavaScript display engine for mathematics that works in all modern browsers'
url='https://www.mathjax.org/'
arch=(any)
license=(Apache)
depends=(xorg-mkfontdir fontconfig)
provides=(otf-stix)
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/mathjax/MathJax/archive/$pkgver.tar.gz")
sha256sums=('e294ea66e87900a0a803281d6c13025db38d27e103f4d9e960bfc554ad92c602')

package() {
  mkdir -p "$pkgdir"/usr/share/{fonts,licenses/mathjax}
  cp -a MathJax-$pkgver "$pkgdir"/usr/share/mathjax
  mv "$pkgdir"/usr/share/mathjax/fonts "$pkgdir"/usr/share/fonts/mathjax
  ln -s /usr/share/fonts/mathjax "$pkgdir"/usr/share/mathjax/fonts
  mv "$pkgdir"/usr/share/mathjax/LICENSE "$pkgdir"/usr/share/licenses/mathjax/LICENSE
  rm -r "$pkgdir"/usr/share/mathjax/unpacked
}
