# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-aiodiscover
pkgver=1.4.15
pkgrel=2
pkgdesc='Discover Hosts via ARP and PTR lookup'
arch=(x86_64)
url=https://github.com/bdraco/aiodiscover
license=(APACHE)
depends=(
  python
  python-dnspython
  python-ifaddr
  python-netifaces
  python-pyroute2
)
makedepends=(
  git
  python-pytest-runner
  python-setuptools
  python-wheel
)
_tag=ee81c585d5f8b473ea322243e26f0293f514acd0
source=(git+https://github.com/bdraco/aiodiscover.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd aiodiscover
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiodiscover
  python setup.py build
}

package() {
  cd aiodiscover
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:
