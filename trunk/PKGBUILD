# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=lib32-libltdl
pkgver=2.4.6
pkgrel=8
pkgdesc="A generic library support script (32-bit)"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
groups=('multilib-devel')
depends=('sh' 'tar' 'lib32-glibc' 'libtool')
makedepends=('gcc-multilib')
provides=("lib32-libtool=$pkgver")
conflicts=('lib32-libtool')
replaces=('lib32-libtool')
source=(ftp://ftp.gnu.org/pub/gnu/libtool/libtool-${pkgver}.tar.xz{,.sig})
md5sums=('1bfb9b923f2c1339b4d2ce1807064aa5'
         'SKIP')
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')   # Gary Vaughan

build() {
  cd ${srcdir}/libtool-${pkgver}
  export CC="gcc -m32" CXX="g++ -m32"
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32" --libdir=/usr/lib32
  make
}

check() {
  cd ${srcdir}/libtool-${pkgver}
  make check
}

package() {
  cd ${srcdir}/libtool-${pkgver}
  make DESTDIR=${pkgdir} install-libLTLIBRARIES
}
