# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Philipp Trommler <aur@philipp-trommler.me>

pkgname=fvextra
pkgver=1.4
pkgrel=3
pkgdesc='Extensions to fancyvrb, including automatic line breaking and improved math mode'
url='https://github.com/gpoore/fvextra'
arch=(any)
license=(LPPL)
makedepends=(git)
depends=(texlive-latexextra)
source=("git+$url#commit=cc1c0c5f7b92023cfec67084e2a87bdac520414c") # tag v1.4
sha256sums=('SKIP')

build() {
  cd "$pkgname/$pkgname"
  yes | tex "$pkgname.ins"
}

package() {
  cd "$pkgname/$pkgname"
  install -Dm644 "$pkgname.sty" "$pkgdir/usr/share/texmf/tex/latex/$pkgname/$pkgname.sty"
  install -Dm644 "$pkgname.pdf" "$pkgdir/usr/share/doc/$pkgname/$pkgname.pdf"
}

# vim: ts=2 sw=2 et:
