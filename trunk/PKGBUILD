# $Id: PKGBUILD 175037 2016-05-12 15:18:07Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(xcas libgiac)
_pkgver=1.4.9-69
pkgver=${_pkgver//-/.}
pkgrel=4
pkgdesc="A free computer algebra system"
arch=(x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(mpfi gsl pari ntl lapack libpng libjpeg fltk texlive-core texlive-science hevea mesa)
source=("http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgbase}_${_pkgver}.tar.gz")
sha256sums=('8cb33344c98a3139e16bc45013ec73fa37470d4ca43b5a967866aae35e2f0f91')

prepare() {
  cd $pkgbase-${pkgver%.*}
  sed -e '/curlbuild/d' -i src/misc.cc # Remove bogus include
}

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

package_xcas() {
  depends=(libgiac fltk)
  conflicts=(giac)
  provides=(giac)
  replaces=(giac)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari ntl lapack libpng curl)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}
