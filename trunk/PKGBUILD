# Maintainer: David Runge <dave@sleepmap.de>

pkgname=eteroj.lv2
pkgver=0.10.0
pkgrel=1
pkgdesc="Open Sound Control for LV2"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/eteroj/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'lv2-host')
makedepends=('lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('d2f6a576b1f5da34ee5a01900d57a04206cb66569b73dff442d8d777e727f902b3c7b3ef4e0f41a30b23385da9803af1d577987123a36044cdf5c32454d0360d')
b2sums=('a7d0a595c636563f10bf30a376a32491a02619b1fd112acf2238f7516ab1d548b44985f8e14ea107e420b2128bc069164a4eb6bd26b5af99d9b93509becb08e9')

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
