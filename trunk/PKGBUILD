# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=hunspell-hu
pkgver=1.7
_lo_ver=7.3.2
pkgrel=4
pkgdesc="Hungarian hunspell dictionary"
arch=(any)
url="http://magyarispell.sourceforge.net/"
license=(LGPL GPL MPL)
makedepends=('qt6-webengine')
optdepends=('hunspell: the spell checking libraries and apps')
source=(https://download.documentfoundation.org/libreoffice/stable/${_lo_ver}/rpm/x86_64/LibreOffice_${_lo_ver}_Linux_x86-64_rpm_langpack_hu.tar.gz{,.asc})
validpgpkeys=('C2839ECAD9408FBE9531C3E9F434A1EFAFEEAEA3') # "LibreOffice Build Team (CODE SIGNING KEY) <build@documentfoundation.org>"
sha256sums=('7c36145daab693c4b77224dd0ac0e904fec3350ad0df11d88076d401fb6d1976'
            'SKIP')

prepare() {
  bsdtar -x -f LibreOffice_*_Linux_x86-64_rpm_langpack_hu/RPMS/libreoffice*-dict-hu-*.x86_64.rpm
}

package() {
  cd opt/libreoffice*/share/extensions/dict-hu/
  install -dm755 "$pkgdir"/usr/share/{hunspell,qt{,6}/qtwebengine_dictionaries}
  install -m644 -t "$pkgdir/usr/share/hunspell" hu_HU.dic hu_HU.aff

  # Install webengine dictionary; the IGNORE command is not supported by bdic
  sed -i '/^IGNORE/d' hu_HU.aff
  /usr/lib/qt6/qwebengine_convert_dict hu_HU.dic "$pkgdir/usr/share/qt6/qtwebengine_dictionaries/hu_HU.bdic"
  ln -rs "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/hu_HU.bdic "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/

  # the symlinks
  install -dm755 "$pkgdir/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  # docs
  install -Dm644 README_hu_HU.txt "$pkgdir/usr/share/doc/$pkgname/README_hu_HU.txt"
}
