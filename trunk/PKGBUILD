# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=isodate
pkgname=('python2-isodate' 'python-isodate')
pkgver=0.6.0
pkgrel=3
pkgdesc="An ISO 8601 date/time/duration parser and formatter"
arch=('any')
url="https://github.com/gweis/isodate/"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/i/isodate/isodate-$pkgver.tar.gz")
sha1sums=('2c69e08c9894a9cd96c627146463260f5731df62')

prepare() {
  cp -r "$pkgbase-$pkgver" "$srcdir/python2"
}

build() {
  cd "$srcdir/python2"
  python2 setup.py build
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/python2"
  python2 setup.py test
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py test
}

package_python2-isodate() {
  depends=('python2-six')

  cd "$srcdir/python2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  head -26 src/isodate/isodates.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-isodate() {
  depends=('python-six')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  head -26 src/isodate/isodates.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
