# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: orbisvicis <gmail.com>

pkgname=xmonad-contrib
pkgver=0.13
pkgrel=2
pkgdesc="Add-ons for xmonad"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc' 'xmonad' 'sh' 'haskell-x11' 'haskell-x11-xft' 'haskell-utf8-string' 'haskell-random'
         'haskell-old-time')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmonad/xmonad-contrib/archive/v${pkgver}.tar.gz")
md5sums=('ccda8bbc3dd0c23ec98a1c6668716f59')

build() {
  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup.lhs configure -O --enable-shared --enable-executable-dynamic \
             --prefix=/usr -fuse_xft --libsubdir=\$compiler/site-local/\$pkgid \
             --docdir=/usr/share/doc/${pkgname}
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup.lhs copy --destdir=$pkgdir
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$pkgname"

  install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove static libs
  find "$pkgdir"/usr/lib -name "*.a" -delete
}
