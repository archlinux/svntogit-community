# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libmatio
pkgver=1.5.1
pkgrel=1
pkgdesc='C library with a fortran 90/95 module interface for reading/writing MATLAB MAT-files'
arch=('x86_64' 'i686')
license=('LGPL')
url='http://sourceforge.net/projects/matio'
depends=('zlib')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/matio/matio-$pkgver.tar.bz2")
sha1sums=('12b8ed59688b2f41903ddc3e7975f21f10fe42bb')

build() {
  cd matio-$pkgver
  ./configure --prefix=/usr --enable-shared 
  make
}

check() {
  cd matio-$pkgver
  make check
}
 
package() {
  cd matio-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
