# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyaml
pkgver=23.5.5
pkgrel=1
pkgdesc="PyYAML-based module to produce pretty and readable YAML-serialized data"
url="https://pypi.python.org/pypi/pyaml"
license=('BSD')
arch=('any')
depends=('python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-unidecode')
optdepends=('python-unidecode: Needed if same-id objects or recursion is used within serialized data')
source=("https://pypi.io/packages/source/p/pyaml/pyaml-$pkgver.tar.gz")
sha512sums=('8ae9c09b1a4be9b3ef16abb49eddb9b7279623a705e8603954fb980ea147c67d7007aa6ccad9e15b37b222faa773e187324462215778b1745f174c4285126a45')

build() {
  cd pyaml-$pkgver
  python setup.py build
}

check() {
  cd pyaml-$pkgver
  python setup.py test
}

package() {
  cd pyaml-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
