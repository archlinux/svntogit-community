# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Letu Ren <fantasquex at gmail dot com>

_pyname=pypcode
pkgname=python-${_pyname}
pkgver=1.1.0
pkgrel=1
pkgdesc="Python bindings to Ghidra's SLEIGH library for disassembly and lifting to p-code IR"
url='https://github.com/angr/pypcode'
license=('custom')
arch=('x86_64')
depends=('python' 'python-cffi')
makedepends=('cmake' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b5cf80efe554a775b2cd582395ac4ac6b16f7f84e24b0168dd5048e83f7bf4cd1b855dc3396cdc0790e56915d8933a465216776743109270033df4872cbec84c')
b2sums=('33e8125a218b30b38782926225b4c6ef3df472e81e17fefc5152d32ec91862455651f8124a8a9bd350873f70c61e4dd581a813382a3a1f1f3eed119d93443c69')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
