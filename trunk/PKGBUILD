# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=antlr4-runtime
pkgver=4.9.2
pkgrel=2
pkgdesc='Antlr C++ runtime'
url='https://www.antlr.org/'
arch=('x86_64')
depends=('gcc-libs')
makedepends=('git' 'cmake')
license=('BSD')
source=("https://www.antlr.org/download/antlr4-cpp-runtime-${pkgver}-source.zip")
sha256sums=('838a2c804573f927c044e5f45a8feb297683a7047ab62dfac8ddc995498db11c')

prepare() {
  # mysql-workbench is built with C++17, so we need it here as well
  sed -i '/CMAKE_CXX_STANDARD/s/11/17/' CMakeLists.txt
}

build() {
  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DANTLR4_INSTALL=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
}
