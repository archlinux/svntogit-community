# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Ingo Bürk <ingo.buerk@airblader.de>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname=i3status
pkgver=2.14
pkgrel=1
pkgdesc='Generates status bar to use with i3bar, dzen2 or xmobar'
arch=('x86_64')
url='https://i3wm.org/i3status/'
license=('BSD')
groups=('i3')
depends=('alsa-lib' 'confuse' 'libnl' 'libpulse' 'yajl')
makedepends=('asciidoc' 'xmlto' 'meson')
backup=('etc/i3status.conf')
options=('docs')
source=("$url/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('5c4d0273410f9fa3301fd32065deda32e9617fcae8b3cb34793061bf21644924'
            'SKIP')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

build() {
  cd $pkgname-$pkgver/meson

  meson --prefix=/usr --buildtype=plain -Dmans=true .. build
  meson compile -C build
  # See https://lists.archlinux.org/pipermail/arch-dev-public/2013-April/024776.html
  #make CPPFLAGS+="-U_FORTIFY_SOURCE"
}

package() {
  cd $pkgname-$pkgver/meson
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname ../LICENSE
}

# vim:set ts=2 sw=2 et:
