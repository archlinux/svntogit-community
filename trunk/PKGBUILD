# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-h2
pkgname=('python-h2' 'python2-h2')
pkgver=2.3.1
pkgrel=1
pkgdesc="HTTP/2 State-Machine based protocol implementation"
arch=('any')
license=('MIT')
url="http://hyper.rtfd.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-hpack' 'python2-hpack'
             'python-hyperframe' 'python2-hyperframe' 'python2-enum34' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-hypothesis'
              'python2-hypothesis')
source=("git+https://github.com/python-hyper/hyper-h2.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a hyper-h2{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/hyper-h2
  python setup.py build

  cd "$srcdir"/hyper-h2-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hyper-h2
  python setup.py ptr

  cd "$srcdir"/hyper-h2-py2
  python2 setup.py ptr
}

package_python-h2() {
  depends=('python-hpack' 'python-hyperframe')

  cd hyper-h2
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-h2() {
  depends=('python2-hpack' 'python2-hyperframe' 'python2-enum34')

  cd hyper-h2-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
