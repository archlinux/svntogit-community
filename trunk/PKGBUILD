# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=e3
pkgver=2.82
pkgrel=1
pkgdesc='Tiny editor without dependencies, written in assembly'
arch=('x86_64')
url='http://sites.google.com/site/e3editor/'
license=('GPL2')
makedepends=('setconf' 'nasm' 'yasm')
options=('!strip')
source=("https://sites.google.com/site/e3editor/Home/$pkgname-$pkgver.tgz")
sha256sums=('53c51d656801fd3ae0179a5b27f028e07eaba328e80c8c55050268359b9a2924')

build() {
  cd "$pkgname-$pkgver"

  [ $CARCH = i686 ] && bits=32 || bits=64
  make clean
  make NASM_OR_YASM=yasm "$bits"
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 e3 "$pkgdir/usr/bin/e3"
  for bf in e3ws e3em e3pi e3vi e3ne; do
    ln -sf /usr/bin/e3 "$pkgdir/usr/bin/$bf"
  done
  install -Dm644 e3.man "$pkgdir/usr/share/man/man1/e3.1"
}

# vim:set ts=2 sw=2 et:
