# $Id: PKGBUILD 121061 2014-10-20 12:57:19Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Dominik Picheta <morfeusz8@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>

pkgname=nim
pkgver=0.10.2
pkgrel=1
pkgdesc='Imperative, multi-paradigm, compiled programming language'
arch=('x86_64' 'i686')
url='http://nimrod-code.org/'
license=('MIT')
makedepends=('git')
replaces=('nimrod')
conflicts=('nimrod')
options=('!emptydirs')
#source=("$pkgname::git://github.com/Araq/Nimrod.git#commit=06a4ab54aa")
source=("$pkgname::git://github.com/Araq/Nimrod.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  rm build/empty.txt && rmdir build
  git clone --depth=1 git://github.com/nimrod-code/csources build
}

build() {
  cd "$pkgname/build"
  sh build.sh
  cd ..

  ./bin/nim c koch
  ./koch boot -d:release -d:useGnuReadline

  export PATH="$srcdir/$pkgname/bin:$PATH"

  cd lib
  nim c --app:lib -d:createNimRtl -d:release nimrtl.nim

  cd ../tools
  nim c -d:release nimgrep.nim
}

package() {
  cd "$pkgname"

  export PATH="$srcdir/$pkgname/bin:$PATH"
  
  ./koch install "$pkgdir"

  install -d "$pkgdir/usr/share/nim/doc" "$pkgdir/usr/lib/nim" "$pkgdir/etc" "$pkgdir/usr/bin"

  cd "$pkgdir/nim"
  mv "lib/"* "$pkgdir/usr/lib/nim/"
  mv "config/"* "$pkgdir/etc/"
  cp -a "$srcdir/$pkgname/lib/packages" "$pkgdir/usr/lib/nim/"
  mv "bin/"* "$pkgdir/usr/bin/"

  cd "$srcdir/$pkgname"
  rm -r "$pkgdir/nim"
  mv examples web "$pkgdir/usr/share/nim/doc/"
  mv "doc/"* "$pkgdir/usr/share/nim/doc/"
  install -m644 "lib/libnimrtl.so" "$pkgdir/usr/lib/libnimrtl.so"
  install -m755 "tools/nimgrep" "$pkgdir/usr/bin/"
  install -Dm644 "copying.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  ln -s /usr/bin/nim "$pkgdir/usr/bin/nimrod"
}

# vim:set ts=2 sw=2 et:
