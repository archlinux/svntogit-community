# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Nick Bolten <Shirakawasuna at gmail _dot_ com>

pkgname=gprename
pkgver=20210415
pkgrel=1
pkgdesc="A GTK2 batch renamer for files and directories"
arch=('any')
url="http://gprename.sourceforge.net/"
license=('GPL3')
depends=('perl-gtk3' 'perl-libintl-perl' 'perl-locale-gettext' 'pango-perl')
source=(https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2
        $pkgname-build.patch)
sha256sums=('9b86979a7b8d884ad6b2b9ff63c9c1b383b9f44368d4164cc533ac92d1f95382'
            '2b1bb4d9b285570a03f9c6cb364dedce2a51e5d934854ec5d60e31ca95a03008')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/$pkgname-build.patch

#29940 fix
  sed 's#/usr/local#/usr#' -i bin/$pkgname
#45250 fix
  sed 's#/usr/share/icons/gprename.png#/usr/share/icons/hicolor/64x64/apps/gprename.png#' -i bin/$pkgname
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make PREFIX=/usr DESTDIR="${pkgdir}"/usr install
}
