# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.46.0
pkgrel=1
pkgdesc='Window manager of GNOME Flashback'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/Metacity'
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'libxpresent' 'libxres'
         'gsettings-desktop-schemas' 'gtk3' 'startup-notification' 'zenity')
makedepends=('vulkan-headers' 'python')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('4ba777e938547bfccbf9acca0967574c46643022e054c93a033419cb3cc7c1ec')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
