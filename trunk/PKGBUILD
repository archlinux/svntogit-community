# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=stubby
pkgver=0.2.2
pkgrel=1
pkgdesc="DNS Privacy stub resolver"
arch=('x86_64')
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=('BSD')
backup=('etc/stubby/stubby.yml')
depends=('getdns')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/getdnsapi/stubby/archive/v${pkgver}.tar.gz"
        "${pkgname}.sysusers")
sha256sums=('d1418d2c4df3b2f32fac668423630f07dbfb38f6f8d402ddfda9304f16263b03'
            '2acedcf2b65d6e802a70deb55a5befbb71ff83be68a0ba5231a09c126910142b')

build() {
    cd ${pkgname}-${pkgver}
    autoreconf -vfi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
    rmdir "${pkgdir}"/var{/run,}

    install -Dm644 systemd/${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 systemd/${pkgname}.conf -t "${pkgdir}"/usr/lib/tmpfiles.d/
    install -Dm644 ../${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}
