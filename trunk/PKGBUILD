# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Armin 'IsSuE' Praher [issue.at.gmx.dot.at]
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=teeworlds
pkgver=0.6.4
pkgrel=1
pkgdesc='Fast-paced multiplayer 2D shooter game'
arch=('x86_64')
url='https://www.teeworlds.com'
license=('custom')
depends=('alsa-lib' 'glu' 'sdl' 'freetype2')
makedepends=('python' 'bam' 'mesa' 'imagemagick' 'gendesk')
source=("https://downloads.teeworlds.com/teeworlds-${pkgver}-src.tar.gz"
        "https://github.com/downloads/matricks/bam/bam-0.4.0.tar.gz")
md5sums=('9733800c12462ac4d5a5a4f6ea750af0'
         '2f8e8336b8884110e8a355b12c9fa58a')

prepare() {
  convert +set date:create +set date:modify "$pkgname-$pkgver-src/other/icons/Teeworlds.ico" "$srcdir/$pkgname.png"
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --name 'Teeworlds' --categories 'Game;ArcadeGame'
}

build() {
  cd bam-0.4.0
  ./make_unix.sh
  cd ..

  cd "$pkgname-$pkgver-src"
  ../bam-0.4.0/bam server_release client_release
}

package() {
  cd "$pkgname-$pkgver-src"

  # Install data files
  mkdir -p ${pkgdir}/usr/share/${pkgname}/data
  cp -r data/* ${pkgdir}/usr/share/${pkgname}/data

  install -Dm755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
  install -Dm755 ${pkgname}_srv ${pkgdir}/usr/bin/${pkgname}_srv

  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${srcdir}/${pkgname}-0.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png

  install -Dm644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

# vim:set ts=2 sw=2 et:
