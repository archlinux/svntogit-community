# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi-fast
pkgver=2.16
pkgrel=1
pkgdesc='CGI Interface for Fast CGI'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' 'perl-cgi>=4' 'perl-fcgi>=0.67')
url=https://metacpan.org/release/CGI-Fast
source=("https://cpan.metacpan.org/authors/id/L/LE/LEEJO/CGI-Fast-$pkgver.tar.gz")
md5sums=(c43a64d1f60bc72727c63d6c78cce951)
sha512sums=(50dce23855809f53c722a9ed7839469400baafdf8cf006cce4a7686a0e9a1a6ee500ffbd3bb418ecfa7d14efa3f9adb96c37dea8282a3d05b9c0e7e723bc7e37)
_ddir="CGI-Fast-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
