# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Maintainer: M Rawash <mrawash@gmail.com>

pkgname=viewnior
pkgver=1.8
pkgrel=2
pkgdesc="A simple, fast and elegant image viewer program"
arch=('x86_64')
url="https://siyanpanayotov.com/project/viewnior"
license=('GPL3')
depends=('gtk2' 'exiv2')
makedepends=('meson')
source=(https://github.com/hellosiyan/Viewnior/archive/viewnior-$pkgver.tar.gz
        fix-appdata.patch)
sha256sums=('b9319171ebca23929e6ea8039c194b1595d92e764db5e3890836fb5c36e233b0'
            '35214a37a1a058261efd442cad1edfe85dbfdc0fb4e14c6984b446d9f47560e4')

prepare() {
  cd Viewnior-viewnior-$pkgver
#  patch -Np1 -i ../fix-appdata.patch
}

build() {
  cd Viewnior-viewnior-$pkgver
  arch-meson build
  ninja -C build
}

check() {
  cd Viewnior-viewnior-$pkgver
  meson test -C build
}

package() {
  cd Viewnior-viewnior-$pkgver
  DESTDIR="$pkgdir" meson install -C build
}
