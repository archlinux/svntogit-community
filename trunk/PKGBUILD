# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Tim Diels <tim@timdiels.be>

pkgbase=python-dropbox
pkgname=(python-dropbox python2-dropbox)
pkgver=2.2.0
pkgrel=2
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://www.dropbox.com/developers/core/sdks/python"
arch=('any')
license=('MIT')
makedepends=(python-urllib3 python2-urllib3 python2-setuptools python-setuptools)
source=("https://www.dropbox.com/developers/downloads/sdks/core/python/dropbox-python-sdk-$pkgver.zip")
sha256sums=('7886c816fddbda6fa0ee707e6f30b1be8b5b2c4298385edceed01e9d5d98c43a')

package_python-dropbox() {
  depends=(python python-urllib3)

  cd "$srcdir/dropbox-python-sdk-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-dropbox() {
  depends=(python2 python2-urllib3)

  cd "$srcdir/dropbox-python-sdk-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
