# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libosinfo
pkgver=0.2.12
pkgrel=1
pkgdesc="GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support"
arch=('i686' 'x86_64')
url="http://libosinfo.org/"
license=('GPL' 'LGPL')
depends=('glib2' 'libxslt')
makedepends=('check' 'gobject-introspection' 'intltool' 'libsoup' 'vala' 'python2')
options=('!libtool')
source=(https://fedorahosted.org/releases/l/i/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('fa00ea8ddbca06c0dcc31e8938ac55cb71e71c6e2449687cd2c9e003a9478fed')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-udev \
              --with-udev-rulesdir=/usr/lib/udev/rules.d \
              --with-usb-ids-path=/usr/share/hwdata/usb.ids \
              --with-pci-ids-path=/usr/share/hwdata/pci.ids
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
