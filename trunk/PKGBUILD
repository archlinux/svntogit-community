# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=opentimelineio
pkgver=0.13
pkgrel=1
pkgdesc='Open Source API and interchange format for editorial timeline information'
url='http://opentimeline.io/'
license=(Apache)
arch=(x86_64)
depends=(python-aaf2)
makedepends=(cmake optional-lite python-pip pybind11 git)
source=(https://github.com/PixarAnimationStudios/OpenTimelineIO/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        git+https://github.com/thelink2012/any#commit=69753a5
        git+https://github.com/Tencent/rapidjson#commit=66eb606)
sha256sums=('33a63891b4656804242512e122b33ed12e35d4038fd78610ccb82b441b9506dd'
            'SKIP'
            'SKIP')

prepare() {
  mv any rapidjson OpenTimelineIO-$pkgver/src/deps
  sed -e '/deps/d' -i OpenTimelineIO-$pkgver/src/CMakeLists.txt # Unbundle pybind11
  sed -e '1 i\find_package(pybind11)' -i OpenTimelineIO-$pkgver/src/py-opentimelineio/opentime-bindings/CMakeLists.txt \
                                      -i OpenTimelineIO-$pkgver/src/py-opentimelineio/opentimelineio-bindings/CMakeLists.txt
}

build() {
  cd OpenTimelineIO-$pkgver
  python setup.py build
}

package() {
  cd OpenTimelineIO-$pkgver
  DESTDIR="$pkgdir" python setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/build/opentimelineio/src/*/*/*/opentimelineio/* "$pkgdir"/usr/lib/python*/site-packages/opentimelineio
  rm -fr "$pkgdir"/build
}
