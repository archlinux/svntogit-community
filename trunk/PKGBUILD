# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: DasIch <dasdasich@googlemail.com>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgbase=python-repoze.lru
pkgname=(python-repoze.lru python2-repoze.lru)
pkgver=0.7
pkgrel=2
pkgdesc="A tiny LRU cache implementation and decorator"
arch=("any")
url="https://pypi.python.org/pypi/repoze.lru"
license=("custom:BSD")
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.io/packages/source/r/repoze.lru/repoze.lru-$pkgver.tar.gz")
sha512sums=('c4d8b015931c4265fa8559f99c9d5016835574b368ebe0d49fe503afe908020b2bf29836606134e00c6c0aa436d52a00bb73d6ab80e98d0802f54bd2e4686476')

prepare() {
  cp -a repoze.lru-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/repoze.lru-$pkgver
  python setup.py build

  cd "$srcdir"/repoze.lru-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/repoze.lru-$pkgver
  python setup.py test

  cd "$srcdir"/repoze.lru-$pkgver-py2
  python2 setup.py test
}

package_python-repoze.lru() {
  depends=("python")

  cd repoze.lru-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-repoze.lru() {
  depends=("python2")

  cd repoze.lru-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
