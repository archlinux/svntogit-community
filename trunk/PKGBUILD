# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
# https://github.com/aws/serverless-application-model/releases
pkgver=1.55.0
pkgrel=1
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/aws/serverless-application-model'
license=(Apache)
depends=(python python-botocore python-boto3 python-jsonschema)
makedepends=(python-setuptools)
# python-yaml is needed by samtranslator.yaml_helper, while the latter is used in tests only
checkdepends=(python-pytest python-pytest-rerunfailures python-yaml python-parameterized)
# Upstream intentionally exclude tests from sdist
# https://github.com/aws/serverless-application-model/pull/381#issuecomment-382143094
source=("https://github.com/aws/serverless-application-model/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "66636f1d9c627bbb69cf1e3c2e7f571eb4dbc2d4.patch"
        "74cc6eb7881be7b9cb5d9fdd90e6d7e30d4639fb.patch")
sha256sums=('e1d53fa663599ccdb0667e25f6f6fe38ea6aee077a2db1d91a24b74fe56fe486'
            '650733dc475753f22ad96d664666ac3304eac595c99662649c39e8c8dd950a5b'
            'd7985de11acb845ea93b615765a8496277c1aaf63b437dd1e3cd53312677d9a4')

prepare() {
  cd serverless-application-model-$pkgver
  # skip pytest-coverage
  sed -i -r 's#--cov\S*\s+\S+##g' pytest.ini
  # For jsonschema 4.x. Backported from
  # https://github.com/aws/serverless-application-model/pull/2511
  patch -Np1 -i ../66636f1d9c627bbb69cf1e3c2e7f571eb4dbc2d4.patch
  # A regression fix - prevent bin/ from installed as a package
  # Backported from https://github.com/aws/serverless-application-model/pull/2597
  patch -Np1 -i ../74cc6eb7881be7b9cb5d9fdd90e6d7e30d4639fb.patch
}

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver
  # See https://github.com/aws/serverless-application-model/blob/v1.52.0/Makefile#L9
  AWS_DEFAULT_REGION=us-east-1 pytest tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
