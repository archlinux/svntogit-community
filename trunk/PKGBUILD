# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gcolor3
pkgver=2.3.1
pkgrel=2
pkgdesc="Simple color chooser written in GTK3"
arch=(x86_64)
url="https://www.hjdskes.nl/projects/gcolor3/"
license=(GPL3)
depends=(gtk3)
makedepends=(git meson)
_commit=17dca0125fc3c931418d8723b684332d35bd5715  # tags/v2.3.1^0
source=("git+https://gitlab.gnome.org/World/gcolor3.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
