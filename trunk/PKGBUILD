# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyhamcrest
pkgver=1.10.1
pkgrel=4
pkgdesc="Hamcrest framework for matcher objects"
arch=('any')
license=('BSD')
url="https://github.com/hamcrest/PyHamcrest"
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=("https://github.com/hamcrest/PyHamcrest/archive/V$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f7e79f7d26c4c6add042052e451898bbf5adf21b05a234cc62bce890d68f5997693b042a9daf136f7026040a8ab7bf28b829288417b947973dcb959bf613a37b')

build() {
  cd PyHamcrest-$pkgver
  python setup.py build
}

check() {
  cd PyHamcrest-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd PyHamcrest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
