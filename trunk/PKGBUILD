# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cytoolz
pkgname=python-cytoolz
pkgver=0.11.2
pkgrel=3
pkgdesc="Cython implementation of Toolz: High performance functional utilities"
arch=('x86_64')
url="https://github.com/pytoolz/cytoolz/"
license=('BSD')
depends=('cython' 'python-toolz')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7df91df94eee56097798ab75222a8c48d7bab042bfb284230c8888121a166fbf356a13ee49dc5fc54d5a8aad3c7bfbe0e4225ba20dbe36927655154e93b7bd13')
b2sums=('9ed21c1a285b2d7dbe5c7e93656f9f062a184c20d81f9739d0c7173deae7c6d90fe85d77a56f08fc9af1cbce8fd6d6cdccdd44a6bfb2eb69268ae4fdee006147')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  # build C extension
  python setup.py build_ext --inplace --with-cython
  # build python extension
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _pyver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  export PYTHONPATH="build/lib.linux-${CARCH}-${_pyver}/:${PYTHONPATH}"
  echo "$PYTHONPATH"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
