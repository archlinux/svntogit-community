# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=zint
pkgname=(zint zint-qt)
pkgver=2.10.0
pkgrel=2
pkgdesc='Barcode encoding library supporting over 50 symbologies'
arch=('x86_64')
url='http://zint.org.uk/'
license=('GPL3')
makedepends=('cmake' 'ninja' 'qt6-tools')
source=("https://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver-src.tar.gz"
        'zint-qt.appdata.xml')
sha256sums=('bb97e98a32e140c344e92c8da84a9df413dca16083f2fcdc29791bec77350339'
            '4286c7f56f0ea8fcf8dfa164b3a3fea08a02ad3b5111c6ac63c4d55134b67564')

build() {
  # Hack to force Qt6
  CMAKE_PREFIX_PATH="6.0.0" \
  cmake -S $pkgbase-$pkgver-src -B build -G Ninja -DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT6=ON \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package_zint() {
  depends=('libpng')

  DESTDIR="$pkgdir" cmake --install build

  rm "$pkgdir/usr/bin/zint-qt" \
     "$pkgdir/usr/include/qzint.h" \
     "$pkgdir/usr/lib/libQZint"*
}

package_zint-qt() {
  pkgdesc='Zint Barcode Studio GUI'
  depends=('hicolor-icon-theme' 'qt6-tools' 'zint')

  DESTDIR="$pkgdir" cmake --install build/backend_qt
  DESTDIR="$pkgdir" cmake --install build/frontend_qt

  install -Dm644 $pkgbase-$pkgver-src/zint-qt.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/zint-qt.png"
  install -Dm644 $pkgbase-$pkgver-src/zint-qt.desktop "$pkgdir/usr/share/applications/zint-qt.desktop"
  install -Dm644 zint-qt.appdata.xml "$pkgdir/usr/share/metainfo/zint-qt.appdata.xml"
}
