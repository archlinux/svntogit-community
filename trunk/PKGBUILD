# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>

pkgbase=python-llfuse
pkgname=(python-llfuse python2-llfuse)
pkgver=1.3
pkgrel=1
pkgdesc="A set of Python bindings for the low level FUSE API."
arch=('x86_64')
url="https://bitbucket.org/nikratio/python-llfuse/"
license=('LGPL3')
depends=('fuse2')
makedepends=('pkg-config' 'python' 'python2' 'python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("https://bitbucket.org/nikratio/python-llfuse/downloads/llfuse-${pkgver}.tar.bz2")
md5sums=('7c8b3ad98920b7786b6aa4fa7de1c07d')

build() {
  cd "${srcdir}/llfuse-${pkgver}"
  python setup.py build
}

package_python-llfuse() {
  depends+=(python python-setuptools)

  cd "${srcdir}/llfuse-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-llfuse() {
  depends+=(python2 python2-setuptools)

  cd "${srcdir}/llfuse-${pkgver}"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
