# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
# https://github.com/p1c2u/openapi-schema-validator/releases
pkgver=0.4.3
# curl https://api.github.com/repos/p1c2u/openapi-schema-validator/git/ref/tags/$pkgver | jq -r .object.sha
_tag=03715982780dae67884228001ed056e88faa6ce2
pkgrel=1
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-jsonschema' 'python-rfc3339-validator')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest')
source=("git+https://github.com/p1c2u/openapi-schema-validator.git#tag=$_tag")
sha512sums=('SKIP')

pkgver() {
  cd openapi-schema-validator
  git describe --tags
}

prepare() {
  cd openapi-schema-validator
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd openapi-schema-validator
  python -m build --wheel --no-isolation
}

check() {
  cd openapi-schema-validator
  python -m pytest
}

package() {
  cd openapi-schema-validator
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
