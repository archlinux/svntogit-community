# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Mubashshir <ahmubashshir@gmail.com>

_name=jq.py
pkgname=python-jq
pkgver=1.2.1
pkgrel=2
pkgdesc='Python bindings for jq'
arch=('x86_64')
url=https://github.com/mwilliamson/jq.py
license=('BSD')
depends=('jq' 'python')
makedepends=('cython' 'python-build' 'python-install' 'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz"
        'jq-py-setup.patch')
b2sums=('8bc2c88ae553dd53137c11d4f599e01398d21811a02b4f33f81aeeafa9009f58a56074c0bcadcc6382d86d2c7ada792d442f9c660787e131d0e85c95fb7cab32'
        '3424a91e0e6330e8d25e167b862554e466f469cdeb5672d4d4e5ff4d4475bfe9c7a575f3c93cd03687e10cc6afac2bb94294c0fef50feec03eaa55684e2907bc')

# Devendor
prepare() {
  cd $_name-$pkgver
  sed -i '/certifi\|requests/d' pyproject.toml
  patch --forward --strip=1 --input=../jq-py-setup.patch
}

build() {
  cd $_name-$pkgver
  cython jq.pyx
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m install --optimize=1 dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $_name-$pkgver
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/jq-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
