# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astor
pkgver=0.8
pkgrel=1
pkgdesc="Read/rewrite/write Python ASTs"
arch=('any')
license=('BSD')
url="https://astor.rtfd.org/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/berkerpeksag/astor/archive/$pkgver.tar.gz")
sha512sums=('ca787234f9a9caae37633be289d6e980e7b44507d74306ad7a30f017442d6cf423748ef35465b7b5d12bdb84ce4594af801de39882f4c11c732dba4dc65ab667')

build() {
  cd astor-$pkgver
  python setup.py build
}

check() {
  cd astor-$pkgver
  nosetests3 || warning "https://github.com/berkerpeksag/astor/issues/146"
}

package() {
  cd astor-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
