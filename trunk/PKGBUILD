# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>

pkgname=xdelta3
pkgver=3.0.8
pkgrel=1
pkgdesc='Diff utility which works with binary files'
arch=('x86_64' 'i686')
url='http://xdelta.org/'
license=('GPL')
depends=('xz')
source=("http://${pkgname/3}.googlecode.com/files/$pkgname-$pkgver.tar.xz"
        'regtest_size_t.patch')
# SHA1 sum from https://code.google.com/p/xdelta/downloads/detail?name=xdelta3-3.0.8.tar.xz&can=2&q=        
sha1sums=('62c7a029e96c0904bb47a5e2f3de08473a185539'
          'c2816b5d4bfb405d3adb771fa09b367a88971240')

prepare() {
  cd "$pkgname-$pkgver"
  
  patch -Np1 -i ../regtest_size_t.patch
}

check() {
  cd "$pkgname-$pkgver"

  ./$pkgname test
}

build() {
  cd "$pkgname-$pkgver"
  
  ./configure --prefix=/usr
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
