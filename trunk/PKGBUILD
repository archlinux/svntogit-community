# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=2.1.1
pkgrel=1
pkgdesc="A KDE program that browses samba shares"
arch=(x86_64)
url="https://smb4k.sourceforge.net/"
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('36f68f75ec3b9ada0f263acbee24a4e9b3f7aa0136e23b12f2af8f06f6c606bc')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
