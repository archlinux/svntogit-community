# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Elvis Angelaccio <elvis.angelaccio@kde.org>

pkgname=hashcat-utils
pkgver=1.8
pkgrel=2
pkgdesc='Set of small utilities that are useful in advanced password cracking'
url='https://github.com/hashcat/hashcat-utils'
arch=('x86_64')
license=('MIT')
depends=('perl')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/hashcat/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('c57f2ff1c43e65972fd9dde31322f4b9178d65a28cca3adc235ae41a5982f11c')
sha512sums=('c096d31bb9d6421a8943ba7da6e13f4778695d8a43b7e9a8e9e302f2971973fdcebda42c91d315c787e4475d8b78bdb90b9d2a0c675c8e55f4687f222e50b4e5')

build() {
  cd ${pkgname}-${pkgver}
  make -C src
}

package() {
  cd ${pkgname}-${pkgver}
  for bin in src/*.bin; do
      install -Dm 755 ${bin} "${pkgdir}/usr/bin/$(basename ${bin} .bin)"
  done
  for script in src/*.pl; do
      install -Dm 755 ${script} "${pkgdir}/usr/bin/$(basename ${script} .pl)"
  done
  install -Dm 644 README.md CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
