# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=1.27.0
pkgrel=1
pkgdesc="User level public file sharing via WebDAV for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('gtk3' 'mod_dnssd' 'gettext')
makedepends=('caja' 'dbus-glib' 'itstool' 'libcanberra' 'libnotify')
optdepends=('caja: File sharing extension')
groups=('mate-extra')
conflicts=('mate-user-share-gtk3')
replaces=('mate-user-share-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('936df4d5c6a0d7a6107524767c0340a3b4080b05a151a71eb8d8174c32396287')

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
	            --prefix=/usr \
            	--libexec=/usr/lib/${pkgname} \
            	--sysconfdir=/etc \
	            --disable-bluetooth

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make  DESTDIR="${pkgdir}" install
}
