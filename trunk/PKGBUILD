# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=lib32-atk
pkgver=2.38.0
pkgrel=1
pkgdesc="Interface definitions of accessibility infrastructure (32-bit)"
url="https://gitlab.gnome.org/GNOME/atk"
arch=(x86_64)
license=(LGPL)
depends=(lib32-glib2 atk)
makedepends=(git gtk-doc meson)
provides=(libatk-1.0.so)
options=(debug)
_commit=f1051ba57a2110c46a136a48f4d60a405bc2c3f5  # tags/2.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/atk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd atk
  git describe --tags | sed 's/^ATK_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd atk
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson atk build --libdir /usr/lib32 -D introspection=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "${pkgdir}"/usr/{include,share}
}
