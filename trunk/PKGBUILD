# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=cni-plugins
pkgver=0.8.5
pkgrel=1
pkgdesc='Some standard networking plugins, maintained by the CNI team'
arch=(x86_64)
url="https://github.com/containernetworking/plugins"
license=(Apache)
depends=(glibc)
makedepends=(go-pie)
source=($pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz)
sha512sums=('e404ef69da189e2cc3e77ae1054fbea5232086066f2963d1ec824f63c02aede020ad8600d821ec47ae695043bd2c4a176d9e02b9a059024ff3a2fbeee9cb797a')

build() {
  cd plugins-$pkgver
  export GOFLAGS="-trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  ./build_linux.sh
}

package() {
  cd plugins-$pkgver/bin
  for bin in *; do
    install -Dm755 $bin "$pkgdir/usr/lib/cni/$bin"
  done
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
