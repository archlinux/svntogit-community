# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cbc
pkgver=2.9.7
pkgrel=2
pkgdesc="COIN-OR branch-and-cut mixed integer programming solver"
arch=(i686 x86_64)
url="https://projects.coin-or.org/Cbc"
license=(EPL)
groups=(coin-or)
depends=(coin-or-cgl)
source=("http://www.coin-or.org/download/source/Cbc/Cbc-${pkgver}.tgz")
sha1sums=('2f1ae1c13cfb686d1f3ec5045acef524b10070c0')

build() {
  cd Cbc-$pkgver/Cbc
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-clp-lib="$(pkg-config --libs clp)" \
              --with-clp-incdir="/usr/include/coin/" \
              --with-cgl-lib="$(pkg-config --libs cgl)" \
              --with-cgl-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --enable-dependency-linking \
              --enable-cbc-parallel
  make
}

check() {
  cd Cbc-$pkgver/Cbc
  make test
}

package() {
  cd Cbc-$pkgver/Cbc
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
