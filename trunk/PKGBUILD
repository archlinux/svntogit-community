# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=starlette
pkgname=python-$_pkgname
pkgver=0.14.2
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=('any')
url='https://github.com/encode/starlette'
license=('BSD')
depends=('python' 'python-anyio')
optdepends=('python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
            'python-yaml' 'python-requests' 'python-ujson' 'python-python-multipart')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-trio' 'python-pytest-asyncio'
              'python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
              'python-yaml' 'python-requests' 'python-ujson'
              'python-unidecode' 'python-python-multipart')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('4321d3d1890c49dea53d7923b456772ecc9d28083d28392e0e9cd1f39f33180cc1cb28914bee443849fa8c87015dd99610ea18e03f0101b0eaf3f8df56249fab')

prepare() {
  cd $_pkgname-$pkgver

  # remove pytest-cov usage
  sed -i '/^  --cov.*/d' setup.cfg

  # needs python-multipart
  rm tests/test_formparsers.py

  # needs python-databases
  rm tests/test_database.py

  # needs a different graphql version
  rm tests/test_graphql.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest -p no:warnings
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
