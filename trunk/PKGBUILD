# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>
# Contributor: scj <scj archlinux us>

pkgname=python-levenshtein
pkgver=0.20.6
pkgrel=1
pkgdesc="Python extension for computing string edit distances and similarities"
url="https://pypi.python.org/pypi/python-Levenshtein/"
license=('GPL')
arch=('x86_64')
depends=('python-rapidfuzz')
makedepends=('python-scikit-build' 'cython' 'rapidfuzz-cpp')
checkdepends=('python-pytest')
#source=("https://files.pythonhosted.org/packages/source/L/Levenshtein/Levenshtein-$pkgver.tar.gz")
source=("https://github.com/maxbachmann/Levenshtein/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4e2d6d7f59e8e2190029d9f5f49ed8407997935076233b79a7c81470a84446851e14f5edbbb42d48a85dbdf0116b40186d896f172f2d740f769a7e7cb9350e7f')

build() {
  cd Levenshtein-$pkgver
  python setup.py build
}

check() {
  cd Levenshtein-$pkgver
  PYTHONPATH=`echo "$PWD"/_skbuild/linux-x86_64-*/setuptools/lib.linux-x86_64-*` \
  pytest
}

package() {
  cd Levenshtein-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
