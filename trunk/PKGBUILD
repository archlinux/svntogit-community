# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=netwatch
pkgver=1.3.1_2
pkgrel=1
pkgdesc="monitor network connections"
arch=(x86_64)
url="http://www.slctech.org/~mackay/NETWATCH/netwatch.html"
license=('GPL')
options=(zipman)
depends=(ncurses)
source=(http://www.slctech.org/~mackay/NETWATCH/netwatch-${pkgver/_/-}.tgz)
md5sums=('6147b0235207912ca941a4e45c9cee14')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  sed -i 's|${CC-cc} |${CC-cc} -O2 |g' configure
}

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  install -D -m0755 netwatch "$pkgdir"/usr/bin/netwatch
  install -D -m0755 netresolv "$pkgdir"/usr/bin/netresolv
  install -D -m0644 netwatch.1 "$pkgdir"/usr/share/man/man1/netwatch.1
}
