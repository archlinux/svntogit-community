# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Ben Booth <benwbooth@gmail.com>

pkgname=perl-text-soundex
pkgver=3.05
pkgrel=10
pkgdesc='Implementation of the soundex algorithm'
arch=('x86_64')
url="https://metacpan.org/release/Text-Soundex"
license=('GPL' 'PerlArtistic')
depends=(perl)
options=('!emptydirs' purge)
source=("https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Text-Soundex-$pkgver.tar.gz")
sha256sums=('f6dd55b4280b25dea978221839864382560074e1d6933395faee2510c2db60ed')

build() (
  cd "$srcdir/Text-Soundex-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd "$srcdir/Text-Soundex-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
)

package() (
  cd "$srcdir/Text-Soundex-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)
