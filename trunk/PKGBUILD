# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Warrick <aur@chriswarrick.com>

pkgbase=python-pygal
pkgname=('python-pygal' 'python2-pygal')
epoch=1
pkgver=2.0.8
pkgrel=1
pkgdesc='A Python SVG graph plotting library'
arch=('any')
url='http://pygal.org/'
license=('LGPL3')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-lxml' 'python2-lxml'
              'python-pyquery' 'python2-pyquery' 'python-cairosvg' 'python2-cairosvg'
              'python-flask' 'python2-flask')
source=("git+https://github.com/Kozea/pygal.git#tag=$pkgver"
        python-3.5.patch)
md5sums=('SKIP'
         '184addfb9698e8e8d67cfdc50c898866')

prepare() {
  # https://github.com/Kozea/pygal/pull/278
  (cd pygal; patch -p1 -i ../python-3.5.patch)

  cp -a "${srcdir}/pygal"{,-py2}
}

build() {
  cd "$srcdir/pygal"
  python setup.py build

  cd "$srcdir/pygal-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pygal"
  LC_CTYPE=en_US.UTF-8 py.test pygal/

  cd "$srcdir/pygal-py2"
  py.test2 pygal/
}

package_python-pygal() {
  depends=('python-setuptools')
  optdepends=('python-lxml: for XML output through lxml instead of xml.etree'
              'python-cairosvg: for PNG output')

  cd "${srcdir}/pygal"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-pygal() {
  depends=('python2-setuptools')
  optdepends=('python2-lxml: for XML output through lxml instead of xml.etree'
              'python2-cairosvg: for PNG output')

  cd "${srcdir}/pygal-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  mv "${pkgdir}"/usr/bin/pygal_gen{,2}.py
}

# vim:set ts=2 sw=2 et:
