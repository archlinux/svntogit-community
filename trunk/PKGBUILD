# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=glabels
pkgver=3.4.1
pkgrel=2
pkgdesc="Creating labels and business cards the very easy way"
arch=('x86_64')
url="https://glabels.org/"
license=('GPL' 'LGPL')
depends=('evolution-data-server' 'iec16022' 'qrencode' 'zint')
makedepends=('intltool' 'itstool' 'python')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('18e457298abb1c617187361109eeae769317686303fc07726af31e11519c5938')

prepare() {
  cd $pkgname-$pkgver

  # Don't use legacy path for AppStream metainfo file
  # https://gitlab.gnome.org/GNOME/glabels/merge_requests/2
  sed -i 's|appdatadir = $(datadir)/appdata|appdatadir = $(datadir)/metainfo|' data/appdata/Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
