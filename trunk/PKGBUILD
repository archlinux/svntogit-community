# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

pkgname=proxytunnel
pkgver=1.9.0.253
pkgrel=3
pkgdesc="a program that connects stdin and stdout to a server somewhere on the network, through a standard HTTPS proxy"
arch=('x86_64')
url="http://proxytunnel.sourceforge.net"
license=('GPL')
depends=('openssl')
makedepends=('subversion' 'asciidoc' 'xmlto')
#source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tgz
source=("svn://svn.code.sf.net/p/proxytunnel/code/trunk/proxytunnel#revision=${pkgver##*.}"
        fix-Makefile-ssl-FTBFS.patch
        migrate-from-SSLv3.patch)
md5sums=('SKIP'
         'facacbf723512e7c94a4d7e6a0d12eeb'
         '71c403c2bd6fb332bc5839894bcafe76')

prepare() {
  cd $pkgname
  patch -Np1 -i ../fix-Makefile-ssl-FTBFS.patch
  patch -Np1 -i ../migrate-from-SSLv3.patch
}

build() {
  cd $pkgname
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make prefix=/usr
}

package() {
  cd $pkgname
  make prefix=/usr DESTDIR="$pkgdir" install
}
