# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-dkim
pkgver=1.0.5
pkgrel=1
pkgdesc='Python library that implements DKIM and ARC email signing and verification'
arch=('any')
url=https://launchpad.net/dkimpy
license=('ZLIB')
depends=('python-dnspython' 'python-setuptools')
checkdepends=('python-authres' 'python-pynacl')
optdepends=('python-aiodns' 'python-authres' 'python-pynacl')
source=("https://files.pythonhosted.org/packages/source/d/dkimpy/dkimpy-$pkgver.tar.gz")
sha256sums=('9a2420bf09af686736773153fca32a02ae11ecbe24b540c26104628959f91121')

build() {
  cd dkimpy-$pkgver
  python setup.py build
}

check() {
  cd dkimpy-$pkgver
  python -m unittest dkim.tests.test_suite
}

package() {
  cd dkimpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
