# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Max Roder <maxroder@web.de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=libhx
pkgver=4.9
pkgrel=1
pkgdesc='A library providing queue, tree, I/O and utility functions'
arch=(x86_64)
url='http://libhx.sourceforge.net/'
license=(GPL)
depends=(glibc)
source=(https://inai.de/files/libhx/libHX-$pkgver.tar.{xz,asc})
sha256sums=('02e225226d937bf7611941e9100c4d278332543d6b3b978fa880222916ba3c9b'
            'SKIP')
validpgpkeys=(BCA0C5C309CAC569E74A921CF76EFE5D0C223A8F) # Jan Engelhardt <jengelh@inai.de> https://inai.de/about/

build() {
  cd libHX-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd libHX-$pkgver
  make DESTDIR="$pkgdir" install
}
