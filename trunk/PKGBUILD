# Maintainer: Alad Wenter <alad@archlinux.org>
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=mc
pkgver=4.8.24
pkgrel=2
pkgdesc="A file manager that emulates Norton Commander"
arch=('x86_64')
url="https://midnight-commander.org/"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'gpm' 'libssh2' 'slang')
makedepends=('libxt' 'libx11' 'unzip')
optdepends=('aspell: spelling corrections'
            'cabextract: ucab extfs'
            'cdparanoia: audio extfs'
            'cdrkit: iso9660 extfs'
            'cvs: CVS support'
            'gawk: hp48+ extfs'
            'mtools: a+ extfs'
            'p7zip: support for 7zip archives'
            'perl: needed by several extfs scripts'
            'python-boto: s3+ extfs'
            'python-pytz: s3+ extfs'
            'samba: VFS support'
            'unace: uace extfs'
            'unarj: uarj extfs'
            'unrar: urar extfs'
            'zip: uzip extfs')
backup=('etc/mc/edit.indent.rc'
        'etc/mc/filehighlight.ini'
        'etc/mc/mc.ext'
        'etc/mc/mc.keymap'
        'etc/mc/mc.menu'
        'etc/mc/mcedit.menu'
        'etc/mc/sfs.ini')
options=('!emptydirs')
source=("http://ftp.midnight-commander.org/${pkgname}-${pkgver}.tar.xz"
        "mc-mksh-subshell-v2.patch"
        "mc-python3.patch")
sha256sums=('859f1cc070450bf6eb4d319ffcb6a5ac29deb0ac0d81559fb2e71242b1176d46'
            '5147afa3f9dfc00d8b7b36bbb144bcdb78d86301a0f8196686262a9eee41fb96'
            '10ab8b8c03770f8fe51f0bdbf0d66a44313bf2eed687cf769397909c07d8e8d5')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "$srcdir"/mc-mksh-subshell-v2.patch
  patch -p1 < "$srcdir"/mc-python3.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
      --prefix=/usr \
      --libexecdir=/usr/lib \
      --sysconfdir=/etc \
      --enable-vfs-smb \
      --with-screen=slang \
      --with-x
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # Replace mc.keymap symlink with target file to fix backup mechanism (FS#50889).
  rm "${pkgdir}"/etc/mc/mc.keymap && cp "${pkgdir}"/etc/mc/mc{.default,}.keymap
}
