# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: TryA <tryagainprod {at} gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libglade
pkgname=lib32-libglade
pkgver=2.6.4
pkgrel=6
pkgdesc="Allows you to load glade interface files in a program at runtime (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-gtk2' 'lib32-libxml2' "${_pkgbasename}")
makedepends=('python2' 'pkgconfig' 'gcc-multilib')
source=("https://download.gnome.org/sources/${_pkgbasename}/2.6/${_pkgbasename}-${pkgver}.tar.bz2"
        'libglade-2.0.1-nowarning.patch')
url='http://www.gnome.org'
md5sums=('d1776b40f4e166b5e9c107f1c8fe4139'
         '4f879c0ce550004905aa0fb24f25c353')

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd "${srcdir}/${_pkgbasename}-${pkgver}"
	patch -Np1 -i "${srcdir}/libglade-2.0.1-nowarning.patch"
	PYTHON=python2 ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var --libdir=/usr/lib32
	make
}

package() {
	cd "${srcdir}/${_pkgbasename}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rm -rf ${pkgdir}/usr/{bin,include,share}
}
