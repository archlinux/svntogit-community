# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marc Moody <marc.r.moody at gmail dot com>
# Contributor: Dinh Bowman <dinh at vagueindustries dot com>

pkgname=rapid-photo-downloader
pkgver=0.9.24
pkgrel=2
pkgdesc="Photo and video downloader"
arch=('any')
url="https://www.damonlynch.net/rapid/"
license=('GPL3')
depends=('desktop-file-utils' 'gstreamer' 'hicolor-icon-theme' 'libgexiv2' 'libnotify'
         'perl-image-exiftool' 'python-arrow' 'python-colour' 'python-dateutil' 'python-easygui'
         'python-gobject' 'python-gphoto2' 'python-psutil' 'python-pymediainfo' 'python-pyqt5'
         'python-pyzmq' 'python-rawkit' 'python-requests' 'python-sortedcontainers' 'python-babel'
         'python-tornado' 'python-xdg' 'qt5-imageformats' 'udisks2' 'python-tenacity' 'qt5-svg')
makedepends=('intltool' 'python-setuptools')
changelog=$pkgname.changelog
source=(https://launchpad.net/rapid/pyqt/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('66b9482c9212043b519416982dacd734f8d05c285eb8d7e3ee3f0d7a29068590'
            'SKIP')
validpgpkeys=('E26048A9F4A803B91CB1BD648005B1F36970BE28') # Damon Lynch <damonlynch@gmail.com>

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  python setup.py build
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  python setup.py install --root="${pkgdir}" --optimize=1
}
