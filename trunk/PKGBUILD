# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:thefatprecious <marty@loomar.com>

pkgname=tvtime
pkgver=1.0.2
pkgrel=5
pkgdesc="A high quality television application for use with video capture cards"
arch=('i686' 'x86_64')
url='http://tvtime.sourceforge.net/'
license=('GPL' 'LGPL')
depends=('gcc-libs' 'libpng' 'libxml2' 'freetype2' 'libxtst' 'libxinerama' 'libxv'
	 'libsm' 'libxxf86vm')
makedepends=('libxt' 'v4l-utils')
source=(http://downloads.sourceforge.net/sourceforge/tvtime/$pkgname-$pkgver.tar.gz
	tvtime-1.0.2+linux-headers-2.6.18.patch
	tvtime-1.0.2-gcc41.patch
	tvtime-1.0.2-locale_t.patch)
md5sums=('4b3d03afe61be239b08b5e522cd8afed'
         'e956821745686b5b3a784f7471996ade'
         'c782789825dfa664e1f6fcc96ac7246c'
         '1069dc5b47bba8456cbf48e34cb5c0b6')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -p1 < ../tvtime-1.0.2+linux-headers-2.6.18.patch
  patch -p1 < ../tvtime-1.0.2-gcc41.patch
  patch -p1 < ../tvtime-1.0.2-locale_t.patch
  sed -i '1,1i#include <zlib.h>' src/pngoutput.c
  echo "StartupWMClass=tvtime" >>docs/net-tvtime.desktop

  [ -f Makefile ] || ./configure --prefix=/usr --mandir=/usr/share/man --localstatedir=/var --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 docs/html/default.tvtime.xml $pkgdir/usr/share/tvtime/default.tvtime.xml
}
