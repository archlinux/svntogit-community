# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=2.10.0
pkgrel=5
pkgdesc="Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=(x86_64)
url="https://www.ospray.org/"
license=(Apache)
depends=(tbb embree ispc libgl glfw glu openmpi openimagedenoise openvkl rkcommon snappy)
optdepends=('benchmark: benchmarks'
            'gtest: tests suite')
makedepends=(cmake benchmark gtest)
source=(https://github.com/ospray/OSPRay/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bd478284f48d2cb775fc41a2855a9d9f5ea16c861abda0f8dc94e02ea7189cb8')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOSPRAY_BUILD_ISA=ALL \
    -DOSPRAY_ENABLE_APPS_EXAMPLES=OFF \
    -DOSPRAY_ENABLE_APPS_TUTORIALS=OFF \
    -DOSPRAY_ENABLE_MODULES=ON \
    -DOSPRAY_MODULE_DENOISER=ON \
    -DOSPRAY_MODULE_MPI=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
