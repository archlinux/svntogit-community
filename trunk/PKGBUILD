# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.envs
pkgver=2.1.1
pkgrel=4
pkgdesc="Classes for orchestrating Python (virtual) environments"
url="https://github.com/jaraco/jaraco.envs"
license=('MIT')
arch=('any')
depends=('python-path' 'python-virtualenv' 'python-tox')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-checkdocs' 'python-pytest-cov'
              'python-pytest-enabler' 'python-pytest-flake8' 'python-pytest-mypy'
              'python-pip')
source=("https://github.com/jaraco/jaraco.envs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('67a4db98ca4306bc9428adafadf824d315efd2e533940bd21b41fa34c5acd9b4da092850f4c7296a81511e029f8ae3861b2108d649eed851d63231f1b954326d')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd jaraco.envs-$pkgver
  python setup.py build
}

check() {
  cd jaraco.envs-$pkgver
  python -m pytest
}

package() {
  cd jaraco.envs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
