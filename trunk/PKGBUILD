# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=yapf
pkgver=0.29.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
depends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/yapf/archive/v$pkgver.tar.gz")
sha512sums=('6ed72dc1ae1e14a015ba9c5e5c516968a87ccee90d6edc479a6e2a27e59066bbcbc4b8641994b80fadc4960c3114ca66c1c58d4e2d9489c74ef3c704ec6760ff')

build() {
  cd yapf-$pkgver
  python setup.py build
}

check() {
  cd yapf-$pkgver
  nosetests3 || warning "https://github.com/google/yapf/issues/586"
}

package() {
  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
