# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=gmt
pkgname=(gmt gmt-doc)
pkgver=6.3.0
pkgrel=1
pkgdesc="Generic Mapping Tools for manipulating and plotting geographic and cartesian data"
arch=(x86_64)
url="https://www.generic-mapping-tools.org"
license=(LGPL)
makedepends=(cmake blas curl ffmpeg fftw gdal geos ghostscript graphicsmagick lapack openmp netcdf pcre zlib)
source=(https://github.com/GenericMappingTools/gmt/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.xz)
sha256sums=('69e29b62ee802a3a64260d6a1e023f1350e3bf4070221aa1307bf8a9e56c1ee5')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGMT_LIBDIR=lib \
    -DGMT_DATADIR=share/gmt \
    -DGMT_MANDIR=share/man \
    -DGMT_DOCDIR=share/doc/gmt \
    -DGMT_ENABLE_OPENMP=ON \
    -DGSHHG_ROOT=/usr/share/gmt/coast \
    -DDCW_ROOT=/usr/share/gmt/dcw \
    -DCOPY_GSHHG=OFF \
    -DCOPY_DCW=OFF
  make -C build
}

package_gmt() {
  depends=(blas curl fftw gdal geos lapack openmp netcdf pcre zlib)
  optdepends=(
    'gmt-doc: documentation'
    'ffmpeg: generate movies'
    'ghostscript: convert PostScript outputs'
    'graphicsmagick: generate animated GIFs'
  )
#    'gmt-coast: coastlines'
#    'gmt-dcw: digital chart of the world polygon map'

  make -C build DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/share/doc "${srcdir}"/
}

package_gmt-doc() {
  install -d "${pkgdir}"/usr/share/
  mv doc "${pkgdir}"/usr/share/
}
