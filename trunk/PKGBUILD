# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Dwayne Bent <dbb@dbb.io>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=prosody
pkgver=0.9.8
pkgrel=1
pkgdesc="Lightweight and extensible Jabber/XMPP server written in Lua"
arch=('i686' 'x86_64')
url="http://prosody.im/"
license=('MIT')
depends=('lua51' 'lua51-socket' 'lua51-expat' 'lua51-filesystem' 'libidn'
         'openssl')
optdepends=('lua51-sec: TLS encryption support'
	    'lua51-zlib: compression support')
install=prosody.install
backup=('etc/logrotate.d/prosody'
        'etc/prosody/prosody.cfg.lua')
validpgpkeys=('32A9EDDE3609931EB98CEAC315907E8E7BDD6BFE')
source=("http://prosody.im/downloads/source/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'prosody.logrotated'
        'fix-config.patch'
        'prosody.tmpfile.d'
        'prosody.service'
        'sysuser.conf')
md5sums=('5144cd832a1860443e21e336dc560ee7'
         'SKIP'
         '5b44aee99f1fa9e7f055e067688cafbd'
         '01b6aa0b1e1e3067756c45b0751bee78'
         'dc8405a6a235b83dc8a0dcdf7b71cbaa'
         'e74045f27cb60908d535969906781f75'
         '385ca73d9f6046f3636266ce9bf38797')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../fix-config.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --ostype=linux --prefix=/usr --sysconfdir=/etc/prosody \
    --datadir=/var/lib/prosody --with-lua-include=/usr/include/lua5.1 \
    --cflags="${CFLAGS} -fPIC -Wall -Wextra -D_GNU_SOURCE" \
    --ldflags="${LDFLAGS} -shared" --no-example-certs \
    --runwith=lua5.1
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install -C tools/migration

#  install -Dm 0644 $srcdir/prosody.logrotated "${pkgdir}"/etc/logrotate.d/prosody
  install -Dm 0644 $srcdir/prosody.tmpfile.d "${pkgdir}"/usr/lib/tmpfiles.d/prosody.conf
  install -Dm 0644 $srcdir/prosody.service "${pkgdir}"/usr/lib/systemd/system/prosody.service
  install -Dm644 $srcdir/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/prosody.conf

  for i in tools/*.lua; do
    install -Dm 0644 ${i} "${pkgdir}"/usr/share/prosody/${i}
  done

  install -Dm 0644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
  rm "${pkgdir}"/etc/prosody/certs/*
}

# vim: ft=sh syn=sh ts=2 sw=2
