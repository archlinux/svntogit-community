# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=1.5.4
pkgrel=2
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/mads-hartmann/bash-language-server"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "python2")
source=("https://github.com/mads-hartmann/$pkgname/archive/server-$pkgver.tar.gz")
sha512sums=('fbbbf9bbc200fd870d2d6e3780df1be48c6253418bc6849bb499860992c3fe681950c3e76867dd97f466bc26bba3302d6a871f6c6cf5d7a65082b11d8af91c1f')

build() {
    cd "$srcdir/$pkgname-server-$pkgver"
    yarn
    cd server
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .

    rm -r node_modules/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
