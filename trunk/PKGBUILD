# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=minder
pkgver=1.13.1
pkgrel=1
pkgdesc='Mind-mapping application designed for Pantheon'
arch=('x86_64')
url='https://github.com/phase1geo/Minder'
license=('GPL3')
depends=('discount' 'granite' 'gtksourceview4' 'libarchive' 'libhandy')
makedepends=('meson' 'vala')
source=("https://github.com/phase1geo/Minder/archive/$pkgver/Minder-$pkgver.tar.gz")
sha256sums=('f319501e40e53e7721864d73e8c071960acc40a4f32dd84e85646a2031be215a')

build() {
  arch-meson Minder-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  ln -s com.github.phase1geo.minder "$pkgdir/usr/bin/$pkgname"
}
