# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=(ansible python-ansible)
pkgver=2.2.1.0
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='http://www.ansible.com'
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-paramiko' 'python2-jinja' 'python2-crypto')
optdepends=('python2-passlib: crypt values for vars_prompt'
            'python2-netaddr: for the ipaddr filter'
            'python2-systemd: log to journal'
            'python2-jmespath: json_query support')
makedepends=('asciidoc' 'fakeroot' 'python' 'python2-setuptools'
             'python-setuptools') 
backup=('etc/ansible/ansible.cfg')
source=("https://releases.ansible.com/ansible/ansible-${pkgver}.tar.gz"
        0001-Revert-Add-jinja2-version-constraint.patch)
sha512sums=('10f90f4ac68215febd14e36fa9ea3b2156677f2b7a29a08633e0702bcbd8a7c3551980deeabcff756380b076ac7ba6a5e9eca07af66d908d8d6627390744bc00'
            'e2eb3a070840984ad7539ba81cd85810b2226d43642dd7b5b0232b85d31294cbc1e370b85d3ec3d7f321b113395606cefb58d0968ef66ccf23020e51786da7b1')

prepare() {
  pushd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Revert-Add-jinja2-version-constraint.patch
  popd

  cp -r ${pkgname}-${pkgver} "${srcdir}"/${pkgname}-${pkgver}-python
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  python2 setup.py build

  cd "${srcdir}"/${pkgname}-${pkgver}-python
  python setup.py build
}

package_ansible() {
  provides=('python2-ansible')

  cd ${pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/ansible/COPYING

  install -d "${pkgdir}"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "${pkgdir}"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "${pkgdir}"/etc/ansible/ansible.cfg

  install -d "${pkgdir}"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "${pkgdir}"/usr/share/man/man1
}

package_python-ansible() {
  pkgdesc='python bindings for ansible'
  depends=('python-paramiko' 'python-jinja' 'python-yaml' 'python-crypto')

  cd "${srcdir}"/ansible-${pkgver}-python
  python setup.py install --root=${pkgdir} --optimize=1
  rm "${pkgdir}"/usr/bin/ansible*
}
