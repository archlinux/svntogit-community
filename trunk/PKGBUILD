# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Konrad Tegtmeier <konrad.tegtmeier+aur@gmail.com>

_pkgname=cli
pkgname=hcloud
pkgver=1.13.0
pkgrel=4
pkgdesc="CLI for Hetzner Cloud"
arch=('x86_64')
url='https://github.com/hetznercloud/cli'
license=('MIT')
optdepends=(
    'bash-completion: tab auto-completion'
    'zsh: tab auto-completion')
makedepends=('go-pie' 'git')
depends=('glibc')
provides=('hcloud')
_hcloud_commit='85b1119b296e9ec5a5509fc82c750d180f2a76b0'
source=("git+https://github.com/hetznercloud/cli#commit=${_hcloud_commit}"
        _hcloud.zsh)
sha512sums=('SKIP'
            '1964c7445200327ee71974591516bdcc3a00751ff3407891a7401ad486b4a0ee51bfc8c0e8d0e074b788936f365a435322bd4c9b2b527f6621b0965b60477897')

prepare(){
  export GO111MODULE=on
}

build(){
  cd "${_pkgname}"
  go build  \
  -ldflags "-w -X github.com/hetznercloud/cli/cli.Version=${pkgver} -extldflags ${LDFLAGS}" \
  github.com/hetznercloud/cli/cmd/hcloud
  chmod +x ./hcloud
  ./hcloud completion bash > "$pkgname-completion.bash"
}

check(){
  cd "${_pkgname}/cli"
  go test -v -x
}

package(){
  cd "${_pkgname}"
  install -Dsm755 ./hcloud "${pkgdir}/usr/bin/hcloud"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${pkgname}-completion.bash" "${pkgdir}/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "${srcdir}/_hcloud.zsh" "${pkgdir}/usr/share/zsh/site-functions/_hcloud"
}
