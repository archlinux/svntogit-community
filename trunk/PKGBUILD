# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
pkgver=2010.04.25
_pkgsubver=253928
pkgrel=1
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools."
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('LGPL')
depends=('open-vm-tools-modules' 'libdnet' 'icu' 'procps' 'glib2' 'uriparser')
makedepends=('chrpath' 'doxygen')
optdepends=('gtkmm' 'libnotify' 'libxtst')
options=('docs')
install=$pkgname.install
source=(http://switch.dl.sourceforge.net/$pkgname/$pkgname-$pkgver-${_pkgsubver}.tar.gz
	scripts-network.patch
	open-vm-tools-libpng.patch
	open-vm-tools.conf.d
	open-vm-tools.rc.d
	tools.conf
	vmware-guestd
	xautostart.conf)
md5sums=('da0ed15d29f4a1f6eca21e8aeeabb476'
         '06f7448e274db2a911f582e276088fc9'
         'b52a4a4c3af590504ebae8a06fac3820'
         '79b0a14d86191fee70a4639da8bd7785'
         '391b0c1c620cb48d49be3ad4ad504b53'
         'b55d15e2c4be396aad709aeca91033d3'
         '73cc1a2665b0dd62427733d62ead8b9a'
         '75a25d83417e683957321f97a00f8465')

build() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"

  [ $NOEXTRACT -eq 1 ] || patch -Np1 -i $srcdir/open-vm-tools-libpng.patch

  [ $NOEXTRACT -eq 1 ] || sed -i 's#3.2.7#3.2.8#' configure
  [ $NOEXTRACT -eq 1 ] || sed -i 's#3.2.7#3.2.8#' configure.ac

  [ -f Makefile ] || ./configure --prefix=/usr --without-kernel-modules
  make || return 1
  make install DESTDIR=$pkgdir || return 1

  cd $pkgdir && \
  patch -Np1 -i $srcdir/scripts-network.patch etc/vmware-tools/scripts/vmware/network || return 1

  ln -fs /usr/sbin/mount.vmhgfs $pkgdir/sbin/mount.vmhgfs || return 1

  install -D -m 755 $startdir/open-vm-tools.rc.d $pkgdir/etc/rc.d/open-vm-tools && \
  install -D -m 644 $startdir/open-vm-tools.conf.d $pkgdir/etc/conf.d/open-vm-tools && \
  install -D -m 644 $startdir/tools.conf $pkgdir/etc/vmware-tools/tools.conf && \
  install -D -m 644 $startdir/xautostart.conf $pkgdir/etc/vmware-tools/xautostart.conf && \
  install -D -m 644 $startdir/vmware-guestd $pkgdir/etc/pam.d/vmware-guestd && \
  rm -rf $pkgdir/usr/etc || return 1

  cd $pkgdir && find -type f -exec sh -c "file {} | grep ELF >/dev/null && echo {} && chrpath -d {}" \;
}
