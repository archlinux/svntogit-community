# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hpack
pkgname=(python-hpack python2-hpack)
_pkgname=hpack
pkgver=2.0.1
pkgrel=1
pkgdesc="Pure-Python HPACK header compression"
arch=('any')
url="http://hyper.rtfd.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver::https://github.com/Lukasa/hpack/archive/v${pkgver}.tar.gz")
sha512sums=('dd014e2e08c3d7cc0e216cc1d18c95e55838e2bf4b7e706b54b0093140750c2cdb76944d14b78dae1320e27028c0be9dd73b66535bf8b02ca78615e4fe241133')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build

  cd ../$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  cd $_pkgname-$pkgver
  python setup.py ptr

  cd ../$_pkgname-$pkgver-py2
  python2 setup.py ptr
}

package_python-hpack() {
  depends=('python')

  cd $_pkgname-$pkgver
  python setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-hpack() {
  depends=('python2')

  cd $_pkgname-$pkgver-py2
  python2 setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

