# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=0.8.0
pkgrel=1
pkgdesc='Download from Usenet using .nzb files'
arch=('x86_64' 'i686')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('gnutls' 'libpar2' 'libxml2' 'libsigc++' 'libgcrypt')
makedepends=('autoconf' 'automake')
changelog=$pkgname.changelog
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('646939596097fafdeb0b4064b433463419e7d0d9af7fe32f6689d6b95ad77d95')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-parcheck
  make CXXFLAGS="-lsigc-2.0"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/$pkgname"
  install -m644 -t "$pkgdir/usr/share/$pkgname" \
    AUTHORS postprocess-example.sh postprocess-example.conf \
    ChangeLog nzbget.conf.example README
}
