# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=atomicparsley
pkgver=20210715.151551.e7ad03a
pkgrel=1
pkgdesc='Command line program for reading, parsing and setting metadata in MPEG-4 files'
arch=('x86_64')
url=https://github.com/wez/atomicparsley
license=('GPL')
depends=('gcc-libs' 'zlib')
makedepends=('cmake')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('986058a8e80b84f30df03f5dfc6e2019ef6949469d8554a5a06ecc75716463b3047820265fd22556f9087a64498e07e994fa2162660f440086eab276be496a9d')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None .
  make
}

check() {
  cd $pkgname-$pkgver
  ./tests/test.sh
}

package() {
  cd $pkgname-$pkgver
  install -D AtomicParsley "$pkgdir"/usr/bin/atomicparsley
}

# vim:set ts=2 sw=2 et:
