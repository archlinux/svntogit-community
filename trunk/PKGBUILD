# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=4.0.1
pkgrel=1
pkgdesc="Open Source IRC Server"
arch=('i686' 'x86_64')
url="http://unrealircd.com"
license=('GPL2')
depends=('openssl' 'curl' 'c-ares')
conflicts=('ircd')
provides=('ircd')
backup=('etc/unrealircd/unrealircd.conf')
install=unrealircd.install
source=(https://www.unrealircd.org/unrealircd4/unrealircd-$pkgver.tar.gz
	unrealircd.service
	unrealircd.tmpfiles.d
	config.settings
	install.pl)
md5sums=('f513a190d5549f00ab030373b912e861'
         '677d8de0bae770488a2c1730f9475a51'
         '3ec519ea7dbe99696eb6c51dfc0d382a'
         '11ce231048c92651e76fdeb58609d0ba'
         '3a23792b03420fc578cf6688b1574ae3')

prepare() {
  cd $srcdir/unrealircd-$pkgver
  cp $srcdir/config.settings .
  sed -i \
    -e 's|$(INSTALL) -m 0700|$(INSTALL) -m 0755|g' \
    -e 's|$(INSTALL) -m 0600|$(INSTALL) -m 0644|g' \
    Makefile.in
  sed -i 's|ARG=" "|ARG="--with-pidfile=/run/unrealircd/ircd.pid "|' Config
}

build() {
  cd $srcdir/unrealircd-$pkgver
  ./Config -quick
  make
}

package() {
  cd $srcdir/unrealircd-$pkgver

  export pkgdir
  make INSTALL=$srcdir/install.pl install
  mv $pkgdir/usr/unrealircd $pkgdir/etc/unrealircd/unrealircd
  cp $pkgdir/etc/unrealircd/examples/example.conf $pkgdir/etc/unrealircd/unrealircd.conf
  rm -rf $pkgdir/tmp

  install -Dm0644 $srcdir/unrealircd.service $pkgdir/usr/lib/systemd/system/unrealircd.service
  install -Dm0644 $srcdir/unrealircd.tmpfiles.d $pkgdir/usr/lib/tmpfiles.d/unrealircd.conf
}
