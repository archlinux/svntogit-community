# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=budgie-extras
pkgver=0.9.1
_quickchar_ref=3bf255325194d8710c358684f44ee5687c57ffba
pkgrel=1
pkgdesc='Additional Budgie Desktop enhancements for the user experience'
arch=('x86_64')
license=('GPL3')
url='https://github.com/UbuntuBudgie/budgie-extras'
depends=('budgie-desktop' 'wmctrl' 'xdotool' 'xprintidle' 'python' 'python-gobject' 'python-cairo' 'libgee'
         'zenity' 'vorbis-tools' 'python-psutil' 'sound-theme-freedesktop' 'imagemagick' 'python-pillow'
         'python-svgwrite' 'python-cairosvg' 'python-pyudev' 'python-requests' 'libpeas' 'json-glib'
         'libkeybinder3')
makedepends=('gobject-introspection' 'meson' 'ninja' 'vala' 'intltool')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname-quickchar-$_quickchar_ref.tar.gz::https://github.com/UbuntuBudgie/QuickChar/archive/$_quickchar_ref.tar.gz")
sha512sums=('a3c57cdf1e5a4ffeddd743d3f067e89229311607f0307afa7f635dd1b5dbd96b84bebe0087cfaa7e739c6e8d9b2486e47811261fe7d8d40155fc99345813aa59'
            '6f7e5aa09dccc85b17ccc22fdedc7473727cf3ae2cc5e3664afba837e521a2f633028f79d70147d23835731e2de7716d236bf2f3357958064c54a854f872f1b6')

prepare() {
  cp -r QuickChar-$_quickchar_ref/* $pkgname-$pkgver/budgie-quickchar/quickchar
}

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson .. \
  	-D b_pie=false

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}

