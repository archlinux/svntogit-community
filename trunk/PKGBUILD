# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=brick
pkgname=haskell-brick
pkgver=0.34.1
pkgrel=10
pkgdesc="A declarative terminal user interface library"
url="https://github.com/jtdaugherty/brick"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-vty' 'haskell-data-clist' 'haskell-dlist' 'haskell-microlens'
         'haskell-microlens-th' 'haskell-microlens-mtl' 'haskell-config-ini' 'haskell-vector'
         'haskell-contravariant' 'haskell-stm' 'haskell-text' 'haskell-text-zipper'
         'haskell-word-wrap')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('55fa396264b59dbc3788e91975dba8e5a43b27c3943ec3630a4a3bfc51868d87c3ea18f04ffe04931483e6b0e536a4e7d176ed4279a5528da9a2d385ad5a7d1a')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-demos
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
