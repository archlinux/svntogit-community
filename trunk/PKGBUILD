# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Dan Serban
# Contributor: Ross Melin <rdmelin@gmail.com>

pkgname=libfaketime
pkgver=0.9.1
pkgrel=1
pkgdesc='Report fake dates and times to programs without having to change the system-wide time'
arch=('i686' 'x86_64')
url='http://www.code-wizards.com/projects/libfaketime/'
license=('GPL2')
source=("http://www.code-wizards.com/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'libfaketime-0.9.1-fix-as-needed.patch'
        'libfaketime-0.9.1-fix-README-NEWS.patch')
md5sums=('ce3f996dfd5826b4ac62f1a7cc36ea27'
         '90873e4df35283463ef54b159c318686'
         'b1b641a8e2298aa1646982e3bde96117')

build() {
  cd "${pkgname}-${pkgver}"

  patch -p1 -i ../libfaketime-0.9.1-fix-as-needed.patch
  patch -p1 -i ../libfaketime-0.9.1-fix-README-NEWS.patch

  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
