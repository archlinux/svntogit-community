# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Mark Rosenstand <mark@borkware.net>
# Contributor: Giorgio Lando <patroclo7@gmail.com> (adjtimex patch)

pkgname=openntpd
pkgver=3.9p1
pkgrel=9
pkgdesc="Free, easy to use implementation of the Network Time Protocol."
url="http://www.openntpd.org/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('openssl')
backup=('etc/ntpd.conf')
install=openntpd.install
source=(ftp://ftp.openbsd.org/pub/OpenBSD/OpenNTPD/$pkgname-$pkgver.tar.gz
	'openntpd' 'openntpd.conf' 'linux-adjtimex.patch')
md5sums=('afc34175f38d08867c1403d9008600b3'
         '83e3cc4914f14758773982c7425cd378'
         'ab38a035cbebe7a52888db6c573944a5'
         '6e04eecf89ee794d6f1ae79f03742cf9')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../linux-adjtimex.patch
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc --with-privsep-user=ntp --with-privsep-path=/var/run/openntpd/ --with-adjtimex
  make || return 1
  make DESTDIR=$pkgdir install
  install -D -m755 $srcdir/openntpd $pkgdir/etc/rc.d/openntpd
  install -D -m644 $srcdir/openntpd.conf $pkgdir/etc/conf.d/openntpd
  install -D -m644 $srcdir/$pkgname-$pkgver/LICENCE  $pkgdir/usr/share/licenses/$pkgname/LICENCE

  # FS#16893  
  sed -i 's/\*/0.0.0.0/' $pkgdir/etc/ntpd.conf
}
