# $Id$
# Maintainer:  Anatol Pomozov <anatol@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>

pkgname=tt-rss
pkgver=17.4.r518.g02bb26a9
pkgrel=1
epoch=1
pkgdesc='Web-based news feed (RSS/Atom) aggregator'
arch=(any)
url='http://tt-rss.org'
license=(GPL)
backup=(etc/webapps/tt-rss/config.php)
depends=(php)
optdepends=('mysql'
            'postgresql'
            'php-gd: for coloured feed badges')
makedepends=('git')
_commit=02bb26a93ff5b60e7bc3236ef3f62c818d32bd12
# periodic releases are over https://tt-rss.org/forum/viewtopic.php?f=10&t=3262
source=(git+https://tt-rss.org/gitlab/fox/tt-rss.git#commit=$_commit
        service)
sha1sums=('SKIP'
          'e3277cd1d717e7803cdc41a0275dcc7c188ef8db')

pkgver() {
  cd tt-rss
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd tt-rss
  _instdir="$pkgdir"/usr/share/webapps/$pkgname

  install -d "$_instdir"
  cp -ra * "$_instdir/"

  install -Dm640 -g http config.php-dist "$pkgdir"/etc/webapps/tt-rss/config.php
  ln -s /etc/webapps/tt-rss/config.php "$_instdir"/config.php

  install -d "$pkgdir"/var/lib/tt-rss
  mv "$_instdir"/{lock,feed-icons,cache} "$pkgdir"/var/lib/tt-rss
  ln -s /var/lib/tt-rss/lock "$_instdir"/lock
  ln -s /var/lib/tt-rss/feed-icons "$_instdir"/feed-icons
  ln -s /var/lib/tt-rss/cache "$_instdir"/cache
  chown -R 33:33 "$pkgdir"/var/lib/tt-rss
  chmod -R 777 "$pkgdir"/var/lib/tt-rss/*

  install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/tt-rss.service
}
