# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Bert Muennich <be.muennich at gmail.com>

pkgname=urxvt-perls
pkgver=2.0
pkgrel=3
pkgdesc="URL and Mouseless text selection for rxvt-unicode"
arch=("any")
url='http://www.github.com/muennich/urxvt-perls'
license=("GPL")
depends=('rxvt-unicode' 'perl' 'xsel')
provides=('urxvt-url-select' 'urxvt-keyboard-select')
conflicts=('urxvt-url-select' 'urxvt-keyboard-select')
replaces=('urxvt-url-select' 'urxvt-keyboard-select')
install=$pkgname.install
source=(https://github.com/downloads/muennich/urxvt-perls/urxvt-perls-$pkgver.tar.gz)
md5sums=('345dc76bb7943ccba678b39979502435')

package() {
  cd "$srcdir"/urxvt-perls-$pkgver

  install -dm755 "$pkgdir"/usr/lib/urxvt/perl
  install -t "$pkgdir"/usr/lib/urxvt/perl -m644 "${provides[@]#urxvt-}" clipboard
}
