# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=warzone2100
pkgver=3.4.1
pkgrel=1
pkgdesc="3D realtime strategy game on a future Earth"
url="https://wz2100.net/"
arch=('x86_64')
license=('GPL')
depends=('sdl2' 'glew' 'fribidi' 'openal' 'libvorbis' 'libtheora' 'physfs' 'ttf-dejavu' 'qt5-script' 
         'xorg-xrandr' 'libsodium')
makedepends=('gawk' 'flex' 'zip' 'unzip' 'asciidoc' 'mesa' 'cmake')
source=(${pkgname}-${pkgver}_src.tar.xz::https://github.com/Warzone2100/warzone2100/releases/download/${pkgver}/${pkgname}_src.tar.xz)
sha256sums=('ea2cd7f016118a89244ebef8ce9424f71c060bcd5895b791d3e1cec02b555b69')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWZ_DISTRIBUTOR="ArchLinux"
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
