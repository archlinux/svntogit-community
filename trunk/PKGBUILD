# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=lablgtk3
pkgver=3.1.1
pkgrel=1
pkgdesc="An Objective Caml interface to gtk3"
arch=('x86_64')
license=('LGPL')
url="http://lablgtk.forge.ocamlcore.org/"
depends=('gtk3' 'gtksourceview3' 'gtkspell3' 'ocaml-cairo')
makedepends=('ocaml-compiler-libs>=4.11.0' 'ocaml-findlib' 'dune')
optdepends=('ocaml: for using the tools')
source=("https://github.com/garrigue/lablgtk/releases/download/${pkgver}/${pkgname}-${pkgver}.tbz")
sha256sums=('22c7061c8acb49c1ccd0a19396d2b1d7d1d677e0ce7954122404be94a00fecf9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # gcc-10 workaround
  LABLGTK_EXTRA_FLAGS=-fcommon dune build -p lablgtk3,lablgtk3-gtkspell3,lablgtk3-sourceview3
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir "lib/ocaml"

  install -dm755 "${pkgdir}/usr/share/"
  mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"
}
