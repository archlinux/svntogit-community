# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Tomas Schertel<tschertel@gmail.com>

pkgbase=python-logbook
pkgname=(python-logbook python2-logbook)
_pypiname=Logbook
pkgver=0.12.5
pkgrel=1
pkgdesc="Logging sytem for Python that replaces the standard libraryâ€™s logging module"
arch=('i686' 'x86_64')
url="http://packages.python.org/Logbook/index.html"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'git'
             'python-six' 'python2-six')
checkdepends=('python-pytest' 'python2-pytest' 'python-sqlalchemy' 'python2-sqlalchemy')
source=("git+https://github.com/mitsuhiko/logbook.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a logbook{,-py2}
}

build() {
  cd "$srcdir/logbook"
  cython logbook/_speedups.pyx
  python setup.py build

  cd "$srcdir/logbook-py2"
  cython2 logbook/_speedups.pyx
  python2 setup.py build
}

check() {
  cd "$srcdir/logbook"
  LC_CTYPE=en_US.UTF-8 py.test tests

  cd "$srcdir/logbook-py2"
  LC_CTYPE=en_US.UTF-8 py.test2 tests
}

package_python-logbook() {
  depends=('python-six')

  cd "${srcdir}/logbook"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-logbook() {
  depends=('python2-six')

  cd "${srcdir}/logbook-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
