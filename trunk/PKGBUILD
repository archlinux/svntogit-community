# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=john
pkgver=1.7.6
pkgrel=1
pkgdesc="John The Ripper - A fast password cracker. Additional patches (NTLM, MySQL, Kerberos V5, etc.) included."
arch=('i686' 'x86_64')
url="http://www.openwall.com/$pkgname/"
license=('GPL2' 'custom')
depends=('openssl')
backup=('/etc/john/john.conf')
source=(http://www.openwall.com/$pkgname/g/$pkgname-$pkgver.tar.bz2 
	http://www.openwall.com/john/contrib/john-$pkgver-jumbo-5.diff.gz 
	http://www2.psy.uq.edu.au/~ftp/Crypto/DES/libdes-4.04b.tar.gz 
	params.h.patch)
md5sums=('321ac0793f1aa4f0603b33a393133756'
         '44272d210d85cac2d39b6dbcfb5bfddc'
         '0a8e954c70328a56685c4e23efdf3416'
         'f69ed632eba8fb9e45847a4b4a323787')

build() {
	# jumbo patch
	cd ${srcdir}/$pkgname-$pkgver
	patch -p1 < ${srcdir}/$pkgname-$pkgver-jumbo-5.diff || return 1
	cd ${srcdir}/john-$pkgver/src/

	# patch default params
	patch -p0 < ${srcdir}/params.h.patch || return 1
	if [ "$CARCH" == "x86_64" ]; then
	    sed -i 's|CFLAGS = -c -Wall -O2|CFLAGS = -c -Wall -O2 -march=x86-64 -DJOHN_SYSTEMWIDE=1|' Makefile || return 1
	    sed -i 's|^LDFLAGS =\(.*\)|LDFLAGS =\1 -lm|' Makefile || return 1
	    sed -i -e 's|-m486||g' Makefile || return 1
	  else sed -i 's|CFLAGS = -c -Wall -O2|CFLAGS = -c -Wall -O2 -march=i686 -DJOHN_SYSTEMWIDE=1|' Makefile || return 1
	fi
	sed -i 's|LIBS = -ldes|LIBS = -ldes -Ldes|' Makefile || return 1
	sed -i 's|#include <des.h>|#include "des/des.h"|' KRB5_fmt.c || return 1
	sed -i 's|#include <des.h>|#include "des/des.h"|' KRB5_std.h || return 1

	# build john
	if [ "$CARCH" == "x86_64" ]; then
	    make linux-x86-64 || return 1
	  else make linux-x86-mmx || return 1
	fi
}

package() {
	# config file
	sed -i 's|$JOHN|/usr/share/john|g' ${srcdir}/john-$pkgver/run/john.conf || return 1
	install -Dm644 ${srcdir}/john-$pkgver/run/john.conf ${pkgdir}/etc/john/john.conf
	
	# docs
	install -d ${pkgdir}/usr/share/doc/john
	install -m644 ${srcdir}/john-$pkgver/doc/* ${pkgdir}/usr/share/doc/john/
	install -d ${pkgdir}/usr/share/john/
	install -m644 ${srcdir}/john-$pkgver/run/*.chr ${pkgdir}/usr/share/john/	
	install -m644 ${srcdir}/john-$pkgver/run/password.lst ${pkgdir}/usr/share/john/	
	install -Dm644 ${srcdir}/john-$pkgver/doc/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

	# install password list and charset files
	install -m644 ${srcdir}/${pkgname}-${pkgver}/run/{{all,alnum,alpha,digits,lanman}.chr,password.lst} \
          ${pkgdir}/usr/share/john/ || return 1

	# install binaries
	install -Dm755 ${srcdir}/john-$pkgver/run/john ${pkgdir}/usr/bin/john
	install -Dm755 ${srcdir}/john-$pkgver/run/mailer ${pkgdir}/usr/bin/john-mailer
	cd ${pkgdir}/usr/bin
	ln -s john unafs
	ln -s john unique
	ln -s john unshadow
	ln -s john undrop
}

