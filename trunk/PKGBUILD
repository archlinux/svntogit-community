# Maintainer: David Runge <dvzrv@archlinux.org>

_name=svglib
pkgname=python-svglib
pkgver=1.5.0
pkgrel=1
pkgdesc="Read SVG files and convert them to other formats"
arch=(any)
url="https://github.com/deeplook/svglib"
license=(LGPL3)
depends=(python-lxml python-reportlab python-tinycss2 python-cssselect2)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pillow python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('e5732fa80363e1d1773e1347960eeb9e5144307cbdfb09a8d9342dd75a3700df9991a1fa0defbdb44930a2283221a7d0fa3192d885e468ba1de644c58edf37e6')
b2sums=('d484171a8687482f1efb99513152b237562efdfca19f63d63e954c8653844e6083b492fb482e1c25fab9888c1635ff660019da01d1c7433ce4790a1428e042ba')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  # disable tests that need remote resources
  pytest -vv -k "not TestWikipediaSymbols and not TestWikipediaFlags"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,CONTRIBUTORS,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
