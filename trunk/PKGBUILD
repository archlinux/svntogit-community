# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgname=dolphin-emu
pkgver=4.0.2
pkgrel=13
epoch=1
pkgdesc='A Gamecube / Wii / Triforce emulator'
arch=('i686' 'x86_64')
url='http://dolphin-emu.org'
license=('GPL2')

makedepends=('cmake' 'opencl-headers')
depends=('bluez-libs' 'ffmpeg' 'glew' 'libao' 'miniupnpc' 'openal' 'portaudio'
         'sdl2' 'soundtouch' 'wxgtk')
optdepends=('pulseaudio: PulseAudio backend')

options=('!emptydirs')

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dolphin-emu/dolphin/archive/${pkgver}.tar.gz"
        'dolphin-emu-findx11.patch'
        'dolphin-emu-gcc49.patch')
md5sums=('95436ade64fc8ed2fe4b2ed5306dc879'
         'f9a692b76263f3efae9793c4879bc2c5'
         '5fead49fc3a0ff9a9d1435368b5df1f9')

prepare() {
  cd dolphin-${pkgver}

  patch -Np1 -i ../dolphin-emu-findx11.patch
  patch -Np1 -i ../dolphin-emu-gcc49.patch

  # Compatibility with miniupnpc 1.9.20150730, which requires a new ttl argument.
  sed -i 's:upnpDiscover(2000, NULL, NULL, 0, 0, &upnperror);:upnpDiscover(2000, NULL, NULL, 0, 0, 2, \&upnperror);:' \
      Source/Core/Core/Src/NetPlayServer.cpp
}

build() {
  cd dolphin-${pkgver}

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_CXX_FLAGS='-fno-inline-functions -fpermissive'
  make
}

package() {
  cd dolphin-${pkgver}/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/libpolarssl.a"
}
