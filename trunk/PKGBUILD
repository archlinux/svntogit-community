# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_basename=gst-plugins-good
pkgname=lib32-gst-plugins-good
pkgver=1.18.4
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins (32-bit)"
url="https://gstreamer.freedesktop.org/"
arch=('x86_64')
license=('LGPL')
depends=(lib32-libpulse lib32-jack2 lib32-libsoup lib32-gst-plugins-base-libs lib32-wavpack
         lib32-aalib lib32-taglib lib32-libdv lib32-libshout lib32-libvpx lib32-gdk-pixbuf2
         lib32-libcaca lib32-libavc1394 lib32-libiec61883 lib32-libxdamage lib32-v4l-utils
         lib32-cairo lib32-libgudev lib32-speex lib32-flac lib32-libraw1394 lib32-mpg123
         lib32-twolame gst-plugins-good)
makedepends=(lib32-gstreamer aalib python meson git)
checkdepends=(lib32-gst-plugins-base)
options=(!emptydirs)
_commit=941312f3d54c89475d5d50aa9dddc0a2987fcf5a
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-plugins-good#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common")
sha512sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_basename
  git describe --tags | sed 's/-//g'
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson $_basename build \
    --libdir=lib32 \
    --libexecdir=lib32 \
    -D doc=disabled \
    -D qt5=disabled \
    -D lame=disabled \
    -D rpicamsrc=disabled \
    -D gobject-cast-checks=disabled \
    -D package-name="GStreamer Good Plugins (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  meson compile -C build
}

# FAIL: elements/rtp-payloading
#check() {
# cd $_basename
# make check
#}

package() {
  DESTDIR="${pkgdir}" meson install -C build

  rm -r "$pkgdir"/usr/share
}
