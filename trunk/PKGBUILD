# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=1.4.8
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=('sqlite' 'postgresql-libs' 'libmariadbclient')
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('dee7a913a91d6a441a2a7b34fe77f0b33fb17a116ee6462825556ce12432d152ee4cfa6d9840399708bcc93af469612fa597246f3e2c498238415d36cbb43d57'
            'aa73d29e7c3b7dc4aeddb553563ca231bf52d7119fbadbb1f8dee34e4848a446f06dcbba485873fafbdabd3f461acda5fb88f0c11e0cf0c637c08f367ca44b81')
b2sums=('c3a174ab53f56131e91ad6de9f95d6b221f6e9d0dcfc9bb0728c9c6d30f4aa806c8e572a7879c3cf1df72e689afabcf80bd86c2392e3aa0b5377fb6e25a990ab'
        '8aee9dfc3d081140a8f0f9593f4f0123b884d3733514b87d454ebec4ef4ff65810d80073391cbc961dfe51c4ab3ab667e8c3d93f6a049a6c29fbef8d5cbce898')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
