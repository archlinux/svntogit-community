# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=spicy-launcher
pkgver=0.3.0
pkgrel=1
pkgdesc="The launcher for Spicy Lobster games"
arch=('x86_64')
url="https://github.com/spicylobstergames/SpicyLauncher"
license=('MIT' 'Apache')
depends=('pkg-config' 'mesa-libgl' 'alsa-lib' 'systemd-libs' 'libudev.so' 'gtk3' 'libsoup' 'webkit2gtk')
makedepends=('cargo' 'yarn' 'npm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('412e846cc28c305a7c203cd6d01d90dc8993c9d676393b34893d6f9fd12b7c437ffcb808da65012b5faf0d34c30bf48a8d08ff1770b0a3395e40833a60cba5eb')
options=('!lto')

prepare() {
	cd "SpicyLauncher-$pkgver"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "SpicyLauncher-$pkgver/gui"
	yarn install --ignore-engines
	yarn tauri build --target "$CARCH-unknown-linux-gnu" --bundles none
	cd ..
	cargo build --release --frozen
}

package() {
	cd "SpicyLauncher-$pkgver"
	install -Dm 755 "target/release/$pkgname-cli" -t "$pkgdir/usr/bin"
	install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
	install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
	install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
