# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Mark Pustjens <pustjens@dds.nl>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Leslie P. Polzer <polzer@gnu.org>

pkgname=isync
pkgver=1.4.1
pkgrel=1
pkgdesc="IMAP and MailDir mailbox synchronizer"
arch=('x86_64')
url="http://isync.sourceforge.net"
license=('GPL2')
depends=('libsasl' 'zlib')
source=(https://downloads.sourceforge.net/sourceforge/isync/$pkgname-$pkgver.tar.gz
        https://downloads.sourceforge.net/sourceforge/isync/$pkgname-$pkgver.tar.gz.asc)
md5sums=('c0c9899c2ff629e4e6de429c394b2613'
         'SKIP')
validpgpkeys=('96DD32BFBF9FAB04B0D95305AA283E0B2F1BB1D1'
              '63BFD037CAD71E8DFF3AEA3AC17714F08D1BDBBA')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
