# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-notifications
pkgver=2.2.0
pkgrel=2
pkgdesc='Switchboard Notifications Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-notifications
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=88a65980cfb86b99a12609b5a6987a617d8df767
source=(git+https://github.com/elementary/switchboard-plug-notifications.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-notifications

  git describe --tags
}

build() {
  arch-meson switchboard-plug-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
