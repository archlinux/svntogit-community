# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-utils
pkgver=5.4.9
pkgrel=1
pkgdesc='DDE network utils'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-utils"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'gio-qt' 'gsettings-qt')
makedepends=('qt5-tools' 'gtest')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-utils/archive/$pkgver.tar.gz")
sha512sums=('77d386631d222cc57df6a82ff22abfa750970ac560c9a1a8ad857bb3f265f00b74577409ca8759e97b98938ce429647eeeecf6be831d4f73c179f9617a33c163')

prepare() {
  cd dde-network-utils-$pkgver
  # Use our own url instead of commercial company's homepage
  sed -i '/www.uniontech.com/i \    "https://archlinux.org/favicon.ico",' dde-network-utils/connectivitychecker.cpp
}

build() {
  cd dde-network-utils-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

check() {
  cd dde-network-utils-$pkgver
  # make test
}

package() {
  cd dde-network-utils-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
