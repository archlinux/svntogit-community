# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-httpretty
pkgname=("python-httpretty" "python2-httpretty")
pkgver=0.8.11
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/httpretty"
license=('MIT')
makedepends=('python-urllib3' 'python2-urllib3' 'python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-coverage' 'python2-coverage' 'python-mock' 'python2-mock'
              'python-sure' 'python2-sure' 'python-tornado' 'python2-tornado' 'python-requests' 'python2-requests'
              'python-httplib2' 'python2-httplib2')
source=("git+https://github.com/gabrielfalcao/HTTPretty.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a HTTPretty{,-py2}

  # Fix the encoding issue
  sed -i 's/key=lambda (matcher, _): matcher.priority,/key=lambda matcher, _: matcher.priority,/' -i HTTPretty/httpretty/core.py
}

check() {
  cd HTTPretty
  nosetests3 -s tests || warning "Tests failed"

  cd ../HTTPretty-py2
  nosetests2 -s tests
}

package_python-httpretty() {
  depends=('python-urllib3')

  cd HTTPretty
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python-httpretty/COPYING"
}

package_python2-httpretty() {
  depends=('python2-urllib3')

  cd HTTPretty-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python2-httpretty/COPYING"
}
