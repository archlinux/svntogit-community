# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Paul Boyd <boyd.paul2@gmail.com>

pkgname=amfora
pkgver=1.8.0
pkgrel=2
pkgdesc='Terminal browser for the Gemini protocol'
arch=(x86_64)
url='https://github.com/makeworld-the-better-one/amfora'
license=(GPL3)
makedepends=(git go)
source=("git+$url#commit=71385e9f4e91ab982076856cac95320173ccea73" # tag: v1.8.0
        'https://roboticoverlords.org/amfora.png')
sha256sums=('SKIP'
            '3d029d05cff8c42e82685ce7a61fcaa2118e4cbb6a547816a7d5150868a11092')

prepare() {
  echo Icon=amfora >> $pkgname/amfora.desktop
}

build() {
  cd $pkgname
  go build \
    -buildmode=pie \
    -ldflags "-s -w -linkmode=external -extldflags $LDFLAGS -X main.version=$pkgver -X main.builtBy=Arch_Linux -X main.commit=${source#*=}" \
    -mod=readonly \
    -modcacherw \
    -trimpath
}

package() {
  cd $pkgname
  install -Dm755 amfora "$pkgdir/usr/bin/amfora"
  install -Dm644 amfora.desktop "$pkgdir/usr/share/applications/amfora.desktop"
  install -Dm644 "$srcdir/amfora.png" "$pkgdir/usr/share/pixmaps/amfora.png"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
