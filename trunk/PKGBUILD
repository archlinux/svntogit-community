# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=15.12.27
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
depends=('flattr-icon-theme' 'faenza-icon-theme')
makedepends=('git' 'inkscape')
source=("git+https://cr.deepin.io/deepin-icon-theme.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"
  sed 's\flattr\Flattr\' -i deepin/index.theme

  mkdir -p build
}

build() {
  cd "${pkgname}"
  python2 tools/convert.py deepin build
}

package() {
  cd "${pkgname}"
  make DESTDIR="$pkgdir" install
}
