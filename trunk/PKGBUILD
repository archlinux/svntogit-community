# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-configuration
pkgver=5.0
pkgrel=1
pkgdesc="Zope Configuration Markup Language (ZCML)"
arch=('any')
url="https://github.com/zopefoundation/zope.configuration"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema' 'python-zope-i18nmessageid')
checkdepends=('python-manuel' 'python-zope-testing' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.configuration/archive/$pkgver.tar.gz")
sha512sums=('5d227f16fbded58655f1dcfb025242d8e5828b150cf974b1f9af402b6a82437f1b91988aac01aa449c237b79cc6f56882e2a02dd7acde7ee2d7b93736a0ea326')

build() {
  cd zope.configuration-$pkgver
  python setup.py build
}

check() {
  cd zope.configuration-$pkgver
  PYTHONPATH="$PWD/build/lib" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.configuration-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
