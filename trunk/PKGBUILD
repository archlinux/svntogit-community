# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hakyll
pkgname=haskell-hakyll
pkgver=4.12.5.0
pkgrel=4
pkgdesc="A static website compiler library"
url="http://jaspervdj.be/hakyll"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-cryptohash'
         'haskell-data-default' 'haskell-file-embed' 'haskell-fsnotify' 'haskell-http-conduit'
         'haskell-http-types' 'haskell-lrucache' 'haskell-network-uri'
         'haskell-optparse-applicative' 'pandoc' 'pandoc-citeproc' 'haskell-random'
         'haskell-regex-tdfa' 'haskell-resourcet' 'haskell-scientific' 'haskell-tagsoup'
         'haskell-time-locale-compat' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-wai' 'haskell-wai-app-static' 'haskell-warp' 'haskell-yaml')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('89153b0f68c745e82a17400037ccf32e2a462fe710746d3665c72f202dade7b7308b7d79e508c6fb89dada1a03058738a634c1609d15474b7342883457658d00')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcheckexternal -fwatchserver -fpreviewserver -fusepandoc
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test || warning "https://github.com/jaspervdj/hakyll/issues/682"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
