# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20220418
pkgrel=2
pkgdesc="A tool for buffering data streams"
arch=(x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL3)
depends=(gcc-libs glibc)
makedepends=(openssl)
source=(https://www.maier-komor.de/software/mbuffer/mbuffer-$pkgver.tgz)
sha512sums=('aad7f764c82e3bdd4ee3fc5ac8174e716fd44cdb0418f1994d3f506d2e58264a13e15596bc8e78e9e8bb2d181875294448085bab122a2a31356d4336bb8b54a4')
b2sums=('8c7eacae791bef8ac6cd2ce57e6a5fbda7c7c233c37ccc504ad6584385df4b4df49aaf67f2d2dc7a3ee9f7e1431ba511847646a4c92616c1a4af5ac094e029e9')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
