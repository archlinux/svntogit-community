# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=zita-njbridge
pkgver=0.4.8
pkgrel=1
pkgdesc="Jack client to transmit audio over a local IP network"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc')
makedepends=('jack' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('0e2b5882bfdc9a67b84c6446bcbb75e496c64ed19ac3ddf06dddc6e10fe57510f27096c341a6e3b2e563d10489ecc1acb5666ff4c4df10586e648c5d62164832')
b2sums=('2079b563959be2ccad95176b62e6634efc1e9f54e9c70cb8de2064a5973aaa04b72a10d318554472ba493e7ce57e37fb9b71ed8962fc66aac22330b58d5904d5')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip march=native
  sed -e '/native/d' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make
}

package() {
  depends+=('libjack.so' 'libzita-resampler.so')
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,README}
}
# vim:set ts=2 sw=2 et:
