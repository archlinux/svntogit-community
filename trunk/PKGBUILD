# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-config
pkgver=0.9.0
pkgrel=2
pkgdesc="LXQt system configuration."
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=("liblxqt" "libxcursor" "xorg-xrandr")
makedepends=("cmake" "qt5-tools")
source=("http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
        "fix-build.patch")
sha256sums=('9d6b0d21846ac5d9c62c5806ce04383b92708ab7bd095b46d0ac9fbb3f84b913'
            '276f04a31089e724793fae278238a397d5a1409fd8348cf529f3e75b47da94cc')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	# fix build with qt 5.5
	# https://github.com/lxde/lxqt-config/commit/a5045dc9
	patch -Np1 -i ../fix-build.patch
}

build() {
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr "$srcdir/$pkgname-$pkgver"
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
