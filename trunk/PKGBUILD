# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.21
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver.tar.gz"
        deepin-calculator-qt5.15.patch)
sha512sums=('74747e7cf6ae172d9adf0e9ab4ebe6506c6a5476f0bb9a6b89806a390b809798bac93571020f227be1d81f11d9571abf54f36ba98ea12f3ae777c00fbdc4cd29'
            'd38fed51d16655c4662bdea3a7e19cbcb27b2d04bccaad90e0089652068b6041b73a8512e717ff18c47426046dcfe188ba1535ef19fc21b74fae7dd4e4933e03')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '/<QList>/i #include <QObject>' src/core/session.h

  patch -p1 -i ../deepin-calculator-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-calculator-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-calculator-$pkgver
  DESTDIR="$pkgdir" ninja install
}
