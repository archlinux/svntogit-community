# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgname=python-breathe
pkgver=4.29.1
pkgrel=1
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output"
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
depends=('python' 'python-sphinx' 'python-docutils' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/michaeljones/breathe/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig"::"https://github.com/michaeljones/breathe/releases/download/v${pkgver}/breathe-${pkgver}.tar.gz.sig")
sha512sums=('a235a7b15dba12e463ffc078e6cc94a60eed8371b51182a4c55691ae2c42d76e1cfbb957ccc9d82a79e2a08297a4b93121811990421e92b9464fd8bf9cbbbb78'
            'SKIP')
validpgpkeys=('8AED58021FEACDD5F27BA0E6A72F627716EA9D96') # Melvin Vermeeren <vermeeren@vermwa.re>

check() {
  cd "breathe-${pkgver}"
  PYTHONPATH="$PWD" make test
}

build() {
  cd "breathe-${pkgver}"
  python setup.py build
}

package_python-breathe() {
  cd "breathe-${pkgver}"
  PYTHONHASHSEED=0 python setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
