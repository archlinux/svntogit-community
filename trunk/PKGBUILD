# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Nathan Baum <n@p12a.org.uk>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=at
pkgver=3.1.20
pkgrel=4
pkgdesc='AT and batch delayed command scheduling utility and daemon'
arch=('x86_64')
url='http://blog.calhariz.com/tag/at'
license=('GPL')
depends=('pam' 'flex')
makedepends=('smtp-forwarder' 'git')
backup=('etc/at.deny' 'var/spool/atd/.SEQ')
options=('!makeflags')
source=("http://blog.calhariz.com/public/sft/$pkgname/${pkgname}_${pkgver}.orig.tar.gz")
sha256sums=('0871923cab73050b98ace020664eb2ddc1e669e4166b5abb64d864d02fcefab9')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-jobdir=/var/spool/atd \
    --with-atspool=/var/spool/atd \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  CFLAGS="$CFLAGS -w" make
}

package() {
  make -C "$pkgname-$pkgver" IROOT="$pkgdir" docdir=/usr/share/doc install
}

# vim:set ts=2 sw=2 et:
