# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgbase=python-xcffib
pkgname=(python-xcffib python2-xcffib)
pkgver=0.7.0
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi' 'python-six'
             'python2-six' 'libxcb' 'haskell-xcffib')
checkdepends=('python-nose' 'python2-nose' 'xorg-server-xvfb' 'xorg-xeyes')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tych0/xcffib/archive/v$pkgver.tar.gz")
sha512sums=('e0c1eb7fd86baf5e9f0f0916651cebc41bc18de3594b76da299967363492ac86499082872ba4dff8cf776354fc35accbd4d2f86ed4570f0d8f77bd5d20ddf1d8')

prepare() {
  cp -a xcffib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/xcffib-$pkgver
  make xcffib GEN=xcffibgen CABAL=true
  python setup.py build

  cd "$srcdir"/xcffib-$pkgver-py2
  make xcffib GEN=xcffibgen CABAL=true
  python2 setup.py build
}

check() {
  cd "$srcdir"/xcffib-$pkgver
  nosetests3

  cd "$srcdir"/xcffib-$pkgver-py2
  nosetests2
}

package_python-xcffib() {
  depends=('python-six' 'python-cffi' 'libxcb')

  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-xcffib() {
  depends=('python2-six' 'python2-cffi' 'libxcb')

  cd xcffib-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
