# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.9.2
pkgrel=3
pkgdesc="A compiler for high-performance SIMD programming on the CPU"
arch=('x86_64')
url="https://ispc.github.io/"
license=('BSD')
depends=('ncurses' 'zlib' 'llvm-libs' 'clang')
makedepends=('llvm' 'python' 'lib32-glibc')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ispc/ispc/archive/v${pkgver}.tar.gz"
        '0001-Remove-uses-of-LLVM-dump-functions.patch' # Borrowed from Fedora
        'llvm6.0_compat.diff')
sha256sums=('76a14e22f05a52fb0b30142686a6cb144b0415b39be6c9fcd3f17ac23447f0b2'
            'aed52cba60197bd20b2aef12e93e2e640d3ef2596bf154fe4a6e0980cfe78cca'
            '22077998f02898bdb2fd14e4decbc894686d8a84dd9877dace9a222fa1e3b84b')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../0001-Remove-uses-of-LLVM-dump-functions.patch
  patch -p1 -i ../llvm6.0_compat.diff
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ispc -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

