# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fixtures
pkgname=('python-fixtures' 'python2-fixtures')
pkgver=1.4.0
pkgrel=1
pkgdesc="Fixtures, reusable state for writing clean tests and more"
arch=('any')
license=('Apache')
url="https://launchpad.net/python-fixtures"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-six' 'python2-six' 'python-testtools' 'python2-testtools')
checkdepends=('python-subunit' 'python2-subunit' 'python-mock' 'python2-mock')
source=("https://pypi.python.org/packages/source/f/fixtures/fixtures-$pkgver.tar.gz")
sha512sums=('62e314821ea507ab5c1465fb626c29c91430b36ae4670fd05a3aa7180452989c393d35ab623da3646b25cf671a84861ab68d9529d04ae759c0d38efca7c7d8cf')

prepare() {
  cp -a fixtures-$pkgver{,-py2}
}

build() {
  cd "$srcdir/fixtures-$pkgver"
  python setup.py build

  cd "$srcdir/fixtures-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/fixtures-$pkgver"
  python -m testtools.run fixtures.tests.test_suite

  cd "$srcdir/fixtures-$pkgver-py2"
  python2 -m testtools.run fixtures.tests.test_suite
}

package_python-fixtures() {
  depends=('python-pbr' 'python-six' 'python-testtools')

  cd fixtures-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-fixtures() {
  depends=('python2-pbr' 'python2-six' 'python2-testtools')

  cd fixtures-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
