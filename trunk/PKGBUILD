# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-helper
pkgver=2.4.2
pkgrel=6
pkgdesc="Development library for quickly writing configurable applications and daemons"
arch=('any')
license=('BSD')
url="https://helper.readthedocs.org"
depends=('python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-coverage')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gmr/helper/archive/$pkgver.tar.gz")
sha512sums=('56eca464eebc3e5a25f1b84c491b28c0f55f72fc3264598639fbd4200f87a1a3644b6edaba2201e97675fb33532a32df5a0e6bf0b2937b9951445666fc19c6f3')

build() {
  cd helper-$pkgver
  python setup.py build
}

check() {
  cd helper-$pkgver
  nosetests3
}

package() {
  cd helper-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
