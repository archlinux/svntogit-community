# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
pkgbase='python-kajiki'
_pkgbase="${pkgbase#python-}"
pkgname=("python-${_pkgbase}" "python2-${_pkgbase}")
pkgver=0.8.0
pkgrel=1
pkgdesc='Python XML-based template engine with Genshi-like syntax and Jinja-style blocks'
arch=('any')
url='https://github.com/nandoflorestan/kajiki'
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-babel' 'python2-babel' 'python-nose' 'python2-nose' 'python-nine' 'python2-nine')
source=("${_pkgbase}-${pkgver}.tar.gz::https://github.com/nandoflorestan/kajiki/archive/v${pkgver}.tar.gz")
sha512sums=('b85adf30855dccf37b98d33ab9ee624866c0b5bba5e38308c2754d3ce578841eaddf11de5555f0ca4f58966e50b5ec64a9bf25afe3195a3becc05e8216002d8a')

prepare(){
  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"

  # fix python interpreter for python2 stuff
  find "${srcdir}/${_pkgbase}-${pkgver}-py2" -type f -name '*.py' \
    -exec sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|1' {} +
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py test
  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 ./setup.py test
}

package_python-kajiki() {
  depends=('python')
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-kajiki() {
  depends=('python2')
  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 ./setup.py install --root="$pkgdir/" --optimize=1
}
