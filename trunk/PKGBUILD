# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jeffrey Zhang <zhang.lei.fly@gmail.com>

_name=pytest-lsp
pkgname=python-pytest-lsp
pkgver=0.2.0
pkgrel=1
pkgdesc='Pytest plugin for end-to-end testing of language servers'
arch=('any')
url=https://swyddfa.github.io/lsp-devtools
license=('MIT')
depends=('python-appdirs' 'python-pygls' 'python-pytest-asyncio')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://github.com/swyddfa/lsp-devtools/archive/pytest-lsp-v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('ad85371dd5f778ce380c5a7f23a2e5bd3dab3659b032446afe3ca5456b9f07a5d1005dde9e4e04ffa5bff3db250a2b72675936e7cb20400f1627e5dfa44cd43f')

build() {
  cd lsp-devtools-pytest-lsp-v$pkgver/lib/pytest-lsp
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd lsp-devtools-pytest-lsp-v$pkgver/lib/pytest-lsp
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd lsp-devtools-pytest-lsp-v$pkgver/lib/pytest-lsp
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pytest_lsp-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
