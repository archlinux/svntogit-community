# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: feuri

pkgname=dunst
pkgver=1.0.0
pkgrel=2
pkgdesc="Customizable and lightweight notification-daemon"
arch=('i686' 'x86_64')
url="http://www.knopwob.org/dunst/"
license=(BSD)
depends=(libxinerama libxss libxdg-basedir pango)
makedepends=(libnotify)
provides=(notification-daemon)
source=("http://www.knopwob.org/public/dunst-release/$pkgname-$pkgver.tar.bz2")
md5sums=('bb5fee3cdf6ee30f7e11b7edd35e6723')

build() {
	make -C "$srcdir/$pkgname-$pkgver" X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	make -C "$pkgname-$pkgver" DESTDIR="${pkgdir}" PREFIX=/usr install
	install -Dm644 "${srcdir}/$pkgname-$pkgver/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
