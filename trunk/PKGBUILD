# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lbreakout2
pkgver=2.6.5
pkgrel=1
pkgdesc="A breakout game with nice effects, graphics, and sounds"
arch=('x86_64')
url="http://lgames.sourceforge.net"
license=('GPL')
depends=('sdl_mixer' 'libpng')
backup=('var/games/lbreakout2/lbreakout2.hscr')
source=("http://downloads.sourceforge.net/lgames/${pkgname}-${pkgver/_/-}.tar.gz"
        'lbreakout2.desktop'
	'lbreakout2.png')
md5sums=('1a9238b83f9f13f09b7a4d53e00b4e84'
         '14cd34a72a8f4e3b8f385f9023625241'
         '94e2fa393eb2a7d4494641dec580573b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver/_/-}

  ./configure --prefix=/usr \
              --localstatedir=/var/games/lbreakout2 
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver/_/-}

  make DESTDIR=${pkgdir}/ doc_dir=/usr/share/doc install
  
  chmod -R 755 ${pkgdir}/usr
  chown root:games ${pkgdir}/var/games
  chmod 775 ${pkgdir}/var/games
  chown root:games ${pkgdir}/var/games/lbreakout2
  chmod 755 ${pkgdir}/var/games/lbreakout2
  chown root:games ${pkgdir}/var/games/lbreakout2/lbreakout2.hscr
  chmod 775 ${pkgdir}/var/games/lbreakout2/lbreakout2.hscr

  install -Dm644 ${srcdir}/lbreakout2.png ${pkgdir}/usr/share/pixmaps/lbreakout2.png
  install -Dm644 ${srcdir}/lbreakout2.desktop ${pkgdir}/usr/share/applications/lbreakout2.desktop
}
