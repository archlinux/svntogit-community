# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Danilo Bargen <gezuru@gmail.com>

pkgname=vim-jedi
_reponame=jedi-vim
pkgver=0.11.0
pkgrel=3
pkgdesc='Vim plugin for jedi, an awesome Python autocompletion'
url='https://github.com/davidhalter/jedi-vim'
arch=('any')
license=('MIT')
depends=('vim' 'python-jedi' 'python')
optdepends=('vim-supertab: tab completion'
            'python2-jedi: Python 2 support')
checkdepends=('python-pytest')
groups=('vim-plugins')
options=('!emptydirs')
source=(https://github.com/davidhalter/${_reponame}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        $pkgname-fix-a-test-around-signatures.patch::https://github.com/davidhalter/jedi-vim/commit/e4daf7a74c50.patch)
sha256sums=('27eec24470b2f96e2a2939e9bab039f8401f2c9ed6e07ee4d8acb59de3b7b879'
            '14f134e88d34c14e3b1ee86360abaab3fbca43a7fd5a2558842653ebd33589ce')
b2sums=('146f60f43a3fc764c66e2ca94c2b4051be3e25b5a94ca3f6d82533b624433946013c7df38e61262e6ac40a00de9d3b9b0e647e794191b6deb8ed09124d2accc7'
        '03c49af18edaf08ddcf18b4bfb5317ca146b61b016fa2d1c417f41edf978a1defb3892af81d27ca8bf7dcfe260fdd0c68aeb75c0f18d1539d069a1ea49f25fee')

prepare() {
  cd ${_reponame}-${pkgver}
  patch -Np1 -i ../$pkgname-fix-a-test-around-signatures.patch
}

check() {
  cd ${_reponame}-${pkgver}
  py.test
}

package() {
  cd ${_reponame}-${pkgver}

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership pythonx "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership doc "${pkgdir}/usr/share/vim/vimfiles/doc"
  cp -dpr --no-preserve=ownership after "${pkgdir}/usr/share/vim/vimfiles/after"
  cp -dpr --no-preserve=ownership autoload "${pkgdir}/usr/share/vim/vimfiles/autoload"
  cp -dpr --no-preserve=ownership ftplugin "${pkgdir}/usr/share/vim/vimfiles/ftplugin"
  cp -dpr --no-preserve=ownership plugin "${pkgdir}/usr/share/vim/vimfiles/plugin"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
