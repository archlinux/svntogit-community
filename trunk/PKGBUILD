# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=virt-manager
pkgname=(virt-install virt-manager)
pkgver=2.2.1
pkgrel=1
arch=('any')
url="https://virt-manager.org/"
license=('GPL')
makedepends=('libosinfo' 'libvirt-python' 'python-gobject' 'python-requests' 'intltool')
source=("https://virt-manager.org/download/sources/virt-manager/virt-manager-$pkgver.tar.gz")
sha512sums=('aa97af52b3552c087beab8701762c21e8e6d818c9787d5376ed4e1ae40a34d5e8870c4c00fda9cb17a2340d1dff5b17e853182df720cdd9ea4c91cf01502a33a')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build
}

package_virt-install() {
  pkgdesc="Console user interface for managing virtual machines"
  depends=('libosinfo' 'libvirt-python' 'python-gobject' 'python-requests')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py --no-update-icon-cache --no-compile-schemas install --root "$pkgdir"
  python -m compileall "${pkgdir}/usr/share/virt-manager"
  python -O -m compileall "${pkgdir}/usr/share/virt-manager"

  # Split virt-manager
  [[ -d $srcdir/virt-manager ]] && rm -r "$srcdir/virt-manager/"
  mkdir "$srcdir"/virt-manager
  mv "$pkgdir"/usr/bin/virt-manager \
    "$pkgdir"/usr/share/{appdata,applications,glib-2.0,icons}/ \
    "$pkgdir"/usr/share/man/man1/virt-manager.1 \
    "$srcdir/virt-manager/"
}

package_virt-manager() {
  pkgdesc="Desktop user interface for managing virtual machines"
  depends=("virt-install=$pkgver" 'gtk-vnc' 'libvirt-glib' 'spice-gtk' 'vte3' 'python-cairo' 'gtksourceview4')

  cd "$srcdir"/virt-manager
  mkdir -p "$pkgdir"/usr/{bin,share/man/man1}
  mv virt-manager "$pkgdir/usr/bin"
  mv appdata applications glib-2.0 icons "$pkgdir"/usr/share
  mv virt-manager.1 "$pkgdir"/usr/share/man/man1
}
