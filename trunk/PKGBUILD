# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ddt
pkgver=1.4.3
pkgrel=1
pkgdesc="Data-Driven/Decorated Tests"
arch=('any')
license=('MIT')
url="https://github.com/txels/ddt"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-six' 'python-yaml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txels/ddt/archive/$pkgver.tar.gz")
sha512sums=('ac66dd0d20c49df6fae6e86dfa1dce6d4326f985f17ad955ec2c7bdb2e411bf633baf2d3b244cd49ac45eb91e976a33460e73e4d155e17fbf90418062c6ca187')

build() {
  cd ddt-$pkgver
  python setup.py build
}

check() {
  cd ddt-$pkgver
  pytest
}

package() {
  cd ddt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
