# Maintainer:

pkgname=verdict
pkgver=1.4.1
pkgrel=1
pkgdesc='Compute quality functions of 2 and 3-dimensional regions'
arch=(x86_64)
url='https://github.com/sandialabs/verdict'
license=(BSD)
makedepends=(cmake)
depends=(glibc)
source=(https://github.com/sandialabs/verdict/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('26fa583265cb2ced2e9b30ed26260f6c9f89c3296221d96ccd5e7bfeec219de7')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DVERDICT_ENABLE_TESTING=OFF \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package () {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
