# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=copr-cli
pkgver=1.96
pkgrel=2
pkgdesc="CLI tool to run copr"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-copr' 'python-humanize' 'python-simplejson' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-responses')
source=("https://pagure.io/copr/copr/archive/copr-cli-$pkgver-1/copr-copr-cli-$pkgver-1.tar.gz")
sha512sums=('8faf03d115dd7e118905fb8db90c2a2226f2fad8420f31b1d0a82a8f75e83d6655dca854a073e0ecb73e8834590be5d0edfb2557c4466b0cc1f1ab8f8e61a424')

build() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py build
}

check() {
  cd copr-copr-cli-$pkgver-1/cli
  python -m pytest
}

package() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py install --root="$pkgdir" --optimize=1
}
