# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.27.1
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libnetfilter_queue' 'iproute2' 'glibc' 'libnfnetlink')
makedepends=('go' 'git')
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs')
source=(https://github.com/bettercap/bettercap/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('157386a9410056667e391826fe04012f934452dd66a9bc3a6d4eee1654827923a7ad987cff07477b30a56476fe92dc1eadf7db7c4fd6bbf2dc2bd62e825e1835')
b2sums=('f10074523407e23a6f504a7bba8c7aa63a81890c8a0dc1cfc0d9ea336a7606e7b73e301c3167149d8bbf9a01addc59e5ab047300cc7594eb4b7e929ff2f0cd8c')

build() {
  cd "${pkgname}-${pkgver}"
  export GO11MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"
  go build
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
