# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Mike Sampson <mike at sambodata dot com>
# Contributor: Adrian Stratulat <adrian.stratulat at inboxcom>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: KillaB <xGenoBlast@gmail.com>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Christian Schmidt <xmucknertx@googlemail.com>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=angband
pkgver=3.3.0
pkgrel=1
pkgdesc="A roguelike dungeon exploration game based on the writings of JRR Tolkien"
arch=('i686' 'x86_64')
url="http://rephial.org/"
license=('GPL2' 'custom')
depends=('sdl_image' 'sdl_ttf' 'sdl_mixer' 'ncurses' 'libglade')
source=("http://rephial.org/downloads/${pkgver:0:3}/${pkgname}-v${pkgver}.tar.gz")
md5sums=('bd7676af9ebee6938f1e54eacc234d8c')

build() {

  cd "${srcdir}/${pkgname}-v${pkgver}"

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sysconfdir=/usr/share/angband \
    --with-configpath=/usr/share/angband \
    --with-libpath=/usr/share/angband \
    --enable-gtk \
    --enable-sdl \
    --enable-sdl-mixer

  make

}

package() {

  cd "${srcdir}/${pkgname}-v${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/share/angband/*/delete.me"
  install -Dm644 copying.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

}
