# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Eric Anderson <ejona86 at gmail.com>
# Contributor: Pierre DOUCET <pierre at equinoxefr.org>
pkgname=openscad
pkgver=2013.06
pkgrel=3
pkgdesc="The programmers' solid 3D CAD modeller"
url="http://openscad.org/"
arch=('i686' 'x86_64')
license=('GPL2')
changelog=CHANGELOG
depends=('qt4' 'cgal' 'opencsg' 'boost-libs')
makedepends=('eigen' 'boost' 'imagemagick')
source=("https://openscad.googlecode.com/files/$pkgname-$pkgver.src.tar.gz")
md5sums=('b7b5faecdffc41e28cdf0c1162299de7')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    qmake-qt4 PREFIX="/usr"
    make
    convert "icons/$pkgname.png" -resize 128x128\> "icons/$pkgname-128.png"
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make INSTALL_ROOT="$pkgdir" install
    install -Dm644 "icons/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "icons/$pkgname-128.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
