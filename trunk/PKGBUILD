# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=bv-sized
pkgname=haskell-bv-sized
pkgver=1.0.3
pkgrel=50
pkgdesc="A bitvector datatype that is parameterized by the vector width"
url="https://github.com/GaloisInc/bv-sized"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bitwise' 'haskell-panic' 'haskell-parameterized-utils'
         'haskell-random' 'haskell-th-lift')
makedepends=('ghc' 'uusi' 'haskell-hedgehog' 'haskell-monadrandom' 'haskell-tasty'
             'haskell-tasty-hedgehog')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('dc4ffcb6aef1f3290c13394764aa4321a05647c05e4e373a90e674f794951ce4b984258410447cd0eaff1b812244bce468592a187d985e60847d0a19caa990e2')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
