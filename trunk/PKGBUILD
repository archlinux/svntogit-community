# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=dep
pkgver=0.5.0
pkgrel=2
pkgdesc="Go dependency management tool"
arch=('x86_64')
url='https://golang.github.io/dep/'
license=('BSD')
makedepends=('go-pie' 'git')
optdepends=('git: fetch git sources')
checkdepends=('mercurial' 'bzr' 'subversion')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/golang/dep/archive/v${pkgver}.tar.gz")
sha256sums=('65c31c8d51513dfabd25f1f37ecd22a38ab3bcdd5893bdf10f54941250e4f19d')

prepare(){
  mkdir -p src/github.com/golang
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/golang/dep
}

build(){
  export GOPATH="${srcdir}"
  cd src/github.com/golang/dep
  go install -gcflags "all=-trimpath=${GOPATH}/src" -asmflags "all=-trimpath=${GOPATH}/src" ./cmd/...
}

check(){
  cd src/github.com/golang/dep
  go test ./...
}

package(){
  install -Dm755 bin/dep "${pkgdir}/usr/bin/dep"

  cd "${pkgname}-${pkgver}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
