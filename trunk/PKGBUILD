# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-pysaml2
pkgname=(python-pysaml2 python2-pysaml2)
pkgver=4.6.1
pkgrel=1

pkgdesc='Python implementation of SAML Version 2'
url='https://pypi.python.org/pypi/pysaml2'
arch=('any')
license=('Apache')

makedepends=('python-setuptools'
             'python2-setuptools')

source=("https://pypi.org/packages/source/p/pysaml2/pysaml2-$pkgver.tar.gz")

md5sums=('cfa746d35b43396a58470cc6ae97bf57')

prepare() {
	cp -a pysaml2-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/pysaml2-$pkgver
	python setup.py build

	cd "$srcdir"/pysaml2-$pkgver-python2
	python2 setup.py build
}

check() {
	cd "$srcdir"/pysaml2-$pkgver
	python setup.py test

	cd "$srcdir"/pysaml2-$pkgver-python2
	python setup.py test
}

package_python-pysaml2() {
	depends=('python'
	         'python-future'
	         'python-requests'
	         'python-zope-interface'
	         'python-pycryptodomex'
	         'python-pytz'
	         'python-pyopenssl'
	         'python-dateutil'
	         'python-six'
	         'python-defusedxml')

	cd pysaml2-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pysaml2() {
	depends=('python2'
	         'python2-future'
	         'python2-requests'
	         'python2-zope-interface'
	         'python2-pycryptodomex'
	         'python2-pytz'
	         'python2-pyopenssl'
	         'python2-dateutil'
	         'python2-six'
	         'python2-defusedxml')

	cd pysaml2-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

	# rename all executables
	find "$pkgdir"/usr/bin -type f -exec mv {} {}2 \;
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
