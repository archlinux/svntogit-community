# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=0.9.4
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=('x86_64')
url="https://qxgedit.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'qt6-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5c2f566e70d314e9cc2c8b73caebb7d1d413cfa4386c43655f43baa1d9cd097343fb480d0dcef256d816c918797b9a45cd84ae49ca94c0ca46baca7deff24945')
b2sums=('daf77ebf49c08180d52f9ba7f4f742612636a067e90b6ebd24c34d0e1079bf4d72768d6c77aecb6dbbc9cc63da2e5ffb3833c9675784af872aff61608f2f6b13')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so')
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
