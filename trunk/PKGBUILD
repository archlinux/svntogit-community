# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=embree
pkgver=2.17.1
pkgrel=2
pkgdesc="A collection of high-performance ray tracing kernels"
arch=('x86_64')
url="https://embree.github.io/"
license=('Apache')
depends=('intel-tbb')
makedepends=('cmake' 'ispc' 'freeglut' 'libxmu' 'openexr')
provides=('embree-isa')
replaces=('embree-isa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/embree/embree/archive/v${pkgver}.tar.gz")
sha256sums=('ab4af2f86690f82228337bb4e54213ca3316fd38726770f720fe2c9b4cf0f066')

build() {
  cd ${pkgname}-${pkgver}

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DEMBREE_TUTORIALS=OFF \
    -DEMBREE_MAX_ISA="AVX2"
  # Embree detects actual ISA at runtime
  # AVX512KNL/SKX: https://github.com/embree/embree/issues/169
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

