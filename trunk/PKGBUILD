# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=3.2.0
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
         'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz")
sha512sums=('85fe2018a3772f63f1a6c315f85d5ea9d226f5169bd456a92eaa1c8a95bb1fed0600680c748acbbceb1f77bbde99a1cf7cd67e3603073ad358560687e1f6f1f2')

export PBR_VERSION=$pkgver

build() {
  cd oslo.serialization-$pkgver
  python setup.py build
}

check() {
  cd oslo.serialization-$pkgver
  stestr run
}

package() {
  cd oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
