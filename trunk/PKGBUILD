# $Id$
# Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-manager
pkgver=7.0.2
pkgrel=1
pkgdesc='A layer between the OpenVAS Scanner and various client applications'
arch=('x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('libxslt' 'openvas-libraries' 'rsync' 'sqlite3')
makedepends=('cmake' 'doxygen' 'xmltoman')
optdepends=('python2: SPLIT_PART_SIZE feature via xml_split script')
groups=(openvas)
install="${pkgname}.install"
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2448/openvas-manager-7.0.2.tar.gz"
        "http://wald.intevation.org/frs/download.php/2449/openvas-manager-7.0.2.tar.gz.sig")
sha512sums=('82b35b8bfcfbc781acabec71133d87352887a1e51e4af5a757c0f0a8b3eb86c37137022fdac693cc8802971c2628b52b900b1ec2af3c6434c3e2401bdbd8bfe2'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i '1c#!/usr/bin/python2' tools/extra/xml_split
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
