# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.4.7
pkgrel=1
pkgdesc="An idiomatic testing library for python with powerful and flexible assertions"
arch=('any')
url="https://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock' 'python-setuptools'
             'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gabrielfalcao/sure/archive/v$pkgver.tar.gz")
sha512sums=('f8cf4616edaac53370be86fa38b7ba49bf5dcbf4fd6322470f7912618319fce679cd4afd47ffbad86e47be0be45ba2c06aafc3236f0ebb9368a1760c0b06726e')

prepare() {
  cp -a sure-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/sure-$pkgver
  python setup.py build

  cd "$srcdir"/sure-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sure-$pkgver
  python setup.py test

  cd "$srcdir"/sure-$pkgver-py2
  python2 setup.py test
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
