# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgname=python-pudb
pkgver=2022.1.3
pkgrel=2
pkgdesc="A full-screen, console-based Python debugger"
url="https://pypi.python.org/pypi/pudb"
arch=('any')
license=('MIT')
depends=('python-urwid' 'python-pygments' 'python-jedi' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://github.com/inducer/pudb/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0db8b4a525649efa9637f35009433ec9b213e0000390f522ef3c245defd2dea6157cf7831aa6b187b9ae2c90885c41d5aed14505f36a6ec2f70fff3b7a09696b')

build() {
  cd pudb-$pkgver
  python setup.py build
}

check() {
  cd pudb-$pkgver
  python -m pytest --tb=native -rxs test/
}

package() {
  cd pudb-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim: set ft=sh ts=2 sw=2 et:
