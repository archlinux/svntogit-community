# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.9.26
pkgrel=2
pkgdesc="An old-school drum-kit sampler"
arch=(x86_64)
url="https://drumkv1.sourceforge.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base qt6-svg)
makedepends=(alsa-lib cmake jack liblo libsndfile lv2 qt6-tools)
optdepends=(
  'alsa-lib: for standalone application'
  'jack: for standalone application'
  'liblo: for standalone application'
  'new-session-manager: for session management using the standalone application'
  'qt6-wayland: for native wayland support'
)
options=(debug)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('678770abd3dd16f70d440dce42a3f2afe3713ee9a90bfb05df95a9ac728ae818a00f28cf0d4789ae52707bda80096af70d4fc33353fb883b8c7d5624de45a2b1')
b2sums=('7d1c4f033d66eb496a52502a9e9b20b0849ba44d4c5529b31d505b25e0a5309d272b90ca5e80a2806ff40a99b89f6a28335169a64a0a4f75452e270ef0ec1d4c')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libsndfile.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
# vim:set ts=2 sw=2 et:
