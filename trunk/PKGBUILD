# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=ct
pkgver=0.9.3
pkgrel=1
pkgdesc='config transpiler for Flatcar Container Linux'
arch=('x86_64' 'aarch64')
url='https://github.com/flatcar-linux/container-linux-config-transpiler'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flatcar-linux/container-linux-config-transpiler/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('b31000fa24607c9480e6e4df67f779e7052e2da160ce0c08c5a0fbb9666d750003b5b75995af08ccc442abaccb059f0f6e7fe8fdfaadad90c4accdd490082078')
b2sums=('23c43d103d960deca8075b92545841c34bc553b28212500846c4f2770b31bc9c803d894a2e20f8d67e6a3f4bebfc55aaa1d9a7442008e407af7a6c94353f2e5f')

build() {
  cd "container-linux-config-transpiler-${pkgver}/internal"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external -X github.com/flatcar-linux/container-linux-config-transpiler/internal/version.Raw=${pkgver}" -o ct
}

#check() {
#  cd "container-linux-config-transpiler-${pkgver}"
#  go test ./...
#}

package() {
  cd "container-linux-config-transpiler-${pkgver}/internal"
  install -Dm755 ct "${pkgdir}/usr/bin/ct"
}
