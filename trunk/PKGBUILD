# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

_gemname=paint
pkgname=ruby-${_gemname}
pkgver=2.1.0
pkgrel=1
pkgdesc='Ruby paint manages terminal colors and effects'
url='https://github.com/janlelis/paint'
arch=('any')
license=('MIT')
depends=('ruby-rspec' 'ruby-rainbow' 'ruby-term-ansicolor')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/janlelis/paint/archive/v${pkgver}.tar.gz)
sha512sums=('0de206c78c678d77cfdff0f26e42f2a045fc98a982943f7104d11e47cd4db9b196dba6a14eb09d98c103b3b70e13b84e48fe6261d2b03a9a0f4d4f10037a39cb')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i paint.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build paint.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -Dm 644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
