# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=vhba-module
pkgver=20110416
_kernver='3.0-ARCH'
pkgrel=2
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('linux>=3.0' 'linux<3.1')
makedepends=('linux-headers>=3.0')
install=vhba-module.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.gz)
md5sums=('2f91dd4ee8648da92d625221d4275b60')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 KDIR=/usr/src/linux-${_kernver}
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D vhba.ko "$pkgdir/lib/modules/${_kernver}/extra/vhba.ko"
  sed -i "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/vhba-module.install"
}
