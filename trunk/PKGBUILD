# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=avrdude
pkgver=7.0
pkgrel=3
epoch=1
pkgdesc="Download/upload/manipulate the ROM and EEPROM contents of AVR microcontrollers"
arch=('x86_64')
url="https://www.nongnu.org/avrdude/"
license=('GPL')
depends=('elfutils' 'hidapi' 'libftdi' 'libusb-compat' 'readline')
source=(https://download.savannah.gnu.org/releases/avrdude/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('EF497ABE47ED91B3FC3D7EA54D902FF7723BDEE9' '5E84F980C3CAFD4BB5841070F48CA81B69A85873' '56628323218C669FF578705C7E9EADC3030D34EB')
b2sums=('260817e81c50492035afb3eacf89c5c418e1e966decc0ecec81e91a8f5372c25ef75cfa9aab47e4a648d5e85b7854e8fa53c65f826aa7752bc8dc6a3c15f588a'
        'SKIP')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --mandir=/usr/share/man \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-linuxgpio \
    --enable-linuxspi
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
