# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.8.5
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64)
url="https://github.com/ijl/orjson"
license=(Apache MIT)
depends=(python)
makedepends=(maturin python-installer rust)
checkdepends=(python-arrow python-pendulum python-psutil python-pytest
python-pytz python-xxhash)
source=($pkgname-$pkgver.tar.gz::https://github.com/ijl/$_name/archive/$pkgver.tar.gz)
sha512sums=('1944729e1cb50da06f8496a0bf3212fc14da1f3870a74ca72da75de602243399132977d662f57f741efb47bc76d74c2c81b1188f0cc377a8c037aed7b864f633')
b2sums=('980455a3c8f00effa545bab56ddd62e31417c1257a5c823da33cd5f0d59f4629ebd258d7931250b39ce4c52cca7b3c7b4a2d9e88ea383af331d9d36f194f1cc9')

build() {
  cd $_name-$pkgver
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE-* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
