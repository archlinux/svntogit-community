# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.46.6
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('i686' 'x86_64')
url="http://msp.ucsd.edu/software.html"
license=('custom:BSD')
depends=('jack' 'tk' 'fftw')
provides=('puredata')
source=("http://msp.ucsd.edu/Software/pd-$_ver.src.tar.gz")
md5sums=('341649683c1ff3902e1578f7eae99272')

build() {
  cd pd-$_ver
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --disable-portaudio \
              --enable-fftw
  make
}

package() {
  cd pd-$_ver
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/pd/LICENSE"
}
