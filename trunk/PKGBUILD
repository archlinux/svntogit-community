# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tesx <tesfabpel@gmail.com>

pkgname=goxel
pkgver=0.11.0
pkgrel=1
pkgdesc='3D program that lets you create voxel volumes'
arch=(x86_64)
url='https://goxel.xyz/'
license=(GPL3)
depends=(glfw gtk3)
makedepends=(git scons)
source=("git+https://github.com/guillaumechereau/goxel#commit=4c22f3304e1fd4478413fda2a8d39098f1c1bb28") # tag: v0.11.0
b2sums=(SKIP)

prepare() {
  cd $pkgname
  sed -i 's|\${SNAP}/icon.png|goxel|;s|Name=goxel|Name=Goxel|' snap/gui/goxel.desktop
  sed -i 's|/share/applications/goxel.desktop|/share/applications/io.github.guillaumechereau.Goxel.desktop|g' Makefile
}

build() {
  cd $pkgname
  scons mode=release werror=n
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" PREFIX=/usr install
}
