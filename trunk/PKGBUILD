# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-inter
pkgver=1.10
pkgrel=1
pkgdesc='Framework for more readable interactive test scripts'
arch=('any')
url="https://metacpan.org/release/Test-Inter"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Test-Inter-$pkgver.tar.gz")
sha512sums=('2e4cdd9009d58dd396dbfbc7b91d69d2193132445bb9e6a95c2e56b772981a39e9a9cae0d22f8b1b94571f2f739fb8f47b5410e422c02f008547fcacc7f83325')

build() {
  cd Test-Inter-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Inter-$pkgver
  make test
}

package() {
  cd Test-Inter-$pkgver
  make DESTDIR="$pkgdir" install
}
