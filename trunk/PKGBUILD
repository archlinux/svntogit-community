# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.envs
pkgver=2.3.0
pkgrel=1
pkgdesc="Classes for orchestrating Python (virtual) environments"
url="https://github.com/jaraco/jaraco.envs"
license=('MIT')
arch=('any')
depends=('python-path' 'python-virtualenv' 'python-tox')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-black' 'python-pytest-checkdocs' 'python-pytest-cov'
              'python-pytest-enabler' 'python-pytest-flake8' 'python-pytest-mypy'
              'python-pip')
source=("https://github.com/jaraco/jaraco.envs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a2b098fb9ee0ec1f1cc281fec7fb177130b1f15768e0072be01e1288df0f328b902745315af3a58b2f2ebd85c224eb118e122e23bffd6cc000793c306abc0d55')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd jaraco.envs-$pkgver
  python -m build -wn
}

check() {
  cd jaraco.envs-$pkgver
  python -m pytest
}

package() {
  cd jaraco.envs-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
