# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: karolina.lindqvist@kramnet.se

pkgname=algol68g
pkgver=2.8
pkgrel=1
pkgdesc='Algol 68 Genie, an Algol 68 compiler-interpreter'
arch=('x86_64' 'i686')
url='http://www.xs4all.nl/~jmvdveer/algol.html'
license=('GPL')
depends=('gsl' 'plotutils')
optdepends=('postgresql-libs: for postgresql support')
source=("https://distfiles.macports.org/algol68g/algol68g-$pkgver.tar.gz"
        'plotutils.patch')
sha256sums=('0c6fc7213f5a00f978faef2d09622d2c3ee01c87594dd6f5c51cd532b719861a'
            '50afcc7812d117e5f0a2f5240c9cd58f8bfcc04b3da0634b9aa0ab06183ed0aa')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../plotutils.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" docdir="/usr/share/doc/$pkgname" install
}

# vim:set ts=2 sw=2 et:
