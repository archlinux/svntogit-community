# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-http2
pkgver=0.15.0
_commit=f69b0e79b7b3594cda313396f4882ce861440848
pkgrel=1
pkgdesc='A low level implementation of the HTTP/2 protocol'
arch=(any)
url='https://github.com/socketry/protocol-http2'
license=(MIT)
depends=(ruby-protocol-hpack ruby-protocol-http)
makedepends=(git)
checkdepends=(ruby-covered ruby-sus ruby-bake-modernize ruby-bake-test ruby-bake-test-external)
options=(!emptydirs)
source=(git+https://github.com/socketry/protocol-http2.git#commit=$_commit)
sha256sums=('SKIP')

prepare() {
  cd protocol-http2
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i protocol-http2.gemspec
  sed -i '/bake-gem/d;/bake-github-pages/d;/utopia-project/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http2
  gem build protocol-http2.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-http2-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http2
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-http2
  cp -a tmp_install/* "$pkgdir"/
}
