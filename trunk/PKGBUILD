# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pythonfinder
pkgname=python-pythonfinder
pkgver=1.3.2
pkgrel=1
pkgdesc="Cross Platform Search Tool for Finding Pythons"
arch=(any)
url="https://github.com/sarugaku/pythonfinder"
license=(MIT)
depends=(python-attrs python-cached-property python-packaging)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(git python-click python-pytest)
optdepends=('python-click: for CLI')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('a1a8efd7ee3f0d7eddf3414f72f59faaedd5fe8bcbd5b86dd205b464d41db19bbdf202a01c27449c2157809222a1b81f9a52f488a811872c3367cbe9f761c4cf')
b2sums=('e8238dd000e4768a12352a4552b174c4ca40f902f64dec2afd99529e6a7b0346ad49f79aa088ff8aeb513353ea783ffbb176f0126b29edbaa566269770a96aa9')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # disable tests that rely on git cloning: https://github.com/sarugaku/pythonfinder/issues/116
  pytest -vv -c /dev/null -k "not test_shims_are_kept and not test_shims_are_removed"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
