# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>

pkgname=('camlp5' 'camlp5-transitional')
_baseversion=6.03
_patchlevel=0
#pkgver=${_baseversion}.${_patchlevel}
pkgver=${_baseversion}
pkgrel=1
arch=('i686' 'x86_64')
url='http://pauillac.inria.fr/~ddr/camlp5/'
license=('BSD')
depends=('ocaml')
source=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/camlp5-${_baseversion}.tgz")
# upstream likes to release occasional patches instead of releasing new versions
for i in $(seq ${_patchlevel}); do
  source+=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/patch-${_baseversion}-${i}")
done

md5sums=('4988b871dc1ce713476819805b6dd7c6')

build() {
  cd camlp5-${_baseversion}

  for i in $(seq ${_patchlevel}); do
    patch -Np0 -i ${srcdir}/patch-${_baseversion}-${i}
  done

  cd ${srcdir}

  cp -r camlp5-${_baseversion} camlp5-transitional-${_baseversion}

  cd ${srcdir}/camlp5-${_baseversion}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict

  make world.opt

  cd ${srcdir}/camlp5-transitional-${_baseversion}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -transitional

  make world.opt
}

package_camlp5() {
  pkgdesc='A preprocessor-pretty-printer of OCaml.'
  conflicts=('camlp5-transitional')

  cd camlp5-${_baseversion}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/camlp5/LICENSE
}

package_camlp5-transitional() {
  pkgdesc='A preprocessor-pretty-printer of OCaml. (transitional)'
  conflicts=('camlp5')

  cd camlp5-transitional-${_baseversion}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/camlp5-transitional/LICENSE
}
