# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-readme-renderer
pkgver=29.0
pkgrel=3
pkgdesc="Safely render long_description/README files in Warehouse"
url="https://github.com/pypa/readme_renderer"
license=('Apache')
arch=('any')
depends=('python-bleach' 'python-docutils' 'python-pygments' 'python-six')
optdepends=('python-cmarkgfm: for Markdown support')
makedepends=('python-setuptools' 'python-cmarkgfm')
checkdepends=('python-pytest-runner' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pypa/readme_renderer/archive/$pkgver.tar.gz")
sha512sums=('a015be365daedd5c11af1d21a2f682f9e8ef5cbb59bedd3430578b2c1e2cbf882cf05e97f5bdc3dac39bc9e2e197b3fbb169d107f027e9b39741ee0afadf43df')

build() {
  cd readme_renderer-$pkgver
  python setup.py build
}

check() {
  cd readme_renderer-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package_python-readme-renderer() {
  cd readme_renderer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
