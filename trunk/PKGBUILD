# Maintainer: Dan Printzell <arch@vild.io>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Severen Redwood <severen@shrike.me>
# Contributor: Robert Welin <robert.welin@gmail.com>
# Contributorr: dsboger <https://github.com/dsboger>

pkgname=gtkd
pkgver=3.8.4
pkgrel=2
pkgdesc='D bindings for GTK+ and related libraries.'
arch=('x86_64')
url='http://gtkd.org/'
license=('LGPL')
depends=('liblphobos' 'gtk3')
makedepends=('ldc')
optdepends=('pango' 'atk' 'gdk-pixbuf2' 'gtksourceview3' 'gstreamer' 'vte3' 'libpeas')
source=("https://github.com/gtkd-developers/GtkD/archive/v${pkgver}.tar.gz")
sha512sums=('2b8ad49a6579a333b563176ded484e0709c4dd84010d100dea71564ff845da9978d239e4dd5429e85b9d4e3553670d84ec0e48ec00605370ab80004ce9d66119')

build() {
    cd ${srcdir}/GtkD-${pkgver}

    make \
    DC='ldc' \
    LDFLAGS="-L=\"$LDFLAGS\"" \
    libdir='lib/' \
    shared-{gtkd,gtkdgl,sv,gstreamer,vte,peas}
}

package() {
    cd ${srcdir}/GtkD-${pkgver}

    make \
    prefix='/usr' \
    libdir='lib/' \
    DESTDIR="${pkgdir}/" \
    install-{shared,headers}-{gtkd,gtkdgl,gtkdsv,gstreamer,vte,peas}
}

