# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-i18n
pkgname=(python-oslo-i18n python2-oslo-i18n)
pkgver=3.25.0
pkgrel=1
pkgdesc="Oslo i18n library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.i18n/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel')
checkdepends=('python-mock' 'python2-mock' 'python-oslotest' 'python2-oslotest' 'python-oslo-config'
              'python2-oslo-config')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.i18n/archive/$pkgver.tar.gz")
sha512sums=('db199eeee4334941e2dacae235a264507f5072c14e476fa91918a8f445e96a6ebd3981ce683f4fad5ca5cd65539492df8642db93f1e445d4af1084421417ffd7')

prepare() {
  cp -a oslo.i18n-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.i18n-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.i18n-$pkgver
  stestr run

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-oslo-i18n() {
  depends=('python-six' 'python-pbr' 'python-babel')

  cd "$srcdir"/oslo.i18n-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-i18n() {
  depends=('python2-six' 'python2-pbr' 'python2-babel')

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
