# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=i2c-tools
pkgver=3.1.0
pkgrel=3
pkgdesc="Heterogeneous set of I2C tools for Linux that used to be part of lm-sensors"
arch=('i686' 'x86_64')
url="http://www.lm-sensors.org/wiki/I2CTools"
license=('GPL')
depends=('python2')
optdepends=('read-edid: for decode-edid script')
source=(http://dl.lm-sensors.org/${pkgname}/releases/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f15019e559e378c6e9d5d6299a00df21')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}" 
  make
  make -C eepromer CFLAGS="$CFLAGS -I../include"
  cd py-smbus
  CPPFLAGS="$CPPFLAGS -I../include" python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm755 eepromer/eeprog eepromer/eeprom eepromer/eepromer "${pkgdir}/usr/sbin"
  cd py-smbus
  python2 setup.py install --prefix "${pkgdir}/usr"
  rm -rf "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
