# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.54.2
pkgrel=2
pkgdesc="Low level core library (32-bit)"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL2.1)
arch=(x86_64)
depends=(lib32-pcre lib32-libffi lib32-util-linux lib32-zlib "$_pkgbasename")
makedepends=(gettext gcc-multilib shared-mime-info python lib32-libelf git util-linux meson lib32-dbus)
checkdepends=(desktop-file-utils)
options=(!emptydirs)
_commit=52b3e434d849e2b0233d6d3d663b5dda82ab263e  # tags/2.54.2^0
source=("git+https://git.gnome.org/browse/glib#commit=$_commit"
        0001-meson-Fix-libmount-support.patch
        libs.diff
        gio-querymodules-32.hook)
sha256sums=('SKIP'
            '14c0dcfb4efb690f7cb44673faf31f5eb40ac68c01974eddb53d5aa7ae366629'
            '2fb828f51727bd9c8b48cfd9d6833c8b4ff82803331f6e2340b0ec8edfe57c52'
            '73b6791b58180e2a02fe908938e6c547ee1b263f8c387262f9a8c4105ffa999a')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir -p build
  cd glib

  # https://bugzilla.gnome.org/show_bug.cgi?id=789681
  patch -Np1 -i ../0001-meson-Fix-libmount-support.patch

  # https://bugzilla.gnome.org/show_bug.cgi?id=788773
  patch -Np1 -i ../libs.diff
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd build
  arch-meson ../glib --libdir=/usr/lib32 \
    -Dwith-docs=no -Dwith-man=no
  ninja
}

check() {
  cd build
  meson test -t 2
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  rm -r "$pkgdir"/usr/{share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -delete

  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  # install hooks
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 ../*.hook
}
