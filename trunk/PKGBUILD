# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Serge Victor <arch@random.re>
# Contributor: Kevin Azzam <aur@azz.am>

_pyname=flask-sqlalchemy
pkgname=python-flask-sqlalchemy
pkgver=2.4.0
pkgrel=3
pkgdesc='Adds SQLAlchemy support to your Flask application'
url='http://flask-sqlalchemy.pocoo.org/'
arch=('any')
license=('BSD')
depends=('python-flask' 'python-sqlalchemy')
makedepends=('python-setuptools' 'python-sphinx' 'python-pallets-sphinx-themes' 'python-sphinx-issues')
checkdepends=('python-pytest' 'python-blinker' 'python-mock')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mitsuhiko/flask-sqlalchemy/archive/${pkgver}.tar.gz)
sha256sums=('bf8c585851e6c8971751a6d23fa7a3f9bf26d8a4b0c1eff08583224a7ed62b2f')
sha512sums=('4a5f247527ea07d58004d532365c955364c0999a0214b63fa1a96f3baf17e86e4a7a0d8f911e83fe965e5e9b23427c315ecfa83634379bf70c7b028f526518d5')

prepare() {
  sed -r "s|(release =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  make -j1 -C docs man text SPHINXBUILD=sphinx-build PYTHONPATH=..
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=".:${PYTHONPATH}" py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
