# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rz-ghidra
_gitcommit=e70aa0f68310f18620153eed57b27fdfb9ba3018
_gitcutter=7eb4311c2be2c0e5e6bcd098abf06beda2e96519
_gitghidra=d87905747fe38d2dcd7492ed2e83bbf438b156d3
pkgver=0.4.0
pkgrel=2
pkgdesc='Deep ghidra decompiler integration for rizin and rz-cutter'
url='https://github.com/rizinorg/rz-ghidra'
arch=('x86_64')
license=('LGPL3')
depends=('rizin' 'glibc')
makedepends=('git' 'cmake' 'qt5-base' 'rz-cutter')
source=("git+${url}#commit=${_gitcommit}"
        "git+https://github.com/rizinorg/cutter#commit=${_gitcutter}"
#       rizin-ghidra::git+https://github.com/rizinorg/ghidra.git
        git+https://github.com/zeux/pugixml.git)
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ${pkgname}
  git tag -d rz-0.2.0 >/dev/null
  git describe --tags --match 'rz-*' | sed 's/^rz-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
# pinned commit in submodule is not avilable in any branch
  git clone https://github.com/rizinorg/ghidra.git rizin-ghidra 
  ( cd rizin-ghidra && git pull origin master && git pull origin $_gitghidra --rebase )

  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git config submodule."ghidra/ghidra".url "${srcdir}/rizin-ghidra"
  git submodule update --recursive
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_CUTTER_PLUGIN=ON \
    -DBUILD_SLASPECS=ON \
    -DCUTTER_SOURCE_DIR="${srcdir}/cutter" \
    -DCUTTER_INSTALL_PLUGDIR=/usr/share/rizin/cutter/plugins/native
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}
  make -C build -j1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
