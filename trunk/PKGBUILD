# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.4.26
pkgrel=3
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-wallpapers' 'kwin')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        $pkgname-tabbox-chameleon-rename.patch
        $pkgname-added-functions-from-their-forked-kwin.patch
        dde-kwin.5.4.26.patch)
sha512sums=('0ce42973d5c4fb861e758f27b7afc3ebe3c743b7157b527c08771bc5d624e124914fbc92ad9e1a9845cc8d302f83352d6991cb287bf2447dbab6a2e87013809c'
            '454889eaaf48f0e329d6cde870f57dd25dc1f9f88ade234fdb058249c5a9602fdd43dbfffe7c79c0d05843cec500285e719233bf1b4141c32b387bd1c86e02d7'
            '5ba72bb39469f77895a171eb49e787d5045df422c4b9937e85362d022d680710c13c6dd526456df34dc151b1f01101dbc08ad53cd31fbaf069b0bfb84f0ea07d'
            '4186adebecc65e8586c7c7594557bc5421a9884a789052b0270007a4268cc0ea017357fce7de61c7a4e78ee59432f68561f9003d9ea37cd47e79b25cfa08e2f2')

prepare() {
  cd dde-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' plugins/kwineffects/multitasking/background.cpp
  patch -p1 -i ../dde-kwin.5.4.26.patch
  patch -Rp1 -i ../$pkgname-added-functions-from-their-forked-kwin.patch
  patch -p1 -i ../$pkgname-tabbox-chameleon-rename.patch
  sed -i 's/kwin 5.21.5/kwin 5.24.4/' configures/kwin_no_scale.in
}

build() {
  cd dde-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr \
          -DUSE_WINDOW_TOOL=OFF \
          -DENABLE_BUILTIN_BLUR=OFF \
          -DENABLE_KDECORATION=ON \
          -DENABLE_BUILTIN_MULTITASKING=OFF \
          -DENABLE_BUILTIN_BLACK_SCREEN=OFF \
          -DUSE_DEEPIN_WAYLAND=OFF
  ninja
}

package() {
  cd dde-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install
}
