# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=homebank
pkgver=5.5.2
pkgrel=1
pkgdesc="Free, easy, personal accounting for everyone"
arch=('x86_64')
url="http://homebank.free.fr/"
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'libofx'
         'librsvg' 'perl-xml-parser' 'shared-mime-info' 'libsoup')
makedepends=('intltool')
changelog=$pkgname.changelog
source=(http://homebank.free.fr/public/$pkgname-$pkgver.tar.gz)
sha256sums=('989ef378e4c9f8234b62bb93d6a4b14bd4c98dc889519838dba1f44f12c51ed2')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
