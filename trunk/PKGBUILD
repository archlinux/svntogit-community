# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=38
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('gcc-libs' 'openssl')
makedepends=('systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz")
sha512sums=('d3421a6b22a22a11316289cfb5102c224b81decf89fd802a6281d7686f72e7ee3fffb5de569d4e09a01b8e9d8f6a5114afc709b90965847fa24fd4579316a80b')

build() {
  cd smartdns-Release$pkgver/src
  make
}

package() {
  cd smartdns-Release$pkgver
  make DESTDIR="$pkgdir" RUNSTATEDIR=/run SBINDIR=/usr/bin install

  rm -r "$pkgdir"/etc/init.d
}
