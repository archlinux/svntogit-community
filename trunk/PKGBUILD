# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=noise-suppression-for-voice
pkgver=1.03
pkgrel=1
pkgdesc='A real-time noise suppression plugin for voice'
arch=(x86_64)
url='https://github.com/werman/noise-suppression-for-voice'
license=(GPL3)
depends=(freetype2)
makedepends=(cmake libx11 libxrandr libxinerama libxcursor)
source=(https://github.com/werman/noise-suppression-for-voice/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8c85cae3ebbb3a18facc38930a3b67ca90e3ad609526a0018c71690de35baf04')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
