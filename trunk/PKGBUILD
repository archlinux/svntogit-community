# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=racket
pkgname=(racket racket-minimal)
pkgver=8.7
pkgrel=2
pkgdesc="A full-spectrum language with DrRacket IDE.  Formerly known as PLT Scheme."
arch=('x86_64')
url="https://racket-lang.org/"
license=('Apache' 'GPL3' 'LGPL3' 'custom')
depends=('gtk3')
makedepends=('gsfonts' 'sqlite')
options=('!strip' '!emptydirs')
conflicts=('racket-docs')
replaces=('racket-docs')
source=("https://download.racket-lang.org/installers/${pkgver}/${pkgname}-${pkgver}-src.tgz")
# "https://mirror.racket-lang.org/installers/${pkgver}/racket-minimal-${pkgver}-src.tgz"
sha512sums=('c196bb0c75a9b24b9bd48336f0323f845595654a4c855b928ee96b3ff973a213aee5290f887d6a1d799c3f887de9af8c0180b4bebb11079093cea3149abb0e3c')
b2sums=('efc2b338aa41a7e1118bc465721b3674d2dc91964d389d55ccb10a17c56e323eb54ffc9b94a805303adb4ebb51a8f4d18a8a8c23d2d424f2acd903e3b26b5d80')

prepare() {
  cd "$pkgbase-$pkgver"
  echo "Icon=drracket" >> share/pkgs/drracket/drracket/drracket.desktop
}

build() {
  cd "$pkgbase-$pkgver/src"
  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared
  make
}

package_racket() {
  cd "$pkgbase-$pkgver/src"
  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*.txt
  install -Dm644 ../share/pkgs/drracket/drracket/drracket.desktop "$pkgdir/usr/share/applications/drracket.desktop"
  install -d "$pkgdir"/usr/share/icons/hicolor/scalable/apps
  ln -s /usr/share/racket/pkgs/icons/racket-logo.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/drracket.svg"
}

package_racket-minimal() {
  pkgdesc="Minimal Racket installation, without DrRacket/docs."
  depends=('libffi')
  conflicts=('racket')
  provides=('racket')
  replaces=()

  # The -minimal tarball uses the SAME extract path.
  # 99% identical sources anyway, manually apply the difference.
  # Thankfully these builds happen in the given order
  # so we can save some time and reuse the previous build.
  cd "$pkgbase-$pkgver"
  echo '((root "pkgs/racket-lib"))' > share/links.rktd
  _libhash=$(grep -oP '\("racket-lib".*?#f\)\)' share/pkgs/pkgs.rktd)
  echo "#hash($_libhash)" > share/pkgs/pkgs.rktd
  cd share/pkgs
  find . -not -name '.' -not -name '*pkgs.rktd' -not -regex '.*/racket-lib.*' -delete
  cd ../../src

  make DESTDIR="${pkgdir}" install

  find "$pkgdir/usr/share/doc/" -delete

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*.txt
}
