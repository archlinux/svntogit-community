# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

pkgname=rubberband
pkgver=1.9.1
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=('x86_64')
url="https://www.breakfastquay.com/rubberband/"
license=('GPL2')
depends=('gcc-libs' 'glibc')
makedepends=('fftw' 'ladspa' 'meson' 'java-environment' 'libsamplerate'
'libsndfile' 'vamp-plugin-sdk')
optdepends=(
  'ladspa-host: for LADSPA plugin'
  'vamp-host: for VAMP plugin'
  'vamp-plugin-sdk: for VAMP plugin'
)
provides=('librubberband.so' 'librubberband-jni.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/breakfastquay/rubberband/archive/v$pkgver.tar.gz")
sha512sums=('de0428ae6644872509835eb1bcc3a6aaad6aa637378ca4664524ba1cdd5d1cbcfab34e0dc29c9300342b3ddd03a38fd2497649b670dfea0793168e45e5d7e9ec')
b2sums=('1cbb791e2a5e5a3c7cb27bb44e7de3a1e50ac29f886d524463dc8c551558abd6222ad1c63c120add570c4b8acbd27bfd28fe05a3bd254cc9485d390dcc24d865')

build() {
  local java_major=$(java --version 2>/dev/null |grep 'openjdk'| cut -d ' ' -f2| cut -d '.' -f1)
  export JAVA_HOME="/usr/lib/jvm/java-${java_major}-openjdk"
  cd "$pkgname-$pkgver"
  arch-meson -Dfft=fftw \
            -Dresampler=libsamplerate \
            -Dextra_include_dirs="/usr/lib/jvm/java-${java_major}-openjdk/include,/usr/lib/jvm/java-${java_major}-openjdk/include/linux" \
            build
  ninja -C build
}

package() {
  depends+=('libfftw3.so' 'libsamplerate.so' 'libsndfile.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
