# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Adrian Stratulat <adrian.stratulat at inboxcom>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: KillaB <xGenoBlast@gmail.com>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Christian Schmidt <xmucknertx@googlemail.com>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=angband
pkgver=4.2.3
pkgrel=1
pkgdesc="A roguelike dungeon exploration game based on the writings of JRR Tolkien"
arch=('x86_64')
url="https://rephial.org/"
license=('GPL2' 'custom')
depends=('sdl_image' 'sdl_ttf' 'sdl_mixer' 'ncurses' 'xorg-fonts-misc')
makedepends=('python-docutils')
source=("https://github.com/angband/angband/releases/download/${pkgver}/Angband-${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}/Angband-${pkgver}"

  # Fix detection of ncurses config script for ncurses 6.0
  sed -i 's/ncursesw5-config/ncursesw6-config/g' acinclude.m4
}

build() {
  cd "${srcdir}/Angband-${pkgver}"

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sysconfdir=/usr/share/angband \
    --with-configpath=/usr/share/angband \
    --with-libpath=/usr/share/angband \
    --enable-gtk \
    --enable-sdl \
    --enable-sdl-mixer

  make
}

package() {
  cd "${srcdir}/Angband-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/share/angband/*/delete.me"
  install -Dm644 docs/copying.rst "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

sha512sums=('7616e49564d5091cc31eae897fcdf2faaec8cdc6a837c52e5028ce370a980b363508b1e6b8520595a83d1e859607a647d3c9278f440c859a90a42867f834fbd4')
