# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Arthur Țițeică arthur.titeica/gmail/com
# Contributor: Hao Zhang <theivorytower [at] gmail [dot] com>

pkgname=opendmarc
pkgver=1.3.3
pkgrel=1
pkgdesc="Free open source software implementation of the DMARC specification"
arch=('x86_64')
url="https://github.com/trusteddomainproject/OpenDMARC"
license=('custom')
depends=('smtp-server' 'libspf2' 'libbsd' 'libidn')
makedepends=('libmilter')
optdepends=('opendbx: acts as a middleware layer between OpenDMARC and a SQL backend of choice'
            'python: run opendmarc scripts at /usr/share/doc/opendmarc'
            'perl: run opendmarc scripts at /usr/share/doc/opendmarc'
            'perl-switch: generate DMARC reports'
            'perl-dbd-mysql: generate DMARC reports'
            'perl-libwww: generate DMARC reports')
backup=('etc/opendmarc/opendmarc.conf')
source=("https://github.com/trusteddomainproject/OpenDMARC/archive/rel-opendmarc-${pkgver//./-}.tar.gz"
        'opendmarc.service'
        'opendmarc.conf'
        'opendmarc.sysusers')
sha512sums=('bb4bf8e3ad2d1732b07e55316819d4fd708e529b54a336d7d00763e13bfc62580bb1b30f132fa786dbca15e526e8dd5e146c7be454e1c42714a9f57126fc5e12'
            '738de0cd286dd30713f32034f9ecf9009b6f64038c573c9f8aedaf10df8293bb9eec9d19492a03a2ebf2d2960289bdf48be9b1eb25395dbe9a490f7e3b25cb34'
            '2753ad4477b499947ca07bb385ad0e10f327efa61a9059884091ead8e8e2bd65793436053d5a9c734e4c0676b7823982083ea7b35fae967eeacaeafb6226ff20'
            'fbd5e81ded35281e3a63b4858a368033fa27696dee22a5dcf52e3e04b0762476e1ffa6edb489cf76612f3b4ffaee0fce586ab97d1da9805a089bbaf3487c907b')

prepare() {
  cd "${srcdir}/OpenDMARC-rel-opendmarc-${pkgver//./-}"
#  sed -i '' configure.ac
  mkdir docs
  touch docs/Makefile.in
}

build() {
  cd "${srcdir}/OpenDMARC-rel-opendmarc-${pkgver//./-}"
#  export LDFLAGS="${LDFLAGS//,--as-needed}"
  test -x configure || autoreconf -v -i
  ./configure --prefix=/usr \
              --bindir=/usr/bin \
              --sbindir=/usr/bin \
              --sysconfdir="/etc/${pkgname}" \
              --with-spf \
              --with-spf2-include=/usr/include/spf2 \
              --with-spf2-lib=/usr/lib/
  make
}

check() {
  cd "${srcdir}/OpenDMARC-rel-opendmarc-${pkgver//./-}"
  make -k check
}

package() {
  cd "${srcdir}/OpenDMARC-rel-opendmarc-${pkgver//./-}"
  make DESTDIR="${pkgdir}/" install
  # config
  install -D -m640 -o 335 -g 12 "${srcdir}/opendmarc.conf" "${pkgdir}/etc/${pkgname}/opendmarc.conf"
  # License
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
  # systemd service
  install -D -m644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  # sysusers.d snippet
  install -D -m644 "${srcdir}/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
