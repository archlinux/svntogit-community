# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

pkgname=ts-node
pkgver=10.6.0
pkgrel=1
pkgdesc="TypeScript execution and REPL for node.js, with source map support"
arch=('any')
url="https://github.com/TypeStrong/ts-node"
license=('MIT')
depends=('nodejs' 'typescript')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('0897a7ebe75f397a25c6e7414179d58d156f61399359bcb3edc9fec6ad974ecbe76976c7aabe205d208d6d2d898fcc9364cb86c14a011122da3a42dab1c555be')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  # Dedup typescript
  rm -r "$pkgdir"/usr/lib/node_modules/ts-node/node_modules/{typescript,.bin/{tsc,tsserver}}
}

# vim:set ts=2 sw=2 et:
