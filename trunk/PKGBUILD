# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: mightyjaym <jm.ambrosino@free.fr>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-e2fsprogs
pkgver=1.46.6
pkgrel=1
pkgdesc='Ext2/3/4 filesystem libraries (32-bit)'
arch=('x86_64')
license=('GPL' 'LGPL' 'MIT')
url='http://e2fsprogs.sourceforge.net'
depends=('lib32-glibc' 'e2fsprogs')
makedepends=('bc' 'lib32-util-linux' 'gcc-multilib')
provides=('libcom_err.so'
          'libe2p.so'
          'libext2fs.so'
          'libss.so')
validpgpkeys=('3AB057B7E78D945C8C5591FBD36F769BC11804F0') # Theodore Ts'o <tytso@mit.edu>
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/e2fsprogs/v${pkgver}/e2fsprogs-${pkgver}.tar."{xz,sign})
sha256sums=('a77517f19ff5e4e97ede63536566865dd5d48654e13fc145f5f2249ef7c4f4fc'
            'SKIP')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${srcdir}/e2fsprogs-${pkgver}"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --with-root-prefix='' \
    --enable-elf-shlibs \
    --disable-{debugfs,imager,resizer,fsck,uuidd,libuuid,libblkid}
  make
}

package() {
  cd "${srcdir}/e2fsprogs-${pkgver}"

  make DESTDIR="${pkgdir}" install-libs

  rm -rf "${pkgdir}"/usr/{bin,include,share}
  mkdir -p "${pkgdir}"/usr/share/licenses
  ln -s e2fsprogs "${pkgdir}/usr/share/licenses/${pkgname}"
}
