# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Loui Chang <louipc dot ist at gmail company>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=tig
pkgver=2.3.3
pkgrel=1
pkgdesc='Text-mode interface for Git.'
depends=('git' 'ncurses')
makedepends=('asciidoc' 'xmlto')
url='https://jonas.github.io/tig/'
license=('GPL')
arch=('x86_64')
backup=('etc/tigrc')
source=("https://github.com/jonas/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('800a63f6772b9fa1cd9b9e1ad99f4eee')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install install-doc-man

  install -Dm0644 contrib/tig-completion.bash "$pkgdir/usr/share/bash-completion/completions/tig"
}
