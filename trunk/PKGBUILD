# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.1.7
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('29015545f59fb4154d9199521c15a217fdebbe48f6cdad97793fed8fc67a0fdd9c133d4a4d3603284b36a4be8b0d17588c1c026e9b3bd44faf80f8553cc2155b')
b2sums=('37ad009b1bf366dded93d069707b826d21c005384850ef4a6dade73e2e1632c7e406dd0b9bcc09f6bc7a5ae7a5d16141e7e2d3323cc92c858d675188cc4b51f9')

build() {
  local cmake_options=(
    -B build
    -S $pkgname-$pkgver
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
