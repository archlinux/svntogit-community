# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.6.1
pkgrel=2
pkgdesc='GObject-based wrapper around the Exiv2 library'
arch=('x86_64' 'i686')
url='http://redmine.yorba.org/projects/gexiv2/roadmap'
# http://yorba.org/download/gexiv2/
license=('GPL2')
depends=('exiv2' 'gobject-introspection')
makedepends=('python-gobject' 'python2-gobject')
source=("http://yorba.org/download/gexiv2/${pkgver%.1}/${pkgname}_$pkgver.tar.xz")
sha256sums=('72ee2fcc6a98c9e83467adea45b10d1b61c55654c204dcce9c2463615389a9e3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # In order for the build process to include any python-related files,
  # 'import gi; print(gi._overridesdir)' must work with either python or
  # python2. The gi module is provided by python-gobject and python2-gobject.
  ./configure --prefix=/usr --enable-introspection
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
