# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jose Valecillos <valecillosjg (at) gmail (dot) com>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
pkgname=gource
pkgver=0.54
pkgrel=1
pkgdesc="software version control visualization"
license=(GPL3)
arch=(x86_64)
url=http://gource.io/
depends=('ftgl' 'sdl2' 'sdl2_image' 'pcre2' 'glew' 'boost-libs')
makedepends=('boost' 'glm' 'mesa')
source=(https://github.com/acaudwell/Gource/releases/download/gource-${pkgver}/gource-${pkgver}.tar.gz)
sha512sums=('a9fd2ff7c51931a543d4ab6dc85dd4b2c187ecca1be630006bb60ac2507180ccf40561e37f50493e052c839d9f5f896ae8f113fb65844c2fa9bde33fcd6b53e8')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}
# vim: ts=2:sw=2 et:
