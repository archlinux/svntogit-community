# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ipvsadm
pkgver=1.26
pkgrel=4
pkgdesc='The IP Virtual Server administration utility'
arch=('i686' 'x86_64')
url='http://www.linuxvirtualserver.org/software/ipvs.html'
license=('GPL2')
depends=('libnl1' 'popt')
backup=('etc/conf.d/ipvsadm')
source=("http://www.linuxvirtualserver.org/software/kernel-2.6/$pkgname-$pkgver.tar.gz"
        'ipvsadm.rc'
        'ipvsadm.conf'
        'ipvsadm.rules'
        '01-fix-not-showing-all-server.patch')
md5sums=('eac3ba3f62cd4dea2da353aeddd353a8'
         'ed1ffa6a463636a311628e10a3fa4011'
         'cd358130967a9cc0d31fc61282df43e7'
         'cd6649c647e495d4d6f981542ce5a93b'
         'e9fb77d2fbe2d3f41d60257316338543')

build() {
  cd $pkgname-$pkgver
  patch -p1 -i ../01-fix-not-showing-all-server.patch
  make INCLUDE="-I/usr/src/linux-$(uname -r)/include -I.. -I."
}

package() {
  cd $pkgname-$pkgver
  make \
    BUILD_ROOT="$pkgdir" \
    MANDIR=usr/share/man \
    SBIN="$pkgdir/usr/sbin" \
    INIT="$pkgdir/etc/rc.d" \
    install
  cd "$srcdir"
  # add initscripts
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # add ipvsadm rules
  install -D -m 644 $pkgname.rules "$pkgdir/etc/$pkgname.rules"
}

# vim:set ts=2 sw=2 ft=sh et:
