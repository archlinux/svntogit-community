# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgbase=python-rfc3986
pkgname=(python-rfc3986 python2-rfc3986)
pkgver=0.3.1
pkgrel=1
pkgdesc="Validating URI References per RFC 3986"
arch=('any')
license=('Apache')
url="https://rfc3986.readthedocs.org/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/r/rfc3986/rfc3986-$pkgver.tar.gz")
md5sums=('4a0cc842d0e1b061f1b5202917095340')

prepare() {
  cp -a rfc3986-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/rfc3986-$pkgver
  python setup.py build

  cd "$srcdir"/rfc3986-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/rfc3986-$pkgver
  python setup.py ptr

  cd "$srcdir"/rfc3986-$pkgver-py2
  python2 setup.py ptr
}

package_python-rfc3986() {
  depends=('python')

  cd rfc3986-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-rfc3986() {
  depends=('python2')

  cd rfc3986-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
