# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Martin Striz < ms AT martinstriz DOT cz >
# Contributor: Jarek Sedlacek <jareksedlacek@gmail.com>

pkgbase=php-mongodb
pkgname=(php-mongodb php7-mongodb)
pkgver=1.14.2
pkgrel=1
pkgdesc="MongoDB driver for PHP"
arch=("x86_64")
url="http://mongodb.github.io/mongo-php-driver"
license=("Apache")
makedepends=('php7' 'php' 're2c')
source=(
	"https://pecl.php.net/get/mongodb-$pkgver.tgz"
	"mongodb.ini"
)
sha512sums=('2706c86f1408a588ea65c2af3706be8fd500306ac2a5d198fd611e5bed4a8ba52d51335c29d046bdb7183e9bd89a7430c651e254689d18109e524b27ac2413b4'
            '47b167ab4d761f4cf8351fef14e46c714dacae6d9a1b6d281bb87129499ee520f55f35f47c16b55ea2ecf41c67aad12439a99d3363d381b2511659b01f0b6ef0')

prepare() {
    cp -a "$srcdir/mongodb-$pkgver" "$srcdir/mongodb7-$pkgver"
}

build() {
	cd "$srcdir/mongodb-$pkgver"
	phpize
	./configure --prefix=/usr

	cd "$srcdir/mongodb7-$pkgver"
	phpize7
	./configure --prefix=/usr
}

package_php-mongodb() {
    pkgdesc="MongoDB driver for PHP"
    depends=('php')
    backup=("etc/php/conf.d/mongodb.ini")
	cd mongodb-$pkgver
	make INSTALL_ROOT="$pkgdir" install
	install -Dm644 "$srcdir/mongodb.ini" "$pkgdir/etc/php/conf.d/mongodb.ini"
}

package_php7-mongodb() {
    pkgdesc="MongoDB driver for PHP7"
    depends=('php7')
    backup=("etc/php7/conf.d/mongodb.ini")
	cd mongodb7-$pkgver
	make INSTALL_ROOT="$pkgdir" install
	install -Dm644 "$srcdir/mongodb.ini" "$pkgdir/etc/php7/conf.d/mongodb.ini"
}
