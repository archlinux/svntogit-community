# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pony
pkgname=python-$_pkgname
pkgver=0.7.16
pkgrel=1
pkgdesc='Pony Object Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
depends=('python')
makedepends=('python-setuptools' 'git')
license=('Apache')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('db9bb06f0e718866eb6f137d5650a2b484119c42f1ade83a7bc1c9e3c27a852ed25e451a226a80bebd050ca3217c0993e59f3b66719a3fe41a98e45349172495')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  python setup.py test
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  rm -rf "$pkgdir"/usr/lib/python*/site-packages/pony/orm/tests
}
