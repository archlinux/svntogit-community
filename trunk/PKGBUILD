# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cliff
pkgname=(python-cliff python2-cliff)
pkgver=2.3.0
pkgrel=2
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="http://docs.openstack.org/developer/cliff"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-cmd2' 'python2-cmd2' 'python-prettytable' 'python2-prettytable'
             'python-pyparsing' 'python2-pyparsing' 'python-stevedore'
             'python2-stevedore' 'python-unicodecsv' 'python2-unicodecsv' 'python-yaml'
             'python2-yaml')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock')
options=('!emptydirs')
source=("git+https://git.openstack.org/openstack/cliff#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a cliff{,-py2}
}

build() {
  cd "$srcdir"/cliff
  python setup.py build

  cd "$srcdir"/cliff-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cliff
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests3 -d

  cd "$srcdir"/cliff-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2 -d
}

package_python-cliff() {
  depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
           'python-stevedore' 'python-unicodecsv' 'python-yaml')

  cd "$srcdir"/cliff
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-cliff() {
  depends=('python2-six' 'python2-pbr' 'python2-cmd2' 'python2-prettytable' 'python2-pyparsing'
           'python2-stevedore' 'python2-unicodecsv' 'python2-yaml')

  cd "$srcdir"/cliff-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
