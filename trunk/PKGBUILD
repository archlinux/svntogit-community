# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgbase=caja-extensions
pkgname=(caja-extensions-common caja-gksu caja-image-converter caja-open-terminal caja-sendto caja-share)
pkgver=1.8.0
pkgrel=3
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'dbus-glib' 'gksu' 'gupnp' 'imagemagick' 'samba')
makedepends=('mate-common' 'perl-xml-parser')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('b4872265988745bfc39531e2eb6ac6d41a01a645')

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=2.0
    make
}

package_caja-extensions-common() {
    pkgdesc="Common files for Caja extensions."
    depends=()
    conflicts=('caja-extensions')
    replaces=('caja-extensions')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C po DESTDIR="${pkgdir}" install
}

package_caja-gksu() {
    pkgdesc="A Caja extension for executing files with elevated privileges."
    depends=('caja' 'caja-extensions-common' 'gksu')
    groups=('mate-extra')
    conflicts=('mate-file-manager-gksu')
    replaces=('mate-file-manager-gksu')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter() {
    pkgdesc="A Caja extension for simple image conversions."
    depends=('caja' 'caja-extensions-common' 'imagemagick')
    groups=('mate-extra')
    conflicts=('mate-file-manager-image-converter')
    replaces=('mate-file-manager-image-converter')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal() {
    pkgdesc="A Caja extension for opening terminals in arbitrary local paths."
    depends=('caja' 'caja-extensions-common')
    groups=('mate-extra')
    conflicts=('mate-file-manager-open-terminal')
    replaces=('mate-file-manager-open-terminal')
    install=caja-open-terminal.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto() {
    pkgdesc="A Caja extension for sending files."
    depends=('caja' 'caja-extensions-common' 'dbus-glib' 'gupnp')
    groups=('mate-extra')
    optdepends=('pidgin: Support for sending to instant messaging networks.'
                'gajim: Support for sending to Jabber.')
    conflicts=('mate-file-manager-sendto')
    replaces=('mate-file-manager-sendto')
    install=caja-sendto.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C sendto DESTDIR="${pkgdir}" install
}

package_caja-share() {
    pkgdesc="A Caja extension to quickly share a folder."
    depends=('caja' 'caja-extensions-common' 'samba')
    groups=('mate-extra')
    conflicts=('mate-file-manager-share')
    replaces=('mate-file-manager-share')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C share DESTDIR="${pkgdir}" install
}
