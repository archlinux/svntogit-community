# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfigstudio
pkgver=1.4.1
pkgrel=1
pkgdesc='Professional vector animation program (GUI)'
arch=('x86_64')
url='https://www.synfig.org/'
license=('GPL3')
depends=('gtkmm3' 'synfig')
makedepends=('etl' 'intltool')
optdepends=('python: for the plugins')
source=("https://downloads.sourceforge.net/synfig/$pkgname-$pkgver.tar.gz")
sha256sums=('af9a9b0064064907ae24a693b607dcba633a8a2cd45fa1357f61c035efd6020e')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|appdatadir = $(datadir)/appdata|appdatadir = $(datadir)/metainfo|' Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-update-mimedb
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
