# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hasktags
pkgver=0.70.0
pkgrel=2
pkgdesc="Produces ctags \"tags\" and etags \"TAGS\" files for Haskell programs"
url="https://github.com/MarcWeber/hasktags"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-json' 'haskell-microlens-platform' 'haskell-utf8-string')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f07196a0438500600b116c03ed9c73b81e16f8c7646558e611b552f165cce7a32902c196ce8c76f0fb47093cc3a3ffdd440141a57dbd0e32e1984966ee85be20')

build() {
    cd $pkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-debug
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $pkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
