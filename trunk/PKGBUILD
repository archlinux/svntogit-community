# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.85
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache' 'MIT')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('b240aa9ceaec07682df68db1207dec45e5d0c3feb1e4cfebc757a56415420034'
            '61e6decf90471b54fe258cca5aa23683a1ba89f127a49a837c071b70f481a69f')
b2sums=('8aedb95e1ba16033aadede2711a63cfcc73c92ec1ecd0cd1bdf5722e7976c75b026b84f150c2bf2d6743b2b5cc6d492e32d07cca5374dbbbe81d1a3a7309d2f8'
        '5a51402b0bb3f707e1efbc4c22017ad07b2e88e827e3f182d3e9acce9b7ce6ec5d6368d968392cdf30b6e49624e54c5c66f7f147ec186b963cef7f6844ea5c9b')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
