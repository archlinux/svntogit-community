# Maintainer: Laurent Carlier <lordheavym@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=lib32-lcms2
pkgver=2.15
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (32-bit)"
arch=('x86_64')
license=('MIT')
depends=('lib32-libtiff' 'lcms2')
provides=('liblcms2.so')
makedepends=('gcc-multilib')
url="https://www.littlecms.com"
source=("https://github.com/mm2/Little-CMS/releases/download/lcms${pkgver}/lcms2-${pkgver}.tar.gz")
sha512sums=('ab038c369e66736e0dd3810fbac6cf5381bc3102c4dd693819367b1224f59d9e853ede081388464c0e7c213d92e6cddc48c23020953af10ad6c6802cfa17e213')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd lcms2-${pkgver}

  ./configure --prefix=/usr \
    --libdir=/usr/lib32
  make
}

check() {
  cd lcms2-${pkgver}

  make check
}

package() {
  cd lcms2-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s lcms2 "${pkgdir}/usr/share/licenses/${pkgname}"
}
