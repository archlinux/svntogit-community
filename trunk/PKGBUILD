# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=libsass
pkgver=3.4.7
pkgrel=1
pkgdesc="C implementation of Sass CSS preprocessor (library)."
arch=("x86_64")
url="http://libsass.org/"
license=("MIT")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha256sums=("855c40528b897d06ae4d24606c2db3cd09bb38de5b46b28e835f9d4fd4d7ab95")


build() {
	cd "$srcdir/$pkgname-$pkgver"

	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir/" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
