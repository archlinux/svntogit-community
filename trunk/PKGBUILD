# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.48.1
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=3
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('x86_64')
url="http://msp.ucsd.edu/software.html"
license=('custom:BSD')
depends=('fftw' 'jack' 'portaudio' 'tk')
makedepends=('portmidi')
optdepends=('portmidi: for alternative portmidi support')
provides=('puredata')
source=("http://msp.ucsd.edu/Software/${pkgname}-${_ver}.src.tar.gz"
        "${pkgname}.desktop"
        "${pkgname}.png")
sha512sums=('69415793b6ecfcde4fbe2e0c30104236958fb79fd241cfad4ad0616934b3d8749e893c83b81a560339193e9ca5ab24baaca3b7ccd6f9021c54c9d2ac753a34e8'
            '4d3596b337456117ecdf0f1709358b4298198e0b347cb13b884e35ad2f2f667561a10b44683cf916c1da123daebee7141e9ab8bd89405a579b69d30b0e718f37'
            '61300b58f10018b0bd28424ac00d1b8158f8ace9855742e19e48c98f915cabcade6c6041460aa795005c0fb6abc93e7116f9999c6d001999916c9fe6c85c162d')

prepare() {
  mv -v "${pkgname}-${_ver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --enable-portaudio \
              --enable-portmidi \
              --without-local-portaudio \
              --without-local-portmidi \
              --enable-fftw \
              --without-local-portaudio
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # license
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # desktop file
  install -vDm 644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  # icon from puredata.info
  # https://puredata.info/Members/claudiusmaximus/icons/index_html/
  install -vDm 644 "${srcdir}/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # readme
  install -vDm 644 README.txt "${pkgdir}/usr/share/doc/${pgkname}/README.txt"
  # fix broken symlink
  rm -v "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ln -sv "/usr/bin/${pkgname}" "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
}
