# Maintainer: jelle van der Waa <jelle@vdwaa.nl>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=bonnie++
pkgver=1.03e
pkgrel=5
pkgdesc="Based on the Bonnie hard drive benchmark by Tim Bray"
arch=('i686' 'x86_64')
url="http://www.coker.com.au/bonnie++/"
license=('GPL')
depends=('perl')
source=(http://www.coker.com.au/bonnie++/$pkgname-$pkgver.tgz)
sha256sums=('cb3866116634bf65760b6806be4afa7e24a1cad6f145c876df8721f01ba2e2cb')

build() {
        cd $srcdir/${pkgname}-${pkgver}
        ./configure --prefix=/usr
        sed -i "s:CFLAGS=-O2  -DNDEBUG \$(WFLAGS) \$(MORECFLAGS):CFLAGS=${CFLAGS}:" Makefile
        make
}

package() {
        cd $srcdir/${pkgname}-${pkgver}
        make prefix="${pkgdir}/usr" install
        install -d $pkgdir/usr/share/doc/bonnie++
        install -Dm644 $srcdir/$pkgname-$pkgver/readme.html $pkgdir/usr/share/doc/bonnie++/readme.html
}

