# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=mpv-mpris
pkgver=1.0
pkgrel=1
pkgdesc='MPRIS plugin for mpv'
arch=('x86_64')
url='https://github.com/hoyon/mpv-mpris'
license=('MIT')
depends=('glib2' 'mpv')
checkdepends=('jq' 'playerctl' 'socat' 'sound-theme-freedesktop' 'xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('edc8218477a3b9b6c0568a122f3c02efde86a4de97f84872b767031e58c6916d4e3b34de75275538e8f18f28f15a139d8fca8631a76cecc04d8df88c457d5fad')

build() {
	cd "$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgver"
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
