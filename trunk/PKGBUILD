# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-control-center
pkgver=3.0.2
pkgrel=1
pkgdesc="Utilities to configure the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-control-center"
groups=('ukui')
depends=('boost-libs' 'lsb-release' 'kconfig' 'kconfigwidgets' 'ki18n' 'kscreen' 'kwidgetsaddons'
         'peony' 'qt5-quickcontrols' 'ukui-interface' 'ukui-panel' 'ukui-media' 'ukui-menus'
         'ukui-power-manager' 'ukui-screensaver' 'ukui-settings-daemon' 'upower')
makedepends=('boost' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-control-center/archive/v$pkgver.tar.gz")
sha512sums=('9126c0255d2eb362e1f8f97f2c4b979e9934eecd5f4defea674e604c368b60660ba01cf12d013800cf65396e3c1fc67496e189c8de2f3ba68b21bd15a5791b6c')

prepare() {
  cd ukui-control-center-$pkgver
  # QSysInfo::productVersion() returns "unknown" on Arch
  #TODO: sed -i 's/productVersion/prettyProductName/' plugins/messages-task/about/about.cpp
}

build() {
  cd ukui-control-center-$pkgver
  qmake-qt5 PREFIX=/usr LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd ukui-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 registeredQDbus/conf/com.control.center.qt.systemdbus.service -t "$pkgdir"/usr/share/dbus-1/system-services/
  install -Dm644 registeredQDbus/conf/com.control.center.qt.systemdbus.conf -t "$pkgdir"/usr/share/dbus-1/system.d/
}
