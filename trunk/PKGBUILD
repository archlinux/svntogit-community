# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-run
pkgver=9.0.0
pkgrel=1
pkgdesc="Install packages and run Python with them"
url="https://github.com/jaraco/pip-run"
license=('MIT')
arch=('any')
depends=('python-autocommand' 'python-packaging' 'python-path' 'python-pip')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'jupyter-nbformat' 'python-pytest-enabler')
source=("https://github.com/jaraco/pip-run/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('117b2260fe628dcc5aae47335ab645c2db47ca9b4d5cd0446ef11bb399c0e91df3a32b61aa7a228aca4d7b837228ddb41fe010896f7debf5fbca6111398cce73')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pip-run-$pkgver
  python -m build -wn
}

check() {
  cd pip-run-$pkgver
  python -m pytest
}

package() {
  cd pip-run-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
