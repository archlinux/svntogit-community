# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libsm
pkgname=lib32-$_pkgbasename
pkgver=1.2.0
pkgrel=1
pkgdesc="X11 Session Management library (32-bit)"
arch=(x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libice' 'lib32-util-linux-ng' $_pkgbasename)
makedepends=('xorg-util-macros' 'xtrans' 'gcc-multilib')
options=('!libtool')
source=(${url}/releases/individual/lib/libSM-${pkgver}.tar.bz2)
sha1sums=('f78bc72f0b3ec26cbf980f84f014422ba854544d')

build() {
  cd "${srcdir}/libSM-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libSM-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
