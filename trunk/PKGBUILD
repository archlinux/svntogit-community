# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.5.0
pkgrel=2
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="http://code.google.com/p/ibus/"
license=('LGPL')
depends=('python2-pyenchant' 'ibus' 'aspell-en')
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        https://github.com/mike-fabian/ibus-table/commit/573da2ededdb96dc4f6ef6b515a43f416b183e7a.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ../573da2ededdb96dc4f6ef6b515a43f416b183e7a.patch

  # python2 fix
  find -type f -exec sed -i -e 's_exec python_exec python2_' -e 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' -e 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' {} \;

  PYTHON=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
sha512sums=('2c52da1b4d6ae8aec29e97a8a413b388912cfcfd15860ba0e65c97b139b14f8a7d4087eb196d516abe731df03a2e4904666194414242da8cf1f3f0b54696248e'
            'ecb1f89ff946309314789aee4b5eb0749a18e881700482df64d1da30ddd438bb115c814fbd67c6c063c6cfdd28a3882aaf6dfdd291e651315ff6f7b58ce5f3ad')
