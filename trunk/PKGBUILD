# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=snd
pkgver=22.3
pkgrel=1
pkgdesc="An advanced sound editor"
arch=(x86_64)
url="https://ccrma.stanford.edu/software/snd/"
license=(custom:free)
groups=(pro-audio)
depends=(glibc glu gmp gsl libglvnd libmpc libx11 libxext
libxpm libxt mpfr openmotif)
# TODO: maybe replace mpg123 with mpg321
makedepends=(alsa-lib fftw flac gendesk jack ladspa libpulse
libsamplerate mpg123 ruby speex timidity++ vorbis-tools wavpack)
optdepends=(
  'flac: For flac support'
  'mpg123: For MPEG support'
  'pulseaudio: For pulseaudio support'
  'ruby: for snd-ruby'
  'speex: For speex support'
  'timidity++: For MIDI to WAVE support'
  'vorbis-tools: For OGG support'
  'wavpack: For wav support'
)
options=(debug)
source=(https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('548f9cf3be2df28e66f51709b6ee078f951e4d8bbaf140a4e7b4b92bbe5f5d93a2343fe9857654efb09662ec5d063113db0204d6d4052f98c4d11e5fddc2c08c')
b2sums=('eba03468e1512daa4ae33a56ad13ec76b7cf90e6a8e3822453bff1ad99b03fb0f0d12f94b29afe9697b6fe9a34fd2e00759c158cb413416ef4814d31a1c969a8')

prepare() {
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-s7" \
          --name "$pkgname (s7)" \
          --exec "$pkgname-s7" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-ruby" \
          --name "$pkgname (ruby)" \
          --exec "$pkgname-ruby" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname" \
          --name $pkgname \
          --exec $pkgname \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  rm -v $pkgname-$pkgver/*.desktop
  cp -av $pkgname-$pkgver $pkgname-ruby-$pkgver
}

build() {
  (
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-s7
  make
  )
  (
  cd $pkgname-ruby-$pkgver
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-ruby
  make
  )
}

package() {
  depends+=(libasound.so libfftw3.so libjack.so libsamplerate.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  mv -v "$pkgdir/usr/bin/snd"{,-s7}

  make DESTDIR="$pkgdir" install -C $pkgname-ruby-$pkgver
  mv -v "$pkgdir/usr/bin/snd"{,-ruby}

  # set s7 version as default
  ln -sv snd-s7 "$pkgdir/usr/bin/snd"

  # XDG desktop integration
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"
  # license
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
  # docs
  install -vDm 644 $pkgname-$pkgver/{{HISTORY,README}.Snd,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/*.html -t "$pkgdir/usr/share/doc/$pkgname/html"
  install -vDm 644 $pkgname-$pkgver/pix/*.png -t "$pkgdir/usr/share/doc/$pkgname/html/pix"

  # remove examples of unsupported extension language forth (not in the repos)
  rm -v "$pkgdir/usr/share/$pkgname/"*.fs
}
