# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgbase='python-qtpy'
_pkgbase='qtpy'
pkgname=('python-qtpy' 'python2-qtpy')
pkgver=1.8.0
pkgrel=1
pkgdesc='Provides an uniform layer to support PyQt5, PyQt4 and PySide with a single codebase'
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
arch=('any')
url="https://github.com/spyder-ide/${_pkgbase}/"
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${_pkgbase}/archive/v${pkgver}.tar.gz")
sha256sums=('f4d5bdeeac2a859ecaf72b52ed6502f00946e255d635911866efa8cbab95238d')

prepare() {
  # Prepare sources for Python 2.x
  cp -r "${_pkgbase}-${pkgver}" "python2-${_pkgbase}-${pkgver}"
  # Fix Python 2.x shebang
  cd "python2-${_pkgbase}-${pkgver}"
  sed -i 's@^#!.*python$@#!/usr/bin/env python2@' "${_pkgbase}/tests/runtests.py"
}

build() {
  cd "${_pkgbase}-${pkgver}"
  python2 setup.py build

  cd "${srcdir}/python2-${_pkgbase}-${pkgver}"
  python setup.py build
}

package_python2-qtpy() {
  depends=('python2')
  optdepends=('python2-pyqt5: Qt5 Python bindings'
              'python2-pyqtwebengine: Qt5-WebEngine python bindings')

  cd "${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-qtpy() {
  depends=('python')
  optdepends=('python-pyqt5: Qt5 Python bindings'
              'python-pyqtwebengine: Qt5-WebEngine python bindings'
              'python-pyside2: PySide Qt5 Python bindings')

  cd "${_pkgbase}-${pkgver}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
