# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=lib32-speexdsp
pkgver=1.2.1
pkgrel=1
pkgdesc="DSP library derived from Speex (32-bit)"
url="https://www.speex.org/"
arch=(x86_64)
license=(BSD)
depends=(lib32-gcc-libs speexdsp)
makedepends=(git)
provides=(libspeexdsp.so)
_commit=1b28a0f61bc31162979e1f26f3981fc3637095c8  # tags/SpeexDSP-1.2.1^0
source=("git+https://gitlab.xiph.org/xiph/speexdsp.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd speexdsp
  git describe --tags | sed 's/^SpeexDSP-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd speexdsp
  ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  cd speexdsp
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libdir=/usr/lib32 \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd speexdsp
  make -k check
}

package() {
  cd speexdsp
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,share}
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}
