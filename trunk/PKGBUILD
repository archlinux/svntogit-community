# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=DRBG
pkgname=haskell-drbg
pkgver=0.5.5
pkgrel=280
pkgdesc="Deterministic random bit generator (aka RNG, PRNG) based HMACs, Hashes, and Ciphers."
url="https://github.com/TomMD/DRBG"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-prettyclass' 'haskell-tagged' 'haskell-crypto-api'
         'haskell-cryptohash-cryptoapi' 'haskell-parallel' 'haskell-cipher-aes128' 'haskell-entropy')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-crypto-api-tests' 'haskell-hunit'
             'haskell-test-framework' 'haskell-test-framework-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e71fc158a7961729ce10d2ca795a935d6a103f31e864f0446cc4faf2b0edb3a5938a4cc79135cf043a375e63f9be4401ce092951b4e8f47c1c3b1c2070c118be')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/TomMD/DRBG/issues/7
    runhaskell Setup test || echo "Tests expected to fail"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
