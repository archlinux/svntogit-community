# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=gpgme
pkgname=ruby-gpgme
pkgver=2.0.19
pkgrel=1
pkgdesc='Ruby interface to GnuPG Made Easy (GPGME)'
url='https://github.com/ueno/ruby-gpgme'
arch=('x86_64')
license=('GPL2')
depends=('ruby' 'ruby-mini_portile2' 'gpgme' 'libgpg-error' 'libassuan')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-yard')
options=('!emptydirs')
source=(https://github.com/ueno/ruby-gpgme/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8a1a832a73bc8cb45207cf0b964b1f78c1698c5f76ca611fb2c118cd8ec5d325')
sha512sums=('a9fea9ad33e423ede54f64e7804b6a940bec9a120be3d612a1985abd06c030441091ba397144cd5b484fcfee26b0885303dcd4d954aefef0ae02575b95f12a36')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${pkgname}-${pkgver}
  export RUBY_GPGME_USE_SYSTEM_LIBRARIES=1
  rake compile
  rake build
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem -- --use-system-libraries
  install -Dm 644 README.rdoc NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
