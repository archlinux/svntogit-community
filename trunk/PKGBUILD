# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Aaron Bull Schaefer <aaron@elasticdog.com>
# Contributor: Ron Huang <ronhuang+aur at gmail dot com>

pkgname=fcgiwrap
pkgver=1.1.0
pkgrel=8
pkgdesc='A simple server for running CGI applications over FastCGI.'
arch=('x86_64')
url='https://www.nginx.com/resources/wiki/start/topics/examples/fcgiwrap/'
license=('MIT')
depends=('fcgi' 'systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gnosek/fcgiwrap/archive/${pkgver}.tar.gz"
        'link-with-libsystemd-instead-of-libsystemd-daemon.patch'
        'dc0c3b14f0d7bb014a9a4c6c17eb55a123496365.patch'
        'LICENSE')
sha256sums=('4c7de0db2634c38297d5fcef61ab4a3e21856dd7247d49c33d9b19542bd1c61f'
            '9be1906005fbf3aefc4acb20432f1217e7611f49d840bc4559591e8dfae45ae3'
            '44342f94e21ba8cb1938c50d9a86bcfaeebb8802b4f5a77d126ddb2387ea2412'
            '22f170fc08e13b4d1110acddab63235135bf6d7483456cc24e11b83c277aff44')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # https://github.com/gnosek/fcgiwrap/pull/37
  patch -Np1 -i ../link-with-libsystemd-instead-of-libsystemd-daemon.patch

  # fix kill() parameters
  patch -Np1 -i ../dc0c3b14f0d7bb014a9a4c6c17eb55a123496365.patch

  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # -Werror is not much fun in release builds
  CFLAGS+=' -Wno-error=implicit-fallthrough'

  # Here --sbindir is implicitly prefixed by /usr
  ./configure --prefix=/usr --mandir=/share/man --sbindir=/bin --with-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
