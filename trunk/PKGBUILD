# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=attoparsec-iso8601
pkgname=haskell-attoparsec-iso8601
pkgver=1.0.2.0
pkgrel=68
pkgdesc="Parsing of ISO 8601 dates, originally from aeson"
url="https://github.com/haskell/aeson"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-compat-batteries' 'haskell-time-compat')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ae9595bbcaccd681afb223455484d0ee60690d8a468291cbde5c9eab6298d46434c43589ad3b55e9509e934aa34c382a6546c41b8e27dbbf7002bde20d6ee2a7')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
    uusi -u attoparsec $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-developer -f-fast
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
