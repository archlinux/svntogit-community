# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=fortune-mod
pkgver=3.14.0
pkgrel=1
pkgdesc='The Fortune Cookie Program from BSD games'
arch=('x86_64')
url='https://www.shlomifish.org/open-source/projects/fortune-mod/'
license=('BSD')
depends=('glibc' 'recode')
makedepends=('cmake' 'rinutils')
source=("https://github.com/shlomif/fortune-mod/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz"
        'not-a-game.patch')
sha512sums=('289f11923bd91ac0c46d8879c57c1f4da20ef2ef165be2eea9635b3aca5fc18bfc2438e85bfbc2a872671f41658b68bb3f7dd74bef7e4268889371ff702d7cae'
            'f12bfed15648bf7e53143bfa5c626e0e614d64c64c4329119e7b1ad8a5d79c89d3e8f2c1336d489abecbf6a8ec0ad9acf3080b142d498f8b3f36cea77c82a23d')
b2sums=('9b7df93cd7ced677a391ed30a84503cbd36689de406bbeb346bc9e465420908c9115b0d58e8e0b245f554557f032a014cc52418da4a69281e3844412c8e9641d'
        '3af374f348d42bf649199a79afc18a8f0dace50d2b4c8aa090a1a54ccffcb65bf069d26f0d1d417a395c5a3574f7376c5ae1ec8b97dd2385d6b4b944643b1215')


prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../not-a-game.patch
}

build() {
  cmake \
    -B build \
    -S "$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_OFFENSIVE=OFF \
    -DLOCALDIR=/usr/share/fortune \
    -DLOCALODIR=/usr/share/fortune/off \
    -DCOOKIEDIR=/usr/share/fortune \
    -DOCOOKIEDIR=/usr/share/fortune/off

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  rm -f "$pkgdir/usr/share/fortune/"{,off/}*.u8

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/COPYING.txt"
}
