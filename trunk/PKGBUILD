# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Paulius Palevicius <paulius@birzai.com>

pkgname=catdoc
pkgver=0.94.4
pkgrel=1
pkgdesc="A convertor for Microsoft Word, Excel, PowerPoint and RTF Files to text"
arch=('i686' 'x86_64')
url="http://www.wagner.pp.ru/~vitus/software/catdoc/"
license=('GPL')
depends=('glibc')
optdepends=('tk: for using wordview')
options=('!makeflags')
source=(http://ftp.de.debian.org/debian/pool/main/c/catdoc/${pkgname}_$pkgver.orig.tar.gz)
md5sums=('4820680e3611392caf2b4dd2413bfae5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/$(prefix)/$(installroot)$(prefix)/' desktop/Makefile.in
  sed -i 's/0.94.2/0.94.4/' configure* doc/*.1 src/makefile.tc
  ./configure --prefix=/usr  --with-wish=/usr/bin/wish
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make installroot="$pkgdir" mandir=/usr/share/man/man1 install
  install -d "$pkgdir"/usr/share/man/man1
  install -m644 doc/*.1 "$pkgdir"/usr/share/man/man1
}
