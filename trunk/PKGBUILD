# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=atomicparsley
pkgver=20210114.184825.1dbe1be
pkgrel=2
pkgdesc='Command line program for reading, parsing and setting metadata in MPEG-4 files'
arch=('x86_64')
url=https://github.com/wez/atomicparsley
license=('GPL')
depends=('gcc-libs' 'zlib')
makedepends=('cmake')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('220d024fc0a9457322a7276bdde704f59309f1a1d59e7c7197e6dafa74acc6c9c71ab89716cb26ae8744a4ef2b21a53dd4a406cd8a66873c4f901a1ee1d4d5c4')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None .
  make
}

package() {
  cd $pkgname-$pkgver
  install -D AtomicParsley "$pkgdir"/usr/bin/atomicparsley
}

# vim:set ts=2 sw=2 et:
