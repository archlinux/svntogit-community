# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>

pkgname=gmtk
pkgver=1.0.6
pkgrel=1
pkgdesc='Common functions for gnome-mplayer and gecko-mediaplay'
arch=('i686' 'x86_64')
url='http://gmtk.googlecode.com/'
license=('GPL')
depends=('glib2' 'gtk3' 'dconf')
makedepends=('intltool' 'pkg-config')
options=(!libtool)
source=("http://gmtk.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('6bf1c63788541272717f63ec370f6b6c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags gtk+-3.0)"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-gtk3
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
