# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-construct
pkgver=2.10.67
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-arrow' 'python-cloudpickle' 'python-lz4' 'python-numpy' 'python-pytest-runner'
              'python-pytest-benchmark' 'python-ruamel-yaml')
optdepends=('python-arrow: for Timestamp'
            'python-numpy: for array serialization with Numpy protocol'
            'python-ruamel-yaml: for KaitaiStruct (KSY) exporter')
source=("$pkgname-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('a4c7cc32b9eb63311c6ae2b1580b7a696c1e0c3960d78936798dd794cb37a77ec8ed8baa8128f12abd50390b9f4cb3f150e3453d5043797f0bd603525b8292a5')

build() {
  cd construct-$pkgver
  python setup.py build
}

check() {
  cd construct-$pkgver
  python setup.py pytest
}

package() {
  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
