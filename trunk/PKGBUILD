# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Adria Arrufat <swiftscythe at gmail dot com>

pkgname=ugrep
pkgver=3.10.0
pkgrel=1
pkgdesc='ultra fast grep with interactive TUI, fuzzy search, boolean queries, hexdumps and more'
arch=(x86_64)
license=(BSD)
url="https://github.com/Genivia/$pkgname"
depends=(bzip2
         lz4
         pcre2
         xz
         zlib
         zstd)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('9bde11c055b922b6c3cecf9e8eb0cb02db9832c46deaa76424df4c4b8a57a718')

build() {
	cd "$_archive"
	./configure --prefix /usr
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
