# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

_hkgname=network-uri
pkgname=haskell-network-uri
pkgver=2.6.4.2
pkgrel=7
pkgdesc="A library for client-side HTTP"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-compat')
makedepends=('ghc')
checkdepends=('haskell-quickcheck' 'haskell-hunit' 'haskell-tasty' 'haskell-tasty-hunit'
              'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('999e94a3053a101d1dc737a26ff93f832040365e949805b1ff5327ade9ab63d20f686c9f1bb3544d6d22c7c6d0b10bfcb64b30aa226d74ed6ad8bbb28fce6c0f')

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
