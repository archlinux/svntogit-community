# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Felix Hanley <felix@seconddrawer.com.au>
# Contributor: SÅ‚awomir Kowalski <suawekk@gmail.com>

pkgname=percona-toolkit
pkgver=3.2.0
pkgrel=2
pkgdesc="Collection of advanced command-line tools to perform a variety of MySQL and system tasks."
url="https://www.percona.com/software/database-tools/percona-toolkit"
arch=('x86_64')
license=('GPL')
depends=('perl-term-readkey')
makedepends=('perl-dbd-mysql' 'perl-dbi' 'go' 'dep' 'git')
optdepends=(
    'perl-dbd-mysql: MySQL connections'
    'perl-dbi: MySQL connections'
)
options=('!emptydirs' 'purge')
source=("https://github.com/percona/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('a9f0b070da0edc3cb56eac403ea5a6e2bd7af4842947624282e17fdda6bcb6ae')

prepare() {
    mkdir -p "$srcdir"/src/github.com/percona
    cd "$srcdir"/src/github.com/percona
    mv "$srcdir"/$pkgname-$pkgver $pkgname
    cd $pkgname

    sed -ri src/go{,/*}/Makefile \
        -e "s#\\$\\(shell git describe --abbrev=0\\)#v$pkgver#" \
        -e "s#\\$\\(shell git rev-(parse|list).* HEAD\\)#v$pkgver#" \
        -e "s#\\$\\(shell git rev-parse --show-toplevel\\)#$srcdir/src/github.com/percona/$pkgname#" \
        -e "s#\\$\\(shell basename \`git rev-parse --show-toplevel\`\\)#$pkgname#" \
        -e "s#\\bLDFLAGS\\b#PTLDFLAGS#" \
        -e 's#^PTLDFLAGS="#PTLDFLAGS="-extldflags \\"$(LDFLAGS)\\" #'
}

build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    export GOPATH="$srcdir"
    export PATH="$GOPATH/bin:$PATH"

    cd "$srcdir"/src/github.com/percona/$pkgname
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps MODULEBUILDRC=/dev/null
    perl Makefile.PL INSTALLDIRS=vendor
    make

    cd src/go
    make linux-amd64
}

package() {
    cd "$srcdir"/src/github.com/percona/$pkgname
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    make install DESTDIR="$pkgdir"
    rm -rf "$pkgdir"/usr/lib

    cd bin
    for bin in *; do
        if [ ! -f "$pkgdir/usr/bin/vendor_perl/$bin" ]; then
            install -Dm755 "$bin" "$pkgdir/usr/bin/$bin"
        fi
    done
}
