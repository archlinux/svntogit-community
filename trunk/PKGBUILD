# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Tim Hütz <tim@huetz.biz>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgbase=python-mpi4py
pkgname=(python-mpi4py python2-mpi4py)
pkgver=2.0.0
pkgrel=2
pkgdesc='Python bindings for the Message Passing Interface (MPI) standard'
arch=(x86_64 i686)
url='https://planet.scipy.org/'
license=(BSD)
makedepends=(python-setuptools python2-setuptools openmpi)
source=(https://pypi.python.org/packages/source/m/mpi4py/mpi4py-$pkgver.tar.gz)
sha1sums=('65a71175a15c53be7de1132fd498f28f38bb1714')

prepare() {
  # fix for https://bitbucket.org/mpi4py/mpi4py/issues/28/test_dltestdl-test-failure
  sed -e 's/libm.so/libm.so.6/' -i  mpi4py-$pkgver/test/test_dl.py

  cp -a mpi4py-$pkgver{,-py2}
}

build() {
  cd "$srcdir/mpi4py-$pkgver"
  python setup.py build

  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py test

  cd "$srcdir/mpi4py-$pkgver"
  python setup.py test
}

package_python-mpi4py() {
  depends=(python openmpi)

  cd "$srcdir/mpi4py-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-mpi4py() {
  depends=(python2 openmpi)

  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
