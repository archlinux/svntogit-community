# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cinderclient
pkgname=(python-cinderclient python2-cinderclient)
pkgver=3.6.1
pkgrel=1
pkgdesc="OpenStack Block Storage API Client Library"
arch=('any')
url="http://docs.openstack.org/developer/python-cinderclient"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-prettytable' 'python2-prettytable' 'python-keystoneclient'
             'python2-keystoneclient' 'python-requests' 'python2-requests' 'python-oslo-utils'
             'python2-oslo-utils')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock' 'python2-requests-mock'
              'python-ddt' 'python2-ddt' 'python-stestr' 'python2-stestr')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-cinderclient/archive/$pkgver.tar.gz")
sha512sums=('6338a6edb93609620b375343ce1166c46416b415eb5152678b261889cb7dc5fce735e6914eebd1edb6f03dea3d20805410b69fae2937ae3f0ed42b2ed06cd72e')

prepare() {
  sed -i '/simplejson/d' python-cinderclient-$pkgver/requirements.txt
  cp -a python-cinderclient-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-cinderclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-cinderclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-cinderclient-$pkgver
  stestr run

  cd "$srcdir"/python-cinderclient-$pkgver-py2
  stestr2 run
}

package_python-cinderclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
           'python-requests' 'python-oslo-utils')

  cd "$srcdir"/python-cinderclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cinderclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-prettytable'
           'python2-keystoneclient' 'python2-requests' 'python2-oslo-utils')

  cd "$srcdir"/python-cinderclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/cinder{,2}
}

# vim:set ts=2 sw=2 et:
