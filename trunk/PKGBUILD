# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Carlo Abelli <carlo@abelli.me>
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Thomas Dziedzic  < gostrc at gmail >
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=ifuse
pkgver=1.1.4
pkgrel=3
pkgdesc="A fuse filesystem to access the contents of an iPhone or iPod Touch"
url="https://libimobiledevice.org/"
arch=(x86_64)
license=(LGPL2.1)
depends=(
  fuse2
  libimobiledevice
  libplist
  usbmuxd
)
makedepends=(git)
_commit=33434dec21198de11cea78325321d55ebb7bd71f  # tags/1.1.4
source=("git+https://github.com/libimobiledevice/ifuse#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd ifuse
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  cd ifuse
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd ifuse
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
