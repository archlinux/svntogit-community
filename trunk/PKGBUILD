# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=libtraceevent
pkgname=(libtraceevent libtraceevent-docs)
pkgver=1.5.0
pkgrel=1
epoch=1
pkgdesc="Linux kernel trace event library"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/libs/libtrace/libtraceevent.git/about/"
license=(GPL2 LGPL2.1)
makedepends=(asciidoc xmlto)
source=(
  "$pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/libs/libtrace/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz"
  "$pkgname-1.5.0-documentation.patch"
)
sha512sums=('eb2c57bfcf9039208776b990a55ee6bb6be0396c8b210e2c31f2264f31512dc6c2dbad090b97f567424fe7ab0768ecf443ae28f2e9b21e0f4696c8666460ed76'
            '74b34a722d3fb9d672826e0b6b137f94a7fcd41bb372f8944bb6a0c652c0e725aaef8e95284fc091c2e9954bcf1656b5428d7e0f121682d40c25623178ee4a1e')
b2sums=('7f917061e13c04e0e25b144c3e02315e7b91c2ddf3d091643dcd7534d5e7a50b40a68f91bb42b0a6bbe6ebaad9a61cb9de91a809355dfe699ccaadaae5f8e438'
        'f66f4f20dfe562407a0d4bb53785d1515c4b5f4be64dd96e06ea6c9e03c299b2f0f613901c30893a976b3874d13e768791632a7cb89be92f3788330e4f5ab97d')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../"$pkgname-1.5.0-documentation.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make
  make -C Documentation
}

package_libtraceevent() {
  depends=(glibc)
  optdepends=('libtraceevent-docs: for documentation')
  provides=(libtraceevent.so)

  cd "$pkgname-$pkgver"
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C Documentation
  (
    cd "${pkgdir}"
    _pick libtraceevent-docs usr/share/doc
  )
}

package_libtraceevent-docs() {
  pkgdesc+=" - documentation"

  mv -v "${pkgname}/"* "${pkgdir}"
}
