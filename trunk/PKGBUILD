# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock
pkgver=0.10.3
pkgrel=1
pkgdesc='A dock based on Plasma Frameworks'
arch=(x86_64)
url='https://store.kde.org/p/1169519/'
license=(GPL)
depends=(plasma-framework knewstuff hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8aa8e35a4fb8fcad7c72277a8fe751ac92d245a3ce3657bba5ba953b27649a8b'
            'SKIP')
validpgpkeys=(325E97C32E601F5D4EADCF3A55999050A2D9110E) # Michail Vourlakos (mvourlakos) <mvourlakos@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
