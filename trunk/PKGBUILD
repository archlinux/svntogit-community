# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.6.0
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a987e2fa2b94a712dbd06bcb743e33f9b86e865f5a9e202dcdbc3410ded73dcca34b69987b7b96c3e464e3ba7cc4a89948b91047a87fb5c38ed4fb33865be257')
b2sums=('bb2f7075ff6a97c3c5049b09259fa1ce916528f757e9993284de65c04f116aceaf9d64e43feda99ec8d4c36419f793c583075a634d9fd0c4633556687c4aaeaa')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter

  for i in bash fish zsh; do
    ./kube-linter completion ${i} > ./kube-linter.${i}
  done
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 kube-linter -t "${pkgdir}/usr/bin"
  install -Dm644 kube-linter.bash "${pkgdir}"/usr/share/bash-completion/completions/kube-linter
  install -Dm644 kube-linter.fish "${pkgdir}"/usr/share/fish/completions/kube-linter.fish
  install -Dm644 kube-linter.zsh "${pkgdir}"/usr/share/zsh/site-functions/_kube-linter
}

# vim: ts=2 sw=2 et:
