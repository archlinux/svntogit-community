# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-onboarding
pkgver=7.1.0
pkgrel=1
pkgdesc='Onboarding app for new users'
arch=(x86_64)
url=https://github.com/elementary/onboarding
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  libadwaita-1.so
  libgranite-7.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=271f0440f00d61200343191b3f7683fb831f1cc5
source=(pantheon-onboarding::git+https://github.com/elementary/onboarding.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-onboarding
  git describe --tags
}

build() {
  arch-meson pantheon-onboarding build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
