# $Id$
# Maintainer: David Runge <dave@sleepmap.de>

pkgbase=fltk
pkgname=('fltk' 'fltk-docs' 'fltk-games')
pkgver=1.3.4
pkgrel=2
arch=('x86_64')
license=('custom:LGPL')
url="http://www.fltk.org/"
makedepends=('mesa' 'glu' 'doxygen' 'libjpeg' 'libxft' 'libxinerama'
             'alsa-lib' 'desktop-file-utils' 'libxcursor')
source=("http://fltk.org/pub/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}-source.tar.gz"
        fltk-config-dynlibs.patch)
sha512sums=('92c236e426ad9b2a2954a6fc401bc9bf4c4c46d47b08f8c9b879da6031ac158ef9b944f28a8bea64d18072d1af8ceaa09c00abebd745fb76dd628636424446c2'
            'c2f15a5c134a258082454dacf8db14b4d276c8c9ed207b1d2275690a2829272cfee81186caf794031339f7ed619898485bd59c6d59d2e71cd98facfbb564c1f5')

prepare() {
  cd "${pkgbase}-${pkgver}"
  patch -p1 -i ../fltk-config-dynlibs.patch
  sed -i 's/class Fl_XFont_On_Demand/class FL_EXPORT Fl_XFont_On_Demand/' FL/x.H
  sed -i 's/x-fluid.desktop/fluid.desktop/' -i fluid/Makefile
  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile
}

build() {
  cd "${pkgbase}-${pkgver}"
  ./configure --prefix=/usr \
    --enable-threads \
    --enable-xft \
    --enable-shared
  make
  make -C documentation html
}

package_fltk() {
  pkgdesc="Graphical user interface toolkit for X"
  depends=('libjpeg' 'libxft' 'libxinerama' 'hicolor-icon-theme'
           'desktop-file-utils' 'xdg-utils' 'libxcursor' 'glu')
  options=('!docs')

  cd "${pkgbase}-${pkgver}"
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C fluid install install-linux
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_fltk-docs() {
  pkgdesc="Graphical user interface toolkit for X (documentation)"

  cd "${pkgbase}-${pkgver}/documentation"
  install -t "${pkgdir}/usr/share/doc/fltk/" -Dm644 html/*
  make DESTDIR="$pkgdir" -C ../test install
  install -Dm 644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_fltk-games() {
  pkgdesc="Example games that accompany FLTK: blocks, checkers and sudoku"
  depends=('alsa-lib' 'fltk')

  cd "${pkgbase}-${pkgver}/test"
  make DESTDIR="$pkgdir" install-linux
  make DESTDIR="$pkgdir" -C ../documentation install-linux
  install -Dm 644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
