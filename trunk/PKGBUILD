# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.18.2
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python python-typing-extensions tmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest procps-ng)
optdepends=('python-pytest: for pytest plugin')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('4e37477872accfc1e194c5cb05feb894374e7db000b5d4943c8e2e0312ac6facf85d082d4e49c4e0bdb9ed38fd4919f7b576647536a9f7a552856bd0280bd7d5')
b2sums=('7e82fd7b97c3d137900e40e629f2c8853b415bc7ccef6a537509b5b01af3c16af751604ae0be786e9550a040860c951de5a00e1ff89e7e91fbd4758150c77582')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages"
  pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
