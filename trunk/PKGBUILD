# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.21
pkgrel=5
pkgdesc='Tool for monitoring webpages for updates'
arch=('any')
url=https://thp.io/2008/urlwatch/
license=('BSD')
depends=('python-appdirs' 'python-cssselect' 'python-keyring' 'python-lxml'
         'python-minidb' 'python-requests' 'python-setuptools' 'python-yaml')
optdepends=('lynx: for one of the html2text filter methods'
            'python-beautifulsoup4: for the beautify filter and one of the html2text filter methods'
            'python-cssbeautifier: for the beautify filter'
            'python-html2text: for one of the html2text filter methods'
            'python-jsbeautifier: for the beautify filter'
            'python-pdftotext: for the pdf2text filter'
            'python-pytesseract: for the ocr filter')
checkdepends=('python-docutils' 'python-pycodestyle' 'python-pytesseract'
              'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        'https://github.com/thp/urlwatch/commit/af522e4f763e76faa80db7c796806f1ea6661cc6.patch')
sha256sums=('c259e0169cc99114b54470d08a8312473159dfdbed1d712d1a222fd8a9d7291a'
            'b968b2a781f888e709dd6d7ac0292da81abff7ab741fe702450bf6be5c710df4')
b2sums=('9e0f39e51e413355974ce886e019eb86dd07fb9a75c02c5c711ccdbba573c2e6b218cf11b42c714172ad00288ea0219a079fd9b8016d33c29f087537d160e80d'
        '28717a102aadbdfd8051ee78c4b25b02647d0bd3dce0c8039950ad30716de3e38bd5c1f021ee90401547720bca629717d161c3854cc01ee4d19ce7335797bea8')

prepare() {
  cd $pkgname-$pkgver
  patch --forward --strip=1 --input=../af522e4f763e76faa80db7c796806f1ea6661cc6.patch
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  pytest -k 'not test_pep8_conformance'
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname COPYING
}

# vim:set ts=2 sw=2 et:
