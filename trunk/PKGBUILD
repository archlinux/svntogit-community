# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongo-c-driver
pkgver=1.14.0
pkgrel=3
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='https://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libsasl' 'icu' 'snappy' 'zlib')
makedepends=('cmake')
provides=('libbson' 'libmongoc')
conflicts=('libbson' 'libmongoc')
replaces=('libbson' 'libmongoc')
source=("https://github.com/mongodb/mongo-c-driver/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('99ca4001341d044a8ffc02a5d7175d40f8a998bdaad15a668a7db0558fa616ec18152faeda6da7f28b34c5d2cf4b649180c9390d2229e1342b6c4f6742816994')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../mongo-c-driver-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_STATIC=OFF -DENABLE_TESTS=OFF
    # Test profiles are not found even with ENABLE_TESTS=ON
  make
}

check() {
  cd build
  # No test profile found
  # pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
