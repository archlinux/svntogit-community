# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=statistics
pkgname=haskell-statistics
pkgver=0.15.2.0
pkgrel=85
pkgdesc="A library of statistical types, data, and functions"
url="https://github.com/bos/statistics"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-async' 'haskell-base-orphans' 'haskell-data-default-class'
         'haskell-dense-linear-algebra' 'haskell-math-functions' 'haskell-monad-par'
         'haskell-mwc-random' 'haskell-primitive' 'haskell-vector' 'haskell-vector-algorithms'
         'haskell-vector-binary-instances' 'haskell-vector-th-unbox')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-erf' 'haskell-ieee754'
             'haskell-tasty' 'haskell-tasty-expected-failure' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7afa3e3edb15e02ff59c72601175d25f1a8baeaa1dd8561322e8e26781dbfbfcc03b2ade773269bab027d7399375f01595145fd2e2f252f3e73abc87f9baed41')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || echo "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
