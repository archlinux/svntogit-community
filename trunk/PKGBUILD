# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: cyrant <cyrant at tuta dot io>

pkgname=opentoonz
pkgver=1.7.1
pkgrel=1
pkgdesc='Software for producing a 2D animation'
arch=('x86_64')
url='https://opentoonz.github.io/e/'
license=('BSD')
depends=('cblas' 'ffmpeg' 'freeglut' 'glew' 'libmypaint' 'opencv' 'qt5-multimedia' 'qt5-script'
         'qt5-serialport' 'qt5-svg' 'superlu')
makedepends=('boost' 'cmake' 'ninja' 'qt5-tools')
source=("https://github.com/opentoonz/$pkgname/archive/v${pkgver}/$pkgname-$pkgver.tar.gz")
sha256sums=('d0daf60d06510efe15d0db3f9eed0162499ed58061504dca27892c2b9ea5f22d')

prepare() {
  cd $pkgname-$pkgver

  # Specify path for ffmpeg
  sed -i 's|"ffmpegPath", QMetaType::QString, ""|"ffmpegPath", QMetaType::QString, "/usr/bin"|' \
    toonz/sources/toonzlib/preferences.cpp
}

build() {
  pushd $pkgname-$pkgver/thirdparty/tiff-4.0.3
  ./configure --with-pic --disable-jbig
  make
  popd

  cmake -S $pkgname-$pkgver/toonz/sources -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_SKIP_RPATH=YES \
    -DWITH_TRANSLATION=OFF
  # WITH_TRANSLATION doesn't do anything, generated translations aren't installed
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
