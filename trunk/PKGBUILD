# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-web
pkgver=1.14
pkgrel=1
pkgdesc="Web interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-aeson' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-case-insensitive' 'haskell-clientsession'
         'haskell-cmdargs' 'haskell-conduit' 'haskell-data-default' 'haskell-decimal'
         'haskell-hjsmin' 'haskell-http-conduit' 'haskell-http-client' 'haskell-http-types'
         'haskell-conduit-extra' 'haskell-safe' 'haskell-shakespeare' 'haskell-wai'
         'haskell-wai-extra' 'haskell-wai-handler-launch' 'haskell-warp' 'haskell-yesod'
         'haskell-yesod-core' 'haskell-yesod-form' 'haskell-yesod-static' 'haskell-json'
         'haskell-megaparsec')
makedepends=('ghc' 'haskell-hspec' 'haskell-yesod-test')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7a984cd324758dd76a1347a23baba60ec2bf6e5d1657a25047d8e2d3a5e3280e02d0ec018809f9c5bbcdf212328dd87a2b9d160dde6ceeeecd208782808e213a')

prepare() {
    cd $pkgname-$pkgver
    sed -i -e '/semigroups/d' $pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev -f-library-only -fthreaded
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
