# $Id$

_pkgbasename=libvorbis
pkgname=lib32-$_pkgbasename
pkgver=1.3.5
pkgrel=1
pkgdesc="Vorbis codec library (32-bit)"
arch=('x86_64')
license=('custom')
url="http://www.xiph.org/ogg/vorbis/"
depends=('lib32-libogg' $_pkgbasename)
provides=('libvorbisfile.so' 'libvorbis.so' 'libvorbisenc.so')
makedepends=(gcc-multilib)
source=(https://downloads.xiph.org/releases/vorbis/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('7220e089f3be3412a2317d6fde9e3944')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
