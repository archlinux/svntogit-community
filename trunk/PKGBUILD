# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: 3ED <krzysztof1987@gmail.com>

pkgname=caja-actions
pkgver=1.8.1
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="An extension for Caja which allows arbitrary programs to be launched via context menu."
url="https://github.com/raveit65/caja-actions"
license=('GPL')
depends=('caja-gtk3' 'libgtop')
makedepends=('mate-common' 'python' 'yelp-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/raveit65/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('c9d09ef73979adbabaa7b4e618164b935cb6c7219e83803d7a61aa3fa498f5ad')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=3

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
