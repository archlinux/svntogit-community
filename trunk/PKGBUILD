# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bhushan Shah <bshah@kde.org>

pkgname=plasma-maliit-plugins
pkgver=0.99.0.20150710
_commit=0b5cdb59bd1b6bc1f88d815e8b97750fdd014993
pkgrel=2
pkgdesc="Plasma Integration for Maliit virtual keyboard"
arch=('x86_64')
license=('GPL')
url="http://projects.kde.org/plasma-maliit-plugins"
depends=('plasma-maliit-framework')
makedepends=('git')
source=("git+https://github.com/KDE/plasma-maliit-plugins.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  qmake -makefile \
    CONFIG+=nohwkeyboard \
    CONFIG+=notests \
    CONFIG+=nodoc \
    ../plasma-maliit-plugins
  make
}

package() {
  cd build
  make install INSTALL_ROOT="$pkgdir/"
}
