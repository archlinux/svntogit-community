# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sus
pkgver=0.16.0
pkgrel=1
pkgdesc='A fast and scalable test runner'
arch=(any)
url='https://github.com/ioquatix/sus'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-bake-test ruby-bake-test-external ruby-covered)
options=(!emptydirs)
source=(https://github.com/ioquatix/sus/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('b3b0b6f980a305caba0a4e27aa225cec85c8e7c181eed300f32abd0a55c6d1a0')

prepare() {
  cd sus-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i sus.gemspec
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sus-$pkgver
  gem build sus.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sus-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sus-$pkgver
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd sus-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
