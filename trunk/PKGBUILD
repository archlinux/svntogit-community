# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

pkgbase=python-igraph
pkgname=(python-igraph python2-igraph)
pkgver=0.7.1.post6
pkgrel=3
url="http://igraph.org/python/"
license=(GPL)
arch=(x86_64)
makedepends=(python python2 igraph) 
source=("https://pypi.python.org/packages/source/p/$pkgbase/$pkgbase-$pkgver.tar.gz")
md5sums=('a4c0b5960735b36adb5d3a40031cb7c0')

prepare() {
  cp -r $pkgbase-$pkgver{,-python2}

  sed -e 's|/usr/bin/env python|/usr/bin/env/python2|g' -i $pkgbase-$pkgver-python2/igraph/vendor/texttable.py
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd ../$pkgbase-$pkgver-python2
  python2 setup.py build
}

package_python-igraph() {
  pkgdesc="Python bindings for the igraph library"
  depends=(igraph python)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-igraph() {
  pkgdesc="Python 2 bindings for the igraph library"
  depends=(igraph python2)

  cd $pkgbase-$pkgver-python2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/igraph{,2}
}
