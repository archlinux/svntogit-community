# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=menu-cache
pkgver=1.0.2+17+g8c85341
pkgrel=1
pkgdesc='Caching mechanism for freedesktop.org compliant menus'
arch=('x86_64')
license=('GPL2')
url='http://lxde.org/'
depends=('libfm-extra')
makedepends=('gtk-doc' 'git')
_commit=8c8534159d4898935b47f9103cf32cd9b21b3df5  # master
source=("git+https://github.com/lxde/menu-cache#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
