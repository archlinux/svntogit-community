# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.28
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(x86_64)
url="https://samplv1.sourceforge.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base qt6-svg)
makedepends=(alsa-lib cmake jack liblo libsndfile lv2 qt6-tools)
optdepends=(
  'alsa-lib: for standalone application'
  'jack: for standalone application'
  'liblo: for standalone application'
  'new-session-manager: for session management using the standalone application'
  'qt6-wayland: for native wayland support'
)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('78468f77bda7bd42f7b9fa38bc8dda385f9ef1d2eb520748f57a09e4eb49ea7f5acce88e74c35614a7f7e9c5d650bdc2c1e325cdc744aee32b128564b2fbb357')
b2sums=('b4d1728e62dafa459ba84c92659a631d0fc9a744e0fbb5c172c53f8157650226de14b7d923300495f52a011976bbdb3a549dcbabab65068d652f8517bc3f42c4')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libsndfile.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644  $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
