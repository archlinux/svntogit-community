# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=batctl
pkgver=2022.3
_advver=$pkgver
pkgrel=1
pkgdesc='B.A.T.M.A.N. advanced control and management tool'
arch=('x86_64')
url='http://www.open-mesh.net/'
license=('GPL')
depends=('libnl')
source=("http://downloads.open-mesh.net/batman/releases/batman-adv-$_advver/batctl-$pkgver.tar.gz"{,.asc})
sha512sums=('8768e8166845b2ecebd419de36015c27badb68036d76b06233a935d9fe8ab781cbb6fe6127c2b5fff5cf96b99b56b0e21cca0dd525feb13bbaa5094248385f52'
            'SKIP')
validpgpkeys=('2DE9541A85CC87D5D9836D5E0C8A47A2ABD72DF9')  # Simon Wunderlich

build(){
  cd batctl-$pkgver
  make
}

package() {
  cd batctl-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr SBINDIR=/usr/bin install
}
