# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=3.53.1
pkgrel=1
url="https://github.com/WoLpH/python-progressbar"
license=('BSD')
arch=('any')
depends=('python-six' 'python-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-pytest' 'python-freezegun')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('4817bbb45d0f51ee2b3feceec56b3177ed5d0561894a13eea53a30e8b9aad5805483b5b669af93f3fd0cc958984b8a5c2a14ba3591e0a85be12180d057bc2f81'
            'SKIP')
b2sums=('80f406adf42c5c57ecad87002426115e1254339d5cd310dd42f560f48a1123702dffbba8f26114a01f2bd5be75a29cb12cc1c091d5554509b6282ceb2b66fb76'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # don't care about custom pytest config
  rm -v pytest.ini
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
