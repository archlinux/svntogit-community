# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rfc
pkgver=20140115
pkgrel=1
pkgdesc="RFCs from rfc-editor.org"
arch=('any')
url="http://www.rfc-editor.org/"
license=("custom:as-is")
options=('docs')
source=(rfc-index-$pkgver.txt::http://www.rfc-editor.org/rfc/rfc-index.txt
        license
        http://www.rfc-editor.org/rfc/tar/RFCs0001-0500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs0501-1000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs1001-1500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs1501-2000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs2001-2500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs2501-3000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs3001-3500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs3501-4000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs4001-4500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs4501-5000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs5001-5500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs5501-6000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs6001-6500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs6501-7000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs7001-latest.tar.gz)
md5sums=('8039f3c40da14c21136c797de71a7290'
         '06a779406b45edddbd19fda96696ab4a'
         'b1cb269a6d9c4de094af087a3a6ab980'
         'fa629f8edddd31f9a00b67d98319b25e'
         '16efff292a2b12586813e5da121330a3'
         '9789d81a3050fe237698541fbb4e32bb'
         '57abdf5842a944b940ef482506c902ac'
         'd2eb71d6f61d3caf12acbfb28c3ce1fa'
         'cecb750d06e770dfd58b7bed5688c40c'
         '351a105d8faf93c1f9b8f8c7d8840673'
         '78af4af7e0d2fed87f3d6563773c3dfd'
         '88208c082a019f8b8234f690f3bd5f50'
         '2b36ce72c41f1d9a162c0f0b6ee1820d'
         '74a4abb2f54849866d2a38613c7daadd'
         '48137dc9d9090589e15a9f997841742f'
         '8f5cc2d6fe24338d922ceda1a13cf902'
         'f1c2cd5d822e80af12d3e8937612f294')

package() {
    cd "$srcdir"

    mkdir -p "$pkgdir/usr/share/doc/rfc"
    cp rfc-index-$pkgver.txt "$pkgdir/usr/share/doc/rfc/rfc-index.txt"
    rm -f rfc-index-$pkgver.txt

    tar xf rfc1305.tar
    rm -f rfc1305.tar
    for n in ntpv3*; do
      mv "$n" "rfc1305-$n"
    done

    for fmt in txt ps pdf; do
      mkdir -p "$pkgdir/usr/share/doc/rfc/${fmt}"
      mv *.${fmt} "$pkgdir/usr/share/doc/rfc/${fmt}/"
    done

    cd "$pkgdir/usr/share/doc/rfc"
    find . -type f -exec chmod 444 {} \;
    find . -type d -exec chmod 555 {} \;
    chown -R root:root .
    install -D -m0644 $srcdir/license $pkgdir/usr/share/licenses/$pkgname/license
}
