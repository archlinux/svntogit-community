# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Noa-Emil Nissinen <aur.satella@spamgourmet.org>


pkgname=stb
pkgver=r2149.5736b15
pkgrel=1
pkgdesc='Single-file public domain (or MIT licensed) libraries for C/C++'
arch=(any)
url=https://github.com/nothings/stb
license=(MIT)
makedepends=(git)
_commit=5736b15f7ea0ffb08dd38af21067c314d6a3aae9
source=(git+https://github.com/nothings/stb.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd stb
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  install -Dm 644 stb/*.{c,h} -t "${pkgdir}"/usr/include/stb/
  install -Dm 644 stb/LICENSE -t "${pkgdir}"/usr/share/licenses/stb/
}

# vim: ts=2 sw=2 et:
