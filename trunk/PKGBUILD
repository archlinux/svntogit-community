# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yaml
pkgname=haskell-yaml
pkgver=0.8.31.1
pkgrel=1
pkgdesc="Support for parsing and rendering YAML documents."
url="https://github.com/snoyberg/yaml/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-conduit' 'haskell-vector'
         'haskell-resourcet' 'haskell-scientific' 'haskell-unordered-containers')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit' 'haskell-mockery' 'haskell-base-compat'
             'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ae82d9eaac0a4c4fe124e9eeeddd411cc722cabc3ae195a71027db187c617564352d4594732c2e2ef56b6a825ea4abc09f8cbe3cfe9fa388b61f4c1e852802aa')

prepare() {
    # Needed only for examples, which won't be built
    sed -e '/raw-strings-qq/d' -e '/semigroups/d' -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-no-unicode -f-system-libyaml -f-no-exe -fno-examples
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
