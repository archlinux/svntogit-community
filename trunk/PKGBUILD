# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor : sebikul <sebikul@gmail.com>
# Contributor : Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=0.5.3
pkgrel=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.yorba.org/projects/geary/'
license=('GPL3')
depends=('gmime' 'gnome-keyring' 'libcanberra' 'libgee' 'libnotify' 'webkitgtk')
makedepends=('cmake' 'gobject-introspection' 'intltool' 'vala')
install="${pkgname}.install"
source=("https://download.gnome.org/sources/${pkgname}/${pkgver%.?}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('17ac5aa25d828d92e5a23d21194fcee71914961cbfa3064dd718bcecb9cbf2e5')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -D{DESKTOP_UPDATE,GSETTINGS_COMPILE{,_IN_PLACE},ICON_UPDATE}='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
