# Maintainer: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Batou <batou at cryptolab net>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgname=copyq
pkgver=6.4.0
pkgrel=1
pkgdesc="Clipboard manager with searchable and editable history"
url="https://github.com/hluk/${pkgname}"
depends=('hicolor-icon-theme' 'qt5-svg' 'qt5-wayland' 'knotifications')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
license=('GPL3')
arch=('x86_64')
source=("git+${url}.git?signed#tag=v${pkgver}")
sha256sums=('SKIP')
validpgpkeys=(5488AC3CCC463028E9CD93FF61D8496346F161CB) # Lukas Holecek

build() {
    cmake -B build -S ${pkgname} \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -Wno-dev
    cmake --build build
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
}
