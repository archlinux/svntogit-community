# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qvkbd
pkgver=git20170102
pkgrel=3
pkgdesc="Virtual keyboard written in Qt"
arch=(x86_64)
url="https://github.com/KivApple/qvkbd"
license=(GPL3)
depends=(qt5-declarative qt5-x11extras libxtst hicolor-icon-theme)
source=(https://github.com/KivApple/qvkbd/archive/5debd6e8d082ca8187a86248b70c4f2360f5c27b.zip)
sha256sums=('6f5136e3e68c92e87cf241e0e465ba113057e2774a29491f2faa548f3b1e1f3a')

prepare() {
  mkdir -p build
  mv qvkbd-* qvkbd
}

build() {
  cd build
  qmake ../qvkbd
  make
}

package() {
  install -Dm755 build/qvkbd -t "$pkgdir"/usr/bin/
  install -Dm755 qvkbd/qvkbd.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 qvkbd/icons/keyboard.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/qvkbd.svg
}
