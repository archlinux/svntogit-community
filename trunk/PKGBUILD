# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
_tag='686797b7d0da4fc24fdfcb0fc6364df78a0b232d' # git rev-parse ${pkgver}
pkgver=0.9.0
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('x86_64')
url='http://christophe.varoqui.free.fr/'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs')
makedepends=('git' 'systemd')
source=("multipath-tools::git+https://github.com/opensvc/multipath-tools#tag=${_tag}")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"

  # Fix systemd version detection in Makefile
  sed -i 's/sed -n .*$/head -n1 | cut -d" " -f2\)/g' Makefile.inc

  # Fix bindir in Makefile
  sed -i 's|$(exec_prefix)/sbin|$(exec_prefix)/bin|g' Makefile.inc
}

build() {
  cd "${pkgname}"

  make \
    FORTIFY_OPT=""
}

package() {
  cd "${pkgname}"

  make -j1 \
    DESTDIR="${pkgdir}" \
    exec_prefix="/usr" \
    LIB="/usr/lib" \
    install

  install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README*
}
