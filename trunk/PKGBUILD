# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=2.3
pkgrel=2
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/about/"
license=(GPL2)
groups=(realtime)
depends=(glibc)
makedepends=(numactl python)
optdepends=('bash: for determine_maximum_mpps.sh'
            'numactl: for cyclictest, oslat and signaltest'
            'python: for get_cyclictest_snapshot and hwlatdetect')
source=("https://git.kernel.org/pub/scm/utils/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('920e04c716a104b08c49ab5886723d68064c7a28a65078201198c3edb695d07e491ed05f62615400efa13b0ece3a74b82037aa23fec9e3b3e9577cc9686e0ed4')
b2sums=('59bb723c1c724a0fbd6938069729e1ac264e72d30ff860079f4d90cf52dd781c211712a61872dd7f3ce28abfb0d5badab08b37ecb69194c7b1d2305d981bbfc9')

build() {
  cd "${pkgname}-${pkgver}"
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -vDm 644 {MAINTAINERS,README.markdown} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
