# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <greg.sutcliffe@gmail.com>>

pkgname=ruby-shadow
pkgver=2.4.1
pkgrel=1
pkgdesc="Shadow password module"
arch=('i686' 'x86_64')
url="https://github.com/apalmblad/ruby-shadow"
license=('CPL')
depends=('ruby')
source=($pkgname-$pkgver.tar.gz::https://github.com/apalmblad/$pkgname/archive/$pkgver.tar.gz)
md5sums=('849154b36db323b404c31a8667411815')

build() {
  cd $pkgname-$pkgver

  ruby extconf.rb
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir"/ sitedir='$(rubylibprefix)/vendor_ruby' \
    sitehdrdir='$(rubyhdrdir)/vendor_ruby' install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
