# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-dbd-odbc
pkgver=1.33
pkgrel=1
pkgdesc="ODBC Driver for DBI"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBD-ODBC/"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'perl' 'unixodbc')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MJ/MJEVANS/DBD-ODBC-${pkgver}.tar.gz")
md5sums=('3e44ea4eb4b0bb711eea93632b76de17')

build() {
  cd  DBD-ODBC-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  DBD-ODBC-${pkgver}
  make install DESTDIR=${pkgdir}
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
