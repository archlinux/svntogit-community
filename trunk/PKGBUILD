# $Id: PKGBUILD 118184 2014-09-02 11:14:17Z fyan $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgbase=python-gammu
pkgname=('python-gammu' 'python2-gammu')
pkgver=2.7
pkgrel=2
pkgdesc="Python bindings for Gammu library"
arch=('i686' 'x86_64')
url="http://wammu.eu/python-gammu/"
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools' 'gammu')
source=(http://dl.cihar.com/python-gammu/python-gammu-$pkgver.tar.bz2)
sha256sums=('007db06a0c5c8d02498a7f9f43052c4a936adddd842640c96ad6f2d4f8cf2140')

prepare() {
  cp -a python-gammu-$pkgver{,-py2}
}

build() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py build

  cd "${srcdir}"/python-gammu-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py test || warning "Tests failed"

  cd "${srcdir}"/python-gammu-$pkgver-py2
  python2 setup.py test || warning "Tests failed"
}

package_python-gammu() {
  depends=('python' 'gammu')

  cd python-gammu-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-gammu() {
  depends=('python2' 'gammu')

  cd python-gammu-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
