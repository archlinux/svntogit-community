# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rauc
pkgver=1.8
pkgrel=3
pkgdesc="Safe and secure software updates for embedded Linux"
arch=(x86_64)
url="https://rauc.io"
license=(LGPL2.1)
depends=(gcc-libs glibc libnl openssl)
makedepends=(curl dbus glib2 json-glib python systemd util-linux)
checkdepends=(squashfs-tools)
optdepends=(
  'casync: for casync conversion support'
  'cryptsetup: for disk encryption support'
  'dosfstools: for creating and checking FAT filesystems'
  'e2fsprogs: for creating and checking ext2/3/4 filesystems'
  'efibootmgr: for interacting with EFI boot manager'
  'grub: for GRUB support'
  'mtd-utils: for interacting with MTD devices'
  'squashfs-tools: for creating and inspecting bundles'
  'uboot-tools: for u-boot support'
)
options=(debug)
source=(https://github.com/rauc/rauc/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
sha512sums=('126601a567d185ac22b7f58cbfff2a8c42d0d80badc939ef48ea988f1897f2957ec83b3611d80d8f34d39050cc5e205e15041b09d474bfa9a075ff04ee461374'
            'SKIP')
b2sums=('d88cc35b773f79515227eb8c7f2b8aa15072add6ad38b249d0bc3ee2430e9a78d2ff235c9b257542d365bad59f2e9e8f7463a6c98a8ae5344519dd2d90d742b6'
        'SKIP')
validpgpkeys=("977843FD5C3EBF76BAD4008EA58CC53DBF05E090")  # Jan LÃ¼bbe <jlu@pengutronix.de>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-gpt

  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    curl libcurl.so
    glib2 lib{gobject,gio,glib}-2.0.so
    json-glib libjson-glib-1.0.so
    util-linux-libs libfdisk.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vdm 755 "$pkgdir/etc/$pkgname/"
}
