# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=aeson-diff
pkgname=haskell-aeson-diff
pkgver=1.1.0.10
pkgrel=37
pkgdesc="Extract and apply patches to JSON documents"
url="https://github.com/thsutton/aeson-diff"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-edit-distance-vector' 'haskell-hashable'
         'haskell-scientific' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-optparse-applicative')
makedepends=('ghc' 'haskell-doctest' 'haskell-glob' 'haskell-quickcheck'
             'haskell-quickcheck-instances' 'hlint')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('fc4f883c3dba0b2191bcc3c1816af4f5277e42a31bb06eafba0cf5052eb350e5ff2b9fc88a7bc9d9233d7c60768466cac7f95fe53c074c2df5aa91e4806607d5')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
