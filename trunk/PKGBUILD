# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=2.6.3.14058
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain libcloudproviders hicolor-icon-theme)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=(https://download.owncloud.com/desktop/stable/owncloudclient-$pkgver.tar.xz{,.asc}
        owncloud-client-qt-5.15.patch)
sha256sums=('e3d38de31e041f0e1836b2b74acfc1fc76eea4955968952a65643cbf36a493f5'
            'SKIP'
            '33f2c09bd174f2fe4db74766db3a8028e96502f912b7d41be05ce211bfe1eaca')
validpgpkeys=(F05F7DD7953A07DF36579DAA498C45EBE94E7B37) # ownCloud Client Team (Signing Key) <info@owncloud.com>

prepare() {
  patch -d $_name-$pkgver -p1 -i ../owncloud-client-qt-5.15.patch # Fix build with Qt 5.15
}

build() {
  cmake -B build -S $_name-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
