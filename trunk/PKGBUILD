# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=hotdoc
pkgname=hotdoc
pkgver=0.14.0
pkgrel=2
pkgdesc="the tastiest API documentation system"
arch=('x86_64')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('LGPL')
_py_deps=('appdirs' 'cchardet' 'dbus-deviation' 'lxml' 'networkx' 'pkgconfig'
          'schema' 'toposort' 'wheezy-template' 'yaml' 'feedgen'
          'charset-normalizer')
depends=("${_py_deps[@]/#/python-}" 'json-glib')
makedepends=('python-setuptools' 'python-build' 'python-installer'
             'python-wheel' 'cmake')
optdepends=('clang: for the C extension'
            'llvm: for the C extension')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('35538b39225713932dae8f35708d0fae2e1c3c4b65ad4021e066577def44bd5c')
b2sums=('204163f8e6b6e5e379620f63ff0a592c1b6ecee48fd5612d6be7a6850d9ca8a954c2c5632bdc73ac55e3c6af47e24a2f6b2f2031e4378cd97ce6975ba3eaf559')

build() {
    cd ${pkgname}-${pkgver}

    python -m build --wheel --no-isolation
}

check() {
    cd ${pkgname}-${pkgver}/build/lib.linux-$CARCH-cpython-*

    python -m unittest
}

package() {
    cd ${pkgname}-${pkgver}

    python -m installer --destdir="${pkgdir}" dist/*.whl
}
