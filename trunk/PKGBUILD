# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent
pkgname=haskell-persistent
pkgver=2.7.3.1
pkgrel=42
pkgdesc="Type-safe, multi-backend data serialization"
url="http://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-conduit' 'haskell-exceptions'
         'haskell-fast-logger' 'haskell-http-api-data' 'haskell-lifted-base'
         'haskell-monad-control' 'haskell-monad-logger' 'haskell-mtl' 'haskell-old-locale'
         'haskell-path-pieces' 'haskell-resource-pool' 'haskell-resourcet' 'haskell-scientific'
         'haskell-silently' 'haskell-src-meta' 'haskell-tagged' 'haskell-text'
         'haskell-transformers-base' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8eaa8c66e9d30e4ebe2efb74ef320d1eaf1122b635775cbefb41c1927a68486b0e33b614f0d012f5958d780da4b25d34836aa7c774b89e75ec7dcbcda75559fe')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-nooverlap
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
