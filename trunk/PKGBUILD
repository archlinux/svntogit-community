# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Benjamin A. Shelton <zancarius@gmail.com>

pkgbase=python-raven
pkgname=(python-raven python2-raven)
pkgver=5.14.0
pkgrel=1
pkgdesc="Python client for Sentry"
arch=('any')
url="http://pypi.python.org/pypi/raven"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python2-contextlib2' 'git')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-bottle' 'python2-bottle'
              'python-celery' 'python2-celery' 'python-django' 'python2-django' 'pep8'
              'python-mock' 'python2-mock' 'python-nose' 'python2-nose' 'python2-gevent'
              'python2-pep8' 'python-pytz' 'python2-pytz' 'python-exam' 'python2-exam'
              'python-requests' 'python2-requests' 'python-tornado' 'python2-tornado'
              'python-paste' 'python2-paste' 'python-webob' 'python2-webob'
              'python-webtest' 'python2-webtest' 'python-anyjson' 'python2-anyjson'
              'python-flask' 'python2-flask' 'python-blinker' 'python2-blinker'
              'python-logbook' 'python2-logbook' 'python-pytest-django' 'python2-pytest-django'
              'python2-webpy' 'python-flask-login' 'python2-flask-login'
              'python-pytest-timeout' 'python2-pytest-timeout')
source=("git+https://github.com/getsentry/raven-python.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a raven-python{,-py2}
}

build() {
  cd "$srcdir"/raven-python
  python setup.py build

  cd "$srcdir"/raven-python-py2
  python2 setup.py build
}

check() {
  # Hack distribution check by installing it

  cd "$srcdir"/raven-python
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH" \
    py.test tests

  cd "$srcdir"/raven-python
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" \
    py.test2 tests
}

package_python-raven() {
  depends=('python')
  optdepends=('python-setuptools: for "raven" script')

  cd "$srcdir"/raven-python
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-raven() {
  depends=('python2-contextlib2')
  optdepends=('python2-setuptools: for "raven2" script')

  cd "$srcdir"/raven-python-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/raven{,2}

  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
