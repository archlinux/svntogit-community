# Maintainer: Christian Rebischke <chris.rebischke at archlinux.org>
# Contributor:  kpcyrd <git@rxv.cc>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: flaccid aka Chris Fordham <chris@fordham.id.au>
# Contributor: Sparadox <etienne.lafarge at gmail.com>

pkgname=cloud-init
pkgver=19.2
pkgrel=4
pkgdesc="The standard for customising cloud instances"
arch=('any')
url="https://cloud-init.io"
license=('GPL3')
depends=('systemd' 'sudo' 'python-yaml' 'python-prettytable'
  'python-oauthlib' 'python-configobj'
  'python-jsonschema' 'python-jsonpatch' 'net-tools'
  'python-requests' 'python-oauthlib' 'python-jinja' 'python-six'
  'netplan')
makedepends=('python' 'python-setuptools')
backup=('etc/cloud/cloud.cfg' 'etc/cloud/cloud.cfg.d/05_logging.cfg')
source=("https://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
  'fix-lib.patch'
  'archlinux.cloud.cfg')
sha512sums=('c6adab5838ddf53fad96c3d680becd23ce7227dc4e6c853863882bf9f5d0b6ea6259d529ec3848247254459b793bbef5a6791b26c65741a68390c970173b3293'
            '6c435fbe1e0a7093b79f8bb3789b05a39afd1e43b7ff96f13e68c9ad16311cc58270b947bae574cfdc3d2e27c7258789880a101c9bdf2c3c141bcbedc47a8a4b'
            '915cbe0da65e754d23661f9b6d5680ed6395fab3df3a9946e51920edcf4191a4fa5475260f5a744a1962801b69f04a583bf9db8ca11c4b385aea8d92d7f09f41')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-lib.patch"
  sed -e 's:/etc/systemd:/usr/lib/systemd:g' -e 's:\"/lib\":\"/usr/lib\":g' -i setup.py
}

# This checks need: python-mock, python-httpretty
# Due to a bug unittest2 is used even when python3 is used for building
# So let's wait until cloud-init implemented a try/except for python3 builds
# check() {
#   cd "${pkgname}"-"${pkgver}"
#   nosetests tests/unittests/
# }

package() {
  cd "${pkgname}-${pkgver}"
  python ./setup.py install --root="${pkgdir}" -O1 --init-system systemd
  install -Dm644 "${srcdir}/archlinux.cloud.cfg" "${pkgdir}/etc/cloud/cloud.cfg"
}
