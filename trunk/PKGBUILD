# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: A.T.W.A. <arch.atwa@gmail.com>

pkgname=gxkb
pkgver=0.9.4
pkgrel=1
pkgdesc="X11 keyboard indicator and switcher"
arch=('x86_64')
url="https://zen-tools.github.io/gxkb"
license=('GPL2')
depends=('libxklavier' 'libwnck3' 'gtk3')
source=("https://github.com/zen-tools/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('93016bca96926db2244d7462f8b6310de5bbbae20e8498a2b5fa923afbe0466f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./autogen.sh
  ./configure
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
