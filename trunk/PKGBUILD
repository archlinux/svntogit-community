# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgbase=python-psutil
pkgname=('python-psutil' 'python2-psutil')
pkgver=5.4.7
pkgrel=1
arch=('x86_64')
url='https://github.com/giampaolo/psutil'
license=('custom: BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
checkdepends=('net-tools' 'procps-ng' 'python2-nose' 'python-nose')
source=("https://github.com/giampaolo/psutil/archive/release-$pkgver.tar.gz")
md5sums=('e8d6259eafe5925e7c522868aedd2caa')

build() {
  cd psutil-release-$pkgver

  msg python
  python setup.py build --build-lib=build/python

  msg python2
  python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

check() {
  cd psutil-release-$pkgver

  msg python
  export PYTHONPATH="$PWD/build/python"
  cd psutil/tests/
  # FIXME: dynamic loading so


  msg python2
  export PYTHONPATH="$PWD/build/python2"
  #python2 psutil/tests/runner.py
}

package_python-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('python')

  cd psutil-release-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

package_python2-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python2'
  depends=('python2')

  cd psutil-release-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
