# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>

pkgname=nbd
pkgver=3.17
pkgrel=3
pkgdesc='tools for network block devices, allowing you to use remote block devices over TCP/IP'
arch=('x86_64')
url='http://nbd.sourceforge.net'
license=('GPL')
depends=('glib2' 'gnutls')
backup=('etc/nbd-server/config')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz"
	'config'
	'nbd.service'
	'sysusers_nbd.conf')
sha256sums=('d95c6bb1a3ab33b953af99b73fb4833e123bd25433513b32d57dbeb1a0a0d189'
            'ee2e9fbbeb8a8b9b71d16b6f32eb41788f6def9d00cc4a47897ed3cb97cdde7c'
            '1b1add55d82d7623eabf7ff3dfec15c1fbe3c340fa74160df3d60e91eb1c71ba'
            '3fd98fe87d0b31b82126dc669f00355865f58fae81af3fccbc0d06504baef6cd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-syslog

  # has unresolved issues, disable for now
  sed -i '/HAVE_NETLINK/c #define HAVE_NETLINK 0' config.h

  make
}

# checks still fail...
#check() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#
#  make check
#}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 systemd/nbd@.service "${pkgdir}"/usr/lib/systemd/system/nbd@.service

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m644 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service
  install -D -m644 "${srcdir}"/sysusers_nbd.conf "${pkgdir}"/usr/lib/sysusers.d/nbd.conf

  install -D -m644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}
