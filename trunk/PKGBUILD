# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.9.0
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="https://www.indilib.org/index.php?title=Main_Page"
license=(GPL2)
arch=(x86_64)
depends=(libnova cfitsio libusb libjpeg gsl libtheora fftw)
makedepends=(cmake boost qt5-base)
#source=("https://github.com/indilib/indi/releases/download/v$pkgver/libindi_$pkgver.tar.gz")
source=($pkgname-$pkgver.tar.gz::"https://github.com/indilib/indi/archive/v$pkgver.tar.gz")
sha256sums=('41943e99f5b3567997c0cb0f707ba45e24f0f1f7cd4c9944055acf34ce52ee6e')

prepare() {
  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i indi-$pkgver/drivers/*/*.rules
}

build() {
  cmake -B build -S indi-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINDI_BUILD_QT5_CLIENT=ON \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
