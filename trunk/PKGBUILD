# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Manuel Mazzuola <origin.of@gmail.com>

pkgname=ferm
pkgver=2.4
pkgrel=1
pkgdesc='Tool to maintain complex firewalls'
arch=('any')
url='http://ferm.foo-projects.org/'
license=('GPL2')
depends=('iptables' 'perl')
optdepends=('ebtables' 'arptables' 'ipset')
backup=("etc/$pkgname.conf")
source=(
  "http://ferm.foo-projects.org/download/${pkgver:0:3}/$pkgname-$pkgver.tar.gz"
  "$pkgname.service")
md5sums=('82a04e5bd064126aa759d9e64e5418dc'
         '9c5ece2628fd86a73b70f9c39df63e9f')

package() {
  # software setup
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/usr" install
  cd "$pkgdir"
  # setup default config
  install -D -m 644 usr/share/doc/ferm/examples/workstation.ferm etc/$pkgname.conf
  # remove fancy docs
  rm usr/share/doc/ferm/ferm.{html,txt}
  # /usr merge
  mv usr/sbin usr/bin
  # systemd
  install -Dm 644 "$srcdir/$pkgname.service" usr/lib/systemd/system/$pkgname.service
}

# vim:set ts=2 sw=2 et:
