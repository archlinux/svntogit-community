# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Steef Hegeman <mail@steefhegeman.com>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Julian Schacher <jspp@posteo.net>

pkgbase=element.io
pkgname=(element-web element-desktop)
pkgver=1.7.15
pkgrel=1
pkgdesc="Glossy Matrix collaboration client â€” "
arch=(x86_64)
url="https://element.io"
license=(Apache)
makedepends=(npm git yarn python rust sqlcipher electron nodejs-lts-fermium)
_url="https://github.com/vector-im/element"
source=(element-web-${pkgver}.tar.gz::${_url}-web/archive/v${pkgver}.tar.gz
        element-web-${pkgver}.tar.gz.asc::${_url}-web/releases/download/v${pkgver}/v${pkgver}-src.tar.gz.asc
        element-desktop-${pkgver}.tar.gz::${_url}-desktop/archive/v${pkgver}.tar.gz
        element-desktop-${pkgver}.tar.gz.asc::${_url}-desktop/releases/download/v${pkgver}/v${pkgver}-src.tar.gz.asc
        io.element.Element.desktop
        element-desktop.sh)
sha256sums=('e5b8f2e67b563bddcdc7762e85d347be18bcb9919e6479788a9c84871d646fd6'
            'SKIP'
            '22db8a3403e2771fd9c1007ef3f076651eebec1252dea9cca87e95de582bbb9f'
            'SKIP'
            '0103f28a32fe31f698836516783c1c70a76a0117b5df7fd0af5c422c224220f9'
            '7b9beb0999e99beebb85e142fe6d26e9b386ce6b636675610d4caccdfb7fd293')
validpgpkeys=(A878CDF66CF4A9B4807CEBE574692659BDA3D940) # Riot Releases <releases@riot.im>

prepare() {
  cd element-web-${pkgver}
  # Switch target to output to directory rather than .deb package
  sed -i 's/"target": "deb"/"target": "dir"/g' package.json
  yarn install

  cd ../element-desktop-${pkgver}
  sed -i 's@"electronVersion": "10.1.6"@"electronVersion": "11.1.0"@g' package.json
  sed -i 's@"https://packages.riot.im/desktop/update/"@null@g' element.io/release/config.json
  yarn install
}

build() {
  cd element-web-${pkgver}
  yarn build

  cd ../element-desktop-${pkgver}
  yarn run build:native
  yarn run build
}

package_element-web() {
  pkgdesc+="web version."
  replaces=(riot-web vector-web)

  cd element-web-${pkgver}

  install -d "${pkgdir}"/{usr/share/webapps,etc/webapps}/element

  cp -r webapp/* "${pkgdir}"/usr/share/webapps/element/
  install -Dm644 config.sample.json -t "${pkgdir}"/etc/webapps/element/
  ln -s /etc/webapps/element/config.json "${pkgdir}"/usr/share/webapps/element/
  echo "${pkgver}" > "${pkgdir}"/usr/share/webapps/element/version
}

package_element-desktop() {
  pkgdesc+="desktop version."
  replaces=(riot-desktop)
  depends=("element-web=${pkgver}" electron sqlcipher)
  backup=('etc/element/config.json')

  cd element-desktop-${pkgver}

  install -d "${pkgdir}"{/usr/lib/element/,/etc/webapps/element}

  # Install the app content, replace the webapp with a symlink to the system package
  cp -r dist/linux-unpacked/resources/* "${pkgdir}"/usr/lib/element/
  ln -s /usr/share/webapps/element "${pkgdir}"/usr/lib/element/webapp

  # Config file
  ln -s /etc/element/config.json "${pkgdir}"/etc/webapps/element/config.json
  install -Dm644 element.io/release/config.json -t "${pkgdir}"/etc/element

  # Required extras
  install -Dm644 ../io.element.Element.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm755 ../${pkgname}.sh "${pkgdir}"/usr/bin/${pkgname}

  # Icons
  install -Dm644 ../element-web-${pkgver}/res/themes/element/img/logos/element-logo.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/io.element.Element.svg
  for i in 16 24 48 64 96 128 256 512; do
    install -Dm644 build/icons/${i}x${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/io.element.Element.png
  done
}
