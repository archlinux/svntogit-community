# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-mgba
pkgver=7842
pkgrel=1
pkgdesc='Nintendo Game Boy Advance core'
arch=(x86_64)
url=https://github.com/libretro/mgba
license=(custom:MPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=eb3ddbe4c4cf9ff0ad7c578e70ed1d78af455bd4
source=(libretro-mgba::git+https://github.com/libretro/mgba.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-mgba

  git rev-list --count HEAD
}

build() {
  make -C libretro-mgba -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mgba/mgba_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mgba/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mgba/
}

# vim: ts=2 sw=2 et:
