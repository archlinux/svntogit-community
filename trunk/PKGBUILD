# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-deferredimport
pkgver=4.4
pkgrel=2
pkgdesc="Allows you to perform imports names that will only be resolved when used in the code"
arch=('any')
url="https://github.com/zopefoundation/zope.deferredimport"
license=('ZPL')
depends=('python-setuptools' 'python-zope-proxy')
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.deferredimport/archive/$pkgver.tar.gz")
sha512sums=('db0719f7b01d72a87c3dc0c37335bfa228871eb9f8423c6ef0584f179462120cf186f13a39ec4b144bae405e32a51423e88682f7bec07166158dd5d6553ba540')

build() {
  cd "$srcdir"/zope.deferredimport-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.deferredimport-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.deferredimport-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
