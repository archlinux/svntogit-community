# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=cosign
pkgver=1.10.1
pkgrel=2
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache')
depends=('pcsclite')
makedepends=('go' 'git')
checkdepends=('go-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('6ae94b8d0572f7240457ad208e3ae45a7ca8bcde95b1b10c3bd0a5bae4801eb2d3c4f9b18162d4c157cbd0fe2269dd9a909323d8c2a216ce2cf52f98e95e6af9')
b2sums=('13af0a02958bc7578d68e28fb3bea2c30fe584ebaf0aef006590dfda04af44c4d454c2d8b6c4ed6dc11381a61759cc74b817bb2eedda0eae4d9eb3b822cea661')
options=('!lto')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_ENABLED=1
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external -ldflags=-buildid=''"
  mkdir build
  go build -o build/ ./cmd/cosign
  go build -o build/ ./cmd/sget
}

check() {
  cd "${pkgname}-${pkgver}"
  GODEBUG=x509sha1=1 make test # we have to allow sha1 certificates for tests
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/cosign "${pkgdir}/usr/bin/${pkgname}"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  "$pkgdir"/usr/bin/cosign completion bash > "${pkgdir}/usr/share/bash-completion/completions/cosign"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  "$pkgdir"/usr/bin/cosign completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_cosign"

  # build fish completions
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  "$pkgdir"/usr/bin/cosign completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/cosign.fish"

  install -Dm755 build/sget "${pkgdir}/usr/bin/sget"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  "$pkgdir"/usr/bin/sget completion bash > "${pkgdir}/usr/share/bash-completion/completions/sget"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  "$pkgdir"/usr/bin/sget completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_sget"

  # build fish completions
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  "$pkgdir"/usr/bin/sget completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/sget.fish"
}

