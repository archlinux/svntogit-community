# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=stunnel
pkgver=5.44
pkgrel=3
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL"
arch=('x86_64')
url="https://www.stunnel.org/"
license=('GPL')
depends=('openssl' 'libnsl')
makedepends=('systemd')
source=("https://www.stunnel.org/downloads/archive/5.x/$pkgname-$pkgver.tar.gz"{,.asc}
        'Makefile.patch'
        'stunnel.sysusers.conf'
        'stunnel.tmpfiles.conf')
sha256sums=('990a325dbb47d77d88772dd02fbbd27d91b1fea3ece76c9ff4461eca93f12299'
            'SKIP'
            '1d60f6504d9d29ef1832c242b62f09dcd8c31fbdf5aa1297779c8e5c54aa33de'
            'c73ade1db94a280778be6c0876862108c31faa1a92d7485d699a725894443c78'
            'e27c3f9fe440550ddc03446c0315710b80ebddb51085f169bbc765d7ab56ea63')
validpgpkeys=('AC915EA30645D9D3D4DAE4FEB1048932DD3AAAA3') # Micha≈Ç Trojnara

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # don't create a certificate...
  patch -p0 <"$srcdir/Makefile.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-ipv6 \
    --disable-libwrap

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 tools/stunnel.service "$pkgdir/usr/lib/systemd/system/stunnel.service"
  install -Dm644 "$srcdir/stunnel.sysusers.conf" "$pkgdir/usr/lib/sysusers.d/stunnel.conf"
  install -Dm644 "$srcdir/stunnel.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/stunnel.conf"

  sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
      -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
      -e "s:no\(body\|group\):stunnel:g" \
      -i "$pkgdir/etc/stunnel/stunnel.conf-sample"
}
