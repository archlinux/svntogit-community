# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=gavl
pkgver=1.4.0
pkgrel=7
pkgdesc='Low level library, upon which multimedia APIs can be built'
arch=('x86_64')
url='http://gmerlin.sourceforge.net/'
license=('GPL')
depends=('glibc')
makedepends=('doxygen')
source=("https://downloads.sourceforge.net/gmerlin/$pkgname-$pkgver.tar.gz")
sha256sums=('51aaac41391a915bd9bad07710957424b046410a276e7deaff24a870929d33ce')

prepare() {
  cd $pkgname-$pkgver
  # Fix build
  sed -i 's|volume_test_LDADD = ../gavl/libgavl.la|volume_test_LDADD = -lm ../gavl/libgavl.la|' src/Makefile.{am,in}
  sed -i 's/LDFLAGS="$GMERLIN_DEP_RPATH"/LDFLAGS="$LDFLAGS $GMERLIN_DEP_RPATH"/' configure{,.ac}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-cpuflags=none
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
