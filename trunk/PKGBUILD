# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=17
_realver=build17
pkgrel=4
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('i686' 'x86_64')
url="http://widelands.org/"
license=('GPL')
depends=('sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'sdl_gfx' 'lua' 'glew' 'python2' 'widelands-data')
makedepends=('cmake' 'boost')
source=("https://launchpad.net/widelands/build17/build-17/+download/$pkgname-$_realver-src.tar.bz2"
        $pkgname.desktop
        $pkgname.png
        $pkgname.sh)
md5sums=('bdc9bc0aa631af1aa7fb450507611a58'
         '15820bf099fd6f16251fe70a75c534bb'
         '3dfda7e9ca76ca00dd98d745d0ceb328'
         '7cae50aba5ed0cd2cfeea79124637b46')

build() {
    cd $srcdir/$pkgname-$_realver-src

    sed -i "1 i #include <unistd.h>" src/main.cc

    mkdir -p build/compile && cd build/compile

    cmake ../.. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_DATADIR=share/$pkgname \
        -DWL_INSTALL_BINDIR=bin
    make
}

package() {
    cd $srcdir/$pkgname-$_realver-src/build/compile

    make DESTDIR="$pkgdir" install

    rm -r $pkgdir/usr/share/widelands/{campaigns,fonts,global,locale,maps,music,pics,scripting,sound,tribes,txts,worlds}

    # Install bin, icon and desktop file
    #install -Dm 755 src/$pkgname $pkgdir/usr/share/$pkgname/$pkgname
    #install -Dm 755 $srcdir/$pkgname.sh $pkgdir/usr/bin/$pkgname
    install -Dm644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
    install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
