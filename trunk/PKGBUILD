# $Id: PKGBUILD 129645 2015-03-20 21:48:17Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=libnsutils
pkgver=0.0.1
_netsurf=3.3
pkgrel=1
pkgdesc='NetSurf utilities'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('MIT')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/netsurf/releases/source-full/netsurf-all-$_netsurf.tar.gz")
sha256sums=('5d7e31e8be6230792bf682640ec16a75d7d67c3845521a7e4c304ae89a88ff1c')

build() {
  make -C "netsurf-all-$_netsurf/$pkgname" \
    PREFIX=/usr LIBDIR=lib COMPONENT_TYPE="lib-shared" 
}

package() {
  cd "netsurf-all-$_netsurf/$pkgname"

  make PREFIX=/usr LIBDIR=lib COMPONENT_TYPE="lib-shared" \
    DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
