# $Id$
# Maintainer:  Chris Brannon <chris@the-brannons.com>
# Maintainer:  Bartłomiej Piotrowski 
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit 
pkgver=2.0.0b11
pkgrel=3
pkgdesc='Just-In-Time Compiler for Lua'
arch=('i686' 'x86_64')
url='http://luajit.org/'
license=('MIT')
depends=('gcc-libs') 
source=(http://luajit.org/download/LuaJIT-${pkgver/b/-beta}.tar.gz
        Makefile.patch)
md5sums=('824aa2684a11e3cc3abe87350a7b6139'
         'c9dff2a84424fa0af322379a5f115fb7')

build() { 
  cd $srcdir/LuaJIT-${pkgver/b/-beta}
  patch -Np0 -i $srcdir/Makefile.patch
  make PREFIX=/usr
}

package() {
  cd $srcdir/LuaJIT-${pkgver/b/-beta}
  make install DESTDIR=$pkgdir PREFIX=/usr

  install -Dm644 $srcdir/LuaJIT-${pkgver/b/-beta}/COPYRIGHT \
                 $pkgdir/usr/share/licenses/$pkgname/COPYRIGHT

  # Poor man's slot system
  ln -s /usr/share/$pkgname $pkgdir/usr/share/$pkgname-${pkgver/b/-beta}
}
