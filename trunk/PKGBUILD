# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

_name=Carla
pkgname=carla
pkgver=1.9.11
pkgrel=2
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('ffmpeg' 'file' 'fluidsynth' 'gtk2' 'gtk3' 'linuxsampler'
'python-pyqt5' 'python-pyliblo' 'python-rdflib' 'qt5-svg')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f5ab74d47a424fae245f3432796369431019023fd8316683275146d5e0a9452fdb50c76cfa54f82f21cfd3fe3deffa696b1371ffa53f5356588e97cd6156eab2')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make features
  make DEFAULT_QT=5
}

package() {
  cd "${pkgname}-${pkgver}"
  make DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
