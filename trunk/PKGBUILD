# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=2.4.1
pkgrel=2
pkgdesc='Captive Portal Assistant'
arch=(x86_64)
url=https://github.com/elementary/capnet-assist
license=(GPL3)
groups=(pantheon)
depends=(
  gcr
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  libsoup
  networkmanager
  webkit2gtk
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=d48d9f82381401fcd914510525dad15abd557533
source=(git+https://github.com/elementary/capnet-assist.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd capnet-assist
  git cherry-pick -n 0e77bf8023ba1b35e3a5badb72c246cabf6552b9
}

pkgver() {
  cd capnet-assist
  git describe --tags
}

build() {
  arch-meson capnet-assist build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
