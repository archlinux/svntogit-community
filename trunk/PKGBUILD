# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=vamp-aubio-plugins
pkgver=0.5.1
pkgrel=3
pkgdesc="Onset detection, pitch tracking, note tracking and tempo tracking plugins using aubio"
arch=('x86_64')
url="https://aubio.org/vamp-aubio-plugins/"
license=('GPL3')
groups=('pro-audio')
depends=('vamp-plugin-sdk' 'aubio')
makedepends=('python2')
source=("https://aubio.org/pub/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('ebbd8466ec3907d55e24824d7a725586df38212994f1398b75974dfac8e282dd8b2cb2a9381ad7e3da1a130b77936f109b36909b23591cc58ff176d6eb22625e')

build() {
  cd "${pkgname}-${pkgver}"
  python2 waf configure --prefix=/usr
  python2 waf build -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
