# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-deny
_commit='706d75cb130740459cdcc1dacff7a772b95d7677'
pkgver=0.13.7
pkgrel=1
pkgdesc='Cargo plugin for linting your dependencies'
arch=('x86_64')
url='https://github.com/EmbarkStudios/cargo-deny'
license=('MIT' 'APACHE')
depends=('gcc-libs' 'zlib')
makedepends=('cargo' 'git')
options=(!lto)
source=("$pkgname-$pkgver::git+$url.git#commit=$_commit"
	"$pkgname-tests-advisory-db::git+https://github.com/rustsec/advisory-db")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd "$pkgname-$pkgver"
  git submodule init
  git config submodule."tests/advisory-db/github.com-2f857891b7f43c59".url "${srcdir}/${pkgname}-tests-advisory-db"
  git -c protocol.file.allow=always submodule update --init --recursive
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 target/release/cargo-deny -t "${pkgdir}"/usr/bin
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
