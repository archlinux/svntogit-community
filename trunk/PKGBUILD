# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgbasename=libcups
pkgname=lib32-$_pkgbasename
pkgver=2.1.2
pkgrel=2
pkgdesc="The CUPS Printing System - client libraries (32-bit)"
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=(lib32-krb5 lib32-libtiff lib32-libpng lib32-gnutls $_pkgbasename)
makedepends=(gcc-multilib)
source=(#ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2
http://www.cups.org/software/${pkgver}/cups-${pkgver}-source.tar.bz2{,.sig})
md5sums=('b3ba0ca676d56857bbd9b182ef4b14b3'
         'SKIP')
validpgpkeys=(3737FD0D0E63B30172440D2DDBA3A7AB08D76223)

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/cups-${pkgver}

  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --disable-ldap --enable-raw-printing --disable-gssapi --disable-dbus \
     --enable-ssl=yes--enable-threads --enable-libusb=no \
	 --disable-avahi --disable-dnssd \
     --with-optim="$CFLAGS" --libdir=/usr/lib32
  make libs
}

package() {
  cd ${srcdir}/cups-${pkgver}
  make BUILDROOT=${pkgdir} install-libs
  rm -rf "$pkgdir/usr/lib"
}

