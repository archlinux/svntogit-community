# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pifpaf
pkgver=2.3.0
pkgrel=1
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-six'
         'python-fixtures' 'python-psutil' 'python-xattr')
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
makedepends=('python-setuptools')
checkdepends=('python-testrepository' 'python-requests' 'python-testtools' 'python-oslotest'
              'memcached' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb')
# 'elasticsearch': test fails
# 'rabbitmq': test hangs
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz")
sha512sums=('9546cb32a931a216845ec6ff879a8ea5af1fad92543c900f711476fde6e2b55eb603b3eddf710c794b3c7e73ed1f5063645c8a1eb0c5a875deea3331aca2ada7')

export PBR_VERSION=$pkgver

build() {
  cd pifpaf-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr
}

package() {
  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
