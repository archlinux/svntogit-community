# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor:JZsolt Varadi (karmaxxl) 

pkgname=gnormalize
pkgver=0.63
pkgrel=5
pkgdesc='Audio normalizer, converter, encoder/decoder, ripper, tagger and player'
arch=('any')
url='http://gnormalize.sourceforge.net/'
license=('GPL')
depends=('perl' 'cddb_get' 'desktop-file-utils' 'gtk2-perl' 'hicolor-icon-theme')
makedepends=('xz' 'gendesk')
optdepends=('faac: full MP4 format'
            'flac: FLAC encoding, FLAC playing in conjunction with mplayer'
            'lame: MP3 encoding'
            'mac: full Monkeys Audio Codec support (available in the AUR)'
            'mpg123: MP3 playing'
            'mplayer: FLAC/WAV playing'
            'musepack-tools: full Musepack support'
            'perl-mp3-info: MP3 ID3 tags'
            'vorbis-tools: full OGG Vorbis support'
            'cdcd: CD playing'
            'cdparanoia: CD ripping'
            'cdrkit: CD ripping - alternative to cdparanoia'
            'wavegain: sound normalization')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('80fd9aa2da8d7fcc88bd180be2adce1aed863bedba7d715144029bd95b61ccb6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --mimetypes \
  'audio/mp3;audio/x-mp3;audio/mpeg;audio/x-mpeg;audio/x-wav;application/x-ogg'
  lzma -qd gnormalize.1.lzma
} 

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -d "$pkgdir/usr/share/$pkgname/animations"
  install -m644 animations/*.gif "$pkgdir/usr/share/$pkgname/animations/"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 "icons/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  for i in 16 32 48 ; do
    install -Dm644 "icons/gnormalize-$i.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x$i/apps/$pkgname.png"
  done
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
