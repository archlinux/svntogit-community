# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.14.1
pkgrel=2
pkgdesc='Plan your next outdoor trip'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack/wiki'
license=('GPL3')
depends=('desktop-file-utils' 'gdal' 'hicolor-icon-theme' 'proj' 'qt5-webengine'
         'quazip' 'routino>=3.1' 'alglib' 'qt5-tools')
makedepends=('cmake' 'patch')
source=($pkgname-$pkgver.tar.gz::https://github.com/Maproom/$pkgname/archive/V_$pkgver.tar.gz
        FindPROJ4.patch
        FindQuaZip5.patch)
sha256sums=('76f64185fc4620da6d9b71998edefcb9091eff1c3486626f87e4f30d444be623'
            '1ffb847b098fedf01e347971da33c58b3fbf1a0f27205d48f6eb52206e228046'
            '23f32e58daa7c69072ea59465f0541e86dcff3085e4e73a948713a02ae421889')

prepare() {
  cd "$pkgname-V_$pkgver"
  patch -Np1 -i "${srcdir}"/FindPROJ4.patch
  patch -Np1 -i "${srcdir}"/FindQuaZip5.patch
}

build() {
  cd "$pkgname-V_$pkgver"

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-V_$pkgver"

  make DESTDIR="${pkgdir}" install
}
