# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=dragonfly-reverb
_name=DragonflyReverb
pkgver=3.2.1
pkgrel=1
pkgdesc="A concert hall reverb and a room reverb"
arch=('x86_64')
url="https://michaelwillis.github.io/dragonfly-reverb/"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libx11')
makedepends=('freeverb3' 'gendesk' 'jack' 'lv2')
checkdepends=('lv2lint')
optdepends=('jack: for standalone applications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/michaelwillis/${pkgname}/releases/download/${pkgver}/${_name}-Source-v${pkgver}.tar.gz")
sha512sums=('63e0f47e8a3197aa65603085e89e4e819580f551be4a2d1c9eedaa479ec9328d9ebd3193f0f4631f06416d7af4859cc9134f23e386209e8b92f60c182203897a')
b2sums=('c198cea906f3346585139ad8cc0b6c89d617e12c8e796d32b4d93d24347a6d916f69f0460016217e35d5c1177939e0debefd5311d30846192f74a5a2694604e7')
_names=('DragonflyEarlyReflections' 'DragonflyHallReverb' 'DragonflyPlateReverb' 'DragonflyRoomReverb')

prepare() {
  declare -A _description=(
    ["DragonflyEarlyReflections"]="A free stereo-to-stereo early reflections reverb effect"
    ["DragonflyHallReverb"]="A free stereo-to-stereo concert hall reverb effect"
    ["DragonflyPlateReverb"]="A free stereo-to-stereo plate reverb effect"
    ["DragonflyRoomReverb"]="A free stereo-to-stereo room reverb effect"
  )
  declare -A _generic=(
    ["DragonflyEarlyReflections"]="Early Reflections Reverb"
    ["DragonflyHallReverb"]="Concert Hall Reverb"
    ["DragonflyPlateReverb"]="Plate Reverb"
    ["DragonflyRoomReverb"]="Room Reverb"
  )
  mv -v "DragonflyReverb-Source-v${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"

  for name in "${_names[@]}"; do
    gendesk -n \
            --pkgname "io.github.michaelwillis.${name}" \
            --name "${name}" \
            --exec "${name}" \
            --pkgdesc "${comments[$name]}" \
            --genericname "${generic[$name]}" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  cd "$pkgname-$pkgver"
  export SYSTEM_FREEVERB3="true"
  make
}

check() {
  cd "$pkgname-$pkgver"
  # TODO: add the other plugins via -I flag as soon as lv2lint supports providing
  # it multiple times
  lv2lint -I "${PWD}/bin/${_names[1]}.lv2/" \
          "https://github.com/michaelwillis/dragonfly-reverb" \
          || echo "Known to fail: https://github.com/michaelwillis/dragonfly-reverb/issues/65"
}

package() {
  depends+=('libfreeverb3.so')
  cd "$pkgname-$pkgver"
  for name in "${_names[@]}"; do
    install -vDm 755 "bin/${name}" -t "${pkgdir}/usr/bin"
    install -vDm 755 "bin/${name}-vst.so" -t "${pkgdir}/usr/lib/vst"
    install -vDm 755 "bin/${name}.lv2/"*.so \
      -t "${pkgdir}/usr/lib/lv2/${name}.lv2/"
    install -vDm 644 "bin/${name}.lv2/"*.ttl \
      -t "${pkgdir}/usr/lib/lv2/${name}.lv2/"
  done
  install -vDm 644 *".desktop" -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
