# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=('python-stevedore' 'python2-stevedore')
pkgver=1.0.0
pkgrel=2
pkgdesc="Manage dynamic plugins for Python applications"
arch=('any')
url="https://github.com/dreamhost/stevedore"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
options=('!emptydirs')
source=(http://pypi.python.org/packages/source/s/stevedore/stevedore-$pkgver.tar.gz)
sha256sums=('4e859756bdba71d7cf4eb1313342cde93cfe36d12fb6b6c35378e9101add229f')

prepare() {
  cd "$srcdir/stevedore-$pkgver"

  # Remove dependency on argparse; it's in Python 2.7+/3.2+ stdlib (FS#42063)
  # https://bugs.launchpad.net/python-stevedore/+bug/1372433
  sed -i '/argparse/d' requirements.txt
}

package_python-stevedore() {
  depends=('python-setuptools' 'python-six')

  cd "$srcdir/stevedore-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-stevedore() {
  depends=('python2-setuptools' 'python2-six')

  cd "$srcdir/stevedore-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
