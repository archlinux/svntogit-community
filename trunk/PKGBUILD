# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=authlib
pkgname=python-authlib
pkgver=0.15.5
_commit=d8e428c9350c792fc3d25dbaaffa3bfefaabd8e3
pkgrel=1
pkgdesc="The ultimate Python library in building OAuth and OpenID Connect servers"
arch=('any')
url="https://github.com/lepture/${_pkgname}"
license=('BSD')
depends=('python-cryptography')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest-asyncio' 'python-pytest-django' 'python-requests'
              'python-django' 'python-flask-sqlalchemy' 'python-httpx' 'python-starlette')
source=("git+${url}.git#commit=${_commit}?signed")
b2sums=('SKIP')
validpgpkeys=('72F8E895A70CEBDF4F2ADFE07E55E3E0118B2B4C') # Hsiaoming (UJET) <lepture@ujet.co>

pkgver() {
    cd "${srcdir}"/${_pkgname}

    git describe --exact-match --tags | sed 's/^v//'
}

prepare() {
    cd "${srcdir}"/${_pkgname}

    # no need to install an additional copy of the stdlib
    find tests -name \*.py -exec sed -i 's/^import mock$/from unittest import mock/' {} +
}

build() {
    cd "${srcdir}"/${_pkgname}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}

    # Some tests fail is run with Django with Flask tests
    # https://github.com/lepture/authlib/issues/284
    python -m pytest --ignore tests/django --ignore tests/flask
    python -m pytest tests/django
    python -m pytest tests/flask
}

package() {
    cd "${srcdir}"/${_pkgname}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
