# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sequencer <liujiuyang1994@gmail.com>

pkgname=python-schemdraw
pkgver=0.8
pkgrel=1
pkgdesc="Electrical circuit schematic drawing."
url="https://bitbucket.org/cdelker/schemdraw"
license=('BSD')
arch=('any')
depends=('python-numpy' 'python-matplotlib')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/cdelker/schemdraw/get/$pkgver.tar.gz")
sha512sums=('2b467a8388b6e50a12b648a0caf1890f2f4ff1db1507d5518c4931c9fc119c234004302513c7b3c9527ddb9a3457d3ae77903ac88fdd5c8dc85d71c077f36895')

prepare() {
  mv cdelker-schemdraw-* schemdraw-$pkgver
}

build() {
  cd schemdraw-$pkgver
  python setup.py build
}

package() {
  cd schemdraw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
