# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.8
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='http://www.dwheeler.com/flawfinder'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python2')
source=(http://www.dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha256sums=('65e05788c2e57b4037de58a42b787abd1e3f249656660cf6a3c1e9ad98a46b37')
sha512sums=('9346c60808087ddf28f433067691c662beb0c36375903fdd0b4dbe3e53fe92b3893e38b13ecb0fe134586cf680fc554d33c92ff910dbd1cd4c3cbeab946038f4')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|env python.*|env python|' -i flawfinder
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
