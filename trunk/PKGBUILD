# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cerberus
pkgver=1.3.1
pkgrel=1
pkgdesc="Lightweight, extensible schema and data validation tool for Python dictionaries"
url="https://github.com/pyeve/cerberus"
license=('ISC')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyeve/cerberus/archive/$pkgver.tar.gz")
sha512sums=('c81655b52a15b0ab9795ea6363654f52327927241a09a5e283f8a380dd13e27d0eae8a5e7c14415d18ba1fce2f2fcab39d477d02365b13a78709cae594111d76')

build() {
  cd cerberus-$pkgver
  python setup.py build
}

check() {
  cd cerberus-$pkgver
  python setup.py pytest --addopts cerberus
}

package() {
  cd cerberus-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
