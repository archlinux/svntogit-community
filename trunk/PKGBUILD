# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyter_events
pkgname=python-jupyter-events
pkgver=0.6.0
pkgrel=1
pkgdesc='Configurable event system for Jupyter applications and extensions'
arch=(any)
url='https://jupyter.org/'
license=(BSD)
depends=(python-jsonschema python-json-logger python-traitlets python-yaml python-click python-rich)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest-console-scripts)
source=(https://github.com/jupyter/jupyter_events/releases/download/v$pkgver/$_pipname-$pkgver.tar.gz)
sha256sums=('bee793d06e124c5a80da3346f96f17aec5e0f28b632a514682b2a18ff548c69a')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  python -m venv --system-site-packages test-env
  export PATH="$PWD/test-env/bin:$PATH"
  test-env/bin/python -m installer dist/*.whl
# https://github.com/jupyter/jupyter_events/issues/50
  test-env/bin/python -m pytest -k 'not test_emit_badschema_format and not test_bad_validations'
  rm -r test-env
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
#  rm -r "$pkgdir"/build
}
