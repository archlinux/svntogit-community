# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=snes9x
pkgname=('snes9x' 'snes9x-gtk')
pkgver=1.53
pkgrel=10
arch=('i686' 'x86_64')
url="https://code.google.com/p/snes9x-gtk/"
license=('custom')
makedepends=('intltool' 'nasm' 'mesa' 'libpulse'  'libpng' 'sdl' 'gtk2' 'libxv'
             'desktop-file-utils' 'gnome-icon-theme')
source=(http://snes9x-gtk.googlecode.com/files/${pkgbase}-${pkgver}-src.tar.bz2
        snes9x-gtk2.24.diff.gz snes9x-fix-crash.patch)
sha1sums=('57be6480369f66b9aaaeb722789ce114f58ed1e2'
          '680dbe0a70f1821bc7931539e72f5a1a95e3938c'
          '995495c68ffc00d8266d10a7bd8fc74fb6c147b1')

prepare() {
  cd ${pkgbase}-${pkgver}-src
  patch -p1 -i "${srcdir}/snes9x-gtk2.24.diff"
  patch -p1 -i "${srcdir}/snes9x-fix-crash.patch"
}

build() {
  cd ${pkgbase}-${pkgver}-src/unix
  ./configure --prefix=/usr --with-sdd1-decomp --enable-netplay
  make

  cd ../gtk
  ./configure --prefix=/usr --with-sdd1-decomp --with-netplay --with-opengl
  make
}

package_snes9x() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System"
  depends=('libpng' 'libxext' 'libsm')

  cd ${pkgbase}-${pkgver}-src/unix
  install -D -m755 snes9x "${pkgdir}/usr/bin/snes9x"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}-src/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_snes9x-gtk() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System - GTK version"
  license=('custom' 'LGPL')
  depends=('sdl' 'libpulse' 'gtk2' 'libxv' 'desktop-file-utils' 'gnome-icon-theme')

  cd ${pkgbase}-${pkgver}-src/gtk
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}-src/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
