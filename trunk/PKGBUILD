# $Id$
# Maintainer : Ionut Biru <ionut@archlinux.ro>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=blueman
pkgver=1.21
pkgrel=6
pkgdesc="GTK+ bluetooth manager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://blueman-project.org"
depends=('python-notify' 'bluez>=4.66' 'dbus-python' 'gtk2>=2.20.1' 'glib2>=2.24.1' \
         'pygobject>=2.20.0' 'startup-notification>=0.10' 'obex-data-server>=0.4.4' \
         'polkit-gnome' 'xdg-utils' 'hicolor-icon-theme')
makedepends=('pyrex>=0.9.8.0' 'intltool')
optdepends=('dnsmasq: For creating PAN network'
            'gconf: For gconf configuration support'
            'pulseaudio: To use the pulseaudio plugin'
            'notification-daemon: for desktop notifications'
            'libnotify: for desktop notifications'
            'modemmanager: for modem management service'
            'gnome-icon-theme:icon theme')
options=('!libtool')
install=$pkgname.install
source=(http://download.tuxfamily.org/blueman/${pkgname}-${pkgver}.tar.gz)
md5sums=('26b70341b3d3da28da62c917c8b20377')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # python2 fix
    for file in apps/blueman*; do
        sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
    done

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/blueman \
        --with-no-runtime-deps-check
    make
    make DESTDIR="${pkgdir}" install
    echo "OnlyShowIn=GNOME;XFCE;" >> ${pkgdir}/etc/xdg/autostart/blueman.desktop
}
