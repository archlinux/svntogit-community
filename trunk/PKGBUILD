# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=3.0.3
pkgrel=4
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel-2.0.so)
_tag=f8b98a59d4d5cecd13f1c1a71f4700aeb006cc21
source=(
  git+https://github.com/elementary/wingpanel.git#tag=${_tag}
  wingpanel-mutter44.patch
)
b2sums=('SKIP'
        'a8027537bab584340c4b91d98d4a84430d44a3958b0d12fb5a2320f74f5443153976e95054eb980c5a592e1d104f410b30ebf24c76f2cc94eba108370f393afe')

prepare() {
  cd wingpanel
  patch -Np1 -i ../wingpanel-mutter44.patch
}

pkgver() {
  cd wingpanel
  git describe --tags
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
