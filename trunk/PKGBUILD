# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Tim Diels <tim@timdiels.be>

pkgbase=python-dropbox
_pkgname=dropbox-sdk-python
pkgname=(python-dropbox python2-dropbox)
pkgver=8.4.1
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://www.dropbox.com/developers/core/sdks/python"
arch=('any')
license=('MIT')
makedepends=(python-requests python2-requests python-urllib3 python2-urllib3
             python-setuptools python2-setuptools)
source=("https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha256sums=('000fd9e91b04d9e3ad2838cf4cf62cd4b0b294ae5260d65d126acb933a981463')

package_python-dropbox() {
  depends=(python python-urllib3 python-requests)

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-dropbox() {
  depends=(python2 python2-urllib3 python2-requests)

  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
