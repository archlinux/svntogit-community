# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-utils
pkgver=4.13.0
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz' 'python-netaddr'
         'python-netifaces' 'python-debtcollector' 'python-pyparsing' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-ddt' 'python-eventlet')
source=("https://github.com/openstack/oslo.utils/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7a21d3b341794ff5378660b05276c807add1414d00e1fe8feebc8811da2fdf722bbcb4a91a7dad330c435b7ad8bc57978de018b1a31aaee4de9984fa6946f6ef')

export PBR_VERSION=$pkgver

build() {
  cd oslo.utils-$pkgver
  python setup.py build
}

check() {
  cd oslo.utils-$pkgver
  stestr run
}

package() {
  cd oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
