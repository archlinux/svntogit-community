# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=openbve
_pkgname=OpenBVE
pkgver=1.5.1.4
pkgrel=1
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=('any')
url="http://openbve-project.net/"
license=('MIT' 'custom:public domain')
depends=('libxi' 'mono' 'openal')
source=($_pkgname-$pkgver.tar.gz::https://github.com/leezer3/$_pkgname/archive/$pkgver.tar.gz
        $pkgname.sh
        $pkgname.desktop)
sha256sums=('6d0bcc3c2bd4c826b51fb7a26b74c9dea186a12051b8abdb8674bc0175c4c94c'
            'cf0f2a28e65248f742d6bb80f3101e36f24097e699c4e5acdf3fd4dbb965d3c3'
            '8190945fa834ecdcf8a3f271264e5be2e288933ecde943060346e16537611c0f')

build() {
  cd $_pkgname-$pkgver
  xbuild /p:Configuration=Release OpenBVE.sln
}

package() {
  cd $_pkgname-$pkgver

  # Binaries
  install -dm755 "$pkgdir/usr/lib/$pkgname/"
  cp -r bin_release/* "$pkgdir/usr/lib/$pkgname/"

  # Data
  install -dm755 "$pkgdir/usr/share/games/$pkgname/"
  mv "$pkgdir/usr/lib/$pkgname/Data" "$pkgdir/usr/share/games/$pkgname/"
  ln -s ../../share/games/$pkgname/Data "$pkgdir/usr/lib/$pkgname/Data"

  # Executable
  install -Dm755 ../$pkgname.sh "$pkgdir/usr/bin/$pkgname"

  # Desktop file and icon
  install -Dm644 ../$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../games/$pkgname/Data/Menu/logo.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # License
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 licenses/* Readme.md \
    "$pkgdir/usr/share/licenses/$pkgname/"
}
