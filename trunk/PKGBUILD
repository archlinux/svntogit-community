# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-listen
pkgver=3.7.1
pkgrel=1
pkgdesc='The Listen gem listens to file modifications and notifies you about the changes.'
arch=(any)
url='https://github.com/guard/listen'
license=(MIT)
depends=(ruby-rb-fsevent ruby-rb-inotify)
checkdepends=(ruby-rake ruby-rspec ruby-thor)
options=(!emptydirs)
source=(https://github.com/guard/listen/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('729bd7ae03e3e617ddd4408d0b4f3681970d28c6271ba56a629229250cdb0997')

prepare() {
  cd listen-$pkgver
  sed -i 's|git ls-files -z|find . -type f -not -path "*/\.git/*" -printf "%P\\\\0"|' listen.gemspec
  sed -i '/[cC]overalls/d' spec/spec_helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd listen-$pkgver
  gem build listen.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    listen-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd listen-$pkgver
  CI=true GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd listen-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
