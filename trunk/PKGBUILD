# Maintainer: Brendan MacDonell <macdonellba at gmail dot com>
# Former Maintainer: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgbase=python-apsw
pkgname=('python-apsw' 'python2-apsw')
pkgver=3.8.8.1
pkgrel=1
arch=('i686' 'x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
makedepends=('python' 'python2')
source=("https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz" "LICENSE")
md5sums=('9a5432a725544079a8c4172d2412dc36'
         '9be3fbee1bae6c66d658a157c67943c0')

build() {
  cp -rf apsw-${pkgver}-r1 apsw2

  cd apsw-${pkgver}-r1
  python setup.py build --enable=load_extension
  cd $srcdir/apsw2
  python2 setup.py build --enable=load_extension
}

package_python-apsw() {
depends=('python' 'sqlite3>=3.7.5')
pkgdesc="Python wrapper for SQLite"
  cd $srcdir/apsw-${pkgver}-r1
  python setup.py install --root=$pkgdir/ 

  install -D -m644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}


package_python2-apsw() {
depends=('python2' 'sqlite3>=3.7.5')
pkgdesc="Python2 wrapper for SQLite"
  cd $srcdir/apsw2
  python2 setup.py install --root=$pkgdir/  

  install -D -m644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
