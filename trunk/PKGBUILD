# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=optics-extra
pkgname=haskell-optics-extra
pkgver=0.4
pkgrel=48
pkgdesc="Extra utilities and instances for optics-core"
url="https://github.com/well-typed/optics-extra"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-indexed-profunctors'
         'haskell-indexed-traversable-instances' 'haskell-optics-core'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('1e860c8e814942e24e5f0e4f0c44334cd333205a884b20791f1784310db947d63d11e7b3d812dbec503c77f419b1555c1273a95df7040c59f8ac4e8ab693ca88')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
