# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=tilda
pkgver=1.1.7
pkgrel=3
pkgdesc="A Gtk based drop down terminal for Linux and Unix"
arch=('i686' 'x86_64')
url="https://github.com/lanoxx/tilda"
license=('GPL')
depends=('vte3' 'confuse')
source=(https://github.com/lanoxx/$pkgname/archive/$pkgname-$pkgver.tar.gz
        start-hidden-fix.patch
        tab-count-fix.patch)
sha256sums=('ed96ae2cf0ceb2a5b2f4a62948c5c0d28d1d54f89ade5153b2e67515cf4dc5e7'
            'e6d6075bb923c2dcae57706fe0555c28f9094f5115f1a3c60b8f85b8ff267b96'
            '5d80a76f904a2094cd7b156654ea6d1860c319fc6c18927f2fb342c0f477c110')

prepare() {
  cd $pkgname-$pkgname-$pkgver

  patch -Np1 -i ../start-hidden-fix.patch
  patch -Np1 -i ../tab-count-fix.patch
  sed 's/Exec=.*/Exec=tilda/; s/Icon=.*/Icon=tilda/' -i tilda.desktop.in

}

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
