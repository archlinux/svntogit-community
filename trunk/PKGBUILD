# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-ppsspp
pkgver=23325
pkgrel=1
pkgdesc='Sony PlayStation Portable core'
arch=('x86_64')
url='https://github.com/libretro/libretro-ppsspp'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libgl' 'libretro-core-info')
makedepends=('git')
_commit='dd73f9110d49a83478216672ea4880a1998c1ed2'
source=("libretro-ppsspp::git+https://github.com/libretro/ppsspp.git#commit=${_commit}"
        'ppsspp-ffmpeg::git+https://github.com/hrydgard/ppsspp-ffmpeg.git'
        'ppsspp-glslang::git+https://github.com/hrydgard/glslang.git'
        'git+https://github.com/hrydgard/ppsspp-lang.git'
        'git+https://github.com/Kingcom/armips.git'
        'armips-tinyformat::git+https://github.com/Kingcom/tinyformat.git'
        'git+https://github.com/KhronosGroup/SPIRV-Cross.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd libretro-ppsspp

  git rev-list --count HEAD
}

prepare() {
  cd libretro-ppsspp

  for submodule in ffmpeg assets/lang ext/glslang; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../ppsspp-${submodule#*/}
    git submodule update ${submodule}
  done
  for submodule in ext/{SPIRV-Cross,armips}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git submodule update ${submodule}
  done

  cd ext/armips

  for submodule in ext/tinyformat; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../../../armips-${submodule#*/}
    git submodule update ${submodule}
  done
}

build() {
  cd libretro-ppsspp/libretro

  make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-ppsspp/libretro

  install -Dm 644 ppsspp_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 ../LICENSE.TXT -t "${pkgdir}"/usr/share/licenses/libretro-ppsspp-git/
}

# vim: ts=2 sw=2 et:
