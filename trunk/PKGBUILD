# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-quart
pkgver=0.13.1
pkgrel=3
pkgdesc='A Python ASGI web microframework with the same API as Flask'
url=https://gitlab.com/pgjones/quart
arch=(any)
license=(MIT)
depends=(
  hypercorn
  python-aiofiles
  python-blinker
  python-click
  python-itsdangerous
  python-jinja
  python-toml
  python-werkzeug
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-asynctest
  python-distlib
  python-hypothesis
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
  python-pytest-sugar
  python-tox
)
_tag=c6bef1fcac63ebd647e44a4888c6dc5f68465360
source=(git+https://gitlab.com/pgjones/quart.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd quart

  git describe --tags
}

build() {
  cd quart

  python setup.py build
}

check() {
  cd quart

  tox -e py39
}

package() {
  cd quart

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-quart/
}

# vim: ts=2 sw=2 et:
