# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>

pkgname=fdkaac
pkgver=0.6.3
pkgrel=1
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=('x86_64')
url='https://github.com/nu774/fdkaac'
license=('custom')
depends=('libfdk-aac.so')
source=("fdkaac-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('16ad555403743b0d288fd113b6d8451a4e787112d4edbfd2da36280a062290c6')

prepare() {
  cd fdkaac-${pkgver}

  autoreconf -fi
}

build() {
  cd fdkaac-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-rpath
  make
}

package() {
  cd fdkaac-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/fdkaac
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/fdkaac/
}

# vim: ts=2 sw=2 et:
