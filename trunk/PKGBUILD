# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-visualization-projectm
epoch=1
pkgver=3.3.0
_codename=Matrix
pkgrel=2
pkgdesc="ProjectM visualizer for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/visualization.projectm'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-visualization')
makedepends=('cmake' 'projectm' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/visualization.projectm/archive/$pkgver-$_codename.tar.gz"
)
sha512sums=('fbce9121a403279a9f54eced08f2069ab6e1f732ef5c6b82380323e2357b70ffa88157428afd4884b6d119aa5a02848718e48602ce9d5f1454c8fa5ed0f78e7b')

build() {
    cd "visualization.projectm-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'projectm')
    cd "visualization.projectm-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

