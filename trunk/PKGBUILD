# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.org>

pkgname=quilt
pkgver=0.60
pkgrel=2
pkgdesc='Manage a series of patches by keeping track of the changes each patch makes'
arch=('any')
url='http://savannah.nongnu.org/projects/quilt'
license=('GPL2')
depends=(
  'bash'
  'bzip2'
  'diffstat'
  'diffutils'
  'findutils'
  'gawk'
  'gettext'
  'gzip'
  'patch'
  'perl'
)
source=("http://savannah.nongnu.org/download/quilt/${pkgname}-${pkgver}.tar.gz")
md5sums=('058a08a9b626bdec9ec8c519dc92158c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --without-rpmbuild
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="$pkgdir/usr" mandir="$pkgdir/usr/share/man" install
  # move bash completion in right place
  install -d -m 755 "$pkgdir/usr/share/bash-completion"
  mv "$pkgdir/etc/bash_completion.d" "$pkgdir/usr/share/bash-completion/completions"
}

# vim:set ts=2 sw=2 et:
