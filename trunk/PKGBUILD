# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-redcloth
pkgver=4.3.2
pkgrel=1
pkgdesc='Textile parser for Ruby'
arch=(x86_64)
url='https://github.com/jgarber/redcloth'
license=(MIT)
depends=(ruby)
makedepends=(ragel ruby-rake ruby-rake-compiler ruby-rspec ruby-diff-lcs)
options=(!emptydirs)
source=(https://github.com/jgarber/redcloth/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/jgarber/redcloth/commit/f1c3076d0409a4ec0ead51190085f82ab97cf923.patch
        https://github.com/jgarber/redcloth/commit/00b55ace17ed408b1b6129e1ba6c90fd4f0a6d2c.patch)
sha256sums=('fbfc709ef742f962896e1cd063b8bd828bdc0ddce36755edbe493cd3231ea850'
            'b790f52f19b4a1ede00c5f929132a4defbe7cd4ca25bc4adfe211003bbab0564'
            '82b790f5be455d12dfa111d67dececd650dc747141067ff1ea5038def8a7a31a')

prepare() {
  cd redcloth-$pkgver
  patch -p1 -i ../f1c3076d0409a4ec0ead51190085f82ab97cf923.patch
  patch -p1 -i ../00b55ace17ed408b1b6129e1ba6c90fd4f0a6d2c.patch

  sed -r -e 's|~>|>=|' -i redcloth.gemspec Gemfile
  sed -i '/rvm/d' Gemfile
  rm tasks/rvm.rake
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd redcloth-$pkgver
  # Avoid building twice by faking a "make".
  # This situation is unfortunate, because RagelExtensionTask extends ExtensionTask and generates
  # the needed source files, but it could only be triggered by the compile task.
  MAKE=true rake compile || :
  gem build redcloth.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    RedCloth-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd redcloth-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd redcloth-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
