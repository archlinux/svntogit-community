# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgbase=php-igbinary
pkgname=('php-igbinary' 'php7-igbinary')
pkgver=3.2.3
pkgrel=1
pkgdesc="A drop in replacement for the standard php serializer"
arch=('x86_64')
url="https://github.com/igbinary/igbinary"
license=('BSD')
depends=('glibc')
makedepends=('php7' 'php')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_name}/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('13bafdb15ee2f9b3b5fea5ca27fef6207466243b42676abc35cc2877935846cc97633725c50665f799627a1bfd31980881da3261acf91d4503a30a5ab99e72cf')
b2sums=('d3a19d6c6fd55d7baca9e5aa819f4a96ffe16a22c2db7e76e2b2b99946fa7e451ab1677dbc364fbdf223df25bc2069669d0f4b1f07fa89f0842d7dadad2682da')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgbase-$pkgver"
  # disable the extension by default
  sed 's/extension/;extension/g' -i "$pkgbase-$pkgver/${_name}.php.ini"

  cp -av "$pkgbase-$pkgver" "${pkgname[1]}-$pkgver"
  (
    cd "${pkgbase}-$pkgver"
    phpize
  )
  (
    cd "${pkgname[1]}-$pkgver"
    phpize7
  )
}

build() {
  (
    cd "$pkgbase-$pkgver"
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
  (
    cd "${pkgname[1]}-$pkgver"
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
}

check() {
  (
    cd "$pkgbase-$pkgver"
    NO_INTERACTION=1 make -k test
  )
  (
    cd "${pkgname[1]}-$pkgver"
    NO_INTERACTION=1 make -k test
  )
}

package_php-igbinary() {
  depends+=('php')
  backup=("etc/php/conf.d/${_name}.ini")

  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_php7-igbinary() {
  depends+=('php7')
  backup=("etc/php7/conf.d/${_name}.ini")

  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php7/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
