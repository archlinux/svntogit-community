# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname=minetest
pkgver=0.4.9
pkgrel=1
pkgdesc='An Infiniminer/Minecraft inspired game'
arch=('i686' 'x86_64')
url='http://minetest.net/'
license=('GPL')
depends=('sqlite' 'libxxf86vm' 'openal' 'libvorbis' 'curl' 'irrlicht' 'mesa' 'hicolor-icon-theme')
makedepends=('cmake')
install=minetest.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}/tarball/${pkgver}
	${pkgname}_game-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}_game/tarball/${pkgver})
md5sums=('2a2629befa86ef72924bcc0e489730c7'
         '097666443a71988cf2d41ebd9be31ec9')

build() {
  cd "$srcdir"/minetest-minetest-*

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"/minetest-minetest-*

  make DESTDIR="$pkgdir" install

  mv -v "$srcdir"/minetest-minetest_game-* "$pkgdir"/usr/share/minetest/games/minetest
  
  # small cleanup
  rm "$pkgdir"/usr/share/minetest/games/minetest/.gitignore
}
