# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=sxhkd
pkgver=0.6.2
pkgrel=1
pkgdesc='Simple X hotkey daemon'
arch=(x86_64)
url='https://github.com/baskerville/sxhkd'
license=(custom:BSD)
depends=(xcb-util-keysyms)
makedepends=(git xcb-util)
source=("git+$url#commit=fe241b0d2d70c9c483b23cf3cd14f1383f0953a2") # tag: 0.6.2
md5sums=('SKIP')

build() {
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make -C $pkgname PREFIX=/usr
}

package() {
  make -C $pkgname PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/baskerville/sxhkd/releases/latest
# vim: ts=2 sw=2 et:
