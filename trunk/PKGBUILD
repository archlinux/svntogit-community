# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

_pkgname=sh
pkgname=("python-$_pkgname" "python2-$_pkgname")
pkgver=1.09
pkgrel=1
pkgdesc='Subprocess interface that allows you to call any program as if it were a function'
arch=('any')
url='http://pypi.python.org/pypi/sh'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/s/sh/$_pkgname-$pkgver.tar.gz")

build() {
  cd "$srcdir"

  cp -r "$_pkgname-$pkgver" "python2-$_pkgname-$pkgver"

  cd "$_pkgname-$pkgver"
  python setup.py build

  cd "../python2-$_pkgname-$pkgver"
  python2 setup.py build
}

package_python-sh() {
  depends=('python')
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/python-sh/LICENSE"
}

package_python2-sh() {
  depends=('python2')
  cd "$srcdir/python2-$_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/python2-sh/LICENSE"
}

# vim:set ts=2 sw=2 et:
md5sums=('b68a2f91de880dce042d4f03ec9e0f47')
