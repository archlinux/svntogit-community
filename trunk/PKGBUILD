# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=pcre-heavy
pkgname=haskell-pcre-heavy
pkgver=1.0.0.2
pkgrel=110
pkgdesc="A regexp library on top of pcre-light you can actually use"
url="https://github.com/myfreeweb/pcre-heavy"
license=("custom:PublicDomain")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-pcre-light' 'haskell-string-conversions')
makedepends=('ghc' 'haskell-glob' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('77691394c7aadb1c81a0974207a8f8ea8ef5f0c85301d0fdb6fb453724c38bd8598b437994b75df5a02a2ca8a98ec22a9473ade52e26a3d2bb1daf22637b54cd')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    doctest library/*/*/*/*.hs
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "UNLICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/UNLICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/UNLICENSE"
}
