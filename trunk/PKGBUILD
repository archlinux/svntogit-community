# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=m2r
pkgname=(m2r python2-m2r)
pkgver=0.1.12
pkgrel=1
pkgdesc="Markdown to reStructuredText converter"
arch=('any')
url="https://github.com/miyakogi/m2r"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-mistune' 'python2-mistune'
             'python-docutils' 'python2-docutils')
checkdepends=('python2-mock' 'python-pygments' 'python2-pygments' 'python-pytest-runner'
              'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/miyakogi/m2r/archive/v$pkgver.tar.gz")
sha512sums=('32b4b49c536087b507722ab5a4d9f9b86ffcc2da511ac8210568cde06de50e817264d7242f459e88c3e78816b2c250f107533fbcb75df4e8d2aabedff9368a9a')

prepare() {
  cp -a m2r-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/m2r-$pkgver
  python setup.py build

  cd "$srcdir"/m2r-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/m2r-$pkgver
  python setup.py pytest

  cd "$srcdir"/m2r-$pkgver-py2
  python2 setup.py pytest
}

package_m2r() {
  depends=('python-mistune' 'python-docutils' 'python-setuptools')

  cd "$srcdir"/m2r-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-m2r() {
  depends=('python2-mistune' 'python2-docutils' 'python2-setuptools')

  cd "$srcdir"/m2r-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/m2r{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
