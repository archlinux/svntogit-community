# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=lxterminal
pkgname=(lxterminal lxterminal-gtk3)
pkgver=0.3.1
pkgrel=1
pkgdesc="VTE-based terminal emulator (part of LXDE)"
arch=('x86_64')
license=('GPL2')
url="https://lxde.org/"
depends=('vte' 'vte3')
makedepends=('intltool')
source=(https://downloads.sourceforge.net/lxde/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('cd8df387c2dd40e03b48caacf700820f9421b421954a0ed56395f505411d2c4b')

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package_lxterminal() {
  groups=('lxde')
  depends=('vte')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxterminal-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('vte3')
  conflicts=('lxterminal')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
