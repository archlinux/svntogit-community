# $Id$
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Bj√∂rn Martensen <bjoern.martensen@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=xcursor-vanilla-dmz
pkgname=('xcursor-vanilla-dmz' 'xcursor-vanilla-dmz-aa')
pkgver=0.4.3
pkgrel=1
arch=('any')
url="http://jimmac.musichall.cz/themes.php?skin=7"
license=('CCPL:by-nc-sa')
makedepends=('xorg-xcursorgen')
source=(http://ftp.de.debian.org/debian/pool/main/d/dmz-cursor-theme/dmz-cursor-theme_${pkgver}.tar.gz)
md5sums=('412256b1b8a65dd05b46f65b32e2f5e6')

build() {
  cd "${srcdir}/dmz-cursor-theme-${pkgver}"
  (cd DMZ-White/pngs ; ./make.sh)
  (cd DMZ-Black/pngs ; ./make.sh)
}

package_xcursor-vanilla-dmz() {
pkgdesc='Vanilla DMZ cursor theme'

  cd "${srcdir}/dmz-cursor-theme-${pkgver}"
  install -d "${pkgdir}"/usr/share/icons/Vanilla-DMZ/cursors
  cp -a DMZ-White/xcursors/* "${pkgdir}"/usr/share/icons/Vanilla-DMZ/cursors
  install -Dm644 DMZ-White/index.theme "${pkgdir}"/usr/share/icons/Vanilla-DMZ/index.theme
}

package_xcursor-vanilla-dmz-aa() {
pkgdesc='Vanilla DMZ AA cursor theme'

  cd "${srcdir}/dmz-cursor-theme-${pkgver}"
  install -d "${pkgdir}"/usr/share/icons/Vanilla-DMZ-AA/cursors
  cp -a DMZ-Black/xcursors/* "${pkgdir}"/usr/share/icons/Vanilla-DMZ-AA/cursors
  install -Dm644 DMZ-Black/index.theme "${pkgdir}"/usr/share/icons/Vanilla-DMZ-AA/index.theme
}
