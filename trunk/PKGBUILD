# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ner0

pkgname=activity-log-manager
pkgver=0.9.7
pkgrel=6
pkgdesc="A graphical user interface which lets you easily control what gets logged by Zeitgeist"
arch=('x86_64')
url="https://launchpad.net/activity-log-manager"
license=('GPL2')
depends=('gtk3' 'libgee' 'zeitgeist')
makedepends=('vala' 'intltool')
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz{,.asc}
        https://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20140812.tar.gz
        fix-build.patch
        fix-build-2.patch
        gee-0.8.patch)
validpgpkeys=('EEE420E481E68B71594F192D361CC5EBA0BA909F') # Manish Sinha
sha256sums=('53014a08056168171579c210a8e5cfd949a0efe3bd23f6d38cd0ce0b0168841f'
            'SKIP'
            '7c930632264720cd2975397ccdee07a09acaba77911dfb870009d47cc4dbd9b7'
            'bc77831fe729e452753260c9ac120d68fed3535d01c370ff3bfcb5c1a6b57c59'
            '93b595524dc6811e1ba1c05eebb0ad2373a3bc1bade63098966809c3f1530e5b'
            '01c4672036e69bc2089398897ae91407d5d25661d450b2d3b62b0ff1670b4122')

prepare() {
  cd $pkgname-$pkgver

  # Install language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  # Regenerate C files
  rm src/activity_log_manager_vala.stamp

  # Fix build
  patch -Np1 -i ../fix-build.patch
  patch -Np1 -i ../fix-build-2.patch

  # Port to gee-0.8
  patch -Np1 -i ../gee-0.8.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
