# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maximilian Stein <theoddbird@posteo.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libsoup
pkgver=2.52.2
pkgrel=1
pkgdesc='GNOME HTTP Library'
arch=('x86_64')
url='http://www.gnome.org'
license=('LGPL')
depends=('lib32-glib-networking' 'lib32-libxml2' 'lib32-sqlite' 'libsoup')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python'
             'vala')
conflicts=('lib32-libsoup-gnome')
replaces=('lib32-libsoup-gnome')
options=('!emptydirs')
source=("https://download.gnome.org/sources/libsoup/${pkgver%.*}/libsoup-${pkgver}.tar.xz")
sha256sums=('db55628b5c7d952945bb71b236469057c8dfb8dea0c271513579c6273c2093dc')

build() {
  cd libsoup-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  make
}

package() {
  cd libsoup-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
