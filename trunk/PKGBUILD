# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=zim
pkgver=0.73.5
pkgrel=1
pkgdesc="A WYSIWYG text editor that aims at bringing the concept of a wiki to the desktop."
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://zim-wiki.org/"
depends=('python' 'python-gobject' 'ttf-font' 'gtk3' 'python-xdg')
checkdepends=('xorg-server-xvfb')
optdepends=('bzr: Version Control plugin'
            'git: Version Control plugin'
            'mercurial: Version Control plugin'
            'gnuplot: Insert Gnuplot plugin'
            'ditaa: Insert Ditaa plugin'
            'graphviz: Insert Diagram plugin'
            'xdot: Link Map plugin'
            'gtkspell3: Spell Checker plugin'
            'r: Insert GNU R Plot plugin'
            'scrot: Insert Screenshot plugin'
            'lilypond: Insert Score plugin'
            'gtksourceview3: Source View plugin'
            'texlive-bin: Insert Equation plugin')
source=("https://www.zim-wiki.org/downloads/${pkgname}-${pkgver}.tar.gz"
	"$pkgname-1379-v2.patch::https://github.com/zim-desktop-wiki/zim-desktop-wiki/pull/1379.patch"
	https://github.com/zim-desktop-wiki/zim-desktop-wiki/commit/85818b30a14c861061c553358262f822e7c067ca.patch)
md5sums=('fa76ceb8ac7d7354fb0e2bc5607e9faa'
         '357942352167ea8c134407dbb127f39f'
         '0a305d2dd465e22280d029c9389be83f')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# fix tests
	patch -p1 -i "$srcdir/$pkgname-1379-v2.patch"
	# fix tests for 0.73.5; should be removed with next release
	patch -p1 -i "$srcdir/85818b30a14c861061c553358262f822e7c067ca.patch"
}


check() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	xvfb-run -w0 ./test.py
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	python setup.py install --root="${pkgdir}" --optimize=1

	install -dm755 "$pkgdir/usr/share/icons/"
	cp -r xdg/hicolor/ "$pkgdir/usr/share/icons/"
}
