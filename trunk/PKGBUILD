# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgbase=python-sympy
pkgname=('python2-sympy' 'python-sympy')
pkgver=0.7.4
pkgrel=1
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='http://sympy.org/en/index.html'
license=('BSD')
makedepends=('python2' 'python')
optdepends=('pyglet: plotting'
            'ipython2: user friendly interface for isympy')
source=("https://github.com/sympy/sympy/releases/download/sympy-${pkgver}/sympy-${pkgver}.tar.gz")

prepare() {
  cp -r sympy-${pkgver} py3-sympy-${pkgver}

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' sympy-${pkgver}/sympy/mpmath/tests/runtests.py sympy-${pkgver}/sympy/utilities/tests/diagnose_imports.py
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python3|' py3-sympy-${pkgver}/sympy/mpmath/tests/runtests.py py3-sympy-${pkgver}/sympy/utilities/tests/diagnose_imports.py
}

build() {
  cd sympy-${pkgver}
  python2 setup.py build

  cd ../py3-sympy-${pkgver}
  python3 setup.py build
}

#check() {
#  cd sympy-${pkgver}
#  python2 setup.py test
#
#  cd ../py3/sympy-${pkgver}
#  python3 setup.py test
#}

package_python2-sympy() {
  depends=('python2>=2.7')
  optdepends=('python2-pyglet: plotting'
              'ipython2: user friendly interface for isympy')

  cd sympy-${pkgver}

  python2 setup.py install --root "${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-sympy() {
  depends=('python>=3.3')
  optdepends=('ipython: user friendly interface for isympy')

  cd py3-sympy-${pkgver}

  python3 setup.py install --root "${pkgdir}" --optimize=1

  # rename files that exists in both 'python2-sympy' and 'python-sympy'
  mv "${pkgdir}"/usr/bin/isympy{,-py3}
  mv "${pkgdir}"/usr/share/man/man1/isympy{,-py3}.1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
sha512sums=('75455b83cf02a72501ad2af0d1139624837ce65b6f02b36dfb949d1372e1291a2f322552b7a699ea973d1c66b8230167ed870445d019b2b08d5fbcc9cc4750d6')
