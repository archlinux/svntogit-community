# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=eva
pkgver=0.3.0
pkgrel=2
pkgdesc='simple calculator REPL, similar to bc(1)'
arch=(x86_64)
url="https://github.com/nerdypepper/$pkgname"
license=(GPL3)
depends=(gcc-libs)
makedepends=(cargo)
options=(debug)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz"
        "$_archive-sync-lockfile.patch::$url/pull/64.patch")
sha256sums=('5ee80a64de05ff952e9fd69fff6050d8e5c602428ad8ad3b357db6b813e1372f'
            '2a9bfc7d9058f66d5cf63032d00fc93d1d8a6c306d6070561945ae0560a8a711')

prepare() {
	cd "$_archive"
	# https://github.com/nerdypepper/eva/issues/63
	patch -p1 < "../${source[1]%::*}"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
