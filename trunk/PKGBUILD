# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastnumbers
pkgname=python-fastnumbers
pkgver=3.2.1
pkgrel=1
pkgdesc="Super-fast and clean conversions to numbers"
arch=('x86_64')
url="https://github.com/SethMMorton/fastnumbers"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-pytest' 'python-typing_extensions')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('af53ebcbabc8530d29c7cbab78b08273498b5293e8a2686dbb7b6eeaa7b33cc464b83c8457f3ace1105f1bc14e05721476d2e4240a43201addb7e49c027f0eec')
b2sums=('91813c6da527b7750ec6dfadc0e7db9681f5a7009ae24b1fdb317e382147c8d4e38723d5905844c076b7aa01c1f444eeb5f78ca2519e76d4065699ec2754a8aa')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _py3_ver=$(python --version | cut -d " " -f2)
  export PYTHONPATH="build/lib.linux-$CARCH-${_py3_ver%"."*}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
