# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-njbridge
pkgver=0.4.1
pkgrel=3
epoch=
pkgdesc="Jack client to transmit audio over a local IP network."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
groups=('pro-audio')
depends=('jack' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('b48fc7c12e0b261290175ef6e20728922d19b12318a31b2ee03fd3e388b61c6c28d99d5c8990442ecc8884a2dab7b4585100549f553ee81d5ff4d069909b96df')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip march=native
  sed -e '/native/d' -i Makefile
  # fix man installation
  sed -e 's/zita-j2n zita-n2j zita-njbridge.1.gz zita-j2n.1.gz zita-n2j.1.gz/zita-j2n zita-n2j/' \
      -e 's/zita-njbridge.1.gz/zita-njbridge.1 -t/g' \
      -e 's/zita-j2n.1.gz/zita-j2n.1 -t/g' \
      -e 's/zita-n2j.1.gz/zita-n2j.1 -t/g' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../{AUTHORS,README}
}
# vim:set ts=2 sw=2 et:
