# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=ropr
pkgver=0.2.14
pkgrel=1
pkgdesc='Blazing fast multithreaded ROP Gadget finder'
url='https://github.com/Ben-Lichtman/ropr'
arch=('x86_64')
license=('Apache')
depends=('glibc' 'gcc-libs')
makedepends=('cargo')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('11f995fe1bc26b793899b08a67932ebd1f0ce2891c8d1e2659e13bb2fe0cc2f6b89b5a85e4ef6a4d8012b540dfe7682f3e115ecff09f796e1055e91c9f3a8464')
b2sums=('7df84784db432890eed6a24e9dd6abe3e4e999276e4dd35b4d3494c43366931c159f5466b150efc9e26981c4a7c5af06256a5be0729a655adb088da8258ece35')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/ropr -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
