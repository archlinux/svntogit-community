# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=galculator
pkgver=2.0
pkgrel=1
pkgdesc='GTK+ based scientific calculator'
arch=('i686' 'x86_64')
url='http://galculator.sourceforge.net/'
license=('GPL')
depends=('gtk3' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'desktop-file-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://downloads.sourceforge.net/galculator/galculator-$pkgver.tar.bz2)
md5sums=('2bec5c1cdab9c5421855f878f8018929')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  LDFLAGS+=' -lm'
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
