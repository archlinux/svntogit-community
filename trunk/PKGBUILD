# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=6.2.2
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-attrs' 'python-iniconfig' 'python-more-itertools' 'python-packaging' 'python-pluggy'
         'python-py' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-argcomplete' 'python-hypothesis' 'python-mock' 'python-nose' 'python-requests'
              'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'python-decorator' 'python-docutils' 'python-jinja' 'python-numpy' 'python-pexpect'
              'python-pygments' 'python-twisted' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('c7b6dfee858984ca842147face869c7a47edd41aa95054de0edaaae7990ce9f8a1a3f4caa9a1b298cd6aebf6961e4a93b6662991866f0970e7a37fb79756f298')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  sed -i '/_pytest.warning_types.PytestUnknownMarkWarning/a \    "ignore:Creating a LegacyVersion has been deprecated and will be removed in the next major release:DeprecationWarning"' pytest-$pkgver/pyproject.toml
}

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  cd pytest-$pkgver
  python setup.py egg_info
  # pdb tests: https://github.com/pytest-dev/pytest/issues/8161
  PYTHONPATH="$PWD"/build/lib python src/pytest/__main__.py --deselect testing/test_debugging.py::TestPDB::test_pdb_interaction_doctest \
                                                            --deselect testing/test_debugging.py::TestPDB::test_pdb_with_injected_do_debug \
                                                            --deselect testing/test_debugging.py::TestPDB::test_pdb_continue_with_recursive_debug \
                                                            --deselect testing/test_debugging.py::test_pdb_suspends_fixture_capturing
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
