# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Auguste <auguste@gmail.com>
# Contributor: Jose Negron <josenj.arch@mailnull.com>

pkgname=libharu
pkgver=2.4.1
pkgrel=1
pkgdesc="C library for generating PDF documents"
arch=(x86_64)
url="https://github.com/libharu/libharu"
license=(custom:ZLIB)
depends=(libpng zlib)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5fd502bafbc4596823d6ff337ab899df2989b5d671cc1c945cd2653dbf5dabb7b5feabc74ab1fe4177441ea139e041401a023a8698b68d2aede75f451a08c512')

prepare() {
  sed -i "s#add_library(haru#add_library(hpdf#" ${pkgname}-${pkgver}/src/CMakeLists.txt
  sed -i "s#target_link_libraries (haru#target_link_libraries (hpdf#" ${pkgname}-${pkgver}/src/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBHPDF_STATIC=OFF
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm755 build/src/libhpdf.so "${pkgdir}"/usr/lib/libhpdf.so
  install -Dm644 ${pkgname}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
