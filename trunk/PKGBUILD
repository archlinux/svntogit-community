# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: Padfoot <padfoot at exemail dot com dot au>
# Contributor: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=lib32-libvisual
pkgver=0.4.2
pkgrel=1
pkgdesc="Abstraction library that comes between applications and audio visualisation plugins (32-bit)"
arch=('x86_64')
url='https://sourceforge.net/projects/libvisual/'
license=('LGPL')
depends=('lib32-glibc' 'lib32-sdl' "${pkgname#lib32-}")
makedepends=('gcc-multilib')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname#lib32-}/${pkgname#lib32-}-${pkgver}.tar.gz")
sha512sums=('ff503464de9870d64fbf79bfcd4040e6e9ab1ae07d1a1f2012be217dc0e6261594ff89a75498d4e51bd55375e23908e30f02593aa330abb78d0c533f9ddc7cc0')

prepare(){
  cd "${srcdir}/${pkgname#lib32-}-${pkgver}"
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS+=' -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
}

build() {
  cd "${srcdir}/${pkgname#lib32-}-${pkgver}"
  ./configure --prefix='/usr' --libdir='/usr/lib32'
  make
}

package() {
  cd "${srcdir}/${pkgname#lib32-}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/"{bin,include,share}
}
