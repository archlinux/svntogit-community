# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-chameleon-gnupg
pkgver=0.1.0
pkgrel=1
pkgdesc="A re-implementation and drop-in replacement of gpg and gpgv"
arch=(x86_64)
url="https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg"
license=(GPL3)
depends=(gcc-libs glibc gmp)
makedepends=(bzip2 cargo clang nettle)
checkdepends=(gnupg)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('40b0af0be2d016bd33d4dcee1825f1d3787dec9c006c39cd9186755bd6284bf1741558a5616b2058ef269dfdbe5145e5bdab66c7a57f7443aa5371a632d3cf75')
b2sums=('8c0ae58afd70995fb132bebcfccc89a74542516ba590631bf50d8c15d61c2999203c721ff45ac3cd8a8266ed92f5c1d0b14c04bda6afb863f220be905fcf2d84')

prepare() {
  cd $pkgname-v$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

# checks broken: https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg/-/issues/8
# check() {
#   cd $pkgname-v$pkgver
#   export RUSTUP_TOOLCHAIN=stable
#   cargo test --frozen --all-features
# }

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libnettle.so libhogweed.so
  )
  cd $pkgname-v$pkgver
  install -vDm 755 target/release/sequoia-chameleon-gpg{,v} -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
