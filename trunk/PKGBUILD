# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=haskell-src-exts-util
pkgname=haskell-src-exts-util
pkgver=0.2.1.2
pkgrel=3
pkgdesc="Helper functions for working with haskell-src-exts trees"
url="https://github.com/pepeiborra/haskell-src-exts-util"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-src-exts' 'haskell-uniplate')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0a78154c24a078d50cf2f25a0a46fb0ad80537eadf2077d022c4b5282941e330b62675a37c04d1a250d82d369eecd5df868c51fa582042eeb225ea8ca50fea53')

prepare() {
    # Remove in next version
    echo -e "import Distribution.Simple\nmain = defaultMain" > ${_hkgname}-${pkgver}/Setup.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
