# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

_pkgname=coveragepy
pkgname=python-coverage
pkgver=6.1.2
pkgrel=1
pkgdesc="A tool for measuring code coverage of Python programs"
arch=('x86_64')
url="https://nedbatchelder.com/code/coverage/"
license=('Apache')
depends=('python')
optdepends=('python-tomli: for pyproject.toml support')
makedepends=('python-setuptools')
checkdepends=('python-flaky' 'python-hypothesis' 'python-mock'
              'python-pycontracts' 'python-pytest' 'python-pytest-xdist' 'python-tomli'
              'python-unittest-mixins' 'python-virtualenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nedbat/coveragepy/archive/$pkgver.tar.gz")
sha512sums=('cc3bd6e0597a11154f9b18b655e6fcf5cee505d4a0b2b2c85e9078aff7abc4f23c5a810797399a4c4ac953f3627217fc2e8510005720a0c59a2a50f5d3bf341a')
b2sums=('b8e4df3054766a05fe80b5ce537f50a54f5f7c8bcfa5445217b133e8e9796a3dc0a256d5ca942478cda33d109515a6e3496b85b88c7f44d107deb192b768b355')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  (
    virtualenv "test_dir" --system-site-packages
    . "test_dir/bin/activate"
    export PYTHONPATH="/usr/lib/python$python_version/site-packages:$PYTHONPATH"
    python setup.py --quiet clean develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  ) || echo "Known failing tests."
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="$pkgdir"
}
