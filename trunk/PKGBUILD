# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-knn-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Index Management Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/search-plugins/knn"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/k-NN/archive/${pkgver}.tar.gz"
)
sha256sums=('2b7c4fb513abda69f88061435b43381720e60d3a2415b46e805fe70eb1bd3fb8')

build() {
  cd "k-NN-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-knn"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-knn"
  unzip "${srcdir}/k-NN-${pkgver}/build/distributions/opensearch-knn-${pkgver}.zip"

  install -Dm644 "${srcdir}/k-NN-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
