# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.path
pkgver=3.4.0
pkgrel=2
pkgdesc="Miscellaneous path functions"
url="https://github.com/jaraco/jaraco.path"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-toml' 'python-wheel')
checkdepends=('python-pytest-black' 'python-pytest-checkdocs' 'python-pytest-cov'
              'python-pytest-enabler' 'python-pytest-flake8' 'python-pytest-mypy'
              'python-pip')
source=("https://github.com/jaraco/jaraco.path/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a9d9cbb605584764ea7ea97a9c8d51a383cb18866787cc20bd08fb5fb2227ea05d8fbf0754d240821530dc493a80634810df1f8446454d5dcca3980eb6144483')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd jaraco.path-$pkgver
  python -m build -wn
}

check() {
  cd jaraco.path-$pkgver
  python -m pytest
}

package() {
  cd jaraco.path-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
