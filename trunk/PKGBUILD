# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.13.5
pkgrel=1
pkgdesc='A library and program to extract, create, and modify WIM files'
arch=('x86_64')
url='https://wimlib.net'
license=('custom')
depends=('openssl' 'fuse' 'libxml2' 'ntfs-3g')
checkdepends=('attr')
optdepends=('cdrkit: for making ISO image of Windows PE'
            'mtools: for making disk image of Windows PE' 
            'syslinux: for making disk image of Windows PE'
            'cabextract: for extracting Windows PE from the WAIK')
provides=('libwim.so')
source=("https://wimlib.net/downloads/$pkgname-$pkgver.tar.gz")
sha256sums=('32fcc9e9b144b7cb1db4c86e104ca78283cdc225e13fe82b273660586aefe323')

build() {
    cd "$pkgname-$pkgver"
    ./configure --with-libcrypto --with-fuse --with-ntfs-3g --prefix=/usr
    make
}

check() {
    cd "$pkgname-$pkgver"
    make check
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm644 NEWS README -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 COPYING* -t "$pkgdir/usr/share/licenses/$pkgname"
}
