# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-poetry-plugin-tweak-dependencies-version
pkgver=1.3.0
pkgrel=1
pkgdesc='Poetry plugin used to tweak dependency versions'
arch=('any')
url='https://github.com/sbrunner/poetry-plugin-tweak-dependencies-version'
license=('BSD')
depends=(
  'python'
  'python-poetry'
  'python-poetry-core'
  'python-cleo'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-poetry-dynamic-versioning'
)
_commit='c5bf57c55ee0c998ee1ef49173238946c55566ac'
source=(
  "$pkgname::git+$url#commit=$_commit"
  'fix-package-version.patch'
)
b2sums=('SKIP'
        '042afd4ef5458b22aa5fb5463e86ad94790e420a1992cddf926cfc6c68e5a3bfd55f98c45a2ce3a9776c14247eef83f062831ba9e4c371be975bc0737f542254')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/fix-package-version.patch"

  # properly set package version
  sed -e "s/@PKGVER@/$pkgver/" -i pyproject.toml
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/poetry_plugin_tweak_dependencies_version-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
