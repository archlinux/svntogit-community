# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Matthias Bauch <matthias.bauch@gmail.com>
# Contributor: Laszlo Papp <djszapi2 at gmail com>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

_features=(amtjtagaccel armjtagew buspirate ftdi gw16012 jlink oocd_trace opendous osbdm
           parport presto_libftdi remote-bitbang rlink stlink ti-icdi ulink usbprog vsllink
           aice cmsis-dap dummy jtag_vpi openjtag_ftdi usb-blaster-2 usb_blaster_libftdi)

pkgname=openocd
pkgver=0.11.0
pkgrel=2
epoch=1
pkgdesc='Debugging, in-system programming and boundary-scan testing for embedded target devices'
arch=('x86_64')
url='http://openocd.org'
license=('GPL')
depends=('libftdi-compat' 'libusb-compat' 'hidapi' 'libudev.so')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2")
sha512sums=('4c8d880945083a05a3781b3fe2a39e4055df635faab1cacd78ca51ced75c9a478f4e16e6c6fa35ac117e2e4d6453cdd19818f1d27aae6edf1bda8c0e846d0de8')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|GROUP="plugdev", ||g' contrib/60-openocd.rules
}

build() {
  cd $pkgname-$pkgver

  #libtoolize -vif
  #autoreconf -vif
  ./configure \
  	--prefix=/usr \
  	--disable-werror \
  	${_features[@]/#/--enable-}

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 contrib/60-openocd.rules "$pkgdir"/usr/lib/udev/rules.d/60-openocd.rules
}
