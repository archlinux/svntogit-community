# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Filippo 'JoeyrS' Civiletti <joeyrs@gmail.com>

pkgname=armagetronad
pkgver=0.2.8.3.3
pkgrel=2
pkgdesc='A Tron Clone in 3D.'
arch=('i686' 'x86_64')
url='http://armagetronad.net/'
license=('GPL')
depends=('sdl_image' 'libxml2' 'sdl_mixer' 'ftgl')
optdepends=('python2: language updater')
source=(http://downloads.sourceforge.net/sourceforge/armagetronad/armagetronad-$pkgver.src.tar.gz)
md5sums=('910d057d575c0ad33eea4d8ce13d8ba3')

build() {
     cd "$srcdir/armagetronad-$pkgver"

     # python2 fix
     sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' language/update.py

     sed -i 's|FTGL.h|ftgl.h|g' configure
     sed -i 's|png_check_sig|png_sig_cmp|g' configure
     ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --mandir=/usr/share/man \
        --disable-games \
        --enable-automakedefaults \
        --disable-uninstall
     make
}

package() {
     cd "$srcdir/armagetronad-$pkgver"
     make DESTDIR="$pkgdir" install
     install -D -m 644 "desktop/armagetronad.desktop" "$pkgdir/usr/share/applications/armagetronad.desktop"
     install -d "$pkgdir/usr/share/pixmaps/"
     ln -s /usr/share/armagetronad/desktop/icons/large/armagetronad.png \
           "$pkgdir/usr/share/pixmaps/armagetronad.png"
     mv "$pkgdir/usr/bin/armagetronad" "$pkgdir/usr/bin/armagetronad_bin"
     printf "#!/bin/bash\n/usr/bin/armagetronad_bin --configdir /etc/armagetronad --datadir /usr/share/armagetronad" > "$pkgdir/usr/bin/armagetronad"
     chmod +x "$pkgdir/usr/bin/armagetronad"
}
