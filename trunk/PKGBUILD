# $Id$
# Maintainer; BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgname=apper
_pkgmainver=0.7
pkgver=0.7.1
pkgrel=6
pkgdesc="KDE tools for PackageKit"
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Apper?content=84745"
license=('GPL')
depends=('kdebase-workspace' 'packagekit-qt2>=0.6.17')
makedepends=('cmake' 'automoc4' 'chrpath')
install="$pkgname.install"
provides=('kpackagekit')
conflicts=('kpackagekit')
replaces=('kpackagekit')
source=("https://launchpad.net/$pkgname/$_pkgmainver/$pkgver/+download/$pkgname-$pkgver.tar.bz2")
sha256sums=('d604e766c788ed26d5e352b7e97477ba58d6fe9e41db0c2649a2a39460bcab98')

build() {
	cd "$pkgname-$pkgver"
	[ -d "build" ] && rm -rf build
	mkdir build
	cd build

	cmake ../ \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$pkgname-$pkgver"
	cd build
	make DESTDIR="$pkgdir" install
	# Hack around cmake rpath bug (debian)
	chrpath --list "$pkgdir/usr/bin/apper"
	chrpath --replace "/usr/lib/apper" "$pkgdir/usr/bin/apper"
}
