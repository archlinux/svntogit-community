# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Ian D. Scott <ian@perebruin.com>
# Contributor: Tony Crisci <tony@dubstepdish.com>
pkgname=playerctl
pkgver=2.2.1
pkgrel=1
pkgdesc="mpris media player controller and lib for spotify, vlc, audacious, bmp, xmms2, and others."
arch=('x86_64')
url="https://github.com/acrisci/playerctl"
license=('LGPL3')
depends=('glib2')
makedepends=('gobject-introspection' 'gtk-doc' 'meson')
source=("playerctl-${pkgver}.tar.gz::https://github.com/acrisci/playerctl/archive/v${pkgver}.tar.gz")
sha512sums=('9c08a3f1d5517d7f4270ed92f226453618fc9f0688688d1af9ff55f0c77efeb1b67daa4adc481546215491550f8528095d376ab099c8ddca05235bfb34b36eb7')
validpgpkeys=('8CDFCC2D6611ACDE63D2C348564F0717F9D84E49')

prepare() {
    cd "${srcdir}/playerctl-${pkgver}"
}

build() {
    cd "${srcdir}/playerctl-${pkgver}"
    arch-meson build
    ninja -C build
}

package() {
    cd "${srcdir}/playerctl-${pkgver}"
    DESTDIR="${pkgdir}" ninja -C build install
}
