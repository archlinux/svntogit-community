# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: J. W. Birdsong <jwbirdsong AT gmail DOT com>

pkgname=luakit
pkgver=2012.03.25
pkgrel=2
pkgdesc='Fast, small, webkit based browser framework extensible by Lua'
arch=('i686' 'x86_64')
url='http://www.luakit.org/projects/luakit'
license=('GPL3')
depends=('libwebkit' 'luafilesystem' 'libunique' 'desktop-file-utils' 'luajit')
makedepends=('git' 'help2man')
options=(!makeflags)
conflicts=('luakit-git' 'luakit-develop-git')
install='luakit.install'
source=("${pkgver}.tar.gz::https://github.com/mason-larobina/luakit/tarball/${pkgver}"
        0001-Dont-mark-desktop-file-as-executable.patch)
md5sums=('e25aaeacfa5758127b3f677eafba9aa2'
         'a1caf95b03659cd02a392a376a13edd7')

build() {
  cd mason-larobina-luakit-*
  patch -Np1 -i $srcdir/0001-Dont-mark-desktop-file-as-executable.patch
  make USE_LUAJIT=1 PREFIX=/usr DESTDIR=${pkgdir} all
}

package() {
  cd mason-larobina-luakit-*
  make PREFIX=/usr DESTDIR=${pkgdir} install 
}
