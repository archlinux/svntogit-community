# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Co-Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=lxqt-build-tools
pkgver=0.11.0
pkgrel=2
pkgdesc="Various packaging tools and scripts for LXQt applications"
arch=("any")
url="https://github.com/lxqt/$pkgname"
license=("LGPL2.1")
depends=("cmake" "qt5-tools" "git" "perl")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
         https://github.com/lxqt/lxqt-build-tools/commit/4991811d.patch
)
sha256sums=('dd5d49e59f9b0552cb40118eb28a463f630b5de6e626b2910410b54ef3bcc1b9'
            'SKIP'
            '52d03bd20cc301b9515d0e5ea1fa7a69e648097d577bce3555d858478626d46c')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
	patch -d $pkgname-$pkgver -p1 < 4991811d.patch # Fix finding glib2 2.73
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
