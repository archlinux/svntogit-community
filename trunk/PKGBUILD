# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Michael Düll <mail@akurei.me>
# Contributor: pumbur

pkgname=opus-tools
pkgver=0.1.10
pkgrel=1
pkgdesc='Collection of tools for Opus audio codec'
arch=('x86_64')
url='http://wiki.xiph.org/Opus-tools'
license=('BSD')
depends=('flac' 'libogg' 'opus')
source=(http://downloads.xiph.org/releases/opus/$pkgname-$pkgver.tar.gz)
md5sums=('6b78535d58b94832710b14e219f65a91')

build(){
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
