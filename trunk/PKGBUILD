# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: eolianoe <eolianoe [at] googlemail [dot] com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Thomas Weißschuh <thomas t-8ch de>
# Contributor: Étienne Deparis <etienne [at] depar [dot] is>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

_name=khal
pkgname=khal
pkgver=0.10.1
pkgrel=3
pkgdesc='CLI calendar application build around CalDAV'
arch=('any')
url="https://lostpackets.de/khal/"
license=('MIT')
makedepends=('python-setuptools-scm' 'python-sphinxcontrib-newsfeed')
depends=('python-atomicwrites' 'python-click' 'python-click-log'
'python-configobj' 'python-dateutil' 'python-icalendar' 'python-pytz'
'python-setuptools' 'python-tzlocal' 'python-urwid' 'python-xdg')
optdepends=('python-setproctitle: Set process name'
            'vdirsyncer: Synchronize CalDAV calendars')
checkdepends=('python-pytest' 'python-freezegun' 'vdirsyncer')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('5b81a60099c201fa112ab17740b73edb17b29efa1548086766d57d9d595451e7d779aad5a2ce7a2afc78050116782ff6696c4bd284021a5e12c87fe361d15e47')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man PYTHONPATH="${PWD}"
}

check() {
  cd "${pkgname}-${pkgver}"
  pytest -v \
    || echo "Tests from hell: https://github.com/pimutils/khal/issues/860"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # executables
  install -vDm 755 bin/{i,}"${pkgname}" -t "$pkgdir/usr/bin/"
  # configuration
  install -vDm 644 "${pkgname}.conf.sample" \
    "${pkgdir}/usr/share/doc/${pkgname}/examples/${pkgname}.conf"
  # man page
  install -vDm 644 doc/build/man/${pkgname}.1 \
    -t "${pkgdir}/usr/share/man/man1/"
  # zsh completion
  install -vDm 644 "misc/__${pkgname}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # license
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS.txt,{CHANGELOG,CONTRIBUTING,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

