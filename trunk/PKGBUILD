# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.16
pkgrel=1
pkgdesc="Tool for monitoring webpages for updates"
arch=('any')
url="https://thp.io/2008/urlwatch/"
license=('BSD')
depends=('python-appdirs' 'python-keyring' 'python-lxml' 'python-cssselect' 'python-minidb' 'python-requests' 'python-yaml')
optdepends=('lynx: convert HTML to plain text'
            'python-beautifulsoup4: prettify HTML'
            'python-html2text: convert HTML to plain text')
source=("$pkgname-$pkgver.tar.gz::https://github.com/thp/urlwatch/archive/$pkgver.tar.gz")
sha256sums=('70e62d655711e1c375f04f92fc0ac3af87a6f8a549ad7c815fd07a1189205195')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
