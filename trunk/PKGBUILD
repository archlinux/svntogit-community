# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-gdk-pixbuf2
pkgver=2.42.10
pkgrel=2
pkgdesc="An image loading library (32-bit)"
url="https://wiki.gnome.org/Projects/GdkPixbuf"
arch=(x86_64)
license=(LGPL)
depends=(lib32-glib2 lib32-libpng lib32-libtiff lib32-libjpeg gdk-pixbuf2)
makedepends=(git meson)
optdepends=(
  "lib32-librsvg: Load .svg, .svgz, and .svg.gz"
)
_commit=9ac257eb38f772fec4253e8051743029e75a2acd  # tags/2.42.10^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#commit=$_commit"
  gdk-pixbuf-query-loaders-32.hook
  multilib.diff
)
sha256sums=('SKIP'
            '60f0f8c2c022f9adef0902bdb4b89881e81f3388cd2e2e4830456eea10f49bae'
            'a87babba459f952148c0e89a046bb5c776e3530d10dd14ee6b9586c92061d500')

pkgver() {
  cd gdk-pixbuf
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gdk-pixbuf

  # Work around tests on lib32
  # https://gitlab.gnome.org/GNOME/gdk-pixbuf/-/issues/215
  git apply -3 ../multilib.diff
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson gdk-pixbuf build \
    --libdir /usr/lib32 \
    -D builtin_loaders=all \
    -D introspection=disabled \
    -D man=false \
    -D installed_tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  provides=(libgdk_pixbuf-2.0.so)
  install=lib32-gdk-pixbuf2.install

  meson install -C build --destdir "$pkgdir"

  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 sts=-1 et:
