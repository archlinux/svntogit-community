# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Benjamin Chr√©tien <chretien + aur [at] lirmm [dot] fr>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Rasi <rasi@xssn.at>
# Contributor: Sean Pringle <sean.pringle@gmail.com>
# Contributor: SanskritFritz (gmail)

pkgname=rofi
pkgver=1.1.0
pkgrel=1
pkgdesc='Popup window switcher roughly based on superswitcher, requiring only xlib and xft'
arch=(i686 x86_64)
url='https://davedavenport.github.io/rofi/'
license=(MIT)
depends=(libx11 libxft freetype2 libxdg-basedir pango startup-notification libxcb libxkbcommon libxkbcommon-x11 xcb-util xcb-util-wm)
makedepends=(i3-wm)
optdepends=('i3-wm: use as a window switcher')
source=(https://github.com/DaveDavenport/rofi/releases/download/$pkgver/rofi-$pkgver.tar.xz)
sha256sums=('1622eca87566baa98d087500b069c7241627d230ef6217990f0bc4032ba01bd1')

build() {
  cd rofi-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd rofi-$pkgver
  make check
}

package() {
  cd rofi-$pkgver
  make install install-man DESTDIR="$pkgdir"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/rofi/COPYING"
  install -dm755 "$pkgdir/usr/share/doc/rofi/examples"
  install -Dm755 Examples/*.sh "$pkgdir/usr/share/doc/rofi/examples"
}
