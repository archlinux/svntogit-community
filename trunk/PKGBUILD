# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>>

pkgname=enlightenment
pkgver=0.25.1
pkgrel=1
pkgdesc="Enlightenment window manager"
arch=('x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('efl' 'udisks2' 'wayland' 'bluez-libs' 'libexif'
         'xorg-server-xwayland' 'bluez' 'pulseaudio' 'bc'
         'xcb-util-keysyms' 'hicolor-icon-theme' 'ttf-font' 'pam')
optdepends=('connman: network module'
            'acpid: power events on laptop lid close'
            'geoip-database: geolocation module'
            'packagekit: needed for packagekit module'
            'ddcutil: for changing monitor settings')
makedepends=('xorg-server-xwayland' 'meson' 'ninja')
provides=('notification-daemon')
backup=('etc/enlightenment/sysactions.conf'
        'etc/enlightenment/system.conf'
        'etc/xdg/menus/e-applications.menu')
source=("https://download.enlightenment.org/rel/apps/${pkgname}/$pkgname-$pkgver.tar.xz")
sha256sums=('2cf05fe3d96ef35e823619dbc0ac513ecabcae2186800ecd804924a637112444')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  CFLAGS+=' -fvisibility=hidden'

  if [ -d build ]; then
    rm -f build
  fi
  mkdir build
  meson --prefix=/usr \
    -Dwl=true \
    . build

  ninja -C build
  }


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  DESTDIR="$pkgdir" ninja -C build install
  
  # install LICENSE
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
