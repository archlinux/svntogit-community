# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Thomas Haider <t.haider@vcnc.org>

pkgname=libev
pkgver=4.27
pkgrel=1
pkgdesc="A full-featured and high-performance event loop"
arch=('x86_64')
url="https://software.schmorp.de/pkg/libev.html"
license=('BSD')
depends=('glibc')
source=(http://dist.schmorp.de/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('b67aff18f6f1ffec4422e188c98d9fe458c5ed0b')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  # fix conflict with libevent
  rm "${pkgdir}"/usr/include/event.h
  
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
