# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=docker-compose
pkgver=1.5.2
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('i686' 'x86_64')
url="https://www.docker.com/"
depends=('python-docopt' 'python-yaml' 'python-requests' 'python-six' 'python-texttable'
         'python-websocket-client' 'python-docker-py' 'python-dockerpty' 'python-setuptools'
         'python-jsonschema')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/compose/archive/$pkgver.tar.gz"
        "remove-requires-upper-bound.patch")
license=("BSD")
sha256sums=('8f0851b417d89bc5023e72918233449a0fa72d9952ac66494c1b37434516583d'
            '06bd229e1d62632dc199d3b2de82571dff73631865fe6108e3a62c8441c281bf')

[[ "$CARCH" == "x86_64" ]] && depends+=('docker')

prepare() {
  cd "$srcdir/compose-$pkgver"

  # Remove upper bound on requires
  patch -p1 -i "$srcdir/remove-requires-upper-bound.patch"
  sed -i 's/==/>=/g' requirements.txt
}

build() {
  cd "$srcdir/compose-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/compose-$pkgver"

  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 contrib/completion/bash/docker-compose "$pkgdir/etc/bash_completion.d/docker-compose"
}
