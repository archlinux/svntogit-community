# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_pkgname=WebTest
pkgname=(python-webtest python2-webtest)
pkgver=2.0.18
pkgrel=1
pkgdesc="Helper to test WSGI applications"
arch=('any')
url='http://webtest.pythonpaste.org/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-webob' 'python2-webob' 'python-six' 'python2-six'
             'python-beautifulsoup4' 'python2-beautifulsoup4')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-coverage' 'python2-coverage'
              'python-pyquery' 'python2-pyquery')
source=("http://pypi.python.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.zip")
sha256sums=('beb569e6d707cb4cd82c226c97b7e1f41cd62bae656d89e687107eef8376c7fd')

prepare() {
  cp -a "$srcdir/$_pkgname-${pkgver}"{,-py2}
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/$_pkgname-${pkgver}"
  PYTHONPATH="$(pwd)/build/lib.linux-$CARCH-3.4:$PYTHONPATH" nosetests3 || warning "Tests failed" # Missing waitress and PasteDeploy

  cd "$srcdir/$_pkgname-${pkgver}-py2"
  PYTHONPATH="$(pwd)/build/lib.linux-$CARCH-2.7:$PYTHONPATH" nosetests2 || warning "Tests failed" # Missing waitress and PasteDeploy
}

package_python-webtest() {
  depends=('python-webob' 'python-six' 'python-beautifulsoup4')
  optdepends=('python-pyquery: for response.pyquery'
              'python-lxml: for response.lxml')

  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "docs/license.rst" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-webtest() {
  depends=('python2-webob' 'python2-six' 'python2-beautifulsoup4')
  optdepends=('python2-pyquery: for response.pyquery'
              'python2-lxml: for response.lxml')

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "docs/license.rst" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
