# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=kdesvn
pkgver=2.0.0
pkgrel=3
pkgdesc="A pure C++ SVN-client for KDE"
arch=('x86_64')
url="https://projects.kde.org/kdesvn"
license=('GPL')
depends=('subversion' 'kcoreaddons' 'kdoctools' 'kio' 'knotifications' 'kparts' 'ktexteditor')
makedepends=('extra-cmake-modules' 'patch')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        kdesvn-desktop.patch::https://cgit.kde.org/kdesvn.git/patch/?id=5ea40285741a885198a351bd9afe58ff746a1192
        kdesvn-qt-5.11.patch::"https://cgit.kde.org/kdesvn.git/patch/?id=605484fd")
sha256sums=('d58d3a895461633718dad580d1590fe99c9d6e44ca64544784a0e9421eb8f8db'
            '7ccbbd548ee3fbd9599fa2f699fef3fa9353d7b6ce7137aff9ab39a40db0330f'
            '67f5a362d5de7c587852c820cbaada05bc06e2fc7eb739249363c8c3b9ef4fa2')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
# FS#52182
  patch -Np1 -i "${srcdir}"/$pkgname-desktop.patch
# fix build with Qt 5.11
  patch -p1 -i ../kdesvn-qt-5.11.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-DQT_THREAD_SUPPORT" \
    -DLIB_SUFFIX=
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
