# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=1.7.0
pkgrel=2
pkgdesc='Minimalistic plugin API for video effects'
arch=('x86_64')
url='https://frei0r.dyne.org/'
license=('GPL2')
depends=('cairo' 'gavl')
makedepends=('cmake' 'doxygen' 'ninja') # opencv - not compatible with 4.0
#optdepends=('opencv: for facebl0r and facedetect plugins')
source=("https://files.dyne.org/frei0r/$pkgname-$pkgver.tar.gz"{,.asc}
         https://github.com/dyne/frei0r/commit/e48da90c.patch
         https://github.com/dyne/frei0r/commit/9e79ba02.patch
         https://github.com/dyne/frei0r/commit/aa7ccf8e.patch
         https://github.com/dyne/frei0r/commit/b8d4e059.patch
         https://github.com/dyne/frei0r/commit/febd7387.patch)
validpgpkeys=('6113D89CA825C5CEDD02C87273B35DA54ACB7D10') # Denis Roio (Jaromil)
sha256sums=('1b1ff8f0f9bc23eed724e94e9a7c1d8f0244bfe33424bb4fe68e6460c088523a'
            'SKIP'
            '7e7db9b0d8d1b6d4a4a16fd586012e9b820933a7dfc163a1e231976d9e88b203'
            'b4649a5251eb39ad8e7d7990d716247190cc3ddf2028a119f7805f3683d2d0f7'
            '41fb30e2dcfefe96aca544ffdd4a94c996adc7397fec44ee6cd6d8a2506a25ae'
            '0997a05a485ac19b1b0053ebf6c624a90b0904f69b04725b305601f2916efc2d'
            'b8151ed31d3003a4109ba5ec15600c182b894ee1b4f2db9f3226dc17c0e48d95')

prepare() {
  cd $pkgname-$pkgver
# Backport fixes for kdenlive
  for _commit in e48da90c 9e79ba02 aa7ccf8e b8d4e059 febd7387; do
    patch -p1 -i ../$_commit.patch
  done
}

build() {
  cmake -S $pkgname-$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
