# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch.de>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>

pkgbase=python-tox
pkgname=(python-tox python2-tox)
pkgver=1.9.2
pkgrel=1
pkgdesc='Python virtualenv management and testing tool'
arch=('any')
url='http://testrun.org/tox/latest/'
license=('GPL2')
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py' 'python-virtualenv' 'python2-virtualenv')
checkdepends=('python-pytest' 'python2-pytest' 'python-pip' 'python2-pip')
source=("http://pypi.python.org/packages/source/t/tox/tox-${pkgver}.tar.gz")
sha256sums=('3125a15da7381f59f835a25bd259a84acb7a69858c81e386c50027bd842bb91b')

prepare() {
  cp -a tox-$pkgver{,-py2}
}

build() {
  cd "$srcdir/tox-$pkgver"
  python setup.py build

  cd "$srcdir/tox-$pkgver-py2"
  python2 setup.py build
}

check() {
  # Not working correctly, as some tests need the "tox" executable

  cd "$srcdir/tox-$pkgver"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test || warning "Tests failed"

  cd "$srcdir/tox-$pkgver-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 || warning "Tests failed"
}

package_python-tox() {
  depends=('python-py' 'python-virtualenv' 'python-setuptools')

  cd "$srcdir/tox-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  ln -s tox "${pkgdir}/usr/bin/tox3"
}

package_python2-tox() {
  depends=('python2-py' 'python2-virtualenv' 'python2-setuptools')

  cd "$srcdir/tox-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "${pkgdir}"/usr/bin/tox{,2}
  mv "${pkgdir}"/usr/bin/{tox-quickstart,tox2-quickstart}
}
