# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-plugins
pkgname=lib32-alsa-plugins
pkgver=1.2.6
pkgrel=1
pkgdesc="Additional ALSA plugins (32-bit)"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
depends=("alsa-plugins=1:${pkgver}" lib32-alsa-lib lib32-glibc)
makedepends=(lib32-dbus lib32-jack lib32-libavtp lib32-libpulse
lib32-libsamplerate lib32-speexdsp)
optdepends=('lib32-dbus: for maemo plugin'
            'lib32-jack: for pcm_jack plugin'
            'lib32-libavtp: for pcm_aaf plugin'
            'lib32-libsamplerate: for rate_samplerate plugin'
            'lib32-libpulse: for conf_pulse, ctl_pulse and pcm_pulse plugins'
            'lib32-speexdsp: for pcm_speex and rate_speexrate plugins')
source=("https://www.alsa-project.org/files/pub/plugins/${_name}-$pkgver.tar.bz2"{,.sig})
sha512sums=('ac54a6f227f1ca5f6e01e08d869e7c06f4bcd68f148932e1902a6fcf3e5ccd3667ac4d8c1a00b77e4b5cb27e17c01cf7526ded4aaf35c4b26fc69e14e6e341f9'
            'SKIP')
b2sums=('bead0b9514725560a4fb738c4b1e14b148bf888cc82e0d7611cb6c494ed97a7e366b57f233095372c26a7bd614655bd58a6505202910de421561b540c94d5cca'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -fiv
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --enable-maemo-plugin
  make
}

package() {
  depends+=(libasound.so)

  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rv "$pkgdir"/{etc,usr/share}
}
# vim:set sw=2 et:
