# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-form
pkgname=haskell-yesod-form
pkgver=1.7.4
pkgrel=8
pkgdesc="Form handling support for Yesod Web Framework"
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-blaze-builder' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-byteable' 'haskell-data-default' 'haskell-email-validate'
         'haskell-network-uri' 'haskell-persistent' 'haskell-resourcet' 'haskell-shakespeare'
         'haskell-wai' 'haskell-xss-sanitize' 'haskell-yesod-core' 'haskell-yesod-persistent')
makedepends=('ghc' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8233cc85d7fd6f6e2d815fd38d3d0d2ebd1c7c11249ee3708dace0934e961319cf4d7063e8982b9f7bd329c3d110bade8013dfe987d6cbdecfc6c15e87abb982')

build() {
  cd $_hkgname-$pkgver
  
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
      -fnetwork-uri
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver
  
  install -D -m744 register.sh   "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
