# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.21.1
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL3)
depends=(glibc gumbo-parser jbig2dec libmupdf openjpeg2 python)
makedepends=(freetype2 libjpeg-turbo python-distro python-build python-installer python-setuptools python-wheel swig systemd)
checkdepends=(python-fonttools python-pillow python-pytest)
optdepends=(
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
)
sha512sums=('863afe65c7f84c941bc040053a09cb14f90d0d7a040c7f2efee94c40c522e959efb91460af8404f0562eb377fae056cd28b9de2b0881a91b9199d09baa6e7e77')
b2sums=('056dc2cab19b120ff5f2dad7c1a71164372565796ce3c419e762b80f93aa130ede06dba7ed6500a28beb4b3de1a161986d091aabf88b44bce27d7e20c3a38308')

prepare() {
  # remove bundled mupdf sources
  rm -frv $_name-$pkgver/*.tgz
}

build() {
  cd $_name-$pkgver
  # NOTE: build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=""
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"

  cd $_name-$pkgver
  mkdir -vp $_test_dir
  # install to test dir for testing
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  # disable broken test: https://github.com/pymupdf/PyMuPDF/issues/2040
  # disable broken test: https://github.com/pymupdf/PyMuPDF/issues/2127
  pytest -vv -c /dev/null tests/ -k 'not test_textbox3 and not test_color_count'
}

package() {
  depends+=(
    freetype2 libfreetype.so
    libjpeg-turbo libjpeg.so
  )

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
