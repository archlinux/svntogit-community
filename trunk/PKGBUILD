# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.12.3
pkgrel=1
pkgdesc="Simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Boxes"
license=('LGPL')
depends=('clutter-gtk' 'gtk-vnc' 'libosinfo' 'libsoup' 'libvirt-glib' 'spice-gtk3'
         'libtracker-sparql' 'qemu' 'cdrkit' 'libcdio' 'mtools' 'dconf'
         'gnome-icon-theme' 'gnome-themes-standard')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'spice-protocol' 'vala')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('bf57525106baaa635d08a4a4c6ed81e4b8c8708897f2fe7c2623810aec9b6408')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --enable-smartcard
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
