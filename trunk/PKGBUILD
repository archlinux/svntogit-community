# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=5.8.6
pkgrel=2
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib'
         'lz4' 'xxhash' 'libxxhash.so' 'file' 'libuv'
         'libzip.so' 'libmagic.so')
makedepends=('meson')
optdepends=('r2ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/radare/radare2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        wasm.patch)
sha512sums=('cd53ed7519d485253182d8d7d414b8d0927e65dd4d0c9716d0d8819e739c8e5e39deb702d93cb5a2a9425804d9798e6eb0acb58a4b904e25a9ba4fcad2315f47'
            '77728bc0d2cd0eb383ea845923e02bed7e8ce19e2cd7d7170d40eb91a37f85ed8582bf3331dd85be441131c68663782b2acd53192dd2d3dc6e1866c1c916b666')
b2sums=('da53500982894a84de0a60640622b6498d3a6cc5236d7141cbd400b76448dc53535c74eef620e13531965c855124065a7f8ccfc3d8622c4e366100dc777e5274'
        'bbe9118239735e513beab1c8ebc01c2e5330b0f6aba033e25f2e139a21b4c6188690a22cd7742b4e89bc750b309ec27ddb80b33933bb64e0fd704ef36d59989f')

prepare() {
  cd ${pkgname}-${pkgver}
# Add wasm to the default plugin list. There is currently no way to append a single plugin to the default list via the meson command line
# https://github.com/radareorg/radare2/discussions/21712#discussioncomment-5826108
  patch -p1 -i ../wasm.patch
}

build() {
  cd ${pkgname}-${pkgver}
  touch libr/config.mk
  arch-meson build \
    -D use_sys_capstone=true \
    -D use_capstone_version=v4 \
    -D use_sys_magic=true \
    -D use_sys_zip=true \
    -D use_sys_zlib=true \
    -D use_sys_lz4=true \
    -D use_sys_xxhash=true \
    -D use_sys_openssl=true \
    -D use_libuv=true \
    -D use_webui=true \
    -D want_threads=false

  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  cp -r doc/* "${pkgdir}/usr/share/doc/radare2"
  ln -s /usr/share/man/man1/radare2.1.gz "${pkgdir}/usr/share/man/man1/r2.1.gz"
# Install missing header
  install -Dm644 shlr/sdb/include/sdb/rangstr.h -t "$pkgdir"/usr/include/libr/sdb
}

# vim: ts=2 sw=2 et:
