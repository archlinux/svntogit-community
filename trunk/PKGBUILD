# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Bob Finch <w9ya@arrl.net>

pkgname=shapelib
pkgver=1.2.10
pkgrel=5
pkgdesc='Simple C API for reading and writing ESRI Shapefiles'
arch=('i686' 'x86_64')
url='http://shapelib.maptools.org/'
license=('LGPL' 'MIT')
options=('!libtool')
source=("http://dl.maptools.org/dl/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'LICENSE')
md5sums=('4d96bd926167193d27bf14d56e2d484e'
         '431dfe7afb1d2c082682ecfcc9ee7a34')

build() {
  cd ${pkgname}-${pkgver}
  
  sed -i -e s:'-O2':"${CFLAGS}": Makefile
  make lib
}

package() {
  cd ${pkgname}-${pkgver}

  sed -i -e s:/usr/local:${pkgdir}/usr: Makefile
  sed -i -e s:/usr/bin/install:/bin/install: Makefile
  make lib_install
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
