# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-polkit
pkgver=1.18.1
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="http://mate-desktop.org"
arch=('x86_64')
license=('LGPL')
groups=('mate')
depends=('gtk3' 'polkit')
makedepends=('accountsservice' 'gobject-introspection' 'intltool')
conflicts=('mate-polkit-gtk3')
replaces=('mate-polkit-gtk3')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('020d770adb42e7b60a74c5253769491860e0b792')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
