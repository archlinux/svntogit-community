# Maintainer: David Runge <dvzrv@archlinux.org>

_name=nkdfu
pkgname=python-nkdfu
pkgver=0.1
pkgrel=4
pkgdesc="Python tool for Nitrokeys' firmware update"
arch=(any)
url="https://github.com/Nitrokey/nkdfu"
license=(GPL2)
depends=(python-fire python-tqdm python-intelhex python-libusb1)
makedepends=(python-build python-flit-core python-installer python-wheel)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-0.1-pep517_build_system.patch::https://github.com/Nitrokey/nkdfu/pull/1/commits/236b1f55e8dae9320357e41f9059a8fe7e19b540.patch
)
sha256sums=('63c1a89df0818b704d32167df5237afd20d26b4f9d6df3c830fa15cc02f01f90'
            'c7de54916d89488ab6c7f486ad9dd889f13414196a06a87323d7b121faa66aa2')
b2sums=('d85d7280edabfe223a0c273d30d4388c78ba50db81a649eb6a45f678df3368e2700498eac12670342fad19f000108f14167adaa010e0ec48024a85c5222be70a'
        '66c747e3fea1ecf5e52d6a4c9d5323eefa7291dd0d3217cf192366609e76105da207ebf033e9a16f45c2813910a8de43447c04eb2611f85b47a63bf78e72891a')

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-0.1-pep517_build_system.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
