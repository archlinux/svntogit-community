# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.3.5
_commit=c1b451090d0770740b5f29897771a95f7e2d8484  # refs/tags/0.3.5
pkgrel=4
pkgdesc="A subprocess.run that works like tee"
arch=(any)
url="https://github.com/pycontribs/subprocess-tee"
license=(MIT)
depends=(python)
makedepends=(git python-build python-installer python-setuptools python-setuptools-scm python-wheel)
checkdepends=(ansible-core molecule python-enrich python-pytest python-pytest-xdist)
optdepends=('python-enrich: for rich text rendering')
# NOTE: switch to git source to not backport own patches: https://github.com/pypa/setuptools/issues/3672
source=(
  git+https://github.com/pycontribs/subprocess-tee#tag=$_commit
  # https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-0.3.5-dependencies.patch
  $pkgname-0.3.5-remove_mock.patch
)
sha512sums=('SKIP'
            'ce6c2b49c5fa86c914e4713e495b3126024836995cdd7549b811c91cc3c7e4a76e8b62518d61a753080ba827118f0522f7cae521c23339191ca12fe8164f38ae'
            'ecc3b991d8b856aa1a72640e7404fe873e095ae7628be3df6083be844363012917610b90cb49ba70af1213482fdcee946fe9ccc93dc0e55e298760dffe70ef92')
b2sums=('SKIP'
        '40045907ce596107a8a56e6d76dc6637b9cfafec46ae496817a8e19599c7de492f422dd2abd830bf39b1a2a367b40690948de4012fb22106bab85ec5fba3b2d5'
        'e2d330fffbb8664a4042940cec8a05a41fc85ad877ca4147dfeb650c8fcbfd4cf7180c5d101f34733f21fac1d326bccabbeedcc1546a28fd9d6adc9570ed891b')

prepare() {
  # remove pip, wheel and python-setuptools-scm-git-archive: https://github.com/pycontribs/subprocess-tee/pull/60
  patch -Np1 -d $_name -i ../$pkgname-0.3.5-dependencies.patch
  # remove mock: https://github.com/pycontribs/subprocess-tee/pull/62
  patch -Np1 -d $_name -i ../$pkgname-0.3.5-remove_mock.patch
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  cd $_name
  export PYTHONPATH="build:$PYTHONPATH"
  # disable broken test: https://github.com/pycontribs/subprocess-tee/issues/58
  pytest -vv -c /dev/null -k "not test_rich_console_ex"
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  # remove tests: https://github.com/pycontribs/subprocess-tee/issues/61
  rm -frv "$pkgdir/$_site_packages/subprocess_tee/test/"
}
