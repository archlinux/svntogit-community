# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com?>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=gnuradio-osmosdr
_pkgname=gr-osmosdr
pkgver=0.1.1
pkgrel=9
_commit=c65d205d3b
pkgdesc="GNU Radio source block for Funcube Dongle, RTL-SDR, USRP and OsmoSDR devices"
url="http://sdr.osmocom.org/trac/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnuradio' 'gnuradio-iqbal' 'rtl-sdr' 'gnuradio-fcdproplus' 'hackrf' 'bladerf')
makedepends=('git' 'cmake' 'boost' 'python2-cheetah' 'swig')
conflicts=('gr-osmosdr-git')
replaces=('gr-osmosdr')
#source=("git://git.osmocom.org/gr-osmosdr#tag=v$pkgver")
source=("git://git.osmocom.org/gr-osmosdr#commit=v$_commit")
md5sums=('SKIP')

# add airspy support?

build() {
  cd "$srcdir/$_pkgname"
  sed -i 's/python$/python2/' apps/osmocom_siggen_base.py
  mkdir build
  cd build
  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$_pkgname/build/"
  make DESTDIR="${pkgdir}" install
}
