# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-audiotools
pkgname=python-zita-audiotools
pkgver=1.3.0
pkgrel=1
pkgdesc="Tools for audio processing"
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=(GPL2 GPL3)
depends=(gcc-libs glibc python-numpy)
makedepends=(libsndfile python-build python-installer python-setuptools python-wheel zita-resampler)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$_name-$pkgver.tar.bz2)
sha512sums=('de4be5421bb2d24405ce10aaecdca631c47cd553a944518e638960360194331ebbc52b47c7e5cf271c561295e692b9896045a51a5bb75e8a6e0385e01c6a8993')
b2sums=('8f3364e19f622b00c524bb90fa7a63b8e6e0904dde2cb3e0d67048e626c5b524d999fe46842973bb5745da05fd1521e7f64964f5a13be3e26f2e195e6b0fcd69')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  depends+=(libsndfile.so libzita-resampler.so)

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
