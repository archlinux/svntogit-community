# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Riley Trautman <asonix@tamu.edu>

pkgname=pantheon-mail
pkgver=6.4.0
pkgrel=1
pkgdesc='Mail app designed for elementary OS'
arch=(x86_64)
url=https://github.com/elementary/mail
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcamel-1.2.so
  libedataserver-1.2.so
  libedataserverui-1.2.so
  libfolks.so
  libgranite.so
  libhandy-1.so
  webkit2gtk
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=b44991d9b1fafa5d7a83b015516c2874f1d9f3d9
source=(pantheon-mail::git+https://github.com/elementary/mail.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd pantheon-mail
  git describe --tags
}

build() {
  arch-meson pantheon-mail build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
