# Maintainer: Daniel Wallace <danielwallace@aur.archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=lib32-curl
pkgname=(lib32-curl lib32-libcurl-compat lib32-libcurl-gnutls)
_tag='b3d2310c6441c075cdad12155da944886f1974a6' # git rev-parse v${_tag_name}
_tag_name='7_88_1'
pkgver="${_tag_name//_/.}"
pkgrel=1
pkgdesc='command line tool and library for transferring data with URLs (32-bit)'
arch=('x86_64')
url='https://curl.haxx.se/'
license=('MIT')
depends=('curl' 'lib32-brotli' 'lib32-libidn2' 'lib32-libssh2' 'lib32-krb5'
         'lib32-libpsl' 'lib32-zlib' 'lib32-zstd')
makedepends=('git' 'lib32-gnutls' 'lib32-openssl')
provides=('libcurl.so')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg
source=("git+https://github.com/bagder/curl.git#tag=${_tag}?signed")
sha512sums=('SKIP')

_backports=(
)

_reverts=(
)

prepare() {
  cd curl

  local _c
  for _c in "${_backports[@]}"; do
    if [[ $_c == *..* ]]; then
      git log --oneline --reverse "${_c}"
    else
      git log --oneline -1 "${_c}"
    fi
    git cherry-pick -n -m1 "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done

  autoreconf -fi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  local _configure_options=(
    --prefix='/usr'
    --disable-dependency-tracking
    --disable-ldap
    --disable-ldaps
    --enable-ipv6
    --enable-manual
    --enable-threaded-resolver
    --with-gssapi
    --with-libssh2
    --with-random='/dev/urandom'
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'
    --libdir='/usr/lib32'
  )

  mkdir build-curl{,-compat,-gnutls}

  # build lib32-curl
  cd "${srcdir}"/build-curl

  "${srcdir}/curl"/configure \
    "${_configure_options[@]}" \
    --with-openssl \
    --enable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
  make libcurl.pc

  # build lib32-libcurl-compat
  cd "${srcdir}"/build-curl-compat

  "${srcdir}/curl"/configure \
    "${_configure_options[@]}" \
    --with-openssl \
    --disable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib

  # build lib32-libcurl-gnutls
  cd "${srcdir}"/build-curl-gnutls

  "${srcdir}/curl"/configure \
    "${_configure_options[@]}" \
    --disable-versioned-symbols \
    --without-openssl \
    --with-gnutls
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
}

package_lib32-curl() {
  depends+=('lib32-openssl')

  cd "${srcdir}"/build-curl

  make -C lib DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pkgconfigDATA

  # license
  install -d "${pkgdir}/usr/share/licenses"
  ln -s curl "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-libcurl-compat() {
  pkgdesc='command line tool and library for transferring data with URLs (32-bit, no versioned symbols)'
  depends+=('lib32-openssl')
  provides=('libcurl-compat.so')

  cd "${srcdir}"/build-curl-compat

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-compat}.so.4.8.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0 4.7.0; do
    ln -s libcurl-compat.so.4.8.0 "${pkgdir}"/usr/lib32/libcurl.so.${version}
  done

  # license
  install -d "${pkgdir}/usr/share/licenses"
  ln -s curl "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-libcurl-gnutls() {
  pkgdesc='command line tool and library for transferring data with URLs (32-bit, no versioned symbols, linked against gnutls)'
  depends+=('lib32-gnutls')
  provides=('libcurl-gnutls.so')

  cd "${srcdir}"/build-curl-gnutls

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-gnutls}.so.4.8.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0 4.7.0; do
    ln -s libcurl-gnutls.so.4.8.0 "${pkgdir}"/usr/lib32/libcurl-gnutls.so.${version}
  done

  # license
  install -d "${pkgdir}/usr/share/licenses"
  ln -s curl "${pkgdir}/usr/share/licenses/${pkgname}"
}
