# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=dask
pkgname=python-${_pkg}
pkgver=2021.10.0
pkgrel=1
pkgdesc="Parallel computing with task scheduling"
arch=(any)
url="https://dask.org"
license=(BSD)
makedepends=(python-setuptools)
depends=(python python-cloudpickle python-fsspec python-packaging python-partd python-toolz python-yaml mpdecimal)
optdepends=(
#    'python-bokeh: visualizing dask diagnostics'
#    'python-cityhash: faster hashing of arrays'
    'python-distributed: distributed computing'
#    'python-fastparquet: storing and reading data from parquet files'
#    'python-gcsfs: file-system interface to Google Cloud Storage'
#    'python-jinja: diagnostics'
#    'python-murmurhash: faster hashing of arrays'
    'python-numpy: arrays and dataframes support'
    'python-pandas: dataframes support'
    'python-psutil: more accurate CPU count'
    'python-pyarrow: Apache Arrow, for parquet support'
#    'python-s3fs: Amazon S3 support'
    'python-sqlalchemy: writing and reading from SQL databases'
    'python-xxhash: faster hashing of arrays'
)
checkdepends=(
    ipython
    python-pytest
    python-pytest-rerunfailures
    python-aiohttp
#    python-bokeh
    python-boto3
    python-botocore
    python-cytoolz
#    python-cityhash
    python-distributed
#    python-fastparquet
#    python-gcsfs
    python-graphviz
    python-h5py
    python-httpretty
    python-jinja
    python-jsonschema
    python-lz4
    python-matplotlib
    python-moto
#    python-murmurhash
#    python-numba
    python-numexpr
    python-numpy
    python-pandas
    python-psutil
    python-pyarrow
    python-pytables
    python-requests
#    python-s3fs
    python-scikit-learn
    python-scipy
    python-snappy
#    python-sparse
    python-sqlalchemy
    python-xarray
    python-xxhash
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz
        ${pkgname}-fix-gen_cluster.py::https://github.com/dask/dask/commit/484ddd920fcd4e71a6b7cd510f018cb17eb5bd50.patch)
#source=(https://github.com/dask/dask/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a98b2e44acaad369bb21f79fc92f756532acfe62c3aeba3982006b7339d0d2e3'
            '64776fe1d858e895686813c7f05e4d424a7ddc21e80781783de2207e1c377432')

prepare() {
  cd ${_pkg}-${pkgver}
  patch -p1 < ../${pkgname}-fix-gen_cluster.py
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -v -rsxfE --color=yes dask/tests dask/array/tests dask/bag/tests dask/bytes/tests dask/dataframe/tests
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
