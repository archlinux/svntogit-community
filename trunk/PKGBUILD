# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=diffuse
pkgver=0.7.7
pkgrel=1
pkgdesc='Graphical tool for merging and comparing text files'
arch=(any)
url='https://github.com/MightyCreak/diffuse'
license=(GPL)
depends=(python-cairo python-gobject)
makedepends=(git meson ninja)
source=("git+$url#commit=c07f6681dc6ae6d80511aed19c81075de1576b18") # tag: v0.7.7
b2sums=('SKIP')

build() {
  mkdir -p build
  arch-meson build $pkgname -D b_ndebug=true
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
