# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jérémie Dimino <jeremie@dimino.org>
# Contributor: Sebastian Hase <sebastian_hase@gmx.de>
# Contributor: Joe Davison <joe@thosedamnpeanuts.com>
pkgname=scummvm-tools
pkgver=2.5.0
pkgrel=2
pkgdesc="A set of tools that enable you to convert, compress and decode game datafiles for ScummVM"
url="http://www.scummvm.org/"
license=('GPL')
depends=('libpng' 'wxgtk3' 'flac' 'libvorbis')
arch=('x86_64')
source=("https://www.scummvm.org/frs/scummvm-tools/${pkgver}/scummvm-tools-${pkgver}.tar.xz")
sha512sums=('40aceabb060df99966125967eb58114810927b35fc58ee607b4e33ceb2dcd02dc93b48a7885d4ab877dbe1a4f70bef88a3eae3feef70af743aec92bdeb6b163b')

prepare() {
  cd ${pkgname}-${pkgver}

  # Build with wxgtk3
  sed -i 's/^_wxconfig=wx-config$/_wxconfig=wx-config-gtk3/' configure
}

build() {
  cd ${pkgname}-${pkgver}

  export CXXFLAGS+=" -ffat-lto-objects"
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}"/usr/bin

  make DESTDIR="${pkgdir}" install

  install -Dm 644 README "${pkgdir}"/usr/share/doc/scummvm-tools/README
  install -Dm 644 convert_dxa.sh "${pkgdir}"/usr/share/doc/scummvm-tools/samples/convert_dxa.sh
}
#vim: sw=2 ts=2 et:
