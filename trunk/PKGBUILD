# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.48.0
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('x86_64')
url="http://msp.ucsd.edu/software.html"
license=('custom:BSD')
depends=('jack' 'fftw' 'tk')
provides=('puredata')
options=('!strip')
source=("http://msp.ucsd.edu/Software/pd-$_ver.src.tar.gz")
md5sums=('b746efd17a8a571c6fcfb536ff607c29')

build() {
  cd pd-$_ver
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --disable-portaudio \
              --enable-fftw \
              --enable-debug
  make
}

package() {
  cd pd-$_ver
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/pd/LICENSE"
}
