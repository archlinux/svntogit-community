# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=param
pkgbase=python-${_pkg,,}
pkgname=("python-${_pkg,,}" "python2-${_pkg,,}")
pkgver=1.9.2
pkgrel=1
pkgdesc="Make your Python code clearer and more reliable by declaring Parameters"
arch=(any)
url="https://param.pyviz.org/"
license=(BSD)
makedepends=(python-setuptools python2-setuptools)
source=("https://files.pythonhosted.org/packages/source/p/${_pkg}/${_pkg}-${pkgver}.tar.gz")
sha256sums=('a2dcb07c6a4ff48bade69bb5d30d84a96911a7e9dcb76b6de975453f933332f8')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py build
}

package_python-param() {
    cd ${_pkg}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-param() {
    cd ${_pkg}-${pkgver}-py2
    python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
