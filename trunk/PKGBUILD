# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=lxlauncher
pkgver=0.2.2
pkgrel=5
pkgdesc='Open source clone of the Asus launcher for EeePC'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2' 'startup-notification' 'lxmenu-data' 'menu-cache')
makedepends=('intltool')
backup=('etc/xdg/lxlauncher/gtkrc' 'etc/xdg/lxlauncher/settings.conf')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.gz
        fix-empty-launcher.patch)
md5sums=('16df627447838b170a72cc3a9ee21497'
         '4dd715f9cccf04ef87c662cee1dcb798')

prepare() {
  cd $pkgname-$pkgver
  # Fix empty launcher
  patch -Np1 -i ../fix-empty-launcher.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
