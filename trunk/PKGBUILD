# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.7.6
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="https://www.indilib.org/index.php?title=Main_Page"
license=(GPL2)
arch=(x86_64)
depends=(libnova cfitsio libusb libjpeg gsl libtheora fftw)
makedepends=(cmake boost qt5-base)
#source=("https://github.com/indilib/indi/releases/download/v$pkgver/libindi_$pkgver.tar.gz")
source=($pkgname-$pkgver.tar.gz::"https://github.com/indilib/indi/archive/v$pkgver.tar.gz")
sha256sums=('db3b176fbff90b059546cf33a1a0bf59f3e84e26b8228e136f6493f3bd4a5e48')

prepare() {
  mkdir -p build

  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i indi-$pkgver/libindi/drivers/*/*.rules
}

build() {
  cd build
  cmake ../indi-$pkgver/libindi \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DINDI_BUILD_QT5_CLIENT=ON \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
