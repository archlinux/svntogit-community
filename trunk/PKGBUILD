# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=libmirage
pkgver=3.0.0
pkgrel=1
pkgdesc="CD-ROM image (B6T/C2D/CCD/CDI/CIF/CUE/ISO/MDS/MDX/NRG/TOC) access library"
arch=(i686 x86_64)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(glib2 libsndfile libsamplerate zlib bzip2 xz shared-mime-info)
makedepends=(gtk-doc gobject-introspection cmake)
install=libmirage.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2
        missing-xml.patch)
sha256sums=('bb0e0f0ed082efe03be4254d1be1310956652b1cec9fad10f9e602a088d17f84'
            '53f98a6b894dbdb2733e17c00aee079d9a36fdd25e295f81487aad3c2d775563')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np1 -i ../missing-xml.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPOST_INSTALL_HOOKS:BOOL=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
