# Maintainer: Anatol Pomozov

pkgname=aws-c-s3
pkgver=0.1.49
pkgrel=1
pkgdesc='C99 library implementation for communicating with the S3 service, designed for maximizing throughput on high bandwidth EC2 instances'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-s3'
license=(Apache)
depends=(aws-c-common aws-c-auth aws-checksums)
makedepends=(cmake)
source=(aws-c-s3-$pkgver.zip::https://github.com/awslabs/aws-c-s3/archive/v$pkgver.zip)
sha256sums=('c1c327efe0f186c556cc116f42c8e132a27a6a0728b2172090a3897896d5970e')

build() {
  cd aws-c-s3-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-s3-$pkgver
  # A lot of tests failing
  # cmake --build build --target test
}

package() {
  cd aws-c-s3-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
