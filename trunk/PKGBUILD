# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-socketio
pkgname=('python-socketio' 'python2-socketio')
pkgver=1.6.1
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-engineio'
             'python2-setuptools' 'python2-sphinx' 'python2-six' 'python2-engineio')
checkdepends=('python-pytest' 'python-mock' 'python2-pytest' 'python2-mock')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}.tar.gz)
sha512sums=('ce7085fcb67e00a5fa89f44a266bc3e6b978c34cce06246f85270174a0f9798b35c2baf088251cba643e1204bd9fefe3e6b4e17d866d82b776283aa9a3053de5')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  (cd ${pkgbase}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${pkgbase}-${pkgver}
    py.test
  )
  (cd ${pkgbase}-${pkgver}-py2
    py.test2
  )
}

package_python-socketio() {
  depends=('python-six' 'python-engineio')

  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-socketio() {
  depends=('python2-six' 'python2-engineio')

  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
