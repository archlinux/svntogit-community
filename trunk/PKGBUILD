# $Id$
# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=harvid
pkgver=0.8.0
pkgrel=1
pkgdesc="HTTP Ardour Video Daemon"
arch=('i686' 'x86_64')
url="http://x42.github.io/harvid/"
license=('GPL')
depends=('ffmpeg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/harvid/archive/v$pkgver.tar.gz")
md5sums=('a3f39791c5eec94be5e43c6545538f9d')

build() {
  cd $pkgname-$pkgver

  # prevent GNU executable stack
  CFLAGS+=" -Wl,-znoexecstack"
  make PREFIX=/usr
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install

  # ffmpeg and ffprobe symlinks (for ardour)
  ln -s ffmpeg "$pkgdir/usr/bin/ffmpeg_harvid"
  ln -s ffprobe "$pkgdir/usr/bin/ffprobe_harvid"

  # doc
  install -Dm644 README.md \
    "$pkgdir/usr/share/doc/$pkgname/README"
}
