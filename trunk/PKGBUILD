# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=gmp-ecm
pkgver=7.0.4
pkgrel=1
pkgdesc='Elliptic Curve Method for Integer Factorization'
url='http://ecm.gforge.inria.fr/'
license=('GPL' 'LGPL')
depends=('gmp')
options=('!libtool')
arch=('i686' 'x86_64')
source=("https://gforge.inria.fr/frs/download.php/file/36224/ecm-${pkgver}.tar.gz")
sha256sums=('0cf7b3eee8462cc6f98b418b47630e1eb6b3f4f8c3fc1fb005b08e2a1811ba43')

build() {
	cd "${srcdir}/ecm-${pkgver}"
	[[ $CARCH = *86* ]] && export CFLAGS+=' -msse'
	./configure --prefix=/usr --enable-shared --enable-openmp
	# make ecm-params
	make
}

check() {
	cd "${srcdir}/ecm-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/ecm-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
