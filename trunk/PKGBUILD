# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Matthew Sexton <wsdmatty (at) _gmail_ (dot) _com_>

pkgname=notes-up
pkgver=2.0.6
pkgrel=1
pkgdesc='Write beautiful notes fast and easy using Markdown'
arch=('x86_64')
url='https://github.com/Philip-Scott/Notes-up'
license=('GPL')
depends=('discount' 'granite' 'gtksourceview4' 'gtkspell3' 'webkit2gtk')
makedepends=('meson' 'vala')
source=("https://github.com/Philip-Scott/Notes-up/archive/$pkgver/Notes-up-$pkgver.tar.gz")
sha256sums=('6e7b6ac4a3a917867a1555db0a6e03e40ccd9955b5d76e980a26fa8e40744f07')

build() {
  meson Notes-up-$pkgver build --prefix='/usr'
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
  ln -s com.github.philip_scott.notes-up "$pkgdir/usr/bin/$pkgname"
}
