# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sebastian Voecking <voeck@web.de>

pkgname=root
pkgver=5.30.03
pkgrel=2
pkgdesc='C++ data analysis framework and interpreter from CERN.'
arch=('i686' 'x86_64')
url='http://root.cern.ch'
license=('LGPL2.1')
depends=('avahi' 'desktop-file-utils' 'ftgl' 'giflib' 'glew' 'graphviz' 'gsl' 'libldap' 'libmysqlclient'
         'libxft' 'postgresql-libs' 'python2' 'unixodbc' 'shared-mime-info' 'xmlrpc-c' 'xorg-fonts-75dpi' 'mesa' 'gcc-fortran')
makedepends=('fftw')
install='root.install'
source=("ftp://root.cern.ch/root/root_v${pkgver}.source.tar.gz"
        'root.sh'
        'rootd'
        'root.desktop'
        'root.xml')
md5sums=('da46aac87047acc41a67b24c05fc465f'
         '0e883ad44f99da9bc7c23bc102800b62'
         'efd06bfa230cc2194b38e0c8939e72af'
         'ac61b17395d75a2705fefa2ef841a6bf'
         'e2cf69b204192b5889ceb5b4dedc66f7')

build() {
  cd root

  if [ ${CARCH} == 'i686' ]; then
    TARGET=linux;
  else
    TARGET=linuxx8664gcc;
  fi

  # python2 switch
  find . -type f -exec sed -i -e 's/python -O/python2 -O/g' -e 's/python -c/python2 -c/g' {} \;
  sed \
    -e 's/python 2/python2 2/g' \
    -i configure
  sed \
    -e 's/python $(pkgpyexecdir)/python2 $(pkgpyexecdir)/g' \
    -i cint/reflex/python/genreflex/Makefile.am
  sed \
    -e 's/python /python2 /' \
    -i config/genreflex.in config/genreflex-rootcint.in

  ./configure \
    ${TARGET} \
    --prefix=/usr \
    --disable-builtin-ftgl \
    --disable-builtin-freetype \
    --disable-builtin-glew \
    --disable-builtin-pcre \
    --disable-builtin-zlib \
    --disable-builtin-lzma \
    --enable-gdml \
    --enable-gsl-shared \
    --enable-minuit2 \
    --enable-soversion \
    --enable-roofit \
    --enable-python \
    --with-python-incdir=/usr/include/python2.7 \
    --with-python-libdir=/usr/lib \
    --enable-explicitlink

    # move from aur
    #--disable-builtin-afterimage \

  make
}

package() {
  cd root

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/root.sh \
    ${pkgdir}/etc/profile.d/root.sh
  install -D ${srcdir}/rootd \
    ${pkgdir}/etc/rc.d/rootd
  install -D -m644 ${srcdir}/root.desktop \
    ${pkgdir}/usr/share/applications/root.desktop
  install -D -m644 ${srcdir}/root.xml \
    ${pkgdir}/usr/share/mime/packages/root.xml

  rm -rf ${pkgdir}/etc/root/daemons
}
