# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: fenuks

pkgname=python-language-server
pkgver=0.29.0
pkgrel=1
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-language-server"
license=("MIT")
depends=("python" "python-jsonrpc-server" "python-future" "python-jedi" "python-pluggy")
optdepends=("python-mccabe: for complexity checking"
            "python-rope: for completions and renaming"
            "python-pyflakes: for linter to detect various errors"
            "flake8: for code linting"
            "python-pycodestyle: for style checking"
            "python-pylint: for code linting"
            "autopep8: for code formatting"
            "yapf: for code formatting (preferred over autopep8)"
            "python-pydocstyle: for docstring style checking")
makedepends=("python" "python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes" "flake8"
              "python-mock" "python-jedi" "python-future" "yapf" "python-mccabe" "python-pycodestyle"
	          "autopep8" "python-rope" "python-pydocstyle" "python-pylint" "python-jsonrpc-server")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('008a1d4eac857f1b01cea3224503b6656d10833d2e08d6a9da7bde43c55ee33d')

build() {
    cd "${pkgname}-${pkgver}"
    python setup.py build
}

# TODO waiting for pylint 2.4 in repos
# check() {
#     cd "${pkgname}-${pkgver}"
#     py.test
# }

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
