# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=shadowsocks
pkgver=2.6.7
pkgrel=1
pkgdesc="A lightweight tunnel proxy"
license=("MIT")
url="http://pypi.python.org/pypi/shadowsocks"
arch=('any')
depends=('python' 'python-setuptools')
optdepends=('libsodium: For salsa20 and chacha20 support')
install=${pkgname}.install
source=("http://pypi.python.org/packages/source/s/shadowsocks/shadowsocks-${pkgver}.tar.gz"
        "$pkgname-LICENSE::https://raw.github.com/clowwindy/shadowsocks/master/LICENSE"
        "shadowsocks@.service"
        "shadowsocks-server@.service")
sha512sums=('8ab52bcaa531a0049664ec628883957a2c3a5a540340dfe635820005a829e4378dd2fd3278e5db668190273a61916e2041743b72f8612afe637c5dfca4a905b2'
            'ff6b57edea530a10559793fe20d1da0359100043855f180f3c7c7c821321ce84ee9f914b170eb2b34ee000493bd2a1864a96a95038373e460306f76f50eee46d'
            '96ecb174a476f64dec3ec086732d93a76d27e9a1cc56b1dc3fa87bf8e00d4b3c44076a01a0d525e42174cacc58fd5c16d5c8af5d4636dae13df549dec8204f7d'
            'f2f8240c18a4483bf3e03b3a5ac8822c3deae713224f732bf28f7fddfcc5d0b02aa91652665ff93f05731e162a9407109c18f43d732a3a9b19d9eacfc806a4df')

check() {
  cd shadowsocks-$pkgver

  # Tests not included
  return
  python tests/test.py -c tests/table.json
  python tests/test.py -c tests/aes.json
  python tests/test.py -c tests/rc4-md5.json
  python tests/test.py -c tests/salsa20.json
  python tests/test.py -c tests/server-multi-ports.json
  python tests/test.py -c tests/server-multi-passwd.json
  python tests/test.py -c tests/server-multi-passwd-table.json
  python tests/test.py -c tests/workers.json
}

package() {
  cd "$srcdir/shadowsocks-$pkgver"

  python setup.py install -O1 --root="$pkgdir"
  
  install -d "$pkgdir/etc/shadowsocks"
  install -Dm644 "$srcdir/shadowsocks@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks@.service"
  install -Dm644 "$srcdir/shadowsocks-server@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-server@.service"
  install -Dm644 "$srcdir/$pkgname-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
