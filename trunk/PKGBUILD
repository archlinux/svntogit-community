# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jérémie Dimino <jeremie@dimino.org>
# Contributor: Sebastian Hase <sebastian_hase@gmx.de>
# Contributor: Joe Davison <joe@thosedamnpeanuts.com>
pkgname=scummvm-tools
pkgver=2.0.0
pkgrel=1
pkgdesc="A set of tools that enable you to convert, compress and decode game datafiles for ScummVM"
url="http://www.scummvm.org/"
license=('GPL')
depends=('libpng' 'wxgtk3' 'flac' 'libvorbis')
arch=('x86_64')
source=("https://www.scummvm.org/frs/scummvm-tools/${pkgver}/scummvm-tools-${pkgver}.tar.xz")
sha512sums=('a4c645f2a05b5f208658bad1a3122115a3077809141f5d4b281414ca32854d14987a8b6d4579f3bdd9d3eeeab880d98e3b540c3f69155197f20a2094a7812c60')

prepare() {
  cd ${pkgname}-${pkgver}

  # Build with wxgtk3
  sed -i 's/^_wxconfig=wx-config$/_wxconfig=wx-config-gtk3/' configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  install -d ${pkgdir}/usr/bin

  make DESTDIR=${pkgdir} install

  install -Dm 644 README ${pkgdir}/usr/share/doc/scummvm-tools/README
  install -Dm 644 convert_dxa.sh ${pkgdir}/usr/share/doc/scummvm-tools/samples/convert_dxa.sh
}
#vim: sw=2 ts=2 et:
