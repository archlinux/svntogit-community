# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mox3
pkgname=('python-mox3' 'python2-mox3')
pkgver=0.26.0
pkgrel=1
arch=('any')
pkgdesc='Mock object framework for Python'
url='http://docs.openstack.org/developer/mox3'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-fixtures' 'python2-fixtures')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/mox3/archive/$pkgver.tar.gz")
sha512sums=('9d02b02fcdcf5167013054552c854642408fe4664db9d0586813fa108dc6c6ae9e737caaca695fe8c831219b258ad1e8e32825166ddf05007e02605d9a61c205')

prepare() {
  # Python 3.6 fixes
  sed -i 's/, flags=4//' mox3-$pkgver/mox3/tests/test_mox.py

  cp -a mox3-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/mox3-$pkgver
  python setup.py build

  cd "$srcdir"/mox3-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mox3-$pkgver
  stestr run

  cd "$srcdir"/mox3-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-mox3() {
  depends=('python-pbr' 'python-fixtures')

  cd mox3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-mox3() {
  depends=('python2-pbr' 'python2-fixtures')

  cd mox3-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
