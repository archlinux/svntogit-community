# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=pytest-xprocess
pkgbase=python-${_name}
pkgname=('python-pytest-xprocess' 'python2-pytest-xprocess')
pkgver=0.12.1
pkgrel=4
pkgdesc='Pytest plugin to manage external processes across test runs.'
arch=('any')
url="https://github.com/pytest-dev/pytest-xprocess"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python-pytest-cache' 'python-psutil' 'python2-pytest' 'python2-pytest-cache' 'python2-psutil')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fda868a12a056070afd5838c4bc466b4906a7138f685d4a0e6e0adbd975e5a1a2b9b37b8de36a45fb700e02ca14ae219cfe29c467f8d03e55d2a375a7efc9927')

prepare(){
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  export PYTHONPATH="."
  py.test
  cd ../"${pkgname[1]}-${pkgver}"
  export PYTHONPATH="."
  py.test2
}

package_python-pytest-xprocess() {
  depends=('python-psutil' 'python-pytest' 'python-setuptools')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/${pkgname}" -vDm 644 {CHANGELOG,README.rst}
}

package_python2-pytest-xprocess() {
  depends=('python2-psutil' 'python2-pytest' 'python2-setuptools')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/${pkgname}" -vDm 644 {CHANGELOG,README.rst}
}
