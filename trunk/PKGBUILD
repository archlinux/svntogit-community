# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: ml <>
pkgname=cmctl
pkgver=1.9.1
pkgrel=1
pkgdesc='Automatically provision and manage TLS certificates in Kubernetes'
arch=('x86_64' 'aarch64')
url='https://github.com/jetstack/cert-manager'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
groups=('kubectl-plugins')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0d2b157035fa316597fcf442d0ec7fa8348d320ad0beda59daf1d313c1f7eda91d0a486365bd9d0ceae2f315025306a8a007e4b377fc9970944515c1c91b5ccc')
b2sums=('f9b5effa6494401f241e7990b8efbb5ac5929d82f38674a8139083afd362432826a8365e4273bb4002a3926077c41418285f84d650c24bef49ba87b4854f6a6d')

build() {
  local _x _commit
  _commit=$(bsdcat "$pkgname-$pkgver.tar.gz" | git get-tar-commit-id)
  _x=(
    AppVersion="v$pkgver"
    AppGitCommit="${_commit:?}"
    AppGitState="clean"
  )

  cd cert-manager-"$pkgver"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-linkmode=external ${_x[*]/#/-X=github.com/jetstack/cert-manager/pkg/util.} 
  -X github.com/jetstack/cert-manager/cmd/ctl/pkg/build.name=cmctl 
  -X github.com/jetstack/cert-manager/cmd/ctl/pkg/build/commands.registerCompletion=true" ./cmd/ctl
}

check() {
  cd cert-manager-"$pkgver"
  # we don't want e2e and other tests that explicitly want bazel
  go test -short ./cmd/ctl/...
}

package() {
  cd cert-manager-"$pkgver"
  install -Dm755 ctl "$pkgdir"/usr/bin/cmctl

  # build bash completions
  #mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  #"$pkgdir"/usr/bin/cmctl completion bash > "${pkgdir}/usr/share/bash-completion/completions/cmctl"

  ## build zsh completions
  #mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  #"$pkgdir"/usr/bin/cmctl completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_cmctl"

  ## build fish completions
  #mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  #"$pkgdir"/usr/bin/cmctl completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/cmctl.fish"
}
