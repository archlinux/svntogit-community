# $Id$
# Maintainer: Morten Linderud
# Maintainer: Anatol Pomozov

pkgbase=python-autobahn
pkgname=(python-autobahn python2-autobahn)
pkgver=18.7.1
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things'
arch=(any)
url='http://autobahn.ws/python/'
license=(MIT)
makedepends=(python-setuptools python2-setuptools python-twisted python-six python-txaio python2-twisted python2-six python2-txaio)
checkdepends=(python2-unittest2)
source=(https://pypi.io/packages/source/a/autobahn/autobahn-$pkgver.tar.gz)
sha256sums=('163dd329c2a29f4ef82f7fe85a6f6e654a2aa6837bd83834cf537e4efa9204be')

prepare() {
  cp -a autobahn-$pkgver{,-py2}
}

build() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py build

  cd "$srcdir/autobahn-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/autobahn-$pkgver"
  #It requires python-unittest2 in [community]
  #python setup.py test

  cd "$srcdir/autobahn-$pkgver-py2"
  #python2 setup.py test
}

package_python-autobahn() {
  depends=(python python-twisted python-six python-txaio)

  cd "$srcdir/autobahn-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-autobahn() {
  depends=(python2 python2-twisted python2-six python2-txaio)

  cd "$srcdir/autobahn-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
