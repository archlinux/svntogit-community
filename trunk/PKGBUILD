# Maintainer:
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=eom
pkgver=1.18.3
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE"
url="https://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gobject-introspection-runtime' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'mate-desktop' 'libpeas')
makedepends=('gobject-introspection' 'intltool' 'itstool')
groups=('mate-extra')
conflicts=('eom-gtk3')
replaces=('eom-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4548f14f89300cf74849e197eaf40e58430c00dc5800e067449d38b0692c28ff')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-librsvg \
        --disable-python

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
