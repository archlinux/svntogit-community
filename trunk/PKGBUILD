# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-base
pkgver=2016.01.30
pkgrel=1
pkgdesc='Default wallpapers and other base component for Deepin'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-base"
license=('GPL3')
makedepends=('git')
groups=('deepin')
source=("git+https://cr.deepin.io/deepin-desktop-base.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # Remove Deepin distro's lsb-release
  rm "$pkgdir"/etc/lsb-release

  # Don't override systemd timeouts
  rm -r "$pkgdir"/etc/systemd

  # Rename default desktop file to avoid conflicts
  mv "$pkgdir"/usr/share/backgrounds/{desktop.jpg,deepin_default_background.jpg}

  # Make a symlink for deepin-version
  ln -s ../usr/lib/deepin/desktop-version "$pkgdir"/etc/deepin-version
}
