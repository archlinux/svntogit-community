# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Magnus Brevik <janmbrevik@gmail.com>

_pkgbase=podcastparser
pkgbase=python-$_pkgbase
pkgname=(python-podcastparser python2-podcastparser)
pkgver=0.6.3
pkgrel=2
pkgdesc='Simplified, fast RSS parsing library in Python'
arch=(any)
url='https://github.com/gpodder/podcastparser'
license=(ISC)
depends=(python python2)
source=($pkgbase-$pkgver.tar.gz::https://github.com/gpodder/$_pkgbase/archive/$pkgver.tar.gz)
sha256sums=('5cd36182bf0c8ec5b36fb309b23bb1c67b507909d94ef402f04f08e64e545489')

package_python-podcastparser() {
  depends=(python)

  cd $_pkgbase-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-podcastparser() {
  depends=(python2)

  cd $_pkgbase-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
