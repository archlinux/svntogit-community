# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-menu
pkgver=2.90.2
_commit=2a85fd538d86c8ba0cf1ea2baac75d0a0973182f
pkgrel=2
pkgdesc="Deepin menu service for building beautiful menus"
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/deepin-menu"
license=('GPL3')
depends=('python2-pyqt5' 'qt5-x11extras')
makedepends=('python2-setuptools' 'qt5-declarative' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-menu.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd deepin-menu

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-menu

  python2 setup.py build
  qmake-qt5 DEFINES+=QT_NO_DEBUG_OUTPUT
  make
}

package() {
  cd deepin-menu

  python2 setup.py install --root="${pkgdir}" --optimize=1

  make INSTALL_ROOT="$pkgdir" install

  install -dm 755 "${pkgdir}"/usr/share/dbus-1/services/
  install -dm 755 "${pkgdir}"/etc/xdg/autostart/

  rm -r "${pkgdir}"/usr/deepin_menu

  install -m 644 *.service "${pkgdir}"/usr/share/dbus-1/services/
  install -m 644 *.desktop "${pkgdir}"/etc/xdg/autostart/
}
