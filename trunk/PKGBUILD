# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=Data-Perl
pkgname=perl-${_pkg,,}
pkgver=0.002009
pkgrel=5
pkgdesc="Base classes wrapping fundamental Perl data types"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers' 'perl-list-moreutils' 'perl-module-runtime'
         'perl-role-tiny' 'perl-scalar-list-utils' 'perl-parent' 'perl-strictures')
checkdepends=('perl-test-deep' 'perl-test-fatal' 'perl-test-output')
source=("https://cpan.metacpan.org/authors/id/M/MA/MATTP/${_pkg}-${pkgver}.tar.gz")
sha512sums=('872eae53641b7ac19a8bcd4a6522475bf042463fbb8380a45481289c6792fa17cf4d52d146228acf089b9cff091413684fa5b39f67bc28f362a5d2eb85d1d01a')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
