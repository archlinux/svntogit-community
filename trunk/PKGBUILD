# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=mitmproxy2swagger
pkgver=0.9.0
pkgrel=1
pkgdesc='Automagically reverse-engineer REST APIs via capturing traffic'
url='https://github.com/alufers/mitmproxy2swagger'
arch=('any')
license=('MIT')
depends=(python mitmproxy python-ruamel-yaml python-json-stream)
makedepends=(python-build python-installer python-wheel python-poetry-core)
source=(https://github.com/alufers/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('246d1dd765849b4ed522d8b21e476cc34b51c240903a51b594908ee812c9405aadb8ccb11ac75077fd59182583148fedeedc0ade7ffaed25964fdb724d9ac695')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
