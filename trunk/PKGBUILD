# $Id: PKGBUILD 162763 2016-02-20 14:34:32Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

_hkgname=path-io
pkgname=haskell-path-io
pkgver=1.3.3
pkgrel=9
pkgdesc="Interface to ‘directory’ package for users of ‘path’"
url="https://github.com/mrkkrp/path-io"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-exceptions' 'haskell-path' 'haskell-temporary'
         'haskell-unix-compat')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('af513783d7ae6a99556d67b8f7ed599b0b5285ced7ed4c0b12fb39ab16a043662cad86d24a54d2220892f8625a8229f04bf6d898419348eaee1992ca79e2f4c0')

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "${srcdir}/${_hkgname}-${pkgver}"
  
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE.md"
}
