# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=2.3
pkgrel=3
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
# setuptools is in depends as pkg_resources is used in sortversion.py
depends=('python' 'python-setuptools' 'python-toml' 'python-structlog' 'python-appdirs' 'python-tornado' 'python-pycurl' 'python-packaging')
makedepends=('python-docutils')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'pyalpm' 'git')
optdepends=(
  'pyalpm: for using "sort_version_key = vercmp" in configuration files and the alpm source'
  'git: support for git repositories'
  'python-gobject: for nvchecker-notify'
  'libnotify: for nvchecker-notify'
)
source=("https://github.com/lilydjwg/nvchecker/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "nvchecker-update-tests-20210506.patch"::"https://github.com/lilydjwg/nvchecker/commit/134a6885f123a3249c8c5cb168d9bae03c0ea585.patch"
        "nvchecker-fix-test-pacman.patch"::"https://github.com/lilydjwg/nvchecker/commit/61bc0c5562d353bc47ff5d6d329f3c78ca632de4.patch"
        "nvchecker-update-tests-20210603.patch"::"https://github.com/lilydjwg/nvchecker/commit/1064f3d948d7983f12bfa433c9dccd9930987396.patch"
        "nvchecker-update-tests-20210623.patch")
sha512sums=('bbdf6348cfd93ae156ae05b3ddb2fd238448e4a9d44eb00d2518b4657f895468af657372453c1a8c8c75f7d2f75467d884347a1aaecf896c0fdb6186607b0a63'
            '0b94b5d72f2322c678425a74c576ecf838e080e905b49b4f2651929fcf5f252b8378cf0df82cc652b604b529c5b202a6013ee4fc22b7204d9abf928bd3a3882d'
            'dca127d5c14980e72d9de03ad4a780fdf47cf4a8b74166d2ceb04d62e0b5f1af65f1001eb9c98d3d90eb4719a507f41d6fb1aa65fa5ba2fe3ad5d7e0701e3d08'
            'b686f90098898f3c6cf978949a47950b88f4731aa7b24b0c739a509416a6b180cf98a147302979f07d58c3312d0b886b5442d13d1c0ea5ce117d167dae6da6bf'
            'cfea145fcb1850a85cb1d229b4646894a98a5088ad2a580b7a50ee349a89ebd3f02509abd4777e151758d8cfd51ff50bd2c3c63a0877b7eb4f24f6d8e7dea9c7')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../nvchecker-update-tests-20210506.patch
  patch -Np1 -i ../nvchecker-fix-test-pacman.patch
  patch -Np1 -i ../nvchecker-update-tests-20210603.patch
  # Part of upstream commit ffaca8c94947d1b9709ab98e187f98ff9a531ac3
  patch -Np1 -i ../nvchecker-update-tests-20210623.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build

  make -C docs man
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

  # bash completion scripts & docs
  install -Dm644 scripts/nvtake.bash_completion "$pkgdir"/usr/share/bash-completion/completions/nvtake
  install -Dm644 docs/usage.rst -t "$pkgdir"/usr/share/doc/$pkgname/
  install -Dm644 sample_config.toml -t "$pkgdir"/usr/share/doc/$pkgname/
  install -Dm644 docs/_build/man/nvchecker.1 -t "$pkgdir"/usr/share/man/man1/
}

# vim:set ts=2 sw=2 et:
