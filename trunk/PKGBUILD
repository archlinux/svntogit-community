# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Benjamin Andresen <bandresen gmail com>

pkgname=namazu
pkgver=2.0.21
pkgrel=2
pkgdesc="Namazu is a full-text search engine intended for easy use."
url="http://namazu.org/"
license=('GPL')
arch=('x86_64')
depends=(perl-file-mmagic)
validpgpkeys=('474E4D938E9711F6662D8A4217F552F410E7D14E')
source=(http://namazu.org/stable/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('5c18afb679db07084a05aca8dffcfb5329173d99db8d07ff6d90b57c333c71f7'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure \
  	--prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var/lib \
	--mandir=/usr/share/man \
	--libexecdir=/usr/lib/namazu \
	--libdir=/usr/lib/namazu
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
