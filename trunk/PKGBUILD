# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=rio
pkgname=haskell-rio
pkgver=0.1.14.0
pkgrel=3
pkgdesc="A standard library for Haskell"
url="https://github.com/commercialhaskell/rio"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions' 'haskell-hashable' 'haskell-microlens'
         'haskell-primitive' 'haskell-typed-process' 'haskell-unliftio' 'haskell-unliftio-core'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('01c51aa0f08ac1a1383f2a934689eb62ab6ee4b13d261a498ef5537097f79b89dbf52c27f3e912574e393c34c73c725e28f08dfc0c26876c234e0a19f3ad153c')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
