# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pystemmer
pkgname=('python-pystemmer' 'python2-pystemmer')
pkgver=1.3.0
pkgrel=3
pkgdesc="Snowball stemming algorithms, for information retrieval"
arch=('x86_64')
license=('BSD' 'MIT')
url="http://snowball.tartarus.org"
makedepends=('python' 'python2' 'cython' 'cython2')
source=("http://snowball.tartarus.org/wrappers/PyStemmer-$pkgver.tar.gz")
sha512sums=('35f40606347ead6a6068dae28aa24f5becfa0e52930998027a7b20955f331c6bd4762158267be7d40857ea8aae0084a07f663dbd48c4aa6aac10ce90f26c2a2d')

prepare() {
  # Force cython rebuild
  rm PyStemmer-$pkgver/src/Stemmer.c
 
  cp -a PyStemmer-$pkgver{,-py2}
}

build() {
  cd "$srcdir/PyStemmer-$pkgver"
  python setup.py build

  cd "$srcdir/PyStemmer-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/PyStemmer-$pkgver"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.7:$PYTHONPATH" python runtests.py

  cd "$srcdir/PyStemmer-$pkgver-py2"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 runtests.py
}

package_python-pystemmer() {
  depends=('python')

  cd PyStemmer-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pystemmer() {
  depends=('python2')

  cd PyStemmer-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
