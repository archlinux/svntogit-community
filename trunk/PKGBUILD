# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libmediainfo
pkgver=22.12
pkgrel=1

pkgdesc="shared library for mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('curl' 'glib2' 'libmms' 'libzen' 'tinyxml2' 'graphviz')

source=("https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz")

sha512sums=('aed358eb550b83b7623ec75481575f4fe79af80d685682a73f5d7b5396643940a5b226f61492a144a0ed481a7b6cf7658e27e117b22a425830d7271c0a3b4121')
b2sums=('e59dabeb4cca7093601e17ded2a123f18dcb807f1dc08c101fe8af37e647ebd6f2ff38dd160407d4d2b18646a9609885b3ef41de04b74d476be30c11ac57b26a')

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms \
    --with-libtinyxml2 \
    --with-graphviz

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
