# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=streamlink
pkgver=5.3.0
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player (livestreamer fork)'
arch=('any')
url='https://streamlink.github.io/'
license=('BSD')
depends=("python-"{certifi,isodate,pycountry,pycryptodome,pysocks,requests,websocket-client})
checkdepends=("python-"{freezegun,pytest,pytest-asyncio,requests-mock})
makedepends=("python-"{build,installer,setuptools,versioningit,wheel})
optdepends=('ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('E2B794C7C2C37162E5E2A097E3DB9E282E390FA0') # Streamlink Signing Key <streamlink@protonmail.com>
sha256sums=('fbd3124b33d867c83039e40b7a1478d529257dd7149fc3dae9323ffe587db701'
            'SKIP')
sha512sums=('7fc18c0604729df358ba396a21e4aa646344d1a1f320ce84831bc13c207a6dd64b2bd6679dc1712483764edd01bd87347d7eca2e3c8d4f6f2113db9c03737343'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$PWD/tmp" dist/*.whl
  local pyver=$(python -c 'import sys;print(".".join(map(str,sys.version_info[:2])))')
  TZ=UTC PYTHONPATH="$PWD/tmp/usr/lib/python${pyver}/site-packages:$PYTHONPATH" python -m pytest
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
