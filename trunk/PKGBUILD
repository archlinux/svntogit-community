# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-latex2mathml
pkgver=3.75.1
pkgrel=1
pkgdesc="Pure Python library for LaTeX to MathML conversion"
url="https://github.com/roniemartinez/latex2mathml"
license=('MIT')
arch=('any')
depends=('python-setuptools')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-multidict' 'python-xmljson')
source=("https://github.com/roniemartinez/latex2mathml/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('41204a97fa25cd5b011d2d43b36e87a1cc263b241cdca112976975a0860ff1f7e1e9b0f3955aa599449ee51cdd7d0c9958ce815eb9ddf518e1e19a67c87d1d05')

prepare() {
  cd latex2mathml-$pkgver
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd latex2mathml-$pkgver
  python -m build -nw
}

check() {
  cd latex2mathml-$pkgver
  pytest
}

package() {
  cd latex2mathml-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
