# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hopenpgp-tools
pkgver=0.23
pkgrel=54
pkgdesc="hOpenPGP-based command-line tools"
url="http://floss.scru.org/hopenpgp-tools"
license=("AGPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-binary-conduit' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-crypto-pubkey'
         'haskell-cryptohash' 'haskell-errors' 'haskell-fgl' 'haskell-graphviz' 'haskell-hopenpgp'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types' 'haskell-ixset-typed'
         'haskell-lens' 'haskell-monad-loops' 'haskell-openpgp-asciiarmor'
         'haskell-optparse-applicative' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal' 'haskell-prettyprinter-convert-ansi-wl-pprint'
         'haskell-resourcet' 'haskell-time-locale-compat' 'haskell-yaml')
makedepends=('alex' 'happy' 'ghc')
source=(https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e7b64f210579bef1a2b80d66a388bbc908c1910ecf919adcff664f50c65826e62d03864d7c4dcc5dd996a35c8f68f83116e1eef44cb7cb462b8ba44e899ab45f')

prepare() {
    cd $pkgname-$pkgver
    sed -i 's/fail (show l/error (show l/' HOpenPGP/Tools/Parser.y
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    rm "$pkgdir/usr/share/doc/hopenpgp-tools/LICENSE"
    rmdir "$pkgdir/usr/share/doc/hopenpgp-tools" "$pkgdir/usr/share/doc" "$pkgdir/usr/share"
}
