# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=netbeans
pkgver=7.0.1
pkgrel=2
pkgdesc='IDE for Java, PHP, Groovy, C, C++, Ruby and Python'
arch=('any')
url='http://netbeans.org/'
license=('CDDL')
depends=('java-environment')
optdepends=('php: for developing programs in php'
            'groovy: for developing programs in groovy'
            'ruby: for developing programs in ruby'
            'python: for developing programs in python')
install=netbeans.install
source=("http://download.netbeans.org/$pkgname/$pkgver/final/zip/$pkgname-$pkgver-201107282000-ml.zip"
        'netbeans.desktop')
md5sums=('4d3c76236e3424a49e99023dfe228124'
         '883e57720cb1da63f2079b121f9b5380')

build() {
  cd "$srcdir/$pkgname"

  for s in '*.exe' '*.bat' 'windows*' 'sunos*' 'mac*' 'solaris*' 'hpux*'; do 
    find . -depth -iname $s -exec rm -rf {} \;
  done
}

package() {
  cd "$srcdir/$pkgname"

  install -d "$pkgdir/usr/share"
  # TODO: Pick out all the individual libraries and binary files...
  cp -r "../$pkgname" "$pkgdir/usr/share"
  install -d "$pkgdir/usr/share/applications"
  install -m644 ../netbeans.desktop "$pkgdir/usr/share/applications"
  install -m644 nb/netbeans.png "$pkgdir/usr/share/pixmaps"
  install -d "$pkgdir/usr/bin"
  ln -s /usr/share/netbeans/bin/netbeans "$pkgdir/usr/bin/netbeans"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
