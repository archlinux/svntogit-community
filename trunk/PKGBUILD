# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-meta
pkgname=haskell-hspec-meta
pkgver=2.4.6
pkgrel=12
pkgdesc="A version of Hspec which is used to test Hspec itself"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-hspec-expectations' 'haskell-hunit'
         'haskell-setenv' 'haskell-random' 'haskell-quickcheck-io' 'haskell-ansi-terminal'
         'haskell-async' 'haskell-call-stack')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8e62dd923e943a3242034d838016280ed3e2d6767c5c22fac76c71433433a938f5e290cd79400cdc659706c54f21a06805b54e91702acdb0daea20c8513e8d8f')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
