# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>

pkgname=hyprpaper
pkgver=0.1.0
pkgrel=1
pkgdesc='a blazing fast wayland wallpaper utility with IPC controls'
arch=(x86_64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD)
depends=(cairo
         libglvnd
         libjpeg
         pango
         wayland)
makedepends=(cmake
             wayland-protocols
             xorgproto)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('26264ef57f554532247e317f2a4202a60f8db2b38ea606498d88333e1507d959')

build() {
	cd "$_archive"
	cmake \
		--no-warn-unused-cli \
		-G "Unix Makefiles" \
		-D CMAKE_BUILD_TYPE=Release \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-B build
	make protocols
	make -C build all
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "build/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$_pkgname" LICENSE
}
