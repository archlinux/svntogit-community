# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <sergej+arch@p5n.pp.ru>
# Old Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

_version=2.4.7-04
pkgname=mod_itk
pkgver=${_version//-/.r}
pkgrel=4
pkgdesc='Apache mpm itk module that allows you to run each of your vhost under a separate uid and gid'
arch=(x86_64)
url='http://mpm-itk.sesse.net/'
license=(APACHE)
depends=(apache)
source=(http://mpm-itk.sesse.net/mpm-itk-$_version.tar.gz
	65627.patch)
sha256sums=('609f83e8995416c5491348e07139f26046a579db20cf8488ebf75d314668efcf'
            'ca15f1244e4afb0d77f3ea4173d0ae8ffd5f89c7da445b512e3b791949f68576')

prepare() {
  cd mpm-itk-$_version
  # https://bz.apache.org/bugzilla/show_bug.cgi?id=65627
  patch -p1 mpm_itk.c <../65627.patch
}

build() {
  cd mpm-itk-$_version
  ./configure --prefix=/usr
  make
}

package() {
  cd mpm-itk-$_version
  install -D -m755 .libs/mpm_itk.so "$pkgdir/usr/lib/httpd/modules/mpm_itk.so"
}
