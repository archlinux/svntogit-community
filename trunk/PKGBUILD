# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marc Moody <marc.r.moody at gmail dot com>
# Contributor: Dinh Bowman <dinh at vagueindustries dot com>

pkgname=rapid-photo-downloader
pkgver=0.9.14
pkgrel=1
pkgdesc="Photo and video downloader"
arch=('any')
url="https://www.damonlynch.net/rapid/"
license=('GPL3')
depends=('desktop-file-utils' 'gstreamer' 'hicolor-icon-theme' 'libgexiv2' 'libnotify'
         'perl-image-exiftool' 'python-arrow' 'python-colour' 'python-dateutil' 'python-easygui'
         'python-gobject' 'python-gphoto2' 'python-psutil' 'python-pymediainfo' 'python-pyqt5'
         'python-pyzmq' 'python-rawkit' 'python-requests' 'python-sortedcontainers'
         'python-tornado' 'python-xdg' 'qt5-imageformats' 'udisks2')
makedepends=('intltool' 'python-setuptools')
changelog=$pkgname.changelog
source=(https://launchpad.net/rapid/pyqt/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('a8080956c366c0981d0a42414799bb0999228f99497dab921ef85fdafd9cdcdb'
            'SKIP')
validpgpkeys=('E26048A9F4A803B91CB1BD648005B1F36970BE28') # Damon Lynch <damonlynch@gmail.com>

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  python setup.py build
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  python setup.py install --root="${pkgdir}" --optimize=1
}
