# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.250
pkgrel=1
pkgdesc='An extremely fast Python linter, written in Rust'
arch=(x86_64)
url="https://github.com/charliermarsh/$pkgname"
license=(MIT)
depends=(gcc-libs glibc)
makedepends=(cargo)
options=(!lto)
_archive="$pkgname-$pkgver"
source=($url/archive/refs/tags/v$pkgver/$_archive.tar.gz)
sha512sums=('02b32ab17c8d322dbe614dd10b603b647e76d88b478f6751ea8f7c564724b3f13a19ba44a625c19539005a255358b8721f901c6283762afca618324494fcf0f3')
b2sums=('329b6aab27cbbc188141ff4787d01287a9df3ad5db21e632481d08a8f1017143b9966c67181dfbea1929f04610fb57d764525ef63b1c372ef2b1a17edf0cdc26')

prepare() {
  cd "$_archive"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_archive"
  cargo build -p ruff_cli --frozen --release --all-features
}

check() {
  cd "$_archive"
  cargo test -p ruff_cli --frozen --all-features
}

package() {
  cd "$_archive"
  local _target="target/release/$pkgname"
  install -Dm0755 -t "$pkgdir/usr/bin/" "$_target"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  $_target --generate-shell-completion bash | install -Dm0644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$pkgname.bash"
  $_target --generate-shell-completion fish | install -Dm0644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  $_target --generate-shell-completion zsh  | install -Dm0644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
