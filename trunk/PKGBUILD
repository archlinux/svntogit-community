# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=camlp4
pkgver=4.09
pkgrel=1
pkgdesc='Caml preprocessor and pretty-printer'
arch=('x86_64')
license=('GPL2')
url='https://github.com/ocaml/camlp4/releases'
depends=('ocaml>=4.10.0')
makedepends=('git' 'ocamlbuild' 'ocaml-compiler-libs')
options=('!makeflags' '!emptydirs' 'staticlibs')
source=("https://github.com/ocaml/camlp4/archive/4.09+1.tar.gz" "0001-compile-with-4.10-AST.patch")
sha256sums=('7b4a8b0632dfb06b85b95c400fcf700230997246240e30d74ff996a36763282c'
            '2750badbbcc10e2c26afbf5b9ca8620d50a15872c2a3b2cf2975860d01c6d50e')

prepare() {
  cd "${srcdir}/$pkgname-${pkgver}-1"
  patch -i "${srcdir}/0001-compile-with-4.10-AST.patch" -p1
}

build() {
  cd "${srcdir}/$pkgname-${pkgver}-1"
  ./configure
  make all camlp4/META
}

package() {
  cd "${srcdir}/$pkgname-${pkgver}-1"
  make \
    BINDIR="$pkgdir/usr/bin" \
    LIBDIR="$pkgdir/usr/lib/ocaml" \
    PKGDIR="$pkgdir/usr/lib/ocaml" \
    install install-META
}

# vim:set ts=2 sw=2 et:
