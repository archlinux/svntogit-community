# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-google
pkgver=0.39.0
pkgrel=2
pkgdesc="Google Cloud DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-mock' 'python-google-api-python-client'
         'python-oauth2client' 'python-setuptools' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('93c4680e974de81013f5fd6453310a2cc46d56a3f1f33ebc1aaa73b9b9e730e2b244387c401024297d58da32587ede8f37783d814a67616beaa7efd0e83c4b0d'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
