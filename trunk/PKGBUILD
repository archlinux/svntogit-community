# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=libtracefs
pkgname=(libtracefs libtracefs-docs)
pkgver=1.4.2
pkgrel=1
pkgdesc="Linux kernel trace file system library"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/about/"
license=(GPL2 LGPL2.1)
makedepends=(asciidoc libtraceevent xmlto)
provides=(libtracefs.so)
options=(debug)
source=(
  $pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/snapshot/$pkgname-$pkgver.tar.gz
  $pkgname-1.2.0-docs_makefile.patch
  $pkgname-1.4.2-ldflags.patch
)
sha512sums=('4a66c3cf8050de5acc4c63996cbcfa2c014e26ad6011eb89b0cff47ffbd6343637009354edf6bb173b9fd08103012774376c33fd6f642741f7990f2d6de3ea0c'
            'bdfffba9957d889c60baea9c197709bf1a62d30ac4b8fd47d930b8d5b67fa040817b9a835e226dd1201f2ccdb7fcb43a12093c2b559fb6c0edc3e9ba167062c2'
            '6b3ffa2dee59edf1ce376b23f38edb29720c1fced4beae3a2e6d8358430839c929bd52c1a3fa193cd3cc86e128dd9a45d79be3ddfebffe1bae4e690f48403864')
b2sums=('a956423370077613417812ad18da284521e8798701d558f4e419af6c43bd8a06c489525bbab26e6bc5d5d5e8fd0e6a7322d34f976a4ea560670eb594818d07fc'
        '1340af2c65dc344912715c69827d5849fda83e4bcf136da3ccdbee4e3b508e9b557af0828914dff8d27abebbdbdab975d8800432d5252b6b1eb0da08b0cb4b02'
        '56b7f27ed2913e1c85b133e5ec15f632bcfa54a530149e3b1450cb7bc9b951c1853fad25f9d467772334471986f492baf198710086f1ab2050932c8c7b96af50')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -d $pkgname-$pkgver -Np 1 -i ../$pkgname-1.2.0-docs_makefile.patch
  patch -d $pkgname-$pkgver -Np 1 -i ../$pkgname-1.4.2-ldflags.patch
}

build() {
  make -C $pkgname-$pkgver
  make sqlhist -C $pkgname-$pkgver
  make doc -C $pkgname-$pkgver
}

package_libtracefs() {
  depends+=(glibc libtraceevent.so)
  optdepends=('libtracefs-docs: for documentation')

  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/Documentation
  install -vDm 755 $pkgname-$pkgver/bin/sqlhist -t "$pkgdir/usr/bin/"

  (
    cd "$pkgdir"
    _pick libtracefs-docs usr/share/doc
  )
}

package_libtracefs-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}
