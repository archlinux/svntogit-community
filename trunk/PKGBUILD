# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=patchmatrix
pkgver=0.20.0
pkgrel=1
pkgdesc="A JACK patchbay in flow matrix style"
arch=('x86_64')
url="https://git.open-music-kontrollers.ch/lad/patchmatrix/about/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'libglvnd' 'libx11')
makedepends=('glu' 'jack' 'lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lad/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('5be32eaad63bd338d7bbce127a95e44ed4450818904e1de757d4dc2c03cb60032e9fb5999e335b2e17530e1cd36ca9398b2b965fddc42f8c5f76faa323521bf3'
            'SKIP')
b2sums=('73779744c27d87365cadd0bb76d18d1a5c4e307ebeb1dd21099d020b2969e82adeeeb5e92ed34692f74f49848f540510b2afca431ec4148845c0fa98f505840d'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  depends+=('libjack.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
