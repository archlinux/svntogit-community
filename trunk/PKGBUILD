# $Id$
# Maintainer: Alad Wenter <alad@mailbox.org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Chris Salzberg <chris@dejimata.com>
# Contributor: Leonidas Spyropoulos <artafinde@gmail.com>

pkgname=neomutt
pkgver=20171215
pkgrel=3
pkgdesc='a version of mutt with added features'
url='http://www.neomutt.org/'
license=('GPL')
arch=('x86_64')
depends=('notmuch-runtime' 'lua')
optdepends=('python: keybase.py')
makedepends=('git' 'gnupg' 'libxslt' 'docbook-xsl' 'lynx')
source=("https://github.com/neomutt/neomutt/archive/$pkgname-$pkgver.tar.gz"
        "https://github.com/neomutt/neomutt/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz.sig")
sha256sums=('7fb76e99a9f23715ad772ad8f7008c6e2db05eed344817055176c76dbd60c1b5'
            'SKIP')
validpgpkeys=('86C2397270DD7A561263CA4E5FAF0A6EE7371805') # Richard Russon (flatcap) <rich@flatcap.org>

build() {
    cd "$pkgname-$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --gpgme \
        --enable-lua \
        --notmuch \
        --gss \
        --ssl \
        --sasl \
        --with-ui=ncurses \
        --with-idn=/usr \
        --gdbm
    make
}

package() {
    cd "$pkgname-$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
