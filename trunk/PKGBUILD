# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000146
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
depends=('perl-module-pluggable' 'perl-importer' 'perl-scope-guard' 'perl-sub-info'
         'perl-term-table' 'perl-test-simple>=1.302176')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('abecf34febe6a4ca5557676ba3b7a315714ffba550209e9b7a46d81e934663da3aee991ee9149b0d19bfa1e57d6f788a49c429ea9a5140ef9f93d0c4da5909ba')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
