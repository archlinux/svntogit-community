# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=bspwm
pkgver=0.9.1
pkgrel=2
pkgdesc='Tiling window manager based on binary space partitioning'
arch=('x86_64' 'i686')
url='https://github.com/baskerville/bspwm'
license=('custom:BSD')
depends=('libxcb' 'xcb-util' 'xcb-util-wm')
makedepends=('git')
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'terminus-font: for the example panel'
            'bar-aint-recursive: for the example panel'
            'xtitle: for the example panel'
            'sutils: for the example panel')
source=("git://github.com/baskerville/$pkgname#tag=$pkgver")
md5sums=('SKIP')

build() {
  make -C "$pkgname" PREFIX=/usr
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: -u 2 raw.githubusercontent.com/baskerville/bspwm/master/doc/bspwm.1
# vim:set ts=2 sw=2 et:
