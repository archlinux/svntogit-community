# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cpanplus
pkgver=0.9156
pkgrel=1
pkgdesc='Ameliorated interface to the CPAN'
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl' 'perl-log-message' 'perl-archive-extract' 'perl-term-ui' 'perl-log-message-simple'
         'perl-module-pluggable' 'perl-object-accessor' 'perl-package-constants')
url=https://metacpan.org/release/CPANPLUS
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPANPLUS-$pkgver.tar.gz")
md5sums=('99134c27de949d2734278c4b121e5b3f')
sha512sums=('1982f731e3c384e2ad0b6a6bfa88d5b510858d01f987b8665151d644088b8ca06517e2651e4cad8573fb6bfd9720a1e2eae834b5fba935f201b0da33d2496ce3')
_ddir="CPANPLUS-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
