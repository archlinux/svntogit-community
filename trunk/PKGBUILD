# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lib32-orc
pkgver=0.4.28
pkgrel=1
pkgdesc='The Oild Runtime Compiler'
url='https://cgit.freedesktop.org/gstreamer/orc'
arch=('x86_64')
license=('custom')
url='https://code.entropywave.com/projects/orc/'
depends=('lib32-glibc' 'orc')
makedepends=('gtk-doc' 'meson' 'valgrind-multilib')
source=("https://gstreamer.freedesktop.org/data/src/orc/orc-${pkgver}.tar.xz"{,.asc})
validpgpkeys=('D78BC13E3280CB8AFDE278EA032D3D833A0BA62A') # Edward Hervey
sha256sums=('bfcd7c6563b05672386c4eedfc4c0d4a0a12b4b4775b74ec6deb88fc2bcd83ce'
            'SKIP')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson ../orc-${pkgver} \
    --libdir='/usr/lib32'
  ninja
}

package() {
  cd build

  DESTDIR="$pkgdir" ninja install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s orc "${pkgdir}"/usr/share/licenses/lib32-orc
}

# vim: ts=2 sw=2 et:
