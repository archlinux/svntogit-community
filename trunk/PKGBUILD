# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=cardinal
pkgname=(cardinal cardinal-clap cardinal-data cardinal-jack cardinal-lv2 cardinal-vst cardinal-vst3)
pkgver=22.10
pkgrel=1
pkgdesc="Virtual modular synthesizer"
arch=(x86_64)
url="https://github.com/DISTRHO/Cardinal"
license=(GPL3)
makedepends=(dbus file gcc-libs gendesk git glibc jansson libarchive libglvnd
liblo libsndfile libsamplerate libx11 libxext libxrandr mold python speexdsp)
checkdepends=(kxstudio-lv2-extensions mod-lv2-extensions lv2 lv2lint xorg-server-xvfb)
options=(debug)
source=(
  https://github.com/DISTRHO/$pkgbase/releases/download/$pkgver/$pkgbase-$pkgver.tar.xz
  $pkgbase-$pkgver.svg::https://raw.githubusercontent.com/DISTRHO/Cardinal/eb95b5990cf734c10f2caab1a246eac893f0266d/plugins/Cardinal/orig/distrho.svg
  $pkgbase-22.10-clap_install_target.patch::https://github.com/DISTRHO/Cardinal/commit/373ff58766f1f72e60284b260b241a08f7eb2249.patch
)
sha512sums=('e4ef72e31d30ecb67c01bfa7df9b3e81c8c48d1e2af2d320a335c37d263a5b84f9e63779337ccbdcbb1ef8810fc2b2af275c52c7f2e73400f6f6a9dfdb5df5cd'
            '31a7d1e548285af0ead1bc844fbb1d35b50f6284159047cb401a829d4068992fa328770e42c377c08d013f6787e55ac12b94eba4d5af4b90373b157971fc8835'
            'c69c6301e9364f3c9747eaaa0352d002eab2f4241f884a263f0a2068baed46df9189c937e24d6df6e41173f5694326af294df01d9936b54920cd2675736341dc')
b2sums=('3afb75dea4733108d34557fa3585049b36869ed6662d72598269ffaa75bf17306f4b46c581c784bdc6baf977dae19e6695c4234453db92e52be8acc9c2e7e1f8'
        'd82fefe15234c1ed3a4d487c6082a2e3ac73b60f11041314e99c5a5de5b3ea141efbd5afd18851510bd4f801fd71f7cd89d54d86258d1ba750bbfd762aa37a8d'
        '74f366b9b377961817af860e25153ede6be0578b31705cb18914183899e0307ac5f4eac7afe5a734e6e53ab2e21bad8d8e880e0fc25b43ded188787951fcaada')

_common_depends=(
  cardinal-data
  dbus libdbus-1.so
  file libmagic.so
  gcc-libs
  glibc
  jansson
  libarchive libarchive.so
  libglvnd libGL.so
  liblo liblo.so
  libsamplerate libsamplerate.so
  libsndfile libsndfile.so
  libx11
  libxext
  libxrandr
  speexdsp libspeexdsp.so
)
_common_optdepends=(
  'carla: for carla module and ildaeil plugin host'
  'mpv: for experimental video support'
  'xdg-desktop-portal-impl: for file browser'
)

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  # add clap plugin to install target
  patch -Np1 -d $pkgbase-$pkgver -i ../$pkgbase-22.10-clap_install_target.patch

  gendesk -n \
          --exec Cardinal \
          --name Cardinal \
          --pkgname studio.kx.distrho.Cardinal \
          --pkgdesc "$pkgdesc JACK standalone" \
          --icon $pkgbase \
          --genericname "Virtual modular synthesizer"
}

build() {
  export CFLAGS+=" -B/usr/lib/mold -Wno-format-security"
  export CXXFLAGS+=" -B/usr/lib/mold -Wno-format-security"
  make PREFIX=/usr SYSDEPS=true WITH_LTO=true -C $pkgname-$pkgver
}

check() {
  declare -A _links=(
    ["Cardinal"]="https://distrho.kx.studio/plugins/cardinal"
    ["CardinalFX"]="https://distrho.kx.studio/plugins/cardinal#fx"
    ["CardinalSynth"]="https://distrho.kx.studio/plugins/cardinal#synth"
  )

  # NOTE: lvlint fails on Cardinal, as it makes use of non-standard Port Class, which it does not support yet
  for _name in Cardinal{FX,Synth}; do
    xvfb-run lv2lint -s "lv2_generate_ttl" -Mpack -I $pkgname-$pkgver/bin/$_name.lv2 "${_links[$_name]}"
  done
}

package_cardinal() {
  depends=(cardinal-jack cardinal-lv2 cardinal-vst cardinal-vst3)

  make PREFIX=/usr SYSDEPS=true DESTDIR="$pkgdir" install -C $pkgbase-$pkgver

  (
    cd "$pkgdir"
    _pick $pkgbase-clap usr/lib/clap/
    _pick $pkgbase-data usr/share/$pkgbase
    _pick $pkgbase-data usr/share/doc
    _pick $pkgbase-jack usr/bin/
    _pick $pkgbase-lv2 usr/lib/lv2/
    _pick $pkgbase-vst usr/lib/vst/
    _pick $pkgbase-vst3 usr/lib/vst3/
  )
}

package_cardinal-clap() {
  pkgdesc+=" - CLAP plugin"
  groups=(clap-plugins pro-audio)
  depends=("${_common_depends[@]}" clap-host)
  optdepends=("${_common_optdepends[@]}")

  mv -v $pkgbase-clap/* "$pkgdir"
}

package_cardinal-data() {
  pkgdesc+=" - data"

  mv -v $pkgbase-data/* "$pkgdir"
}

package_cardinal-jack() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=("${_common_depends[@]}" hicolor-icon-theme jack)
  optdepends=("${_common_optdepends[@]}")

  mv -v $pkgbase-jack/* "$pkgdir"
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications"
  install -vDm 644 $pkgbase-$pkgver.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgbase.svg"
}

package_cardinal-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(lv2-plugins pro-audio)
  depends=("${_common_depends[@]}" lv2-host)
  optdepends=("${_common_optdepends[@]}")

  mv -v $pkgbase-lv2/* "$pkgdir"
}

package_cardinal-vst() {
  pkgdesc+=" - VST2 plugin"
  groups=(pro-audio vst-plugins)
  depends=("${_common_depends[@]}" vst-host)
  optdepends=("${_common_optdepends[@]}")

  mv -v $pkgbase-vst/* "$pkgdir"
}

package_cardinal-vst3() {
  pkgdesc+=" - VST3 plugin"
  groups=(pro-audio vst3-plugins)
  depends=("${_common_depends[@]}" vst3-host)
  optdepends=("${_common_optdepends[@]}")

  mv -v $pkgbase-vst3/* "$pkgdir"
}
