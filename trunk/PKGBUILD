# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-music
pkgver=7.0.1
pkgrel=1
pkgdesc='The Pantheon Music Player'
arch=(x86_64)
url=https://github.com/elementary/music
license=(GPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gst-plugins-base-libs
  gstreamer
  libgee
  libgranite-7.so
  pango
)
makedepends=(
  git
  gobject-introspection
  intltool
  meson
  vala
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
)
_tag=9072b41e932c94441dab1402dfb1748d17d8e431
source=(pantheon-music::git+https://github.com/elementary/music.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-music
  git describe --tags
}

build() {
  arch-meson pantheon-music build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
