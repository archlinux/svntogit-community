# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=react-native-debugger
pkgver=0.10.6
_commit=6b9f4535d4524d5f828b8ba939615c861b89e4a6
pkgrel=1
pkgdesc='The standalone app for React Native Debugger, with React DevTools / Redux DevTools'
arch=('any')
url='https://github.com/jhen0409/react-native-debugger'
license=('MIT')
depends=('electron')
makedepends=('asar' 'git' 'yarn')
source=("git+https://github.com/jhen0409/react-native-debugger.git#commit=${_commit}"
        'iconPath.patch'
        'react-native-debugger.desktop'
        'react-native-debugger.js')
sha256sums=('SKIP'
            '3db29267301692d6362a2ff4971299de4a0f3f49feb12c338e4ec02a5b0e560d'
            'f63e79516db0feb2e76c366402cf266165c0f7eb282dc55703b800b91152867d'
            '8cd406c3b2d091f2a2b73e5755682dc925bb1f75d0d425b82507136b350a4ceb')

pkgver() {
    cd ${pkgname}
    git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
    cd ${pkgname}

    patch -Np1 -i ../iconPath.patch
}

build() {
    cd ${pkgname}

    yarn --ignore-scripts
    cd dist
    yarn
    node ../scripts/patch-modules.js
    cd ..
    yarn build
}

package() {
    cd ${pkgname}

    install -dm755 "${pkgdir}/usr/lib"
    rm dist/logo.png  # see iconPath.patch
    asar pack dist "${pkgdir}/usr/lib/${pkgname}.asar"

    install -Dm755 ../${pkgname}.js "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/applications" ../${pkgname}.desktop
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.md
    install -Dm644 electron/logo.png \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
