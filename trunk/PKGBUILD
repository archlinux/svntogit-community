# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-recipes
pkgver=1.6.2
pkgrel=1
pkgdesc="Recipe management application for GNOME"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Recipes"
license=(GPL3)
depends=(gnome-autoar gspell libcanberra libsoup)
makedepends=(git itstool meson)
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('8a11ed483b422dbbedb10237dac8b47fe94d76d40cac87ae6d19292942c94f9c')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  # Fix build
  sed -i 's/default/value/' meson_options.txt
}

build() {
  cd build
  meson --prefix=/usr --buildtype=release ../$pkgname-$pkgver
  ninja
}

check() {
  cd build
  # FAIL
  # ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
