# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.8
pkgrel=1
pkgdesc="A tool for monitoring webpages for updates"
arch=('any')
url="https://thp.io/2008/urlwatch/"
license=('BSD')
depends=('python-appdirs' 'python-keyring' 'python-minidb' 'python-requests' 'python-yaml')
optdepends=('lynx: convert HTML to plain text'
            'python-beautifulsoup4: prettify HTML'
            'python-html2text: convert HTML to plain text')
source=("$pkgname-$pkgver.tar.gz::https://github.com/thp/urlwatch/archive/$pkgver.tar.gz")
sha256sums=('e0b75f81920f547b01e6bc1cfb34eff1a0c9513914ed61013876b8b8d2fe20fb')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
