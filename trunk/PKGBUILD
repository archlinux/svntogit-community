# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libstrophe
pkgver=20131107
pkgrel=1
pkgdesc='Simple, lightweight C library for writing XMPP clients'
arch=('i686' 'x86_64')
url='http://strophe.im/libstrophe/'
license=('MIT' 'GPL3')
makedepends=('git' 'doxygen' 'expat')
checkdepends=('check')
options=('staticlibs')
source=(git://github.com/strophe/libstrophe.git#commit=d408eaf)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git log -1 --format="%cd" --date=short | tr -d '-'
}

prepare() {
  cd $pkgname
  ./bootstrap.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
  doxygen
}

check() {
  make -C $pkgname check
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" install
}
