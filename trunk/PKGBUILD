# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-dbd-sybase
pkgver=1.09
pkgrel=2
pkgdesc="Sybase Driver for DBI"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBD-Sybase/"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi>=1.52' 'perl>=5.10.0' 'freetds')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/ME/MEWP/DBD-Sybase-$pkgver.tar.gz)
md5sums=('8ed2c2dd5df35cdee890fa8135e981ec')

build() {
  cd  $srcdir/DBD-Sybase-$pkgver
  export SYBASE=/usr
  echo -e "\n\n\n\n\n\n" | PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor || return 1

  patch -p1 dbdimp.c <<EOF
diff -r DBD-Sybase-1.08/dbdimp.c DBD-Sybase-1.08.my/dbdimp.c
13a14,17
> #define BLK_VERSION_120 BLK_VERSION_100
> #define BLK_VERSION_125 BLK_VERSION_100
> #define BLK_VERSION_150 BLK_VERSION_100
> 
EOF
  make || return 1
  make install DESTDIR=$pkgdir || return 1
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
