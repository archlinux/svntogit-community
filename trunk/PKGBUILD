# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=serd
pkgname=(serd serd-docs)
pkgver=0.30.14
pkgrel=1
pkgdesc="Lightweight C library for RDF syntax supporting reading/ writing Turtle and NTriples"
arch=(x86_64)
url="https://drobilla.net/software/serd.html"
license=(custom:ISC)
makedepends=(doxygen meson python-sphinx)
options=(debug)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('d753bf60d4ff37220e64fda6a5dc6c69dc1a52ef435ab5b1e698f72dc5a7280ea764bb53b015753c3dc0910f252ca96c0ebafcb902a9956fcbf0a4480ce4e1dc'
            'SKIP')
b2sums=('42ed92e8eed3564c2625dd2f8e9bff55b65d1c8c993f13f2aa7ea0ab41a4c261b6b8fe9d4c61a915157616df1dba11d37f3813cd7bf7cd4571cb535895cb8f9c'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package_serd() {
  depends=(glibc)
  optdepends=('serd-docs: for developer documentation')
  provides=(libserd-0.so)

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )

  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_serd-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/"serd{-0,}
}
