# Maintainer: Anatol Pomozov

pkgname=aws-sdk-cpp
pkgver=1.7.284
pkgrel=1
pkgdesc='AWS SDK for C++'
arch=(x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache)
depends=(openssl curl zlib libutil-linux aws-c-common aws-c-event-stream libpulse)
makedepends=(cmake)
source=(aws-sdk-cpp-$pkgver.zip::https://github.com/aws/aws-sdk-cpp/archive/$pkgver.zip)
sha256sums=('0bf89ab9738e99ebdbfbbc50109715cbceb26d108c716db497c5694d0c1372c7')

build() {
  cd aws-sdk-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCUSTOM_MEMORY_MANAGEMENT=OFF -DENABLE_TESTING=OFF -DBUILD_DEPS=OFF -S . -B build
  cmake --build build
}

package() {
  cd aws-sdk-cpp-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
