# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-shell
pkgver=5.1.0.10
pkgrel=1
pkgdesc='Deepin desktop-environment - session-shell module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-shell"
license=('GPL3')
depends=('deepin-daemon' 'deepin-qt5integration' 'deepin-wallpapers' 'gsettings-qt' 'liblightdm-qt5'
         'startdde')
makedepends=('cmake' 'ninja' 'qt5-tools')
conflicts=('deepin-session-ui<5')
provides=('lightdm-deepin-greeter')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-shell/archive/$pkgver.tar.gz"
        icon-size.patch)
sha512sums=('1e8703ddc0988a13ea8129db21207f93896b24475fe081d0b382dde599cf6007c6209dcd8d18033c8006e3d31d65381789c62ec1a2200818eb721ee43001c885'
            'bfd3b15a5dce361eea2f8ced63a240edbbc19096a372963dc788d45faa372f841db2b2a6f29cb87aebadc00c921d56a99b21cec951e595acf220c8fc687395b6')

prepare() {
  cd dde-session-shell-$pkgver
  patch -p1 -i ../icon-size.patch
  sed -i '/darrowrectangle/d' CMakeLists.txt src/widgets/widgets.pri
  sed -i 's/include "darrowrectangle.h"/include <darrowrectangle.h>/' src/widgets/errortooltip.h
  sed -i '/#include <QFile>/a #include <QPainterPath>' src/widgets/useravatar.cpp

  # We don't have common-auth on Arch
  sed -i 's/common-auth/system-login/' src/libdde-auth/authagent.cpp
}

build() {
  cd dde-session-shell-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-session-shell-$pkgver
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/deepin-greeter
}
