# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cryptohash-sha1
pkgname=haskell-cryptohash-sha1
pkgver=0.11.101.0
pkgrel=59
pkgdesc="Fast, pure and practical SHA-1 implementation"
url="https://github.com/hvr/cryptohash-sha1"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-sha' 'haskell-base16-bytestring' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4384fc2f63750400561b5dbea0a343510306d755131121346d6a52cb9420cb12fc7d20247f044beb0ea55ae254ce666edc982d76e999c0762894f405e9b6441e')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
