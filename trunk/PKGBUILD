# Maintainer: christian rebischke <chris.rebischke@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=gopass-jsonapi
pkgver=1.15.5
pkgrel=1
pkgdesc="jsonapi for gopass used for bridging to browsers"
arch=('x86_64')
url="https://github.com/gopasspw/gopass-jsonapi"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass-jsonapi/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('dbc8de9cf16f1c20af1e94bc1c0a7c168b76db28a889f0f51d04f8a922f6ec70fd6b921ef5413708bd6832f79e82521f48feef3f9eb9742062d1e2c806ff3067')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  sed -i 's/install-completion//g' Makefile
}

build(){
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
