# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fuzzel
pkgver=1.8.2
pkgrel=1
pkgdesc='Application launcher for wlroots based Wayland compositors'
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
makedepends=(meson
             scdoc
             tllist
             wayland-protocols)
depends=(cairo
         fcft
         libfcft.so
         libpng
         libxkbcommon
         pixman
         wayland) # nanosvg is bundled
options=(debug)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('2e7debba9d56a989921e0ce518a026152d9fbea33abafe384a4aad074db89de8')

build() {
	cd "$pkgname"
	arch-meson \
		-Denable-cairo=enabled \
		-Dpng-backend=libpng \
		-Dsvg-backend=nanosvg \
		. build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
