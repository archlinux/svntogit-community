# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

pkgname=grunt-cli
pkgver=1.3.2
pkgrel=1
pkgdesc="The grunt command line interface."
arch=('any')
url="https://gruntjs.com/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz"
        "$pkgname-$pkgver.LICENSE::https://github.com/gruntjs/grunt-cli/raw/v$pkgver/LICENSE-MIT")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('f0e1c3899664729b71557b4c7c3c49be637b31524d13c2ffc8870f6f81c1ed3972143d640ef8c7adbeb6ba1c92b14d78c09c7d3913274ee851310e34947fe8ad'
            'e10d81276cacf81f58deca8411a4d3ab0d5213ef4829d755ea0056c0da5ccad600e3b172cdc47949b07b575cf03773f64d9e46b644f99764a051f7f3bca5f903')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # install completions properly
 install -Dm644 "$pkgdir"/usr/lib/node_modules/grunt-cli/completion/bash \
   "$pkgdir"/usr/share/bash-completion/completions/grunt-cli
 install -Dm644 "$pkgdir"/usr/lib/node_modules/grunt-cli/completion/zsh \
   "$pkgdir"/usr/share/zsh/site-functions/_grunt-cli

  install -Dm644 "$srcdir"/$pkgname-$pkgver.LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
