# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-flask-socketio
_pkgname=Flask-SocketIO
pkgver=5.3.2
pkgrel=1
pkgdesc='Socket.IO integration for Flask applications'
url='https://flask-socketio.readthedocs.org/'
arch=('any')
license=('MIT')
depends=('python' 'python-flask' 'python-eventlet' 'python-socketio')
makedepends=('python-setuptools' 'python-sphinx' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-coverage' 'python-pytest' 'python-tox-current-env' 'flake8' 'python-redis')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('aef66512c4e5e3b887e91800068b0c9a38c962e2476b97e1b9a145e7e6e7ce433d48f39ebef9ff784d7459d7c4e3a2932b77731c2009de2e698810568bf12115')
b2sums=('84967a1d6cd5a31334f5ac451608d42b750e05f88e43b95a7c96409a662148831ac84ce55217c20f92bdaed7b2e5ec0f139f670debbcb5f5d2453695a2fd9f31')

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pkgname}-${pkgver}
  PYTHONPATH="build/lib" pytest test_socketio.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
