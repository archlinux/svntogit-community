# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgname=apper
pkgver=1.0.0
pkgrel=5
pkgdesc='An application and package manager using PackageKit'
arch=(x86_64)
url='https://www.kde.org/applications/system/apper/'
license=(GPL)
depends=(packagekit-qt5 kcmutils appstream-qt qt5-xmlpatterns)
makedepends=(extra-cmake-modules kdoctools plasma-workspace)
optdepends=('plasma-workspace: Updates daemon')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig} apper-rpath.patch
        apper-qt5.11.patch::https://invent.kde.org/system/apper/-/commit/d8960ea9.patch
        apper-qt5.14.patch::https://invent.kde.org/system/apper/-/commit/1815d19f.patch
        kdebug-401461.patch::https://invent.kde.org/system/apper/-/commit/d486706f.patch
        apper-qt5.15.patch::https://invent.kde.org/system/apper/-/commit/f2fb923e.patch)
sha256sums=('1a30be92aab8bd258c2a8824f533c5646b934e06b4268edbd11724ea450f4923'
            'SKIP'
            '17ce82a3f9ba8888febc5d2b2dc63e5c73d7be4b216e9110f0ca73050be0010d'
            '667301c14bc6b4c83eaf78b5c5724f1a6cfe7f49aadd3a3fde2e3e1192f9374e'
            'a662281b518929c5ae1d91bc829d60a02a7cf20fb434a1a3809017c5dc971312'
            'fedc93b12289136b777d3b4441ce7997bea089c996463eec491328f2e6c1f390'
            'b91e81fc476c443f6e20920476f5907e5e1695e5551595b42b866607ba2bcb9c')
validpgpkeys=(70C26659D184ABC01FFAF45228DDEDC6E4480AD0) # Daniel Nicoletti <dantti12@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < apper-rpath.patch # Fix RPATH of apper binary
  patch -d $pkgname-$pkgver -p1 < apper-qt5.11.patch # Fix build with Qt 5.11
  patch -d $pkgname-$pkgver -p1 < apper-qt5.14.patch # Fix build with Qt 5.14
  patch -d $pkgname-$pkgver -p1 < kdebug-401461.patch # Fix crash
  patch -d $pkgname-$pkgver -p1 < apper-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DAPPSTREAM=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# fix conflicts with gnome-software
  mv "$pkgdir"/usr/share/dbus-1/services/{,kde_}org.freedesktop.PackageKit.service
}
