# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-sub-exporter
pkgver=0.987
pkgrel=4
pkgdesc="A sophisticated exporter for custom-built routines"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-data-optlist' 'perl-params-util' 'perl-sub-install')
url="https://metacpan.org/release/Sub-Exporter"
source=(http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Sub-Exporter-$pkgver.tar.gz)
md5sums=('5332d269a7ba387773fcd140b72a0ed2')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL="--skipdeps"            \
      PERL_MM_OPT="INSTALLDIRS=vendor"         \
      PERL_MB_OPT="--installdirs vendor"       \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/Sub-Exporter-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""

    cd "$srcdir"/Sub-Exporter-$pkgver

    make test
  )
}

package() {
  cd "$srcdir"/Sub-Exporter-$pkgver

  make DESTDIR="$pkgdir"/ install
}
