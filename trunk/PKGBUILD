# Maintainer: Brendan MacDonell <macdonellba at gmail dot com>
# Former Maintainer: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgbase=python-apsw
pkgname=('python-apsw' 'python2-apsw')
pkgver=3.22.0
pkgrel=1
arch=('x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('sqlite')
makedepends=('python' 'python2')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
md5sums=('2d1975480a1749801c3865aeefaa4438')

build() {
  cp -rf apsw-${pkgver}-r1 apsw2

  cd apsw-${pkgver}-r1
  python setup.py build --enable=load_extension
  cd "$srcdir"/apsw2
  python2 setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw2
  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH"
  python2 setup.py test

  cd "$srcdir"/apsw-${pkgver}-r1
  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH"
  python setup.py test
}

package_python-apsw() {
depends+=('python')
pkgdesc="Python wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apsw() {
depends+=('python2')
pkgdesc="Python2 wrapper for SQLite"
  cd "$srcdir"/apsw2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
