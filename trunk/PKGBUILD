# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.11.4
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ginuerzh/gost/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('31a7feac1a1d7e1f4baa228cc13a48af95f46a551d9558554b0e89f6eeb05226cbe31971e42dc7f4516d52fd192c3af591b0e2fad9bc8f1e3d30c801a9f30f2a'
            '8815c27a49d747e90fbcbf03c6011a76b95bc72a6c963ee8df9490e6aba9a3f4f9895e6a7759988aef123160ef8a6d2de85e8fce01c5980f8744e212792ad07c')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd gost-$pkgver/cmd/gost
  go build
}

package() {
  cd gost-$pkgver
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
