# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgbase=python-pyicu
pkgname=('python-pyicu' 'python2-pyicu')
pkgver=1.9.3
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('i686' 'x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'icu')
checkdepends=('python-fonttools' 'python2-fonttools')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz"
        issue19.patch)
sha512sums=('93720f6fd4131b6cff7fe89996a3851b5993c74859e85de7813883a30152b63a0065b8c51c536cd40417b6e3ccf0ec4e26ed471cec84cd48761a306a35bfd15c'
            '037153c73076c1e6b291171de3794f79ac315a6495e6231dc6e8fc4ae2a4d750ab77a7706edc46a5021561ebf429902fa3103c5279d3d95b3ea8093ff75a1810')

prepare() {
  # https://github.com/ovalhub/pyicu/issues/19
  (cd PyICU-$pkgver; patch -p1 -i ../issue19.patch)

  cp -a PyICU-$pkgver{,-py2}
}

build() {
  cd "$srcdir/PyICU-$pkgver"
  python setup.py build

  cd "$srcdir/PyICU-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/PyICU-$pkgver"
  python setup.py test

  cd "$srcdir/PyICU-$pkgver-py2"
  python2 setup.py test
}

package_python-pyicu() {
  depends=('python' 'icu')

  cd PyICU-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pyicu() {
  depends=('python2' 'icu')

  cd PyICU-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
