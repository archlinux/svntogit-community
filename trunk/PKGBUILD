# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sebastian LaVine <mail@smlavine.com>
# Contributor: Lance Chen <cyen0312@gmail.com>

pkgname=jo
pkgver=1.7
pkgrel=1
pkgdesc='JSON output from a shell'
arch=('x86_64')
url='https://github.com/jpmens/jo'
license=('GPL2')
depends=('glibc')
makedepends=('git' 'pandoc')
_commit='0904bbd456833ef35059e12609088eb3a28be3b4'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    bashcompdir=/usr/share/bash-completion/completions

  make all
}

check() {
  cd "$pkgname"

  make -k check
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # TODO: patch in autotools, or just leave as is ...
  cd "$pkgdir/usr/share/bash-completion/completions"
  mv jo.bash jo
}
