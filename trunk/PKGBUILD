# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Beno√Æt Allard <benoit dot allard at gmx dot de>
# Contributor: killruana <killruana@gmail.com>

pkgname=yosys
pkgver=0.23
pkgrel=1
pkgdesc='A framework for RTL synthesis'
arch=('x86_64')
url='https://yosyshq.net/yosys/'
license=('custom:ISC')
depends=('abc' 'bash' 'boost-libs' 'tcl' 'libffi' 'python' 'protobuf')
makedepends=('boost')
checkdepends=('iverilog')
optdepends=('graphviz: Schematics display support'
            'xdot: Display netlists'
            'yices: default solver for yosys-smtbmc'
            'cvc4: alternative solver for yosys-smtbmc'
            'z3: alternative solver for yosys-smtbmc')
options=('!lto') # getting SIGSEGV when running the tests with LTO
source=("https://github.com/cliffordwolf/yosys/archive/$pkgname-$pkgver.tar.gz")
sha512sums=('63efce6d64367a0f3f14003d6bdae1621ba6d12dc358d46d765c4f6bc960ed1fd4e78e60a100685b3c8ad751be2120cbd68de1cfda128681aeb9d24172b3f24d')

_make() {
    make \
        PREFIX="/usr" \
        CONFIG=gcc \
        ENABLE_TCL=1 \
        ENABLE_ABC=1 \
        ENABLE_GLOB=1 \
        ENABLE_PLUGINS=1 \
        ENABLE_LIBYOSYS=1 \
        ENABLE_PROTOBUF=1 \
        ENABLE_ZLIB=1 \
        ENABLE_PYOSYS=1 \
        ABCEXTERNAL=abc \
        BOOST_PYTHON_LIB="-lpython3.10 -lboost_python310" $@
}

build() {
    cd $pkgname-$pkgname-$pkgver
    _make
}

check() {
    cd $pkgname-$pkgname-$pkgver
    _make test
}

package() {
    cd $pkgname-$pkgname-$pkgver
    _make DESTDIR="$pkgdir" PYTHON_PREFIX="$pkgdir/usr" install

    install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
