# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>

pkgname=steam
pkgver=1.0.0.76
pkgrel=1
pkgdesc="Valve's digital software delivery system"
url='https://steampowered.com/'
arch=('x86_64')
license=('custom')
depends=('bash' 'desktop-file-utils' 'diffutils' 'hicolor-icon-theme' 'curl' 'dbus'
         'freetype2' 'gdk-pixbuf2' 'ttf-font' 'zenity' 'lsb-release' 'nss' 'usbutils'
         'xorg-xrandr' 'vulkan-driver' 'vulkan-icd-loader' 'lsof' 'python')
depends_x86_64=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss'
                'lib32-alsa-plugins' 'lib32-libgpg-error'
                'lib32-nss' 'lib32-vulkan-driver' 'lib32-vulkan-icd-loader')
optdepends=('steam-native-runtime: steam native runtime support')
source=(https://repo.steampowered.com/${pkgname}/pool/${pkgname}/s/${pkgname}/${pkgname}_${pkgver}{.tar.gz,.dsc}
        steam-runtime.sh)
sha512sums=('cb2fd0a55f23c92ec46c27edd4595421d4ac346f8cd4faeb2b8e0b017232ac637ee20ed3a00c8b79ee106e922ade6efc5e09c970807757cc372fa9fc26fe9ed1'
            '1f179c8a9eab81b5fa9cc1274398322ae5f9dcf5f0d900a43a0a2ac53549389dd3508757d4624291e99c09fc37bf98e2a0247b3e1299d668dc6dc9986372f0ca'
            '5e75c019e9fe8c67d686c4e3343dac1180a69a4bdb7d39b333415c63201eef9b98da5619dbf6fd8daa6884e65bc7f8afc9e52778682425e5a75987d527eae6f0')
b2sums=('de38c2604b3fb7a65a25d317718272c607f60e4bedf2efb83eebcf30e01fbccd94fac572f65e6b995e95e981191c241138cd9a9a6f51ba768cd0b3efd9edc0a5'
        '6d034e6c5d90d62b2d96f1e0760fff57a1196e7a7bfd7da26989cfc567a40bb90568c2885f1e1fdd7f83dc68bcd9d8166a684abc315967bff85607b5b7bc1e52'
        'c6bac99336b7c30fec7cdbaf9e949555c687dd9dff50bcae136134d6314f4b841f5fc66ddb2caac1b003690b926fd4afbdc11da143b4674db4b75f27709fdd23')
validpgpkeys=('BA1816EF8E75005FCF5E27A1F24AEA9FB05498B7') # linux@steampowered.com

prepare() {
  cd ${pkgname}-launcher
  # apply roundups for udev rules
  sed -r 's|("0666")|"0660", TAG+="uaccess"|g' -i subprojects/steam-devices/60-steam-input.rules
  sed -r 's|("misc")|\1, OPTIONS+="static_node=uinput"|g' -i subprojects/steam-devices/60-steam-input.rules
  sed -r 's|(, TAG\+="uaccess")|, MODE="0660"\1|g' -i subprojects/steam-devices/60-steam-vr.rules

  # separated runtime/native desktop files
  sed -r 's|(Name=Steam)|\1 (Runtime)|' -i steam.desktop
  sed -r 's|(/usr/bin/steam)|\1-runtime|' -i steam.desktop
}

package() {
  cd ${pkgname}-launcher
  make DESTDIR="${pkgdir}" install

  install -Dm 755 "${srcdir}/steam-runtime.sh" "${pkgdir}/usr/bin/steam-runtime"
  install -d "${pkgdir}/usr/lib/steam"
  mv "${pkgdir}/usr/bin/steam" "${pkgdir}/usr/lib/steam/steam"
  ln -sf /usr/bin/steam-runtime "${pkgdir}/usr/bin/steam"

  install -Dm 644 COPYING steam_subscriber_agreement.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 debian/changelog -t "${pkgdir}/usr/share/doc/${pkgname}"

  # blank steamdeps because apt-get
  ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

  install -Dm 644 subprojects/steam-devices/60-steam-input.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-input.rules"
  install -Dm 644 subprojects/steam-devices/60-steam-vr.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-vr.rules"
}

# vim: ts=2 sw=2 et:
