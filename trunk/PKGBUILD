# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=10.2.5
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL3)
depends=(glibc lua)
makedepends=(cmake flac jq libid3tag openssl pcre2 perl)
source=(https://github.com/jcorporation/$pkgname/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('5d48f52dc803678ff751e9c45da6e0c2caec8caf377baa1952eb7d71a1994fcad88cf0bd1d08c60c5664914e9990c7c485111bad881999cd03af2abe34212810')
b2sums=('735c983dc5ba5cfeaf338a593990f7459cfca6df934fdb26ebc5118d7019264ae5f1c4e0fcf2c58d305e23d1b0a0d8d7c8327e8f94d6c28556013634400e1164')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_FLAC=ON
    -S $_name-$pkgver
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
