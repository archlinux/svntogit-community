# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=popeye
pkgver=0.10.0
pkgrel=1
pkgdesc="Kubernetes cluster resource sanitizer"
arch=('x86_64')
url="https://github.com/derailed/popeye"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derailed/popeye/archive/v${pkgver}.tar.gz")
sha512sums=('bfe9aca79645a82efd4912741d3d46ce6e54b96c8fce0029ed45f15352652308ca2327a8a9aae9cd72f93bf1d41010806fb5c560b4b5c39447e7eb219753882b')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build .
  chmod +x ./popeye
}

check(){
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package(){
  cd "${pkgname}-${pkgver}"
  install -Dsm755 ./popeye "${pkgdir}/usr/bin/popeye"
}
