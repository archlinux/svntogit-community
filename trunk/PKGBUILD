# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
epoch=1
pkgver=1.2.32
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
arch=(x86_64)
url="https://www.remmina.org/"
license=('GPL')
depends=('avahi' 'libgcrypt' 'libssh' 'vte3')
makedepends=('cmake' 'freerdp' 'libvncserver' 'spice-gtk' 'spice-protocol' 'telepathy-glib')
optdepends=('freerdp: RDP plugin'
            'libsecret: Secret plugin'
            'libvncserver: VNC plugin'
            'libxkbfile: NX plugin'
            'nxproxy: NX plugin'
            'spice-gtk: Spice plugin'
            'telepathy-glib: Telepathy plugin'
            'xorg-server-xephyr: XDMCP plugin')
replaces=('remmina-plugins')
provides=('remmina-plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/Remmina/archive/v${pkgver/rc/-rc}.tar.gz")
sha256sums=('803d8cd4ec5a860f32297517f4d61f03de720259afe98eef58e4f83af46e1ae0')

prepare() {
  cd Remmina-${pkgver/rc/-rc}
  sed -e 's|ssh_threads|ssh|' -i cmake/FindLIBSSH.cmake # Fix build with libssh 0.8
}

build() {
  cd "$srcdir"/Remmina-${pkgver/rc/-rc}/
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd "$srcdir"/Remmina-${pkgver/rc/-rc}/
  make DESTDIR="$pkgdir" install
}
