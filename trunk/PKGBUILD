# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-h2
pkgver=4.0.0
pkgrel=2
pkgdesc="HTTP/2 State-Machine based protocol implementation"
arch=('any')
license=('MIT')
url="https://hyper.rtfd.org"
depends=('python-hpack' 'python-hyperframe')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis')
source=("https://github.com/python-hyper/h2/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ae145421549ecd29845b100e3895f1088598142824d82cf0b59293b0f647939e4acd0ff809187cabd67a3a524a0337cc925a3f17624c86767bd87a2d18a59b2b')

build() {
  cd h2-$pkgver
  python setup.py build
}

check() {
  cd h2-$pkgver
  python setup.py pytest
}

package() {
  cd h2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
