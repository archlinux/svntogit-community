# Maintainer: Leonidas Spyropoulos (artafinde@archlinux.org)
# Contributor: scrawler@gmail.com

pkgname=freeplane
_majorver=1
_minorver=10
_patchver=6
_updatever=1
pkgver=${_majorver}.${_minorver}.${_patchver}.u${_updatever}
_git_tag=${_majorver}.${_minorver}.${_patchver}u${_updatever}
pkgrel=1
pkgdesc="A Java mindmapping tool"
arch=('any')
url="http://freeplane.sourceforge.net"
license=('GPL2')
_jrever=11
_jdkver=17
makedepends=('gradle' "jdk${_jdkver}-openjdk" 'gnu-free-fonts' 'fontconfig' 'unzip')
depends=("java-runtime>=${_jrever}" "java-runtime<=${_jdkver}")
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}_src-${_git_tag}.tar.gz"
        "freeplane.desktop" "freeplane.run"
        "0001-Upgrade-ospackage-plugin-to-support-gradle-8.patch::https://github.com/inglor/freeplane/commit/ef9398cf209a1326a78b8b265c6e199f0fff9757.patch"
        "0002-Add-dependsOn-for-tasks-which-fail-with-gradle-8.x.patch::https://github.com/inglor/freeplane/commit/068213e1a6b10d02928c501bd5feefda5caddf0a.patch")
b2sums=('043779d9a60ce86313430737508f75b1968590e55a11b7baa123660f80d0ebaff0dcad0c481e0295ce00fa953d432c73c1370f0598c0888f8b6181a0688535f5'
        '87c25331e01823e38668e4b394a51a279c05b24b088f4ffc1482d3783e24018da8f9e51b3ad1a62c5a863f85a6ccb30bbe8999cb861dc1b93d5483019644cfa5'
        '24ca56b7c7894b9bb38600b4d37973769243e1bdb221f33125b60bf4f878a3b630775710fab9dee97fa45a69319455037e294860ba7fbd608529982c6b0b1538'
        '055fd2208a94b74db8f47acde169a8112eef23503ede7cc7435d7304182d366a8ad83848a58deeec15bb3c58e0b31d141605a029444ca14d920e26b74d47cc77'
        'ca9ea164bedf54b95a01017031f9b85707139d258d191d90c4d3584e492ab58859265394b747d1fddafebc8c75055588f468ee5027d812fd4096c99460a9176c')

prepare() {
  cd "$pkgname-$_git_tag"
  patch -Np1 < "${srcdir}/0001-Upgrade-ospackage-plugin-to-support-gradle-8.patch"
  patch -Np1 < "${srcdir}/0002-Add-dependsOn-for-tasks-which-fail-with-gradle-8.x.patch"
}

build() {
  cd "$pkgname-${_git_tag}"
  export JAVA_HOME="/usr/lib/jvm/java-$_jdkver-openjdk"
  export PATH="/usr/lib/jvm/java-$_jdkver-openjdk/bin:$PATH"
  /usr/bin/gradle -Dorg.gradle.daemon=false build dist

  install -dm755 "$srcdir/$pkgname-${_git_tag}/dist"
  unzip -d "$srcdir/$pkgname-${_git_tag}/dist" \
    "$srcdir/$pkgname-${_git_tag}/DIST/${pkgname}_bin-${_majorver}.${_minorver}.${_patchver}.zip"
}

package() {
  install -dm755 "$pkgdir/usr/share/freeplane/"
  cd "$srcdir/$pkgname-${_git_tag}/dist/$pkgname-${_majorver}.${_minorver}.${_patchver}"
  cp -dr --no-preserve='ownership' core doc plugins resources scripts framework.jar freeplane.l4j.ini \
    freeplane.png freeplane.policy freeplane.sh freeplaneConsole.l4j.ini freeplaneIcons.dll \
    freeplanelauncher.jar gitinfo.txt init.xargs license.txt props.xargs \
    "$pkgdir/usr/share/freeplane/"

  # Install license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  mv license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"

  # Install icons
  install -dm755 "$pkgdir/usr/share/pixmaps/"
  mv freeplane.svg "$pkgdir"/usr/share/pixmaps/freeplane.svg

  # Install the desktop entry
  install -Dm644 "$srcdir/freeplane.desktop" "$pkgdir"/usr/share/applications/$pkgname.desktop

  # Install the executable script
  install -Dm755 "$srcdir/freeplane.run" "$pkgdir"/usr/bin/freeplane
}

# vim:set ft=sh sw=2 sts=2 et:
