# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.8.3
pkgrel=1
pkgdesc="A library for creating and manipulating (un)directed graphs."
arch=(x86_64)
url="https://igraph.org/c/"
license=(GPL2)
depends=(glpk arpack libxml2)
source=("https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('cc935826d3725a9d95f1b0cc46e3c08c554b29cdd6943f0286d965898120b3f1')

build() {
  cd $pkgname-$pkgver
  ./configure \
   --prefix=/usr \
   --with-external-blas \
   --with-external-lapack \
   --with-external-arpack \
   --with-external-glpk
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
