# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libcss
pkgver=0.8.0
pkgrel=1
pkgdesc='CSS parser and selection engine'
arch=('x86_64')
url='http://www.netsurf-browser.org/projects/libcss/' # no https available
license=('MIT')
makedepends=('netsurf-buildsystem>=1.7')
depends=('libparserutils>=0.2.4' 'libwapcaplet>=0.4.1')
source=("http://download.netsurf-browser.org/libs/releases/libcss-$pkgver-src.tar.gz")
sha256sums=('35a0490d98c23e09b8d2b5e4619926131c5b98da7a9db2ed00322f63fbc2ad5f')

prepare() {
  sed -i 's,_BSD_SOURCE,_DEFAULT_SOURCE,' "$pkgname-$pkgver/Makefile"
}

build() {
  export CFLAGS="$CFLAGS -w"
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    install

  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
