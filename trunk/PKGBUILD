# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.9.9
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=(x86_64)
url="https://qsynth.sourceforge.io/"
license=(GPL)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(cmake fluidsynth qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('79a3085a884b3bd3251ad059b8ab67eb3329f2e6b25df5dfa6d210d7d313000b52ccdfd8b9f18aa49bdc9db2a5cd1738f05c529cb4673bd579b4ba987b1edff6')
b2sums=('efc22bc509445e1948d7dd28de8e6c6281967964dcf54e688a8830280945b37c51c70e0a64596bff2a7a12ccfcbaa3fd65d9465b11dc7abe2cca060cce6e3997')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfluidsynth.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README,TRANSLATORS} -t "$pkgdir/usr/share/doc/$pkgname"
}
