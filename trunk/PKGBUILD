# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libsbsms
pkgver=2.3.0
pkgrel=3
pkgdesc="A library for high quality time and pitch scale modification"
arch=(x86_64)
url="https://github.com/claytonotey/libsbsms"
license=(GPL2)
depends=(gcc-libs)
makedepends=(cmake)
provides=(libsbsms.so)
source=($pkgname-$pkgver.tar.gz::https://github.com/claytonotey/libsbsms/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('e5b544c2bdbaa2169236987c7a043838c8d1761b25280c476d7a32656d482c6485cb33f579ea9d1ce586ec7b2913ed8fdcf1abe5c7cc8b9e4eef9ce87de54627')
b2sums=('3707b573ffef414e5373526704d4e54bf0f53b6bc5d9ce6e69bf22e11e5380eccb65a6c26e3598ee4bf43f341a7dcc6e1a7d7f5113bb649ef9e94b01ba36cc0f')

build() {
  cmake -B build -S $pkgname-$pkgver \
      -DCMAKE_BUILD_TYPE='None' \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
