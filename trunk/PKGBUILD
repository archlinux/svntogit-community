# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sequel
pkgver=5.61.0
pkgrel=1
pkgdesc='Sequel: The Database Toolkit for Ruby'
arch=(any)
url='https://github.com/jeremyevans/sequel'
license=(MIT)
depends=(ruby)
makedepends=(ruby-minitest ruby-minitest-hooks ruby-minitest-global_expectations ruby-tzinfo ruby-nokogiri)
options=(!emptydirs)
source=(https://github.com/jeremyevans/sequel/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('4f9af72ad382556a16e2bb45dc5143e6a39fe0e7592d1cc73e6c24b3ac8c8f78')

prepare() {
  cd sequel-$pkgver
  sed '/activemodel/d' -i sequel.gemspec
  rm spec/extensions/active_model_spec.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sequel-$pkgver
  gem build sequel.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sequel-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sequel-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sequel-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
