# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=hamlib
pkgver=4.5
pkgrel=2
pkgdesc='Ham radio equipment control libraries'
arch=('x86_64')
url='https://hamlib.github.io'
license=('GPL' 'LGPL')
depends=('perl' 'python' 'lua' 'tcl' 'libxml2' 'libusb-compat' 'libtool' 'libuhd' 'boost-libs' 'libindi')
makedepends=('swig' 'boost')
source=("https://github.com/Hamlib/Hamlib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('671da06aed54951ca810b4de7291a16de3a2f6ed54b8d024d4d9bfed9dfc6a21336c3cadd6f5b128c3ab54f1edd2b22db491b074b7759b9dfd9e71e1ce4055f3'
            'SKIP')
validpgpkeys=('82D64F6B0E67CD41F689BBA6FB2C5130D55A8819') # Nate Bargmann <n0nb@n0nb.us>

build() {
  cd $pkgname-$pkgver

  ./configure \
  	--prefix=/usr \
  	--sbindir=/usr/bin \
  	--with-perl-binding \
  	--with-python-binding \
  	--with-tcl-binding \
  	--with-lua-binding \
  	--with-xml-support \
  	--disable-uhd  # https://github.com/Hamlib/Hamlib/issues/355

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
