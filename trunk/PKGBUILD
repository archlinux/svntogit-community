# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yamllint
pkgver=1.27.1
pkgrel=1
pkgdesc='Linter for YAML files'
arch=('any')
url=https://github.com/adrienverge/yamllint
license=('GPL3')
depends=('python-pathspec' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('e688324b58560ab68a1a3cff2c0a474e3fed371dfe8da5d1b9817b7df55039ce')
b2sums=('37021bd577384821b75ddd747f1dd3c4261317582d42179e0731214cb67f79bf9302c34acaa721440edb118ef8900cc2952d1f8e441def9e3143b57e03af87bd')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/setuptools/d' setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python -m unittest discover tests
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
