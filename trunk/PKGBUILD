# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=dino
pkgver=0.2.0
pkgrel=2
pkgdesc="Modern XMPP (Jabber) chat client written in Vala"
arch=(x86_64)
url="https://github.com/dino/dino"
license=(GPL3)
depends=(glib2 glib-networking gtk3 gpgme libgee libgcrypt libsignal-protocol-c
         libsoup sqlite qrencode)
makedepends=(git cmake vala ninja)
source=(${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc}
        ${pkgname}-vala.patch::https://github.com/dino/dino/commit/9acb54df9254.patch)
sha256sums=('565b95d9fc697412edc898225890f95d26d42a3594e12680cbd7d038d7812f46'
            'SKIP'
            '56a9c90f3df4dc960dca2d404a436eaeedfc619f9ed0e93a9ad7bc75f6f2d686')
validpgpkeys=(A32134DD19952FB12EC43E4501FABCC83FEA225E)

prepare() {
    cd ${pkgname}-${pkgver}

    # https://github.com/dino/dino/issues/1010
    patch -Np1 -i ../${pkgname}-vala.patch
}

build() {
    cmake -G Ninja -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
}
