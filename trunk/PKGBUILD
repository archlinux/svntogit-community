# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-attr_extras
pkgver=7.1.0
_commit=4913a40d2cea9cd90fbdc444441e35268cf3c6c3
pkgrel=1
pkgdesc='Takes some boilerplate out of Ruby with methods like attr_initialize'
arch=(any)
url='https://github.com/barsoom/attr_extras'
license=(MIT)
depends=(ruby)
makedepends=(git)
checkdepends=(ruby-m ruby-minitest ruby-rake)
options=(!emptydirs)
source=(git+https://github.com/barsoom/attr_extras.git#commit=$_commit)
sha256sums=('SKIP')

build() {
  local _gemdir="$(gem env gemdir)"
  cd attr_extras
  gem build attr_extras.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    attr_extras-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd attr_extras
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd attr_extras
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
