# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehg@gmail.com>
# Contributor: champus
# Contributor: madeye
# Contributor: ranguvar
# Contributor: ninja_pt

pkgname=hwinfo
pkgver=21.81
pkgrel=1
pkgdesc='Hardware detection tool from openSUSE'
arch=(x86_64)
url='https://github.com/openSUSE/hwinfo'
license=(GPL2)
depends=('libx86emu=3.5' perl-xml-parser perl-xml-writer)
makedepends=(flex git sysfsutils)
options=(!emptydirs)
source=(custom_ioctl.patch
        "git+$url#commit=3d7add656d689e53329b390f4ef898a269e2a288") # tag: 21.80
b2sums=('2b1bd3b53bbfc1e545e1a70ffd6cca08f704639a104928b2c02ccca3e82000f07a470fbdf129566ece6dfb2b98fdad0e82f18cc5c2016ebfeed043b4edb295b4'
        'SKIP')

prepare() {
  cd $pkgname
  patch -p1 -i ../custom_ioctl.patch
  echo touch changelog > git2log
}

build() {
  make -C $pkgname -j1 \
    CFLAGS+="-fPIC -I$srcdir/$pkgname/src/hd -w" \
    HWINFO_VERSION="$pkgver" \
    LIBDIR=/usr/lib
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" LIBDIR=/usr/lib install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
  rmdir "$pkgdir/sbin"
}
