# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

pkgbase=python-coverage
pkgname=(python-coverage python2-coverage)
pkgver=5.5
pkgrel=2
pkgdesc="A tool for measuring code coverage of Python programs"
arch=('x86_64')
url="https://nedbatchelder.com/code/coverage/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-flaky' 'python-hypothesis' 'python-mock'
'python-pycontracts' 'python-pytest' 'python-pytest-xdist' 'python-toml'
'python-unittest-mixins' 'python-virtualenv')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/nedbat/coveragepy/archive/coverage-$pkgver.tar.gz")
sha512sums=('1083e3c91b60256cfbcae16efdfe0490aa7bd8e466e7693b54d79459c621d842c8e40935774d653c991c5479cb92a4f442d90d984a8069e7fa57761fa9dd6b76')
b2sums=('b2b2c2989e3af527874359bba7ad793ebbfee24120bde9467d2577c6694fe2949aca2e67b9f33e673e761bdb375a4177939d3b723e6b9e7d0e527135a0d612b9')

prepare() {
  mv -v "coveragepy-coverage-$pkgver" "${pkgbase}-$pkgver"
}

build() {
  cd "${pkgbase}-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${pkgbase}-$pkgver"
  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  (
    virtualenv "test_dir" --system-site-packages
    . "test_dir/bin/activate"
    export PYTHONPATH="/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
    python setup.py --quiet clean develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  ) || echo "Known failing tests."
}

package_python-coverage() {
  depends=('python')
  optdepends=('python-toml: for pyproject.toml support')
  cd "${pkgbase}-$pkgver"

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
}

package_python2-coverage() {
  depends=('python2-setuptools')
  optdepends=('python2-toml: for pyproject.toml support')
  cd "${pkgbase}-$pkgver"

  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # circumvent file conflicts with python-coverage
  mv -v "$pkgdir/usr/bin/coverage"{,2}
}
