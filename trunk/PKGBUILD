# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=bspwm
pkgver=0.8.9
pkgrel=5
pkgdesc='Tiling window manager based on binary space partitioning'
arch=('x86_64' 'i686')
url='https://github.com/baskerville/bspwm'
license=('custom:BSD')
depends=('libxcb' 'xcb-util' 'xcb-util-wm')
makedepends=('gendesk' 'git')
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'xtitle-git: for the example panel'
	          'sutils-git: for the example panel'
	          'bar-aint-recursive: for the example panel'
	          'terminus-font: for the example panel')
source=("git://github.com/baskerville/$pkgname#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  gendesk -f -n -wm --pkgname "$pkgname" --exec "$pkgname"
}

build() {
  make -C "$pkgname" PREFIX=/usr
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/xsessions/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
