# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flexmock
pkgname=('python-flexmock' 'python2-flexmock')
pkgver=0.10.4
pkgrel=1
pkgdesc="Mock/Stub/Spy library for Python"
arch=('any')
license=('BSD')
url="https://has207.github.com/flexmock"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bkabrda/flexmock/archive/$pkgver.tar.gz")
sha512sums=('96d3af9625b1dbd987b291c7ed26a77476f54fe4548d16ad70adeefc0093f51a3b47d8b3fb4339611dfadd8b2d55a1bc734e70bc093683192f1f351badf08553')

export LC_CTYPE=en_US.UTF-8

build() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-flexmock() {
  depends=('python')

  cd flexmock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flexmock() {
  depends=('python2')

  cd flexmock-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
