# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(xcas libgiac)
_pkgver=1.6.0-25
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="A free computer algebra system"
arch=(x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(python mpfi gsl pari ntl lapack libpng libjpeg fltk texlive-core texlive-science hevea glpk libao gmp-ecm nauty libsamplerate)
source=("http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgbase}_${_pkgver}.tar.gz"
         giac-test-pari-2.11.patch)
sha256sums=('b09ada1af3c9ed77a63af3359a6242cc82a7d40c02c6782df03cbda699987186'
            'c5f091986f0a0807fc1ae5a3b7454132816e9bc3ed64411f0a7bdf90a8c8e624')

prepare() {
  cd $pkgbase-${pkgver%.*}
  patch -p1 -i ../giac-test-pari-2.11.patch # Fix tests with pari 2.11
}

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgbase-${pkgver%.*}

  make check
}

package_xcas() {
  depends=(libgiac fltk)
  optdepends=('perl: for pgiac')
  conflicts=(giac)
  provides=(giac)
  replaces=(giac)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari ntl lapack libpng curl glpk libao gmp-ecm libsamplerate)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}
