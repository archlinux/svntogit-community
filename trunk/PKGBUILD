# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=pytest-xprocess
pkgname=python-pytest-xprocess
pkgver=0.12.1
pkgrel=5
pkgdesc='Pytest plugin to manage external processes across test runs.'
arch=('any')
url="https://github.com/pytest-dev/pytest-xprocess"
license=('MIT')
depends=('python-psutil' 'python-pytest' 'python-pytest-cache' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fda868a12a056070afd5838c4bc466b4906a7138f685d4a0e6e0adbd975e5a1a2b9b37b8de36a45fb700e02ca14ae219cfe29c467f8d03e55d2a375a7efc9927')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="."
  py.test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG,README.rst} -t "${pkgdir}/usr/share/${pkgname}"
}
