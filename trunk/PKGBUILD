# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=warp-tls
pkgname=haskell-warp-tls
pkgver=3.3.2
pkgrel=13
pkgdesc="HTTP over TLS support for Warp via the TLS package"
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-cryptonite" "haskell-data-default-class" "haskell-network"
         "haskell-streaming-commons" "haskell-tls" "haskell-tls-session-manager" "haskell-unliftio"
         "haskell-wai" "haskell-warp")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4ac7c2c4161502c4167ee5c941c6147146f453467c77fb84da8bdd2810719b099b908b67c4bcce248d6bc0d26657f91f1cc6812cedd0497516a7594b243fb783')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
