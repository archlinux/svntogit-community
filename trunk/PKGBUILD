# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>

pkgname=python-graphene
pkgver=3.2.2
pkgrel=2
pkgdesc='GraphQL framework for Python'
arch=(any)
url=https://graphene-python.org/
license=(MIT)
depends=(
  python-aniso8601
  python-graphql-core
  python-graphql-relay
  python-six
)
makedepends=(
  git
  python-setuptools
)
_tag=57cbef6666e2e466808cce21b8a1769ecd3fd118
source=(python-graphene::git+https://github.com/graphql-python/graphene.git#tag=${_tag})
sha512sums=(SKIP)

pkgver() {
  cd python-graphene
  git describe --tags | sed 's/^v//'
}

build() {
  cd python-graphene
  python setup.py build
}

package() {
  cd python-graphene
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-graphene
}

# vim: ts=2 sw=2 et:
