# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=zim-tools
pkgver=3.0.0
pkgrel=1
pkgdesc="Various ZIM command line tools"
license=('GPL3')
arch=('x86_64')
provides=('zimwriterfs')
conflicts=('zimwriterfs')
replaces=('zimwriterfs')
depends=('file' 'libzim' 'docopt' 'gumbo-parser')
makedepends=('meson' 'mustache')
url='https://github.com/openzim/zim-tools'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('27d2e4dfda86b887fea459b15320f6c1f824c819b3d0b7633bbff581777e952c')

build() {
	cd "${pkgname}-${pkgver}"
	arch-meson build
	ninja -C build
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
