# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: hekel <hekel@archlinux.info>

pkgname=mate-power-manager
pkgver=1.6.3
pkgrel=3
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system for Mate Desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'libcanberra' 'libmatekeyring' 'libnotify'
         'libunique' 'upower')
makedepends=('docbook2x' 'docbook-xml' 'mate-common' 'mate-doc-utils'
             'mate-panel' 'perl-xml-parser' 'xmlto')
optdepends=('mate-panel: Set brightness and inhibit power management from the panel')
options=('!emptydirs')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('a949e34494cb70bcb2fa349dcc35847f022be187')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    #Work around a problem where the .sgml fail to parse.
    sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --enable-unique \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
