# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
pkgver=2.11.1
pkgrel=1

pkgdesc="cli interface for Github"
url="https://hub.github.com"
arch=('x86_64')
license=('MIT')

depends=('git')
makedepends=('go-pie')

source=("hub-$pkgver.tar.gz::https://github.com/github/hub/archive/v$pkgver.tar.gz")
sha256sums=('dfd6531cdd31f004dcbcd2764dea4e1c040e72358ae79758a6c3060b9e743371')
b2sums=('af3ac833c874f15ce183a41a4b6920ba77b9d882b343e12fddeba58453c2c03cd45fa1ec3e7a488dc2d916d0e12f2cf14d7e4d749848d3a10e44b039e7efae49')

prepare() {
  mkdir -p src/github.com/github
  cp -r hub-$pkgver src/github.com/github/hub
}

build() {
  cd src/github.com/github/hub

  export GOPATH="$srcdir"
  make
  make man-pages
}

check() {
  cd src/github.com/github/hub

  GOPATH="$srcdir" make test
}

package() {
  cd src/github.com/github/hub

  make PREFIX="$pkgdir"/usr install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 etc/hub.bash_completion.sh "$pkgdir"/usr/share/bash-completion/completions/hub
  install -Dm644 etc/hub.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_hub
  install -Dm644 etc/hub.fish_completion "$pkgdir"/usr/share/fish/vendor_completions.d/hub.fish
}
