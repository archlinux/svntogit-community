# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgname=lib32-libnm
pkgver=1.20.4
pkgrel=1
pkgdesc='NetworkManager client library'
arch=(x86_64)
url=https://wiki.gnome.org/Projects/NetworkManager
license=(
  GPL2
  LGPL2.1
)
depends=(
  lib32-glib2
  lib32-glibc
  lib32-nspr
  lib32-nss
  lib32-util-linux
  libnm
)
makedepends=(
  dhclient
  git
  gtk-doc
  intltool
  iptables
  iwd
  lib32-curl
  lib32-dbus
  lib32-dbus-glib
  lib32-jansson
  lib32-libgudev
  lib32-libmm-glib
  lib32-libndp
  lib32-libnewt
  lib32-libnl
  lib32-libpsl
  lib32-libsoup
  lib32-polkit
  meson
  modemmanager
  perl-yaml
  python-gobject
  systemd
  wpa_supplicant
)
source=(git+https://gitlab.freedesktop.org/NetworkManager/NetworkManager.git#tag=27dee3b113bb45ffb53367f30a7c750eb146d980)
sha256sums=(SKIP)

pkgver() {
  cd NetworkManager

  git describe --tags
}

prepare() {
  sed "s/'lib'/'lib32'/g" -i NetworkManager/meson.build
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson NetworkManager build \
    --libdir=lib32 \
    -D config_plugins_default=keyfile,ibft \
    -D dbus_conf_dir=/usr/share/dbus-1/system.d \
    -D dist_version="${pkgver}-${pkgrel}" \
    -D docs=false \
    -D ebpf=true \
    -D ibft=true \
    -D introspection=false \
    -D iwd=true \
    -D libaudit=no \
    -D libnm_glib=true \
    -D modify_system=true \
    -D more_asserts=no \
    -D more_logging=false \
    -D nmcli=false \
    -D nmtui=false \
    -D polkit_agent=true \
    -D qt=false \
    -D selinux=false \
    -D session_tracking_consolekit=false \
    -D suspend_resume=systemd \
    -D tests=no \
    -D vapi=false
  ninja -C build
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib32/pkgconfig
  cp -dr --no-preserve=ownership build/libnm/libnm.so* "${pkgdir}"/usr/lib32/
  cp -dr --no-preserve=ownership build/meson-private/libnm.pc "${pkgdir}"/usr/lib32/pkgconfig/

}

# vim: ts=2 sw=2 et:
