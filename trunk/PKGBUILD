# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.0.874
pkgrel=2
pkgdesc="userland tools"
arch=('i686' 'x86_64')
url="http://www.open-iscsi.com/"
license=('GPL')
depends=('libutil-linux' 'openssl')
makedepends=('open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz"
        "open-iscsi.service")
md5sums=('caf2c92bfe58c07f781ab74942e4b9f2'
         'fe90c76239e2ec56118a711a5ebc50d8')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  # include iscsistart in the package
  sed -i -e '/^PROGRAMS = /s/$/ usr\/iscsistart/' Makefile

  # build breaks if the openslp package is installed
  sed -i -e 's/\(\.\/configure\)/ \1 --without-slp/g' Makefile

  make user
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install_user

  install -D -m644 "$srcdir"/${pkgname}-${pkgver}/etc/iscsid.conf "$pkgdir"/etc/iscsi
  install -D -m644 "$srcdir"/open-iscsi.service "$pkgdir"/usr/lib/systemd/system/open-iscsi.service

  echo -n >"$pkgdir"/etc/iscsi/initiatorname.iscsi

  # copy docs
  mkdir -p "$pkgdir"/usr/share/doc/${pkgname}
  install -m644 Changelog "$pkgdir"/usr/share/doc/${pkgname}/
  install -m644 README "$pkgdir"/usr/share/doc/${pkgname}/

  mv "$pkgdir"/sbin "$pkgdir"/usr/bin
}
