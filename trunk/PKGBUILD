# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Ila√Ø Deutel

pkgname=typst
pkgver=0.1
pkgrel=1
epoch=1
pkgdesc='A markup-based typesetting system for the sciences'
arch=(x86_64)
url="https://$pkgname.app"
_url="https://github.com/$pkgname/$pkgname"
license=(Apache)
makedepends=(cargo
             git)
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('e8533e6cba1422bc2ac5a0c89c936a792fa07df1aaabaf6ac1800f2d79061419')

prepare() {
	local hash=$(gunzip < "$_archive.tar.gz" | git get-tar-commit-id)
	cd "$_archive"
	sed -i -e "/unwrap_or_else/s/(unknown hash)/${hash::8}/" cli/build.rs
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	cargo build -p typst-cli --frozen --release --all-features
}

check() {
	cd "$_archive"
	cargo test --workspace --frozen --all-features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
