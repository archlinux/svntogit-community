# $Id$
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=tarsnap
pkgver=1.0.35
pkgrel=1
pkgdesc='An online encrypted snapshotted backup service'
arch=('i686' 'x86_64')
url='http://www.tarsnap.com/'
license=('custom')
depends=('bzip2' 'openssl' 'xz>=5.0.0')
makedepends=('e2fsprogs')
source=("https://www.tarsnap.com/download/$pkgname-autoconf-$pkgver.tgz")
validpgpkeys=('B881A3F3507402F4748DDE1C5F96DA672F102ABB')
md5sums=('74daeff5479ec6f101a903f4047b526f')
sha256sums=('6c9f6756bc43bc225b842f7e3a0ec7204e0cf606e10559d27704e1cc33098c9a')

build() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  ./configure --prefix=/usr --sysconfdir="/etc/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
