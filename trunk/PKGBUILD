# Maintainer: David Runge <dvzrv@archlinux.org>

_name=natsort
pkgname=python-natsort
pkgver=8.3.0
pkgrel=1
pkgdesc="Simple yet flexible natural sorting in Python"
arch=(any)
url="https://github.com/SethMMorton/natsort"
license=(MIT)
depends=(python python-typing-extensions)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-hypothesis python-pytest-mock)
optdepends=(
  'python-fastnumbers: more efficient sorting'
  'python-pyicu: sort in locale dependant manner'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('63a65a99c9475e8ae96d722ae4c093cc73034c0dfdd254b24ae02904dd81cc8be36038c8a3fc514080f436d4124847f0f331d06a87c435f0b7b7d1d72313ae0d')
b2sums=('7fd480c8bfaceaca7431716e7721acac89d4a5c87a951ecf2754d80062437776eee823d7b94c58e2b9bc6864d9c0f595ee8667c538609c296694a548c126ab58')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGELOG.md,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
