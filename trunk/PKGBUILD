# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.10.1
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
depends=('qt6-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11'
         'qt6-declarative' 'qt6-quickcontrols2' 'qt6-svg' 'zstd')
makedepends=('mesa' 'qt6-tools' 'python' 'qbs')
optdepends=('python: Python plugin'
            'qt6-imageformats: Support for additional image formats (including WebP)')
source=($pkgname-$pkgver.tar.gz::https://github.com/mapeditor/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('7cf2f87640b1fd78b251737c6da2e0b9c7dbeb6df4b7f9b9aa9634c98157e00b334b35c2162741cf46ece00dcce38f5a81e3f9b83de8087764e2f1c77ad2755c')

prepare() {
  qbs setup-qt /usr/bin/qmake6 qt6
  qbs config defaultProfile qt6
}

build() {
  cd $pkgname-$pkgver
  qbs qbs.installPrefix:"/usr" projects.Tiled.useRPaths:false projects.Tiled.installHeaders:true
}

package() {
  cd $pkgname-$pkgver
  qbs install --install-root "$pkgdir"
}
