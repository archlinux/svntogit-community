# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=16.0
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.net/"
license=('GPL')
depends=('libxml2' 'python')
changelog=$pkgname.changelog
source=(https://github.com/nzbget/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver-src.tar.gz)
sha256sums=('95bf4d1b888c631da06ef2699219c855a8d5433a3907791aee0d075c413ccdd0')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck --with-tlslib=OpenSSL
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/$pkgname
  install -m644 -t "${pkgdir}"/usr/share/$pkgname ChangeLog README
}
