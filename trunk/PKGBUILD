# Maintainer : Daniel Bermond < archlinux-org: dbermond >
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgbase=python-breathe
pkgname=('python-breathe' 'python2-breathe')
pkgver=4.11.1
pkgrel=1
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output."
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
makedepends=('python' 'git' 'python-setuptools' 'python2' 'python2-setuptools')
source=("git+https://github.com/michaeljones/breathe#tag=v${pkgver}?signed")
checkdepends=('python-nose' 'python2-nose' 'python-sphinx' 'python2-sphinx')
sha256sums=('SKIP')
validpgpkeys=('79DC0FAF18C31807E82E1A6B9EF1DC92B5F301BD') # Melvin Vermeeren <mail@mel.vin>

prepare() {
  cp -a "breathe" "breathe2"
}

# FIXME: tests are disabled for now, as sphinx 1.8.1+ break them.
# See upstream bug: https://github.com/michaeljones/breathe/issues/394
# check() {
#   cd breathe
# 
#   PYTHONPATH=$PWD make test
# 
#   cd "${srcdir}/breathe2"
# 
#   PYTHONPATH=$PWD make test
# 
# }

build() {
  cd breathe
  python setup.py build

  cd "${srcdir}/breathe2"
  python setup.py build
}

package_python-breathe() {
  depends=('python' 'python-setuptools' 'python-sphinx' 'python-docutils' 'python-six')

  cd "breathe"
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}


package_python2-breathe() {
  pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output (python2)"
  depends=('python2' 'python2-setuptools' 'python2-sphinx' 'python2-docutils' 'python2-six')

  cd "breathe2"
  python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1

  # To avoid file conflict with the python3 version
  mv "$pkgdir"/usr/bin/breathe-apidoc{,2}

  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
