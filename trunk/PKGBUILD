# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnac
pkgver=0.2.4.1
pkgrel=5
pkgdesc="Audio converter for GNOME"
arch=('x86_64')
url="http://gnac.sourceforge.net/"
license=('GPL')
depends=('libnotify' 'gst-plugins-base' 'gtk3')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common')
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        fix-build.patch
        gstreamer-1.0.patch)
sha256sums=('8f9c4731bbc40fde57c311a2a7fabe4e3200198abfe7415c659b98a2d54869da'
            'a7a8cc9d3cc9408776e64be50beb7c48cc1a3d9499cb98e2dac5f45a151475ac'
            '57331aaf1987e8920594a4a753bed2c719d2152c7448ab53ba09ec208521abae')

prepare() {
  cd $pkgname-$pkgver

  # Fix build
  patch -Np1 -i ../fix-build.patch

  # Port to GStreamer 1.0
  patch -Np1 -i ../gstreamer-1.0.patch

  autoreconf -fi
  intltoolize --force
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
