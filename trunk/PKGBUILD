# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
pkgver=0.4.2
_commit=f9165ecf70e8d00584a723ade7e0aedec48dd70f
pkgrel=1
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-jsonschema')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-rfc3339-validator')
optdepends=('python-rfc3339-validator: validate datetime with rfc3339_validator')
source=("git+https://github.com/p1c2u/openapi-schema-validator.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cd openapi-schema-validator
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd openapi-schema-validator
  python -m build --wheel --no-isolation
}

check() {
  cd openapi-schema-validator
  python -m pytest
}

package() {
  cd openapi-schema-validator
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
