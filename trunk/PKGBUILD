# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.6.0
pkgrel=4
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'lcms2' 'libraw' 'openexr' 'qt5-webengine' 'qt5-svg'
         'qt5-webkit' 'ccfits' 'desktop-file-utils' 'hicolor-icon-theme' 'boost-libs')
makedepends=('cmake' 'boost' 'gtest' 'qt5-tools' 'eigen')
optdepends=('hugin: align image stack functionality')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(https://sourceforge.net/projects/qtpfsgui/files/luminance/$pkgver/luminance-hdr-$pkgver.tar.bz2)
sha256sums=('d7d2003e0ef4ead6f4b4c526bc44749a1fb845e795e0c5b7c35a393b0c0518a1')

build() {
  cd "${srcdir}"/luminance-hdr-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/luminance-hdr-$pkgver

  make DESTDIR="${pkgdir}" install
}
