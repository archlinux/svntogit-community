# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-params-validate
pkgver=1.31
pkgrel=1
pkgdesc="Validate sub params against a spec"
arch=('x86_64')
url="https://search.cpan.org/dist/Params-Validate"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-module-implementation')
makedepends=('perl-module-build')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/D/DR/DROLSKY/Params-Validate-${pkgver}.tar.gz")
sha256sums=('1bf2518ef2c4869f91590e219f545c8ef12ed53cf313e0eb5704adf7f1b2961e')

build() {
  cd  Params-Validate-${pkgver}
  perl ./Build.PL --installdirs=vendor
  ./Build
}
package() {
  cd  Params-Validate-${pkgver}
  ./Build install --destdir="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
