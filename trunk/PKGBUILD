# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-at-spi2-core
pkgname=(lib32-at-spi2-core)
pkgver=2.46.0
pkgrel=2
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
arch=(x86_64)
license=(GPL2)
depends=(
  at-spi2-core
  lib32-dbus
  lib32-glib2
  lib32-libx11
  lib32-libxml2
  lib32-libxtst
)
makedepends=(
  dbus-broker
  git
  meson
)
_commit=97d9ba04e19d0a9edd209d97db81eb6ede5fe4d6  # tags/AT_SPI2_CORE_2_46_0^0
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd at-spi2-core
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd at-spi2-core
}

build() {
  local meson_options=(
    --libdir=/usr/lib32
    -D introspection=no
    -D default_bus=dbus-broker
    -D docs=false
  )

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson at-spi2-core build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # memory test fails without desktop
  # broker fails to launch without journald
  dbus-run-session meson test -C build --print-errorlogs || :
}

package_lib32-at-spi2-core() {
  provides=(
    "lib32-at-spi2-atk=$pkgver-$pkgrel"
    "lib32-atk=$pkgver-$pkgrel"
    libatk-1.0.so
    libatk-bridge-2.0.so
    libatspi.so
  )
  conflicts=(
    'lib32-at-spi2-atk<=2.38.0-2'
    'lib32-atk<=2.38.0-2'
  )
  replaces=(
    'lib32-at-spi2-atk<=2.38.0-2'
    'lib32-atk<=2.38.0-2'
  )

  meson install -C build --destdir "$pkgdir"

  rm -r "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim:set sw=2 sts=-1 et:
