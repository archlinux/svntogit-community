# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=3.0.4
pkgrel=3
pkgdesc="File manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gsettings-qt' 'kwindowsystem' 'libnotify' 'poppler-qt5' 'qt5-ukui-platformtheme' 'qt5-x11extras' 'udisks2')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('4d958548007a64fed522adbce2abb855c963c76b7ae5b22d107eecaf268eeee4f7eca40aea1a6019148853be622401b783304ca025b89b5307f5dfb2c007cc94')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
