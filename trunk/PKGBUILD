# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

pkgname=python-black
pkgver=21.8b0
pkgrel=1
pkgdesc='Uncompromising Python code formatter'
arch=('any')
url=https://github.com/psf/black
license=('MIT')
depends=('python' 'python-click' 'python-mypy_extensions' 'python-pathspec'
         'python-platformdirs' 'python-regex' 'python-tomli'
         'python-typing_extensions')
makedepends=('python-build' 'python-install' 'python-setuptools-scm'
             'python-wheel')
checkdepends=('ipython' 'python-aiohttp' 'python-aiohttp-cors'
              'python-parameterized' 'python-pytest' 'python-typed-ast'
              'python-tokenize-rt')
optdepends=('ipython: for Jupyter notebook support'
            'python-tokenize-rt: for Jupyter notebook support'
            'python-aiohttp: for the blackd HTTP server'
            'python-aiohttp-cors: for the blackd HTTP server'
            'python-colorama: for colored diffs'
            'python-typed-ast: to format Python 2 code')
source=("https://files.pythonhosted.org/packages/source/b/black/black-$pkgver.tar.gz")
sha256sums=('570608d28aa3af1792b98c4a337dbac6367877b47b12b88ab42095cfc1a627c2')
b2sums=('5028edb7ff82ec2840aa4f32311ede415ee998c0e29a085760fdf59538aca2ff18dba102e37d382da4bda6f550b9dfaf362f9fe86561eabef56ea7731168757e')

build() {
  cd "black-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "black-$pkgver"
  mkdir -p temp
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m install --optimize=1 --destdir=temp dist/*.whl
  chmod +x temp/usr/bin/* # https://github.com/FFY00/python-install/pull/6
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" pytest tests -m "not without_python2"
}

package() {
  cd "black-$pkgver"
  export PYTHONHASHSEED=0
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl

  # https://github.com/FFY00/python-install/issues/7
  sed -i 's/patched_main \[d]/patched_main/' "$pkgdir"/usr/bin/blackd

  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/*

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s $site_packages/black-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
