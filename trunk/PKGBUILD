# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=20
_realver=build${pkgver}
pkgrel=9
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('x86_64')
url="https://widelands.org/"
license=('GPL')
depends=('sdl2_mixer' 'sdl2_image' 'sdl2_net' 'sdl2_ttf' 'sdl2_gfx' 'lua' 'glew' 'python' 'boost-libs')
makedepends=('cmake' 'boost' 'mesa' 'ninja' 'git')
replaces=('widelands-data')
source=("git+https://github.com/widelands/widelands.git#commit=ce578db"
        $pkgname.desktop
        $pkgname.png
        45249a2c7172fdc2d5cd856a1ee05670f21f0db7.patch)
sha512sums=('SKIP'
            'ef7e54ffba11a2fe54d2cafc0a12fcbe5028e861b9b980f549317b159c8532ee62423af8724b05cfd344017097e022215fa750f4ca9afffa57703d434017d727'
            'f7db9d7584ec8431f2f1857baddeb32cd3ae182343e7f0369e61fb444514bd9c2e6a3931ae34f24cc994840dffbbe4451f33845798f3d582f41b31f451b261ac'
            '51b1fb6bed262cc62023403c5d1bd2663c7e37ed9e89afbe11eec54a39bde8c48e1413f80b4044df5b7e972287843e1fa8bf5a8c0af7c5afa146c9f087181b7c')

prepare() {
  cd $pkgname

  patch -Np1 -i "$srcdir"/45249a2c7172fdc2d5cd856a1ee05670f21f0db7.patch

  mkdir -p build && cd build
}

build() {
  cd $pkgname/build

  cmake -G Ninja .. \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWL_INSTALL_BASEDIR=/usr \
      -DWL_INSTALL_DATADIR=/usr/share/widelands
  ninja
}

package() {
  cd $pkgname/build

  DESTDIR="$pkgdir" ninja install

  mkdir -p "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{widelands,wl_map_object_info,wl_map_info} "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{VERSION,ChangeLog,CREDITS} "$pkgdir"/usr/share/widelands

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/COPYING "$pkgdir"/usr/share/licenses/$pkgname

  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
