# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Lorenzo Nizzi Grifi Gargiolli <lorenzo.nizzi.grifi@gmail.com>
# Contributor: marc0s <marc0s@fsfe.org>

pkgname=pcsc-tools
pkgver=1.4.26
pkgrel=1
pkgdesc="PC/SC Architecture smartcard tools"
arch=('i686' 'x86_64')
url="http://ludovic.rousseau.free.fr/softwares/pcsc-tools/"
license=('GPL')
depends=('pcsclite' 'pcsc-perl' 'gtk2-perl' 'glib-perl')
makedepends=('pkg-config')
source=("http://ludovic.rousseau.free.fr/softwares/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
md5sums=('b8376b1dc14bb43f799e5be67f55efe3'
         'SKIP')
validpgpkeys=('F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E') # Ludovic Rousseau <rousseau@debian.org>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's:-Wall -O2:${CFLAGS}:g' Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/usr" install
}
