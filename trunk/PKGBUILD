# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgbase=python-pysnmp
pkgname=('python-pysnmp' 'python2-pysnmp')
_pyname=pysnmp
pkgver=4.4.11
pkgrel=3
pkgdesc='Open source and free implementation of v1/v2c/v3 SNMP engine'
url='https://github.com/etingof/pysnmp'
arch=('any')
license=('BSD')
makedepends=('python' 'python-setuptools' 'python-sphinx' 'python-pysmi' 'python-pyasn1'
             'python2' 'python2-setuptools' 'python2-sphinx' 'python2-pysmi' 'python2-pyasn1')
options=('!makeflags')
source=(https://github.com/etingof/pysnmp/archive/v${pkgver}/${_pyname}-v${pkgver}.tar.gz)
sha256sums=('e09aa9f9089f14f05db43be214be9c854b6fa9036820864774305285b9ea8fd6')
sha512sums=('1b4312c425765d9fc10a5382a93a171f98afa9f53dd053d6254ac741c8fa08b3bd5639cf1483e5cbf6986a5fd1aaace65c4486d6c3df56f7f0b01aaf45d87016')

prepare() {
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  export PYTHONPATH=.
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -C docs text man
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -C docs text man
  )
}

package_python-pysnmp() {
  depends=('python' 'python-pysmi' 'python-pyasn1')
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -Dm 644 docs/build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${_pyname}.1"
  install -Dm 644 docs/build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-pysnmp() {
  depends=('python2' 'python2-pysmi' 'python2-pyasn1')
  cd ${_pyname}-${pkgver}
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -Dm 644 docs/build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
