# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=tilda
pkgver=1.3.3
pkgrel=1
pkgdesc="A Gtk based drop down terminal for Linux and Unix"
arch=('i686' 'x86_64')
url="https://github.com/lanoxx/tilda"
license=('GPL')
depends=('vte3' 'confuse')
changelog=$pkgname.changelog
source=(https://github.com/lanoxx/$pkgname/archive/$pkgname-$pkgver.tar.gz)
sha256sums=('71baa5572c498e5b3e94f5b57e8118c1ef0fdaea269a5b2425208e1adec284b1')

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
