# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.45.2
pkgrel=1
pkgdesc="Sass makes CSS fun again"
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('dart')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('60050e1a52eb6558fd6b5a2cd8f1f31abb52bdce2834b94ab87032b7ab596f0b4414f3e3dce622775aa13c4196177a327463ecfea13fae41f9b16b5c4dad7bb3')
b2sums=('1455e92af345615f8f9a9e538b30d9dea7a0fcef5e18774834261a905f975a3eac50127a902fa9abd7d348120b2b1b37029473ea3914fd5e30255939287a3542')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  pub get
}

build() {
  cd "$pkgname-$pkgver"
  dart compile exe -Dversion=$pkgver -o sass bin/sass.dart
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
