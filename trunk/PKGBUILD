# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

pkgname=python-networkx
pkgver=2.4
pkgrel=1
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.github.io"
depends=('python-decorator')
optdepends=('python-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
            'python-scipy: Provides flexible drawing of graphs.'
            'python-pyparsing: Required for pydot, GML file reading.'
            'python-yaml: Required for YAML format reading and writing.')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://github.com/networkx/networkx/archive/networkx-$pkgver.tar.gz")
sha512sums=('2fedc556d068d2b0b1d238f5f2715c928c7999df33c318b720650893a3e536459545769c8b86c97c62ef56a62cc808848bd6897df9f37656810b11990f5d6be0')

build() {
  cd networkx-networkx-$pkgver
  python setup.py build
}

check() {
  cd networkx-networkx-$pkgver
  python setup.py pytest
}

package() {
  cd networkx-networkx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv "$pkgdir"/usr/share/doc/networkx-$pkgver "$pkgdir"/usr/share/doc/$pkgname
}
