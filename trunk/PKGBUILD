# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
pkgver=1.26.0
pkgrel=2
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=('any')
url='https://github.com/flori/tins'
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/tins-${pkgver}.gem")
noextract=("tins-${pkgver}.gem")
sha256sums=('b86e70b20c1554a9b2585ed96bf13160ed5daf5db49588374c82cc5fb683016c')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} tins-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ruby-tins
  ln -s ${_gemdir}/gems/tins-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/ruby-tins/
}
