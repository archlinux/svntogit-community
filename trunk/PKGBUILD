# $Id$
# Maintainer: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Contributor: Andre Klitzing <aklitzing () online () de>
# Contributor : Allan McRae <mcrae_allan@hotmail.com>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi> aka Neverth

_dlid=4134
pkgname=po4a
pkgver=0.46
pkgrel=1
pkgdesc="tools for helping translation of documentation"
arch=('any')
license=('GPL')
url="http://po4a.alioth.debian.org/"
depends=('perl' 'gettext')
makedepends=('docbook-xsl' 'perl-text-wrapi18n' 'perl-locale-gettext' 
             'perl-term-readkey' 'perl-sgmls' 'perl-module-build')
options=('!emptydirs')
source=(https://alioth.debian.org/frs/download.php/file/${_dlid}/${pkgname}-${pkgver}.tar.gz)
md5sums=('27bddc74edab0dd0c7615779a9b17506')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	perl Build.PL installdirs=vendor create_packlist=0
	perl Build
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	perl Build destdir=${pkgdir} install
	# remove perllocal.pod and .packlist
	find ${pkgdir} -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
