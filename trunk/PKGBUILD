# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.6.3
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
options=('debug')
depends=('dtkcore' 'freeimage' 'libqtxdg' 'librsvg')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtkgui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('79f084934c7c1a44275ce5423a5bb7905020168e34ec9bc0ae57d1c2fad3433e64ab263865ce7079fd75cabc2576aea93092b0e41463f356039a371eb550eaee')

build() {
  cd dtkgui-$pkgver
  cmake -GNinja \
    -DNOTPACKAGE=OFF \
    -DLINUXNAME="archlinux"\
    -DBUILD_DOCS=OFF \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkgui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
