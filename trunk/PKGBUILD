# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=python-prettytable
pkgver=3.6.0
pkgrel=1
pkgdesc="A simple Python library for easily displaying tabular data"
url="https://github.com/jazzband/prettytable"
arch=('any')
license=('BSD')
depends=('python-wcwidth')
makedepends=('python-build' 'python-installer' 'python-hatchling' 'python-hatch-vcs')
checkdepends=('python-pytest-lazy-fixture')
conflicts=('python-ptable')
source=("https://github.com/jazzband/prettytable/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b4693a3d30555f598cb955923560d02ce7c757e87ee49bf09c8d50d3ce8bef67ebfad1c780914f488e89852c0e363c2cad098addc058a88864efb8eefa8e9cd8')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd prettytable-$pkgver
  python -m build -nw
}

check() {
  cd prettytable-$pkgver
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest
}

package() {
  cd prettytable-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
