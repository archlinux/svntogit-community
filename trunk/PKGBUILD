# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=postgrest
pkgver=0.4.2.0
pkgrel=25
pkgdesc="REST API for any Postgres database"
url="https://github.com/begriffs/postgrest"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-auto-update" "haskell-hasql" "haskell-hasql-pool" "haskell-protolude"
         "haskell-text" "haskell-warp" "haskell-base64-bytestring" "haskell-retry" "haskell-aeson"
         "haskell-ansi-wl-pprint" "haskell-case-insensitive" "haskell-cassava"
         "haskell-configurator-ng" "haskell-contravariant" "haskell-either"
         "haskell-hasql-transaction" "haskell-heredoc" "haskell-http" "haskell-http-types"
         "haskell-insert-ordered-containers" "haskell-interpolatedstring-perl6" "haskell-jwt"
         "haskell-lens" "haskell-lens-aeson" "haskell-network-uri" "haskell-optparse-applicative"
         "haskell-parsec" "haskell-ranged-sets" "haskell-regex-tdfa" "haskell-safe"
         "haskell-scientific" "haskell-swagger2" "haskell-unordered-containers" "haskell-vector"
         "haskell-wai" "haskell-wai-cors" "haskell-wai-extra" "haskell-wai-middleware-static"
         "haskell-cookie")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        protolude-0.2.patch)
sha256sums=('15b0902c2318e44bcf2d2c1cd654abc8acbbe00a9794e5ad821ab09eb0aca608'
            '8b2eb2a999dba5bfc1953926e170c2ff5086efed75a038923249840d5370f9a6')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 -i ../protolude-0.2.patch
    sed -i 's/, optparse-applicative >= 0.13 && < 0.14/, optparse-applicative >= 0.13/' ${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-CI
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
