# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=srslte
pkgname=($pkgbase $pkgbase-avx2)
_pkgname=srsLTE
pkgver=19.12
_pkgver=release_${pkgver/./_}
pkgrel=5
pkgdesc='Open source SDR LTE software suite from Software Radio Systems (SRS)'
arch=('x86_64')
url='https://www.srslte.com'
license=('AGPL3')
depends=('fftw' 'mbedtls' 'boost-libs' 'lksctp-tools' 'libconfig'
         'libuhd' 'soapysdr' 'bladerf' 'zeromq' 'pcsclite' 'srsgui')
makedepends=('cmake' 'boost')
provides=('srsue' 'srsenb' 'srsenb')
conflicts=('srsue' 'srsenb' 'srsenb')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/srsLTE/srsLTE/archive/$_pkgver.tar.gz"
        'tunable-simd.patch::https://github.com/FFY00/srsLTE/commit/6b4fd9490bb7b6c2a388e78ee26aa84017585a35.patch'
        'remove-install-targets.patch::https://github.com/FFY00/srsLTE/commit/738785f4fb9f308c5270fb1b0bac83e1e96838df.patch')
sha512sums=('bd3e39761e8fe48661d3639f44bb724d4ba2e20d03d0fc9871fc5d9a38fea2f3cd05f5b7254c0b0b63d2973b8cf41f8f65981f5206bb42c30588d0b593d1f69f'
            'e4a9c4457e06fb71bf18b2f8e91234588c01327c15f1267c60ee0ea747baaf9795b55767e99d359e6140f2ccace3d00a17e4fc151798eafa5f523decfcdb35ee'
            '0bfbb73d47bda2280007c7a71a81fa9a35ee4a414b809cdb18d76e3bdbceb2e1ee4797a05f68b4ad89843002b6dd58b3eef45ad3a407da35fe614ffb5f685ce3')

prepare() {
  cd $_pkgname-$_pkgver

  patch -p1 -i ../tunable-simd.patch
  patch -p1 -i ../remove-install-targets.patch

  # Disable failing test
  sed -i '/add_test(ue_rf_failure/d' srsue/src/CMakeLists.txt

  mkdir build

  cp -r "$srcdir"/$_pkgname-$_pkgver "$srcdir"/$_pkgname-avx2-$_pkgver
}

build() {
  echo 'Building srslte...'
  cd "$srcdir"/$_pkgname-$_pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_GENERIC=True

  make

  echo 'Building srslte-avx2...'
  cd "$srcdir"/$_pkgname-avx2-$_pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_GENERIC=True \
    -DHAVE_AVX2=True

  make
}

check() {
  echo 'Running tests for srslte...'
  cd "$srcdir"/$_pkgname-$_pkgver/build

  ctest $(echo $MAKEFLAGS | grep -Po '\-j[0-9][0-9]*')

  echo 'Running tests for srslte-avx2...'
  cd "$srcdir"/$_pkgname-avx2-$_pkgver/build

  ctest $(echo $MAKEFLAGS | grep -Po '\-j[0-9][0-9]*')
}

package_srslte() {
  install=$pkgname.install

  cd $_pkgname-$_pkgver/build

  make DESTDIR="$pkgdir" install
}

package_srslte-avx2() {
  pkgdesc="$pkgdesc (with AVX2 extensions)"
  provides=($pkgbase)
  conflicts=($pkgbase)
  install=$pkgname.install

  cd $_pkgname-avx2-$_pkgver/build

  make DESTDIR="$pkgdir" install
}

