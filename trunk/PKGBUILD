# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >

pkgname=par2cmdline
pkgver=0.5.4
pkgrel=1
pkgdesc='PAR 2.0 compatible file verification and repair tool'
url='https://github.com/BlackIkeEagle/par2cmdline'
license=('GPL2')
arch=('i686' 'x86_64')
source=("https://github.com/downloads/BlackIkeEagle/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('96b4fb91859c0214d2c562636abea3af')

build() {
	cd ${pkgname}-${pkgver}
	aclocal
	automake --add-missing
	autoconf
	./configure --prefix=/usr
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 ft=sh et:
