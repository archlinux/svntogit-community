# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-socketio
pkgver=5.7.2
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
depends=('python' 'python-six' 'python-engineio' 'python-bidict' 'python-msgpack')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel'
             'python-sphinx' 'python-tornado')
optdepends=('python-tornado: tornado asyncio webserver')
checkdepends=('python-pytest')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('7acc082c5df552ed9d393d7fe38b698a868481ccb6a981d0b4501522400124eedd54b43d42c4051515d38efbaff277c01bef5c1f7c9d59720bd00c7b9ae0c3ab')
b2sums=('0570b530938caa0f1827c3ce8b33e6cc5bc3c7f6bd2acd416cb394e6c8b6231790cefacf884d8df18bbc7c5fca718302e1e8005a61dc3efc2cfa54c54342717f')

build() {
  cd ${pkgbase}-${pkgver}
  python -m build --wheel --no-isolation
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgbase}-${pkgver}
  PYTHONPATH=build/lib python setup.py test
}

package() {
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
