# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=tilt
pkgname=ruby-tilt
pkgver=2.0.8
pkgrel=2
pkgdesc='Generic interface to multiple Ruby template engines'
url='https://github.com/rtomayko/tilt'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-ronn')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rtomayko/tilt/archive/v${pkgver}.tar.gz)
sha256sums=('025b32ea25c5650bf4bcfb89d1e075b11c28793ff812819467762834d1970e4c')
sha512sums=('451d734226cdb06b6f5c3a0a41e5e0d7f5bcb390eeed0bf4b60e5419cfa663a067b57ee241027f88bd99a6b3582340d64a9bb7a795775edf41941f95e463608b')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
  ronn -w -s toc -r5 --markdown man/*.ronn
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md man/tilt.1.{markdown,html} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 man/tilt.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  rm -r "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/man"
}

# vim: ts=2 sw=2 et:
