# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=qbittorrent
pkgname=(qbittorrent qbittorrent-nox)
pkgver=4.1.1
pkgrel=1
arch=('x86_64')
url="https://www.qbittorrent.org"
license=('custom' 'GPL')
makedepends=('boost' 'libtorrent-rasterbar' 'qt5-tools' 'qt5-svg')
source=("https://downloads.sourceforge.net/sourceforge/qbittorrent/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('8edc7a7fff8aa45e7bf763c91f30e064e1d7acd3f6efecb12fee420b75457ee7'
            'SKIP')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>

build() {
  cd ${pkgbase}-${pkgver}

  mkdir -p ${pkgbase}
  pushd ${pkgbase}
  ../configure --prefix=/usr
  make
  popd

  # Build nox variant
  mkdir -p "${pkgbase}-nox"
  pushd "${pkgbase}-nox"
  ../configure --prefix=/usr --disable-gui --enable-systemd
  make
}

package_qbittorrent() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends=('libtorrent-rasterbar' 'qt5-svg' 'hicolor-icon-theme')
  optdepends=('python: needed for torrent search tab')

  cd ${pkgbase}-${pkgver}/${pkgbase}

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/${pkgbase}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

package_qbittorrent-nox() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui"
  depends=('libtorrent-rasterbar' 'qt5-base')

  cd ${pkgbase}-${pkgver}/${pkgbase}-nox

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/${pkgbase}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
