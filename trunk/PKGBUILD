# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=5.6.2
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL LGPL)
depends=(accountsservice gtk3 libxkbfile libpulse)
makedepends=(meson samurai gobject-introspection intltool)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        config.patch)
sha256sums=('34bdf8b8acec0eaf3cc726e1e90e86752c1cf62d874c37ca49acca7e62dafae8'
            'e70227c64eca6857fe9adc21c6aee851df40b3a54bf34a145e0dc4b9819a7b6a')
b2sums=('b96be728da24a43b88a14b7cd5e9dd6313c753e32e7f2dcb2a5ac4f2c8eb27266f261a1a1e5499d9a94ccbc679d9c4ea9cdaae4ee6b1187de44d518a9a483ca7'
        '5ae28fca1f3fee35222569afdba3f6f2df00f2b27709d1135589f33a3b80546ca541c391f5ee02759467fcaffe380dc1cbc4117dffe3c10cdf7046fd8588e384')

prepare() {
  cd ${pkgname}-${pkgver}
  # Adjust configuration
  patch -p1 < ../config.patch
  mkdir -p build
}

build() {
  cd ${pkgname}-${pkgver}/build
  arch-meson ..
  samu
}

package() {
  cd ${pkgname}-${pkgver}/build
  DESTDIR="${pkgdir}" samu install
}
