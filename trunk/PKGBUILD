# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>

pkgbase=python-markups
pkgname=(python-markups python2-markups)
pkgver=0.6.3
pkgrel=1
pkgdesc='Wrapper around various text markups'
arch=('any')
url='https://launchpad.net/python-markups'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=(http://pypi.python.org/packages/source/M/Markups/Markups-$pkgver.tar.gz)
sha256sums=('e3ff5de2be018240c526e017972b37181cb3d5dfb7c96ad14eae6639140f58ef')

package_python-markups() {
  depends=('python')
  optdepends=('python-markdown: markdown support'
              'python-docutils: reST support')

  cd "${srcdir}"/Markups-$pkgver
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-markups() {
  depends=('python2')
  optdepends=('python-markdown2: markdown support'
              'python-docutils2: reST support')

  cd "${srcdir}"/Markups-$pkgver
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
