# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

_pyname=jupyter_console
pkgname=${_pyname/_/-}
pkgver=6.5.1
pkgrel=1
pkgdesc='An IPython-like terminal frontend for Jupyter kernels in any language'
arch=(any)
url='https://pypi.org/project/jupyter_console/'
license=(BSD)
depends=(python-jupyter-client)
makedepends=(python-build python-installer python-wheel python-setuptools)
checkdepends=(python-pytest)
conflicts=(jupyter_console)
provides=(jupyter_console)
replaces=(jupyter_console)
source=(https://pypi.python.org/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('6b91b7b6e8a715053b536db209a2f4b02429d7b28db27373a56a26b0bebd620b')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -r "$pkgdir"/usr/lib/python*/site-packages/$_pyname/tests
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
