# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Preecha Patumchareonpol <yumyai at gmail.com> 

pkgname=podofo
pkgver=0.10.0
pkgrel=1
pkgdesc='A C++ library to work with the PDF file format'
arch=(x86_64)
url="http://$pkgname.sourceforge.net"
_url="https://github.com/$pkgname/$pkgname"
license=(GPL2)
depends=(fontconfig
         freetype2
         libidn
         libjpeg-turbo
         libpng
         libtiff
         libxml2
         lua
         openssl
         zlib)
makedepends=(cmake)
_archive="$pkgname-$pkgver"
source=("$_url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('c9bf607fe4862b5cc6eac1754f0e39674c553350e8bbde68f5fff3e3eb2ed4bc')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D PODOFO_HAVE_JPEG_LIB=True \
		-D PODOFO_HAVE_PNG_LIB=True \
		-D PODOFO_HAVE_TIFF_LIB=True
	make -C build
}

package() {
	cd "$_archive"
	make -C build DESTDIR="$pkgdir" install
}
