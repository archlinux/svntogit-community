# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>

pkgname=tensorboard
pkgver=2.8.0
pkgrel=2
pkgdesc="TensorFlow's Visualization Toolkit"
url='https://github.com/tensorflow/tensorboard'
arch=('x86_64')
license=('APACHE')
depends=('python' 'python-werkzeug' 'python-numpy' 'python-bleach' 'python-wheel' 'python-markdown'
         'python-html5lib' 'python-protobuf' 'python-grpcio' 'python-google-auth-oauthlib'
         'python-tensorboard_plugin_wit' 'absl-py')
makedepends=('bazel' 'python-setuptools' 'tree' 'rust' 'python-pip')
optdepends=('python-tensorflow: advanced features for TensorBoard')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tensorflow/tensorboard/archive/${pkgver}.tar.gz"
        oauthlib-version.patch
        bazel.patch)
options=('!lto')
sha512sums=('9bb53d4ce2e4735bf01e135e382ff5bcf55d41b7e8bd81061096b6a2010ac48793288eb9da9d2ee3c5ce7cd0532cbcaff72d4c0905d3ebee1c5a42fd3851dff7'
            'c85bf0c6e67a19bce0af3ad2dd65851991ba15a6bb7f96433650d5a2725706e015a34166656cfb76874d2498067e6c56bdabb07d114fd9299436f68ebdf50ec3'
            'b85a33ea074c1a6022fd9c71a0d2477953400c49a1ddbad149ed356061a70f5c60b26b65dae272d1f88c8f6b22b6a12fd1a9a32322876b38d173201f87ab6846')

prepare() {
  cd "$pkgname-$pkgver"
  # FS#74460
  patch -Np1 -i "${srcdir}"/oauthlib-version.patch
  # fix build
  patch -Np1 -i "${srcdir}"/bazel.patch
}

build() {
  cd "$pkgname-$pkgver"

  PYTHONWARNINGS=ignore bazel build //tensorboard
  bazel build //tensorboard/pip_package:build_pip_package

  cd tensorboard/data/server
  cargo build --release
  python pip_package/build.py --server-binary target/release/rustboard --out-dir /tmp
  cd "$srcdir/$pkgname-$pkgver"

  mkdir pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/external pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/tensorboard pypackage
  cp tensorboard/pip_package/{README.rst,MANIFEST.in,setup.py,requirements.txt} pypackage
  cp README.md pypackage
  cd pypackage
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  pip install --ignore-installed --root "$pkgdir" /tmp/*.whl

  cd pypackage
  python setup.py install --root="$pkgdir" --optimize=1

  rm -r "$pkgdir"/usr/lib/python3.10/site-packages/external/

  _requires="$pkgdir"/usr/lib/python3.10/site-packages/tensorboard-${pkgver}-py3.10.egg-info/requires.txt
  sed -i 's/tensorboard-data-server.*/tensorboard-data-server/' ${_requires}
}

# vim:set ts=2 sw=2 et:
