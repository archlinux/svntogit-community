# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=marco
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=2
pkgdesc="A window manager for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libcanberra' 'libgtop' 'mate-desktop-schemas' 'startup-notification' 'zenity')
makedepends=('intltool' 'itstool')
groups=('mate')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('b9b3c14812fcb7ab5dfd2c3498eac639d2e63be6')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-startup-notification

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
