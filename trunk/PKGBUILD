# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libhubbub
pkgver=0.3.0
pkgrel=2
pkgdesc='HTML5 compliant parsing library'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/hubbub/'
license=('MIT')
provides=('hubbub')
replaces=('hubbub')
conflicts=('hubbub')
depends=('libparserutils>=0.2.0')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('dbf3b55bf2531d2a8e70f370a9b396cc78bfd5a092f87beb10b05c04fd7cdaa7')

prepare() {
  sed -i 's:-Werror::' "$pkgname-$pkgver/Makefile"
}

build() {
  make -C "$pkgname-$pkgver" LIBDIR=lib PREFIX=/usr COMPONENT_TYPE='lib-shared'
}

package() {
  cd "$pkgname-$pkgver"
  make install \
    LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE='lib-shared'
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
