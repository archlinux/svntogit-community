# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>

pkgbase=python-yaml
pkgname=(python-yaml python2-yaml)
pkgver=5.1.2
pkgrel=1
pkgdesc='Python bindings for YAML, using fast libYAML library'
url='https://pyyaml.org/wiki/PyYAML'
arch=('x86_64')
license=('MIT')
makedepends=('python' 'python2' 'libyaml' 'cython' 'cython2')
source=(https://pyyaml.org/download/pyyaml/PyYAML-${pkgver}.tar.gz)
sha512sums=('b5ee201c612486a95c87b7e41d474313160284359b8b30ababc80b51325aef9dd7dc27eb3b8a03a4749cb8c7e3789c5684d5c285fe35d4b20be1f189641531e0')

prepare() {
  # Force cython rebuild
  rm PyYAML-$pkgver/ext/_yaml.c

  cp -a PyYAML-$pkgver{,-py2}
}

build() {
  msg2 "Building python..."
  (cd PyYAML-$pkgver
    python setup.py --with-libyaml build
  )
  msg2 "Building python2..."
  (cd PyYAML-$pkgver-py2
    python2 setup.py --with-libyaml build
  )
}

check() {
  msg2 "Checking python..."
  (cd PyYAML-$pkgver
    python setup.py test || warning "https://github.com/yaml/pyyaml/issues/184"
  )
  msg2 "Checking python2..."
  (cd PyYAML-$pkgver-py2
    python2 setup.py test || warning "https://github.com/yaml/pyyaml/issues/184"
  )
}

package_python-yaml() {
  depends=('python' 'libyaml')

  cd PyYAML-$pkgver
  python setup.py  --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

package_python2-yaml() {
  depends=('python2' 'libyaml')

  cd PyYAML-$pkgver-py2
  python2 setup.py --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

# vim: ts=2 sw=2 et:
