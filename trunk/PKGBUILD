# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Lucas Hoffmann
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Kazuo Teramoto <kaz.rag at gmail.com>

pkgname=afew
pkgver=3.0.1
pkgrel=1
pkgdesc='Initial tagging script for notmuch mail'
arch=(any)
url=https://github.com/afewmail/afew
license=('custom:BSD')
depends=('notmuch' 'python-chardet' 'python-dkim' 'python-setuptools')
makedepends=('python-setuptools-scm' 'python-sphinx')
checkdepends=('python-freezegun')
source=("https://files.pythonhosted.org/packages/source/a/afew/afew-$pkgver.tar.gz")
sha256sums=('ce857fe1a3bc0982c1dac81aef66cacc148e4f9db06da720f869392af1c2ee72')

build() {
  cd afew-$pkgver
  python setup.py build
  python setup.py build_sphinx -b man
}

check() {
  cd afew-$pkgver
  python -m unittest discover afew/tests
}

package() {
  cd afew-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 build/sphinx/man/afew.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/afew LICENSE
}

# vim:set ts=2 sw=2 et:
