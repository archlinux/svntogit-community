# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=linkify-it-py
pkgname=python-$_pkgname
pkgver=1.0.1
pkgrel=3
pkgdesc='Links recognition library with full unicode support'
arch=('any')
url='https://github.com/tsutsu3/linkify-it-py'
license=('MIT')
depends=('python-uc-micro-py')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('dd3aca108e9b8c246f41b856ac6a23531885b60ce39bbb6268ecc6c73a3329a815146d0ce4768050c703557fd3eb58576477cf5880fdb2e0bcefe4a35f2cfd79')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
