# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-cli
pkgver=0.19.0
pkgrel=1
pkgdesc="A zero trust swiss army knife for working with X509, OAuth, JWT, OATH OTP, etc."
url="https://github.com/smallstep/cli"
arch=(x86_64)
license=(Apache)
depends=(glibc)
makedepends=(go)
optdepends=("step-ca: setup CA/SSO/ACME server")
source=(https://github.com/smallstep/cli/releases/download/v${pkgver}/step_${pkgver}.tar.gz)
sha512sums=('3d7c35f851d39df67dcedc74de0707cfea0e87c1cfec52b952db121daf051cb0813eea9800e2b5b3534b1d0c107d87fa2686570b4a3f65746bf70d3191b086ff')

prepare() {
  sed -i "s/step/${pkgname}/g" "autocomplete/zsh_autocomplete"
  sed -i "s/step/${pkgname}/g" "autocomplete/bash_autocomplete"
}

build() {
  BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%d)"

  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o step-cli cmd/step/main.go
}

check() {
  go test ./...
}

package() {
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 "autocomplete/bash_autocomplete" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "autocomplete/zsh_autocomplete" "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
}
