# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=paperkey
pkgver=1.6
pkgrel=3
pkgdesc='Archive OpenPGP keys on paper'
arch=('x86_64')
url='https://www.jabberwocky.com/software/paperkey/'
license=('GPL')
depends=('glibc')
source=("$url/$pkgname-$pkgver.tar.gz"{,.sig})
sha512sums=('a07fc22bf56cd9524ecba61d1bf00102e6e97842e219682771187a2a2327c9daa885d457417bb2ea960f5ebd66bd711fa1d86687fda159647ff47980dee21b01'
            'SKIP')
validpgpkeys=('7D92FD313AB6F3734CC59CA1DB698D7199242560') # David M. Shaw <dshaw@jabberwocky.com>

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}
