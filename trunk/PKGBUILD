# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-initial-setup
pkgver=3.14.2.1
pkgrel=1
pkgdesc="Simple, easy, and safe way to prepare a new system"
arch=('i686' 'x86_64')
url="http://www.gnome.org/"
license=('GPL')
depends=('libnm-gtk' 'accountsservice' 'cheese' 'gnome-desktop' 'libgweather'
         'gnome-online-accounts' 'libgdm' 'libpwquality' 'libibus' 'libgnomekbd')
makedepends=('intltool')
optdepends=('gdm: GDM Initial Setup mode'
            'gnome-getting-started-docs: GNOME Welcome Tour')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/3.14/$pkgname-$pkgver.tar.xz
        fix-crasher.patch)
sha256sums=('a5f36a584c8a727c2fbe76e0022f7027e501ce74d33503b3d67afc9f58459e30'
            '5bc918b45530fb03c793c5637169526e5caf58217c99030ca7fc9335927ccee9')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-crasher.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/var/lib/gnome-initial-setup"

  # Fix a warning
  chown 102:0 "$pkgdir/usr/share/polkit-1/rules.d"
  chmod 700 "$pkgdir/usr/share/polkit-1/rules.d"
}
