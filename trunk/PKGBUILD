# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Matthias-Christian Ott <matthias.christian.at.tiscali.de>

pkgname=diffstat
pkgver=1.62
pkgrel=1
pkgdesc="Display a histogram of diff changes"
arch=('x86_64')
url="https://invisible-island.net/diffstat"
depends=('glibc')
license=('MIT')
source=("ftp://ftp.invisible-island.net/$pkgname/$pkgname-$pkgver.tgz"
        "ftp://ftp.invisible-island.net/$pkgname/$pkgname-$pkgver.tgz.asc")
sha256sums=('7f09183644ed77a156b15346bbad4e89c93543e140add9dab18747e30522591f'
            'SKIP')
# Thomas Dickey <dickey@invisible-island.net>"
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')

build () {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 package/debian/copyright "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
