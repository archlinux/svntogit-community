# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.9.0
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="https://github.com/zopefoundation/persistent/"
depends=('python-cffi' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("https://github.com/zopefoundation/persistent/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('41a276987e0a830c2cb4e7a5284c72fb1992de8d156b9ad94f17e7922e3635b115b4dbcd8bbea53797452d48a223543e20a80e8b32b8dc959fd56ec0734164d9')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=src
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
