# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-office
pkgver=1.1.6
pkgrel=1
pkgdesc='DBeaver plugin to export data to Microsoft Office format'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-apache-poi')
source=("https://dbeaver.jkiss.org/update/office/latest/plugins/org.jkiss.dbeaver.data.office_${pkgver}.jar"
        "https://dbeaver.jkiss.org/update/office/latest/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('ec8947fc8bd09ff2ca51e045359a071f6c3197d26f0d7a78828d5ccd94b83e26'
            'ff0c33180fa94ab641eb59293db4bb457c34059f0ebfba077dba9ab812d2921d'
            'c82774a0b1393a325884beaad24cc0c5441e761398f735743e30c6e8fd31981a')
noextract=("org.jkiss.dbeaver.data.office_${pkgver}.jar"
           "org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" "org.jkiss.dbeaver.data.office_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
