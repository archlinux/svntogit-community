# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: neodreams <yanbrodeur@gmail.com>

pkgname=giggle
pkgver=0.7
pkgrel=6
pkgdesc='Git repository viewer'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/giggle'
license=('GPL')
depends=('git' 'gtksourceview3' 'vte3')
makedepends=('intltool' 'itstool' 'gnome-common' 'yelp-tools')
source=("https://ftp.gnome.org/pub/GNOME/sources/giggle/$pkgver/$pkgname-$pkgver.tar.xz"
        'gtksourceview3.7.patch'
        'vte2.91.patch')
sha256sums=('ae2e2237fa0d0999d99d0a0ee9b7ec147e80bd2472d59a045b2b01eb02261f59'
            'ca0b841d11bb5072741784dbeb72705eddf8e2bde780b8712355d5b66e456b37'
            '2eba9ff9b11ac8ae2d1f3d104a61026b0b04e745f810f719c8f4657f8c23ce02')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i ../gtksourceview3.7.patch
  patch -Np1 -i ../vte2.91.patch
  NOCONFIGURE=1 AUTOPOINT=intltoolize gnome-autogen.sh
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

check() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: -u 7 git.gnome.org/browse/giggle/plain/NEWS
# vim:set ts=2 sw=2 et:
