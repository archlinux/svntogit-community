# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-eventlet
_pyname=eventlet
pkgver=0.30.2
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='https://eventlet.net'
arch=('any')
license=('MIT')
depends=('python' 'python-greenlet' 'python-monotonic' 'python-dnspython')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq')
optdepends=(
  'python-psycopg2: non-blocking PostgreSQL support'
  'python-pyopenssl: non-blocking SSL support'
  'python-httplib2: non-blocking HTTP support'
  'python-pyzmq: non-blocking ZeroMQ support'
  'python-dnspython: non-blocking DNS support'
)
options=('!makeflags')
source=(https://github.com/eventlet/eventlet/archive/v${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('330fe89e7921c920726e90d5bf3989c485f1113d22dade15d858368fc032d661dc181b10f2a65502715142af80ef5c26c6be005770221699ebdefc8ac58df690')
b2sums=('e18ee3f7c79abf53e572ee58a70ae29220ac28153be39eb17abb17c7af987bc9669f20d70a02c825b9c675b04d9f04b09c59115b18ba3c74fdb30ec8b109982e')

prepare() {
  cd ${_pyname}-${pkgver}
  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i tests/*_test.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  make -C doc text
}

check() {
  cd ${_pyname}-${pkgver}
  nosetests -sv tests
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
