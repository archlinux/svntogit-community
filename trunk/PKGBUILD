# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=xandikos
pkgver=0.2.8
pkgrel=4
pkgdesc='Lightweight yet complete CardDAV/CalDAV server that backs onto a Git repository'
arch=('any')
url=https://www.xandikos.org
license=('GPL3')
depends=(
  'python-aiohttp'
  'python-defusedxml'
  'python-dulwich'
  'python-icalendar'
  'python-jinja'
  'python-multidict'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
optdepends=(
  'python-aiohttp-openmetrics: for metrics with the standalone server'
  'uwsgi-plugin-python: for the WSGI interface'
)
source=(
  "https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
  "$pkgname.sysusers.conf"
  "$pkgname.tmpfiles.conf"
)
sha256sums=('16f174f75d0b4a766f9a41e2fe2700e5d77e77d9a9990cee3d619dc1d8aff001'
            'f522f2dcb238f4d5c7ad8733bdae2e72cf4485a92adc05d38eb0ecce119726c8'
            'ccfd854a6a36e5f04a42ac19166b67f4bc92cdac6981bf22f67cee75bf412dfa')
b2sums=('9e4ac9eb668bddcf186c35e2927f079887388e42dc269d9be4999a479fe8f4bcf42b383e2d155cd5b70e0f5b2c7c08cb4d8313ee0583eca7297661a89462f82e'
        '086423882a085cb7fbecad3f53ad60aa6c322a545c180fe8b4c9f2bba5331fa52d6fc3e96ec068c589d3ab3e0b8f215e1a94473c511560188bd873b7ff5780d6'
        'be8df371a3cdd55e2efd4549251b44cbb5699762f14aa3dcb0e85cdbb3710d75fae8e38b615057f6da37fe744019aabc2c366136ca577a152cd03d380db189c2')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m unittest $pkgname.tests.test_suite
}

package() {
  install -Dm644 $pkgname.sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -Dm644 $pkgname.tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.rst
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname/examples examples/*
  #install -Dm644 -t "$pkgdir"/usr/share/man/man8 man/$pkgname.8
}
