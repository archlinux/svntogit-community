# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=2.4.1
pkgrel=1
pkgdesc="A viewer and analyser of music audio files."
arch=('i686' 'x86_64')
url="http://www.sonicvisualiser.org/"
license=('GPL')
depends=('desktop-file-utils' 'libfishsound' 'libid3tag' 'liblo' 'liblrdf' 'libmad'
         'liboggz' 'libpulse' 'portaudio' 'qt5-base' 'rubberband' 'sord' 'hicolor-icon-theme')
install="$pkgname.install"
options=('!makeflags')
source=("http://code.soundsoftware.ac.uk/attachments/download/1185/$pkgname-$pkgver.tar.gz")
md5sums=('24c8fa91a333c7776e39a30fda9a7783')

prepare() {
  cd $pkgname-$pkgver

  # wrong version in tarball
  sed -i 's/2.4/2.4.1/' configure.ac
}

build() {
  cd $pkgname-$pkgver
  sh bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  # bin
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -d "$pkgdir/usr/share/applications"
  install -Dm644 *.desktop "$pkgdir/usr/share/applications"

  # icons
  for _s in 16 22 24 32 48 64 128; do
    install -Dm644 icons/sv-${_s}x${_s}.png \
            "$pkgdir/usr/share/icons/hicolor/${_s}x${_s}/apps/sv-icon.png"
  done
  install -Dm644 icons/sv-icon.svg \
          "$pkgdir/usr/share/icons/hicolor/scalable/apps/sv-icon.svg"

  # samples
  install -d "$pkgdir/usr/share/$pkgname/samples"
  install -Dm644 samples/* "$pkgdir/usr/share/$pkgname/samples"

  # templates
  install -d "$pkgdir/usr/share/$pkgname/templates"
  install -Dm644 templates/* "$pkgdir/usr/share/$pkgname/templates"
}
