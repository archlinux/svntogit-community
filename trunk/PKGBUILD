# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=43.0
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Boxes'
license=('LGPL')
depends=(
  'cdrtools'
  'edk2-ovmf'
  'libarchive'
  'libgudev'
  'libhandy'
  'libosinfo'
  'libsecret'
  'libsoup3'
  'libvirt-glib'
  'mtools'
  'qemu-desktop'
  'spice-gtk'
  'tracker3'
  'webkit2gtk-4.1'
)
makedepends=(
  'appstream-glib'
  'gobject-introspection'
  'meson'
  'spice-protocol'
  'vala'
  'yelp-tools'
)
groups=('gnome-extra')
options=('debug')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('574505096414c820b808ecd0f71e2d19a9332351e12a3dd826f762e432bf6b26')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
