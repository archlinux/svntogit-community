# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

_id=browserpass@maximbaz.com
pkgname=browserpass-firefox
pkgver=3.7.1
pkgrel=1
pkgdesc="Firefox extension for Browserpass, browser extension for zx2c4's pass (password manager)"
arch=('any')
url="https://github.com/browserpass/browserpass-extension"
license=('ISC')
groups=('firefox-addons')
depends=('browserpass')
source=("${pkgname}-${pkgver}.xpi::https://addons.mozilla.org/firefox/downloads/file/3632396/browserpass-${pkgver}-fx.xpi"
        "https://raw.githubusercontent.com/browserpass/browserpass-extension/master/LICENSE")
noextract=("${pkgname}-${pkgver}.xpi")
sha256sums=('8daa6bc1e674639a613906504d45636e15a39cf8f380da3d40802fa8d3e0c25c'
            'f9fc49e2b3977f857bf3cbfbeb193bab8b2e17545978c162409d5270e6e0405a')

package() {
    install -Dm644 "${pkgname}-${pkgver}.xpi" "${pkgdir}/usr/lib/firefox/browser/extensions/${_id}.xpi"

    # Install host json from browserpass package
    install -dm755 "${pkgdir}/usr/lib/mozilla/native-messaging-hosts/"
    ln -sf "/usr/lib/browserpass/hosts/firefox/com.github.browserpass.native.json" "${pkgdir}/usr/lib/mozilla/native-messaging-hosts/"

    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}

# vim:set ts=4 sw=4 et:
