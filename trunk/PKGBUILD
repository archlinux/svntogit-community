# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-session-manager
pkgver=1.22.1
pkgrel=1
pkgdesc="The MATE Session Handler"
url="http://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('dbus-glib' 'gtk3' 'libsm' 'mate-desktop')
makedepends=('intltool' 'xtrans' 'systemd' 'python')
optdepends=('gnome-keyring: keyring support'
            'xdg-user-dirs-gtk: manage user directories')
groups=('mate')
conflicts=('mate-session-manager-gtk3')
replaces=('mate-session-manager-gtk3')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2f1a68447a2ec30791e07865fd3747e367c3fffe8373d07ea948b0d759bca8c7')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--disable-upower
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
