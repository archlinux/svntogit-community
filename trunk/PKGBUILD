# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=ogre
pkgver=13.3.1
pkgrel=1
pkgdesc='Scene-oriented, flexible 3D engine written in C++'
arch=('x86_64')
url='http://www.ogre3d.org'
license=('custom:MIT')
depends=('freeimage' 'freetype2' 'libxaw' 'libxrandr' 'sdl2' 'glu' 'pugixml' 'zlib')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'python' 'ninja' 'cppunit')
install=ogre.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/OGRECave/ogre/archive/v${pkgver}.tar.gz")
optdepends=('python: python bindings')
sha512sums=('903994fafe0aaa41fbfcb34093a04253ee1c3df2419268035a32136bdc3ca1bb60c4e650437b96764b6466a234ac75499ec109c7a77903d027b62c86529453e5')

build() {
  cd ogre-${pkgver}

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOGRE_BUILD_TESTS=ON \
    -DOGRE_BUILD_DEPENDENCIES=OFF

  ninja -C build
  ninja -C build OgreDoc
}

check() {
  cd ogre-${pkgver}

  ninja -C build test
}

package() {
  cd ogre-${pkgver}

  DESTDIR="${pkgdir}" ninja -C build install

  mkdir -p "${pkgdir}"/opt/ogre/samples
  mv "${pkgdir}"/usr/bin/SampleBrowser "${pkgdir}"/opt/ogre/samples
  mv "${pkgdir}"/usr/bin/Test_* "${pkgdir}"/opt/ogre/samples
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
