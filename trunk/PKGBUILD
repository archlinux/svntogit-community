# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gramps
pkgver=4.2.6
pkgrel=2
epoch=2
pkgdesc="Genealogy program, which helps you track your family tree"
arch=('any')
url="https://gramps-project.org/"
license=('GPL')
depends=('gtk3' 'python-bsddb' 'python-cairo' 'python-gobject' 'xdg-utils')
makedepends=('intltool')
optdepends=('osm-gps-map: show maps in the geography view'
            'graphviz: enable creation of graphs (required for tree reports)'
            'python-pyicu: improves localised sorting'
            'gtkspell3: enable spell checking in the notes'
            'rcs: manage multiple revisions of your family trees'
            'python-pillow: crop and convert images to JPG'
            'libgexiv2: manage Exif metadata embedded in your media')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gramps-project/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('6f6f2afe745179e2e66f216f25ca2ba4bf4d5f6bd3b553bbb2946df0636a0bed')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  echo -n '/usr/share' >"$pkgdir"/usr/lib/python*/site-packages/gramps/gen/utils/resource-path
}
