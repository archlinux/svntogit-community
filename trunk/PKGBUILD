# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=nicotine+
pkgver=3.2.9
pkgrel=2
pkgdesc='Soulseek music-sharing client, written in python'
arch=(any)
url='https://github.com/Nicotine-Plus/nicotine-plus'
license=(GPL)
depends=(python-gobject gtk3 gspell)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://github.com/Nicotine-Plus/nicotine-plus/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('aeaf45742a915345d1635f36ca334c3f332788c7a27262408be9998985f99e41')

build() {
  cd nicotine-plus-$pkgver
  
  python -m build --wheel --no-isolation
}

package() {
  cd nicotine-plus-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
}
