# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=uvloop
pkgname=python-$_pkgname
pkgver=0.15.2
pkgrel=1
pkgdesc='Ultra fast asyncio event loop'
arch=('x86_64')
url='https://github.com/MagicStack/uvloop'
license=('Apache' 'MIT')
depends=('python' 'libuv')
makedepends=('python-setuptools' 'cython')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c7e1a1c0fd52047dc3c353628e1c103e44f96c00ac52d17de27dd469f0936603f631a8f32bfe42b83c4f5becf4fb05cab54667874fdfa15d4c4c7e4c925c9797')

build() {
  cd $_pkgname-$pkgver

  python setup.py build build_ext --use-system-libuv
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm 644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
