# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.2.2
pkgrel=3
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(x86_64)
url="https://www.synfig.org/"
license=(GPL2)
depends=(boost-libs fftw imagemagick libjpeg-turbo libmng libxml++2.6 mlt openexr pango)
makedepends=(boost etl intltool)
source=(https://downloads.sourceforge.net/synfig/$pkgname-$pkgver.tar.gz
        fix-build.patch)
sha256sums=('5f5f7b33483041165f9a63fe4d95eb815971c7444569e574206f6964e83cc2ef'
            '5f6e6b56a2d55074fb187e48611fbf0ca337e9d4ddd78669e9d836ce6ebedb78')

prepare() {
  cd $pkgname-$pkgver
  patch -Np2 -i ../fix-build.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
