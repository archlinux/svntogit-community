# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=HsOpenSSL
pkgname=haskell-hsopenssl
pkgver=0.11.7.4
pkgrel=6
pkgdesc="Partial OpenSSL binding for Haskell"
url="https://github.com/haskell-cryptography/HsOpenSSL"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-network' 'openssl')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0a03fea3d7aca0145543e923fc7530a238187ba5d3c0c59ae2fe90f5f4f9b5641d7b365c7cf9a590babc167cc7c248306186d476fc5be82c999c8c2debb2ff40')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
        -fuse-pkg-config
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
