# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=pantry
pkgname=haskell-pantry
pkgver=0.4.0.1
pkgrel=45
pkgdesc="Content addressable Haskell package management"
url="https://github.com/commercialhaskell/pantry"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-casa-client'
         'haskell-casa-types' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-cryptonite'
         'haskell-cryptonite-conduit' 'haskell-digest' 'haskell-filelock' 'haskell-generic-deriving'
         'haskell-hackage-security' 'haskell-hpack' 'haskell-http-client' 'haskell-http-client-tls'
         'haskell-http-conduit' 'haskell-http-download' 'haskell-http-types' 'haskell-memory'
         'haskell-mono-traversable' 'haskell-network-uri' 'haskell-path' 'haskell-path-io'
         'haskell-persistent' 'haskell-persistent-sqlite' 'haskell-persistent-template'
         'haskell-primitive' 'haskell-resourcet' 'haskell-rio' 'haskell-rio-orphans'
         'haskell-rio-prettyprint' 'haskell-tar-conduit' 'haskell-text-metrics'
         'haskell-unix-compat' 'haskell-unliftio' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-yaml' 'haskell-zip-archive')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-hspec' 'haskell-quickcheck' 'haskell-raw-strings-qq')
checkdepends=('git' 'mercurial')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        pantry-cabal-3.2.patch::https://github.com/commercialhaskell/pantry/pull/22.patch)
sha512sums=('0fbed425e3079bf1a06805efff092345c5f09bca430d2167d132cba4cd4a4c66567ce1aba921a9ac01d0db368f255395b2ad38d8b16ea5d81e263b100ad5a51d'
            'b04469f535468a6e3d698a4d02e13673070fd3b78efde82829c30080f2ab587e63f5cd9b1c090c59d91c2a2b3a534eb5efa78c3b7d52967bcaefc8e6f803173b')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../pantry-cabal-3.2.patch || :
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
