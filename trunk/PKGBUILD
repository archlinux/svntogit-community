# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Alexander RÃ¸dseth
# Contributor: Andrea Scarpino
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>
# Contributor: Dominic Tubach

pkgbase=sword
pkgname=(sword python-sword)
_mainver=1.9
pkgver=1.9.0
pkgrel=7
pkgdesc="Library for Bible study programs"
arch=('x86_64')
url="https://www.crosswire.org/sword/"
license=('GPL')
makedepends=('curl' 'clucene' 'swig' 'cmake' 'python')
source=("https://www.crosswire.org/ftpmirror/pub/$pkgbase/source/v$_mainver/$pkgbase-$pkgver.tar.gz")
sha512sums=('9ed3fbb5024af1f93b1473bae0d95534d02a5b00b3c9d41a0f855cee8106dc4e330844080adbee7c3f74c0e5ce1480bf16c87c842421337a341f641bae11137f')

build() {
  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CXXFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  #./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc
  cmake -B build -S $pkgbase-$pkgver \
      -DCMAKE_BUILD_TYPE='None' \
      -DLIBSWORD_LIBRARY_TYPE="Shared" \
      -DSYSCONF_INSTALL_DIR='/etc' \
      -DSWORD_BUILD_TESTS="Yes" \
      -DSWORD_PYTHON_3=TRUE \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -Wno-dev
  cmake --build build
}

package_sword() {
  depends=('curl' 'clucene' 'xz')
  backup=('etc/sword.conf')

  DESTDIR="$pkgdir" cmake --install build
}

package_python-sword() {
  depends=('sword' 'python')

  cd build/bindings/swig/python3
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
