# Maintainer: David Runge <dave@sleepmap.de>
_name=pymediainfo
pkgname=python-pymediainfo
pkgver=4.1
pkgrel=1
pkgdesc="A Python wrapper around the MediaInfo library"
arch=('any')
url="https://github.com/sbraz/pymediainfo/"
license=('MIT')
depends=('libmediainfo' 'python')
makedepends=('mediainfo' 'python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('08ffca0c3179aa6dff5b3007faaec1f1a2f372b2995824039021f41b2c772a2a13370c1dbd37d2408d023be497cff71dd6f46b09c8989f168976d75723cd7722')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python setup.py test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
