# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=('grafana-agent' 'grafana-agentctl')
pkgver=0.18.2
pkgrel=1
pkgdesc='Grafana Agent is a telemetry collector for sending metrics, logs, and trace data to the opinionated Grafana observability stack'
url='https://github.com/grafana/agent'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go')
source=($pkgname-$pkgver.tar.gz::https://github.com/grafana/agent/archive/v$pkgver.tar.gz
        grafana-agent.sysusers grafana-agent.service grafana-agent.tmpfiles)
sha512sums=('c39d5cb0d356dd1ed412e44c548e112768ed58520722b07be0e00fe84e07ed944ebdd7d738f8b2e309870b5b0c803df3987814547f06cc42e3f89193e61f9f1c'
            '1e58f6273562fd6ddeae41bb6d223230ed301199af6bd2f85fa3d2c1e6352952c600cd4488ad769069519c42b6863be84aaa2c93c88f696e5a0bedbb93758d0e'
            '1902dfa919fc68503d464073c4d2e4497a0eab06a9eedabc2dc09b9b18d75f28ffc73b161c83dbc9c6c2fbd0a3d24e721b4c92aff3a02e3b3675e5baa0396c55'
            '3a4c4896b2454272b5a2d53ba5aa7009fa5b42a573fef521afb5d9712e53119539c4195d2b149d04d086c5fa197ccf7dc9bbdc407efb55514e3c92b994121fa8')

build() {
  cd "agent-$pkgver"

  for path in cmd/agent cmd/agentctl; do
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags " \
         -X github.com/grafana/agent/pkg/build.Version=$pkgver
         -X github.com/grafana/agent/pkg/build.BuildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH})
         -linkmode external
         -extldflags \"${LDFLAGS}\"" \
      "./$path"
  done
}

check() {
  cd "agent-$pkgver"

  go test -v ./...
}

package_grafana-agent() {
  backup=('etc/grafana-agent.yaml')

  cd "agent-$pkgver"

  install -Dm755 agent "$pkgdir/usr/bin/grafana-agent"
  install -Dm644 packaging/grafana-agent.yaml "$pkgdir/etc/grafana-agent.yaml"

  install -Dm644 "$srcdir/grafana-agent.sysusers" "$pkgdir/usr/lib/sysusers.d/grafana-agent.conf"
  install -Dm644 "$srcdir/grafana-agent.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/grafana-agent.conf"
  install -Dm644 "$srcdir/grafana-agent.service" "$pkgdir/usr/lib/systemd/system/grafana-agent.service"
}

package_grafana-agentctl() {
  pkgdesc="provides utilities for interacting with Grafana Agent"

  cd "agent-$pkgver"

  install -Dm755 -t "$pkgdir"/usr/bin agentctl
}
