# Maintainer: David Runge <dvzrv@archlinux.org>

_name=publicsuffix2
pkgname=python-publicsuffix2
_version=2.2019-12-21
pkgver=2.20191221
pkgrel=7
pkgdesc="Library to deal with publicsuffix data"
arch=('any')
url="https://github.com/nexB/python-publicsuffix2"
license=('MIT' 'MPL2')
depends=('publicsuffix-list' 'python')
makedepends=('python-setuptools' 'python-requests')
checkdepends=('python-pytest')
# tests are not included in pypi sdist tarball and bizarre tagging:
# https://github.com/nexB/python-publicsuffix2/issues/12
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${_major}.${_date//./}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/nexB/${pkgname}/archive/release-${_version}.tar.gz")
sha512sums=('983bdb17b76d10eb73cf69f0ba7590806bf6786ae0c30526593ce1ab97f3b22c967f9acae83b880dd56be41a29eae9cca5726926667eb74013d0d1078433bf05')

prepare() {
  mv -v "$pkgname-release-${_version}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  local python_stdlib_basepath="$(python -c "from sysconfig import get_path; print(get_path('stdlib'))")"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGELOG,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "${_name}.LICENSE" \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # use the system provided publicsuffix list (publicsuffix-list package)
  ln -svf /usr/share/publicsuffix/public_suffix_list.dat \
    "${pkgdir}/$python_stdlib_basepath/site-packages/${_name}/public_suffix_list.dat"
}
