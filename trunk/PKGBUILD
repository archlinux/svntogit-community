# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=conduit
pkgname=haskell-conduit
pkgver=1.3.1.2
pkgrel=24
pkgdesc="Streaming data processing library."
url="https://github.com/snoyberg/conduit"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions' 'haskell-mono-traversable' 'haskell-primitive'
         'haskell-resourcet' 'haskell-unliftio-core' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck' 'haskell-safe' 'haskell-silently'
             'haskell-split' 'haskell-unliftio')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a5873f42a75a3d9f404070fcd3c91efd5a38d7d9bb9cd313c2881ae82a79e79a5b87f76b11eb72dc6c6050306c55ecd5faca4ab24ae781503cefd66907911f32')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
