# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=4.4.0
pkgrel=3
pkgdesc="GTK+ client for Matrix written in Rust"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Fractal"
license=(GPL3)
depends=(gtk3 gst-plugins-base-libs gst-plugin-gtk gst-plugins-bad gst-editing-services gtksourceview4 gspell libhandy0)
optdepends=('org.freedesktop.secrets: password storage')
makedepends=(meson rust)
_md5=d4168ac40fd681240964705e000dd353
source=("https://gitlab.gnome.org/GNOME/fractal/uploads/${_md5}/${pkgname}-${pkgver}.tar.xz"
         fractal-4.4.0-i18n-meson-fix.patch::https://gitlab.gnome.org/GNOME/fractal/-/commit/6fa1a23596d65d94aa889efe725174e6cd2903f0.patch
         fractal-4.4.0-socket2-crates-update.patch
         fractal-4.4.0-openssl-crates-update.patch)
sha256sums=('65af7912f3d04bd6b2386b023415fef26afc48d256af205256f86396a1415825'
            'd417d6fef72d83ac790b22f6264ce36135aa45aea338db1ae6b6a771bf03c550'
            '3a5ae5c93a86385b6a90fe31f0de0e0630715afd99726006df660022c9c84914'
            '48ef0eb97bb9b1ebbfe178a8a0e7e1c8eca13816ba50a599245abb9f771a4d52')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../fractal-4.4.0-i18n-meson-fix.patch
    patch -p1 -i ../fractal-4.4.0-socket2-crates-update.patch
    patch -p1 -i ../fractal-4.4.0-openssl-crates-update.patch
    rm .cargo/config # remove config enabling vendoring so updated crates will be downloaded
}

build() {
    cd ${pkgname}-${pkgver}
    CFLAGS+=' -ffat-lto-objects'
    arch-meson . _build
    meson compile -C _build
}

package() {
    cd ${pkgname}-${pkgver}
    DESTDIR="${pkgdir}" meson install -C _build
}
