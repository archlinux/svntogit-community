# Maintainer:m Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-at-spi2-core
pkgver=2.30.1
pkgrel=1
pkgdesc='Protocol definitions and daemon for D-Bus at-spi'
arch=(x86_64)
url=https://www.gnome.org
license=(GPL2)
depends=(
  at-spi2-core
  lib32-dbus
  lib32-glib2
  lib32-libxtst
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  intltool
  meson
)
source=(git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#tag=AT_SPI2_CORE_${pkgver//./_})
sha256sums=(SKIP)

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson at-spi2-core build \
    --libdir=/usr/lib32 \
    -Denable_docs=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -r "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim: ts=2 sw=2 et:
