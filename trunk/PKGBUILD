# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=python-linux-procfs
pkgver=0.7.1
pkgrel=2
pkgdesc="Linux /proc abstraction classes for python"
arch=(any)
url="https://git.kernel.org/pub/scm/libs/python/python-linux-procfs/python-linux-procfs.git/"
license=(GPL2)
depends=(python-six)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://git.kernel.org/pub/scm/libs/python/$pkgname/$pkgname.git/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('47b54e2a79e427eff1b92c7da80e8d307ba0e89f743388d937bc8be8d28ccc71fe76e7731571d96338a9318daec7f25ca4e2809c54c0fe82d772538d5a5209c5')
b2sums=('e106d2fc6c7527a3276a4603bcff8ed0ba6604a352912c64ff913e2e7309c78c936a463aaa6512a718961d5dcc56c18d2a687f9ffa74ace6d0991114293be832')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python bitmasklist_test.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
