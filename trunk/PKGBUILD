# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Frank Ickstadt (frank dot ickstadt at gmail dot com)

pkgname=argyllcms
_pkgname=Argyll
pkgver=1.6.1
_pkgver="V${pkgver}"
pkgrel=1
pkgdesc="An ICC compatible color management system with support for different colorimeter hardware"
arch=(i686 x86_64)
depends=('libxss' 'libxinerama' 'libxxf86vm' 'libxrandr')
makedepends=('ftjam' 'zip' 'unzip')
source=(http://www.argyllcms.com/${_pkgname}_${_pkgver}_src.zip)
url="http://www.argyllcms.com/"
license=("GPL" "AGPL")
install=${pkgname}.install
md5sums=('aed439b76054ef8e8b065106d314681c')

build() {
  cd "${srcdir}"/${_pkgname}_${_pkgver}
  echo "***running makeall.sh***"
  sh ./makeall.sh
  echo "***running makeinstall.sh***"
  sh ./makeinstall.sh
  echo "***running makepackagebin.sh***"
  sh ./makepackagebin.sh
  rm "${srcdir}"/${_pkgname}_${_pkgver}/bin/License.txt
  rm "${srcdir}"/${_pkgname}_${_pkgver}/ref/License.txt
  rm "${srcdir}"/${_pkgname}_${_pkgver}/ref/afiles
  rm "${srcdir}"/${_pkgname}_${_pkgver}/doc/afiles
}

package() {
  mkdir -p "${pkgdir}"/usr/bin
  install -m755 "${srcdir}"/${_pkgname}_${_pkgver}/bin/* "${pkgdir}"/usr/bin
  mkdir -p "${pkgdir}"/usr/share/${pkgname}/ref
  install -m644 "${srcdir}"/${_pkgname}_${_pkgver}/ref/*.* "${pkgdir}"/usr/share/${pkgname}/ref
  install -m644 "${srcdir}"/${_pkgname}_${_pkgver}/profile/*.sp "${pkgdir}"/usr/share/${pkgname}/ref
  install -m644 "${srcdir}"/${_pkgname}_${_pkgver}/scanin/*.c?? "${pkgdir}"/usr/share/${pkgname}/ref
  # Because there is just html documentation we have to move it
  mkdir -p "${pkgdir}"/usr/share/${pkgname}/doc/ccmxs
  install -m644 "${srcdir}"/${_pkgname}_${_pkgver}/doc/ccmxs/*.ccmx "${pkgdir}"/usr/share/${pkgname}/doc/ccmxs
  install -m644 "${srcdir}"/${_pkgname}_${_pkgver}/doc/*.* "${pkgdir}"/usr/share/${pkgname}/doc
  mkdir -p "${pkgdir}"/usr/lib/udev/rules.d
  install -m644 "${srcdir}"/${_pkgname}_${_pkgver}/usb/55-Argyll.rules "${pkgdir}"/usr/lib/udev/rules.d/55-Argyll.rules
  sed -i 's|IMPORT{program}="usb|#IMPORT{program}="usb|' "${pkgdir}"/usr/lib/udev/rules.d/55-Argyll.rules
  cat <<EOF >>"${pkgdir}"/usr/lib/udev/rules.d/55-Argyll.rules

# As mentioned in https://bugzilla.redhat.com/show_bug.cgi?id=859244 and https://bbs.archlinux.org/viewtopic.php?id=155589
# I've commented out the line:
# IMPORT{program}="usb-db %p"
# to prevent the error messages:
# systemd-udevd[242]: failed to execute '/usr/lib/udev/usb-db'...
EOF
}

# vim:set ts=2 sw=2 et:
