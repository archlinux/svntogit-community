# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=libyaml
pkgname=haskell-libyaml
pkgver=0.1.1.0
pkgrel=9
pkgdesc="Low-level, streaming YAML interface."
url="https://github.com/snoyberg/libyaml"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'libyaml' 'haskell-conduit' 'haskell-resourcet')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d63e95128e12059813fdb16d8b6f432b72d057a83f229b587c9d9e8bf4de3f2777a1d2920e32ea58d8c16aa0d8e79775dec5392e19b95a36d7347994635f53e0')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsystem-libyaml
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
