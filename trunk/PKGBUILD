# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_name=elfutils
pkgname=lib32-libelf
pkgver=0.185
pkgrel=1
pkgdesc="Handle ELF object files and DWARF debugging information (libraries) (32-bit)"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL3')
depends=('lib32-curl' 'lib32-bzip2' 'lib32-gcc-libs' 'lib32-xz' 'lib32-zlib'
'lib32-zstd' "libelf=${pkgver}")
# NOTE: the shared objects can not be added to provides as they are not versioned
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2"{,.sig})
options=('staticlibs')
sha512sums=('34de0de1355b11740e036e0fc64f2fc063587c8eb121b19216ee5548d3f0f268d8fc3995176c47190466b9d881007cfa11a9d01e9a50e38af6119492bf8bb47f'
            'SKIP')
b2sums=('57cfa7e4060975c4bf6170dbd354687a19a92f7069c060db1ac2fa4da5a9d34e8bddd07c7ce376b5c28b5670ad5b8c1df795164cbcfb600a44cf1db60c20c4af'
        'SKIP')
validpgpkeys=(
  '47CC0331081B8BC6D0FD4DA08370665B57816A6A' # Mark J. Wielaard <mark@klomp.org>
  'EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A' # Mark Wielaard <mjw@gnu.org>
)

build() {
  cd "${_name}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --disable-debuginfod
  make
}

check() {
  cd "${_name}-${pkgver}"
  make check || echo "Known problem: The build environment might lack enough resources."
}

package() {
  cd "${_name}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  # remove anything that would conflict with elfutils and debuginfod
  rm -rf "${pkgdir}/"{etc,usr/{bin,include,share}}
}
