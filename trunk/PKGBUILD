# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

pkgname=tor
pkgver=0.2.4.20
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('i686' 'x86_64')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash')
optdepends=('torsocks: for torify')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf')
install='tor.install'
source=("http://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc'
        'tor.service')
md5sums=('a8cd8e3b3a3f6a7770f2c22d280f19b8'
         'SKIP'
         'c1dd4004c63edaeaa829e01be5413cfc'
         '51c81cd06b75edd87aafff5a7941822c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
}
