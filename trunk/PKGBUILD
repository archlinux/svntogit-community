# Maintainer: SÃ©bastien Luttringer
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.org>

pkgname=quilt
pkgver=0.65
pkgrel=1
pkgdesc='Manage a series of patches by keeping track of the changes each patch makes'
arch=('any')
url='https://savannah.nongnu.org/projects/quilt/'
license=('GPL2')
depends=(
  'bash'
  'bzip2'
  'diffstat'
  'diffutils'
  'findutils'
  'gawk'
  'gettext'
  'gzip'
  'patch'
  'perl'
)
validpgpkeys=('90DFD6523C57373D81F63D19865688D038F02FC8') # Jean Delvare
source=("https://savannah.nongnu.org/download/quilt/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('c67ba0228f5b7b8bbe469474661f92d6'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --without-rpmbuild
  make
}

package() {
  cd $pkgname-$pkgver
  make BUILD_ROOT="$pkgdir" install
  # move bash completion in right place
  install -d -m 755 "$pkgdir/usr/share/bash-completion"
  mv "$pkgdir/etc/bash_completion.d" "$pkgdir/usr/share/bash-completion/completions"
}

# vim:set ts=2 sw=2 et:
