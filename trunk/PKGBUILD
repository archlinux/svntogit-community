# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer

pkgname=perl-io-dirent
pkgver=0.05
pkgrel=16
pkgdesc='Perl IO::Dirent module - Access to dirent structs returned by readdir'
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url='https://search.cpan.org/~scottw/IO-Dirent/'
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SC/SCOTTW/IO-Dirent-$pkgver.tar.gz")
sha256sums=('10b775f23123e527e67745af52a03da4f216b4c4e7797c4570be9c1be36cd97c')

build() {
  cd IO-Dirent-$pkgver
  USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Dirent-$pkgver
  make test
}

package() {
  cd IO-Dirent-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
