# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=8.2.0
_srcver=${pkgver//./}
pkgrel=2
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('x86_64')
url="https://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
# Fix https://bugs.archlinux.org/task/56689
install="crypto++.install"
source=("https://www.cryptopp.com/cryptopp${_srcver}.zip"{,.sig}
        'libcrypto++.pc'
        'cve-2019-14318.patch')
# Checksums from https://www.cryptopp.com/release600.html
sha1sums=('b042d2f0c93410abdec7c12bcd92787d019f8da1'
          'SKIP'
          'ef530175d27101dcb23a3f92d3c80a529f1d7b02'
          '4788135c92536cac42a98e59d219a9e859b759e3')
sha256sums=('03f0e2242e11b9d19b28d0ec5a3fa8ed5cc7b27640e6bed365744f593e858058'
            'SKIP'
            '8722862336f9fe0181734619c197bf4248f0e07b93bdcd693709f57b2f6aa9e6'
            'd9cabc1eab0dfbab1d4bfff75fa99766995089e52b83a175918e738516efbb41')
sha512sums=('753513a4ec8dd0fff2f551853ce6bd265d82219c28b033565b565b5e567fbee17adb419f4cde58a97e62b7d6533f4099aa4996cd0ba4775c6a2e7ae63a879da5'
            'SKIP'
            '3be1569e81af1f9b35e944faae3e9962ee2e492fb38e94fe7f847b85da033a79bbfeff193e0edb2d69f2d893f6e8279be144b9395653db67374300f7feb23276'
            'c5075963acc0f8f5bac38306bac324e0ca5aa74abed417cf5f626267c4c409f84c31a89e351b07a1880cfd30c1451e0f1e3dd8721050df74c1d3d080097a84d9')
validpgpkeys=('B8CC19802062211A508B2F5CCE0586AF1F8E37BD') # Jeffrey Walton (Crypto++ Release) <noloader@gmail.com>

prepare() {
  patch -p0 < "$srcdir"/cve-2019-14318.patch
}

build() {
  CXXFLAGS+=" -DNDEBUG -fPIC" make dynamic cryptest.exe
}

check() {
  make test
}

package() {
  make install DESTDIR="$pkgdir" PREFIX="/usr"
  # Install pkgconfig file
  install -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 "${srcdir}/libcrypto++.pc" "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  # Remove cryptest.exe and test files, only needed for check() and bloats the package
  # because cryptest.exe is linked statically.
  rm "${pkgdir}/usr/bin/cryptest.exe"
  rmdir "${pkgdir}/usr/bin/"
  rm -r "${pkgdir}/usr/share/cryptopp/"
  # Install license
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
