# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vincent Kriek <vincent@vincentkriek.nl>

pkgname=wmname
pkgver=0.1
pkgrel=3
pkgdesc="A utility to set the name of your window manager"
url="http://tools.suckless.org/x/wmname"
license=('MIT')
depends=('libx11')
arch=('i686' 'x86_64')
source=("http://dl.suckless.org/tools/wmname-$pkgver.tar.gz")
sha256sums=('559ad188b2913167dcbb37ecfbb7ed474a7ec4bbcb0129d8d5d08cb9208d02c5')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make PREFIX="${pkgdir}"/usr install
  install -Dm0644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
