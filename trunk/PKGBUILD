# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

pkgname=ocaml-num
pkgver=1.1
pkgrel=2
pkgdesc="Library for arbitrary-precision integer and rational arithmetic that used to be part of the OCaml core distribution"
arch=('x86_64')
url="https://github.com/ocaml/num"
license=('LGPL2.1')
depends=('ocaml')
makedepends=('ocaml-findlib')
options=('!strip' '!makeflags')
source=($pkgname-$pkgver.tar.gz::https://github.com/ocaml/num/archive/v$pkgver.tar.gz
        provide-findlib-install-target.patch)
sha256sums=('04ac85f6465b9b2bf99e814ddc798a25bcadb3cca2667b74c1af02b6356893f6'
            'a5d6bcb0383e6246da26137d8306c767d9ea5877f67b39db9232d35b5406322d')

prepare() {
  cd "$srcdir/num-$pkgver"
  patch -p1 < "$srcdir/provide-findlib-install-target.patch"
}

build() {
  cd "$srcdir/num-$pkgver"
  make
}

package() {
  cd "$srcdir/num-$pkgver"

  local _destdir="$pkgdir$(ocamlfind printconf destdir)"
  install -d "$_destdir/stublibs"

  make OCAMLFIND_DESTDIR="$_destdir" findlib-install
}

# vim:set ts=2 sw=2 et:
