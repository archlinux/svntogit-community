# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=kubo
pkgver=0.14.0
pkgrel=1

pkgdesc='A peer-to-peer hypermedia distribution protocol'
url='https://github.com/ipfs/kubo'
arch=(x86_64)
license=(MIT)

provides=('go-ipfs')
replaces=('go-ipfs')
conflicts=('go-ipfs')

depends=(glibc)
optdepends=('fuse2: for mounting/advanced use')
makedepends=(go git)

source=(kubo-$pkgver.tar.gz::https://github.com/ipfs/kubo/releases/download/v$pkgver/kubo-source.tar.gz
        # https://github.com/ipfs/kubo/pull/8213#issuecomment-881866789
        rb.patch
        ipfs.service
        ipfs@.service)
noextract=(kubo-$pkgver.tar.gz)

sha512sums=('78d6be3f6e2250c260d697690b544d666817ac6826aee0871a867996b6aaeb8947b8d98a26dfab060e015801aa3e330a1956c20d6d3b1901d816acec7e2a7a92'
            '5591bb5b309ccf6464723650831e7ed1bf6ffc7e18645c3563452df965091b94d265b079db92ae0b359aa964540b2ee1c1b60b3000102168bfd32730b640f12c'
            '1a6bc7fffc3c3640cda3f33c107424edd698c62bd8509731856b8827ea18be1a1845ef0d5ca15325d48d7d871a78daacc011ff9eacea926bc35899850990c1c6'
            'aed168a35228c4f873e0040d1412f3568bfe1ad1e94ffb8b1a2507058daaf75d7346381d299cf9f0b91951b21553170249ff5f45d5fc060e711cf628ab5e5aad')

prepare() {
  mkdir -p kubo
  tar -C kubo -xf kubo-$pkgver.tar.gz
  cd kubo
  patch -Np1 -i ../rb.patch
}

build() {
  cd kubo

  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
  make build
  cmd/ipfs/ipfs commands completion bash >"$srcdir"/ipfs-completion.bash
}

package() {
  cd kubo
  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644 "$srcdir"/ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 "$srcdir"/ipfs@.service "$pkgdir/usr/lib/systemd/system/ipfs@.service"
  install -Dm 644 "$srcdir"/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/MIT" LICENSE-MIT
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/APACHE" LICENSE-APACHE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
