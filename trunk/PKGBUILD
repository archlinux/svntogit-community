# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=1.1.0
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=('x86_64')
url="http://drumstick.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'fluidsynth' 'hicolor-icon-theme' 'shared-mime-info' 'qt5-svg')
makedepends=('cmake' 'doxygen' 'docbook-xsl')
source=("http://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('8b72ff32bbd3887c53b282a7111e683f')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DLIB_SUFFIX=
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}
