# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.18.3
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python python-typing-extensions tmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest procps-ng)
optdepends=('python-pytest: for pytest plugin')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('d9de76c57b0b5401b8735b4215ecc363e920b73f39dd50600e57b7c5bbcbd7721a1507f462a3b477ea635bed21f13340c04a54c7912803da9a4d4e257d0474bf')
b2sums=('c062b7677de2c2b70ef09abfd68cf8de9c351a799d31cb160fa25a9770d2e4d43871d3c2d6ccc50227095c2fdb270dc2316f7bbd33e22ac56942931cd6047c79')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages"
  pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
