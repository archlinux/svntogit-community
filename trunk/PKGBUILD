# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sirius Bakke <sirius\at/nonline.org>

pkgname=pam_mount
pkgver=2.16
pkgrel=7
pkgdesc="A PAM module that can mount volumes for a user session"
arch=(x86_64)
url="http://pam-mount.sourceforge.net/"
license=(GPL)
depends=(pcre util-linux libhx libxml2 cryptsetup)
backup=('etc/security/pam_mount.conf.xml')
options=(!emptydirs)
source=("https://downloads.sourceforge.net/project/pam-mount/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
         pam_mount-luks2.patch)
sha256sums=('ae0207ea6c96189414f906a7b0880abc7751a98e26b3d0375c9afab3722471e7'
            'SKIP'
            'b11f3ed8d95b25089963b74bead65d8100825c7d6043187008334bea8ebeb091')
validpgpkeys=(2F8BFE96D14F8B560FBC1F1DA28BDFEEB5A2B2FF) # Jan Engelhardt <jengelh@inai.de>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../pam_mount-luks2.patch # Add support for LUKS2
}

build() {
  cd $pkgname-$pkgver
  aclocal
  libtoolize
  automake --add-missing
  autoreconf
  ./configure \
	--prefix=/usr \
	--with-ssbindir=/usr/bin \
	--sbindir=/usr/bin \
	--with-slibdir=/usr/lib \
	--sysconfdir=/etc \
	--localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
