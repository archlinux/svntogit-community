# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-quart
pkgver=0.16.1
pkgrel=1
pkgdesc='A Python ASGI web microframework with the same API as Flask'
url=https://gitlab.com/pgjones/quart
arch=(any)
license=(MIT)
depends=(
  hypercorn
  python-aiofiles
  python-blinker
  python-click
  python-itsdangerous
  python-jinja
  python-toml
  python-werkzeug
)
makedepends=(
  git
  python-pip
  python-poetry
)
checkdepends=(
  python-asynctest
  python-distlib
  python-hypothesis
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
  python-pytest-sugar
  python-tox
)
_tag=1e953f655d13f54ad5e24e82f955992314285cd7
source=(git+https://gitlab.com/pgjones/quart.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd quart
  git describe --tags
}

build() {
  cd quart
  poetry build -f wheel
}

check() {
  cd quart
  tox -e py39
}

package() {
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="${pkgdir}" --ignore-installed --no-deps quart/dist/*.whl
  install -Dm 644 quart/LICENSE -t "${pkgdir}"/usr/share/licenses/python-quart/
}

# vim: ts=2 sw=2 et:
