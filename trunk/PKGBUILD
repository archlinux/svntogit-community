# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemordy@archlinux.org>

pkgname=foremost
pkgver=1.5.7
pkgrel=3
pkgdesc="A console program to recover files based on their headers, footers, and internal data structures"
arch=('x86_64')
url="http://foremost.sourceforge.net/"
license=('custom')
depends=('glibc')
makedepends=('patch')
backup=(etc/foremost.conf)
source=(http://foremost.sourceforge.net/pkg/$pkgname-$pkgver.tar.gz
        $pkgname-makefile.patch)
sha256sums=('502054ef212e3d90b292e99c7f7ac91f89f024720cd5a7e7680c3d1901ef5f34'
            '9073fe84c1355e6b1b1c16ccba06893ebd4d35590e45ecbcb4b89e02083a5848')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np0 -i "${srcdir}"/$pkgname-makefile.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  make
}

package () {
  cd "${srcdir}"/$pkgname-$pkgver

  install -d "${pkgdir}"/{etc,usr/bin,usr/share/man/man8,usr/share/licenses/foremost}
  make DESTDIR="${pkgdir}" install

#license
  ./foremost -V > "${pkgdir}"/usr/share/licenses/$pkgname/COPYRIGHT
}
