# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=budgie-backgrounds
pkgver="1.0"
pkgrel=2
pkgdesc="The default background set for the Budgie Desktop"
arch=(any)
url="https://github.com/BuddiesOfBudgie/budgie-backgrounds"
license=("custom:CC0-1.0")
groups=("budgie")
depends=()
makedepends=(meson imagemagick jhead)
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc})
sha256sums=("38c23f9f0c0829a6282b81d934136e780a1341ccce338acc2dbf9f14ff539d0c"
            "SKIP")
validpgpkeys=("8A59B135826B2DA453B9707408AD7343E6BD4B56") # Campbell Jones (Git) <git at serebit dot com>

build() {
	arch-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	install -Dm 00644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 
}
