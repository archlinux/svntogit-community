# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

pkgbase=flatbuffers
pkgname=(flatbuffers python-flatbuffers)
# https://github.com/google/flatbuffers/blob/master/CHANGELOG.md
pkgver=2.0.7
pkgrel=2
pkgdesc='An efficient cross platform serialization library for C++, with support for Java, C# and Go'
arch=(x86_64)
url='https://google.github.io/flatbuffers/'
license=(Apache)
depends=(gcc-libs)
makedepends=(cmake python-setuptools python-numpy)
source=(https://github.com/google/$pkgbase/archive/v$pkgver/$pkgbase-$pkgver.tar.gz
        $pkgbase-issue7486.patch::https://github.com/google/flatbuffers/commit/fef2ffc4d09ff6f9d878f7b426c57abafc0bb1ca.patch)
sha256sums=('4c7986174dc3941220bf14feaacaad409c3e1526d9ad7f490366fede9a6f43fa'
            '4a9243b333733f303cc6ef7ca444336e4a239e7f6a9d7da39cf7b8ad925b5a7c')

prepare() {
  sed -i 's/-Werror=/-W/g;s/-Werror//g' $pkgname-$pkgver/CMakeLists.txt

  cd $pkgbase-$pkgver
  # Work-around missing out-of-source build support in Python tests
  sed -i 's#${test_dir}/../flatc#${test_dir}/../../build/flatc#' tests/PythonTest.sh

  # Backport for https://bugs.archlinux.org/task/75720
  patch -Np1 -i ../$pkgbase-issue7486.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFLATBUFFERS_BUILD_FLATLIB=OFF \
    -DFLATBUFFERS_BUILD_SHAREDLIB=ON
  cmake --build build

# Python bindings
  cd $pkgbase-$pkgver/python
  VERSION=$pkgver python setup.py build
}

check() {
  # tests are broken with out-of-source builds
  # https://github.com/google/flatbuffers/issues/7282
  cmake --build build --target test || true

  ./$pkgbase-$pkgver/tests/PythonTest.sh
}

package_flatbuffers() {
  DESTDIR="$pkgdir" cmake --install build
}

package_python-flatbuffers() {
  pkgdesc='An efficient cross platform serialization library for Python'
  depends=(python)
  optdepends=(
    'python-numpy: support for Numpy arrays'
  )

  cd $pkgbase-$pkgver/python
  VERSION=$pkgver python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
