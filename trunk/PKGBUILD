# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=dillo
pkgver=3.0.5
pkgrel=5
pkgdesc="A small, fast graphical web browser built on FLTK"
arch=(x86_64)
url="http://www.dillo.org"
license=('GPL')
depends=('fltk' 'libjpeg' 'perl' 'openssl' 'libpng' 'gcc-libs' 'libxcursor'
         'libxi' 'libxinerama' 'openssl-1.0')
backup=(etc/dillo/{dillorc,dpidrc})
validpgpkeys=('D91CE08D8679CF2A6F8792E05B1004E936929CEE')
source=(http://www.dillo.org/download/$pkgname-$pkgver.tar.bz2{,.asc})
sha256sums=('db1be16c1c5842ebe07b419aa7c6ef11a45603a75df2877f99635f4f8345148b'
            'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  CFLAGS="-I/usr/include/openssl-1.0" LDFLAGS="-L/usr/lib/openssl-1.0" \
      ./configure --prefix=/usr --sysconfdir=/etc --enable-cookies --enable-dlgui \
              --enable-ssl
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
