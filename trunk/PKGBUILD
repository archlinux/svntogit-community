# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Letu Ren <fantasquex at gmail dot com>

_pyname=mulpyplexer
pkgname=python-${_pyname}
pkgver=0.9
pkgrel=2
pkgdesc='Module that multiplexes interactions with lists of python objects'
url='https://github.com/zardus/mulpyplexer'
arch=('any')
license=('custom:BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d7df91b3ed606a0865cd7df815de8971354b372d2bbca20529cf7ac6492dc4607fc20a3587431c86c4d1d3eb1a59cde4796bc7a0b4421443086f24b42b86cd2b')
b2sums=('3918dee824f0aee8c95562fa29c1e3ed64c7d37844f81078e7e2883f1e4d994f9a996c798e4f5f5a12afa5cd40ec09a0b942b89fe5c52183898c20038b8fbe4b')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
