# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libzdb
pkgver=3.2.2
pkgrel=2
pkgdesc="Zild Database Library"
arch=('x86_64')
url="https://www.tildeslash.com/libzdb/"
license=('GPL')
depends=('postgresql-libs' 'sqlite' 'mariadb-libs')
source=(https://www.tildeslash.com/libzdb/dist/libzdb-$pkgver.tar.gz)
sha256sums=('d51e4e21ee1ee84ac8763de91bf485360cd76860b951ca998e891824c4f195ae')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '1,1i#include <cassert>' test/zdbpp.cpp
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
