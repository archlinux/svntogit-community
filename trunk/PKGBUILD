# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=7.6.4
pkgrel=1
pkgdesc='Prolog environment'
arch=('x86_64')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
depends=('gmp' 'libarchive' 'openssl' 'readline')
makedepends=('libjpeg' 'libxft' 'libxinerama' 'libxpm' 'unixodbc')
optdepends=('unixodbc:    for using the odbc4pl library'
            'libjpeg:     for using the pl2xpce library'
            'libxpm:      for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft:      for using the pl2xpce library')
source=("http://www.swi-prolog.org/download/stable/src/swipl-${pkgver/_/-}.tar.gz") # invalid HTTPS cert
sha256sums=('2d3d7aabd6d99a02dcc2da5d7604e3500329e541c6f857edc5aa06a3b1267891')

build() {
  cd "swipl-${pkgver/_/-}"

  ./configure --with-world --prefix=/usr
  (cd src; ./configure --enable-readline --prefix=/usr)
  make
}

check() {
  make -C "swipl-${pkgver/_/-}" check || true
}

package() {
  make -C "swipl-${pkgver/_/-}" DESTDIR="$pkgdir" install

  # Fix for FS#20873
  chmod +x "$pkgdir/usr/lib/swipl-${pkgver/_/-}/library/dialect/sicstus/swipl-lfr.pl"
}

# getver: swi-prolog.org/download/stable
# vim: ts=2 sw=2 et:
