# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hunter
pkgver=3.0.3
pkgrel=2
arch=('x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD')
url='https://github.com/ionelmc/python-hunter'
depends=('python-manhole' 'python-colorama')
optdepends=('gdb: for remote support')
makedepends=('python-setuptools' 'python-manhole' 'python-colorama' 'cython')
checkdepends=('python-pytest-benchmark' 'python-virtualenv' 'python-pytest-runner' 'gdb'
              'python-process-tests' 'python-aspectlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-hunter/archive/v$pkgver.tar.gz")
sha512sums=('366346c7029ad2a5885ff6f1a374f26c351a1a3a57e045cb9752a30b80766876487497ee311e5350295e5eb540e99d038fa775722c3bf7e5291b707200d8b1c0')

prepare() {
  rm python-hunter-$pkgver/src/hunter/*.c
}

build() {
  cd "$srcdir"/python-hunter-$pkgver
  python setup.py build
}

check() {
  # Hack to process .pth files properly
  # ptrace needed to pass the remote tests

  cd "$srcdir"/python-hunter-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  ) || warning "Tests failed"
}

package() {
  cd python-hunter-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
