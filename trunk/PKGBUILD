# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: cth451 <cth451@gmail.com>

pkgname=materia-gtk-theme
pkgver=20210322
pkgrel=2
pkgdesc="A Material Design theme for GNOME/GTK+ based desktop environments"
arch=(any)
url="https://github.com/nana-4/materia-theme"
license=(GPL)
depends=(gnome-themes-extra)
makedepends=(meson sassc gtk3 gtk4 gnome-shell)
optdepends=('gtk-engine-murrine: for gtk2 theme')
provides=(materia-theme)
conflicts=(materia-theme)
replaces=(materia-theme)
source=(https://github.com/nana-4/materia-theme/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4460af612cd70242543ac71023f8963eea39be059486e9ebe58b3846be74bcb5')

build() {
  arch-meson ${pkgname/gtk-/}-${pkgver} build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
