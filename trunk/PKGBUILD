# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.6.8
pkgrel=1
epoch=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base qt5-declarative qt5-networkauth qt5-svg qt5-websockets)
makedepends=(help2man meson jack python-jinja python-yaml)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('dc0bf64461eddfbedaa773c177a4c1387e205b5f3efeaf1e999b5ead21abe53122eebdeb75e853cdaeeb03c73eeb968ac7d5d6962b4af27b5009b724775a823c')
b2sums=('ebe1b0c6ade7b40ff6e06bd9f628ba6423542dd496aeb447e5eed5865901b27c3f4ab5dbbc55bfdf22daa0ca03d8a2b734dcd8f80f3c9d64a0c8efda95941fbb')

build() {
  arch-meson -Drtaudio=disabled build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
