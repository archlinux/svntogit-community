# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.24.3
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('gcc-libs' 'libgit2' 'libssh' 'openssl' 'zlib')
makedepends=('cargo' 'pkg-config')
options=('!lto')
source=(https://github.com/crate-ci/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('5c35c5efe4733fbcda65417dbfd5cd2e36abe640ca7bf9c90742442bc9c3da46be177232155d52749204daf8803dd98c038c05c46b5386e2e60249a66e5ff704')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/"vendored-libgit2"//' Cargo.toml
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
