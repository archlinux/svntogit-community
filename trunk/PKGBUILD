# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.9.2
pkgrel=1.2
pkgdesc="A lightweight X11 tiled window manager written in Haskell"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.0.2' 'gmp' 'haskell-x11=1.5.0.0' 'sh' 'haskell-mtl=2.0.1.0' 'haskell-utf8-string=0.3.6')
install='xmonad.install'
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        xmonad.desktop xmonad.svg)
md5sums=('5a4117f798664d772d2a834ea6c90f1c'
         'f61961459f190b6d948f1f2782afa7a7'
         '72bfa5e62e4e44fe7fa59b6a7593d993')

build() {
  cd $srcdir/$pkgname-$pkgver
  runhaskell Setup.lhs configure --ghc --enable-split-objs --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
#  runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE

  install -D -m644 $srcdir/xmonad.desktop $pkgdir/usr/share/applications/xmonad.desktop
  mkdir -p $pkgdir/etc/X11/sessions
  ln -s /usr/share/applications/xmonad.desktop $pkgdir/etc/X11/sessions/xmonad.desktop
  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg
}
