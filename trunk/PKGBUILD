# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='nats-pure'
pkgname="ruby-${_gemname}"
pkgver=2.1.2
pkgrel=1
pkgdesc="Ruby client for the NATS messaging system"
url="https://github.com/nats-io/nats-pure.rb"
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-rspec' 'ruby-bundler' 'ruby-rdoc' 'ruby-test-unit')
options=('!emptydirs')
source=("${_gemname}-v${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('8f30462312d9a278f6bdb7f820e45e4969b17b48f981116ece2f1168e69a4e7d61447468a1054ef98a8773f5a63d919c6ad048503096980922592beef5148369')


prepare() {
  cd "${srcdir}/${_gemname}.rb-${pkgver}"
  :
}

build() {
  cd "${srcdir}/${_gemname}.rb-${pkgver}"
  mkdir -p pkg
  gem build -o "pkg/${_gemname}-${pkgver}.gem"
}

check() {
  cd "${srcdir}/${_gemname}.rb-${pkgver}"
  # testsuite requires a running nats server to test against
  #rake spec
}

package() {
  cd "${srcdir}/${_gemname}.rb-${pkgver}"
  local _gemdir="$(gem env gemdir)"

  gem install \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "pkg/${_gemname}-${pkgver}.gem"

  rm -rf "${pkgdir}/${_gemdir}/cache"

  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
