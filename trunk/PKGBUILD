# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.12.14
pkgrel=3
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
makedepends=('gcc-multilib' 'lib32-gtk2' 'lib32-mesa>=9.1' 'lib32-mesa-libgl>=9.1' 'mesa-libgl') # we need to avoid old libgl in [extra]
source=(http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz)
sha1sums=('9106ab09b2e7b9f90521b18dd4a7e9577eba6c15')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --enable-tee \
      --enable-gl \
      --enable-egl
      # --disable-xlib-xcb
  make
}

package() {
  depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender' 'lib32-fontconfig'
           'lib32-pixman' 'lib32-glib2' 'lib32-mesa>=9.1' 'lib32-libgl' ${_pkgbasename})

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
