# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=0.3.9.3
pkgrel=1
pkgdesc='A firewall daemon with D-BUS interface providing a dynamic firewall'
arch=('any')
url="http://fedorahosted.org/${pkgname}"
license=('GPL2')
depends=('ebtables' 'gtk3' 'iptables' 'libnotify' 'networkmanager' 'python-slip')
makedepends=('intltool' 'docbook-xsl')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
install="${pkgname}.install"
source=("http://fedorahosted.org/released/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'firewalld-paths.patch')
sha256sums=('2076665137ad93d574bb5c9688fc83b4971cf70e36bdf0fd20f885dd42553002'
            'e2f96a3fbd8e8b9168d49c9f39e76a69fff169e69f302d0c12e458ed698cabc6')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../firewalld-paths.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --disable-schemas-compile
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
