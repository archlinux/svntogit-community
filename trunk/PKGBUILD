# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=5.4.0
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL LGPL)
depends=(accountsservice gtk3 libxkbfile libpulse)
makedepends=(meson samurai gobject-introspection intltool)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        config.patch)
sha256sums=('c1b7ec54a5432a42f71bb4c5d4ad6f8ed5fca60b18bfd37ee0939965b56ec87e'
            'e70227c64eca6857fe9adc21c6aee851df40b3a54bf34a145e0dc4b9819a7b6a')
b2sums=('eb78b942804009d0082dc8233a749c1060e132b499a8483c6f459c6d7aa436c97c227c7a062974e207b21c98e49140a4bd4725c6ca201509b6e08b3b2bb2f0ed'
        '5ae28fca1f3fee35222569afdba3f6f2df00f2b27709d1135589f33a3b80546ca541c391f5ee02759467fcaffe380dc1cbc4117dffe3c10cdf7046fd8588e384')

prepare() {
  cd ${pkgname}-${pkgver}
  # Adjust configuration
  patch -p1 < ../config.patch
  mkdir -p build
}

build() {
  cd ${pkgname}-${pkgver}/build
  arch-meson ..
  samu
}

package() {
  cd ${pkgname}-${pkgver}/build
  DESTDIR="${pkgdir}" samu install
}
