# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxdmcp
pkgname=lib32-$_pkgbasename
pkgver=1.1.3
pkgrel=2
pkgdesc="X11 Display Manager Control Protocol library (32-bit)"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('xorg-util-macros' 'xorgproto')
provides=('libXdmcp.so')
options=('debug')
source=(${url}/releases/individual/lib/libXdmcp-${pkgver}.tar.bz2{,.sig})
sha512sums=('cb1d4650f97d66e73acd2465ec7d757b9b797cce2f85e301860a44997a461837eea845ec9bd5b639ec5ca34c804f8bdd870697a5ce3f4e270b687c9ef74f25ec'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd libXdmcp-${pkgver}

  export CC="gcc -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
              --libdir=/usr/lib32
  make
}

package() {
  cd libXdmcp-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
