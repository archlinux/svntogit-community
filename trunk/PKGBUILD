# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.233
pkgrel=1
pkgdesc="An extremely fast Python linter, written in Rust"
arch=(x86_64)
url="https://github.com/charliermarsh/ruff"
license=(MIT)
depends=(gcc-libs glibc python)
makedepends=(cargo maturin python-installer)
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('7b0e263b6331d08581ec543b1df11e64366f3ca9cbd4169064ac60fa0286ccbb45ac2563627d9993d8dd5a251f232e07fb1cafaa9f0c80ed57e5724b969cc76a')
b2sums=('84f1b8cc6484336ca05e0a528af642118a4105e245007765a279dd5de488989db16347c083daa8456994255f2dc4f8b8984e87322de0739bee59d146214d292f')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  local maturin_options=(
    --release
    --strip
    --locked
    --all-features
    --target $CARCH-unknown-linux-gnu
  )

  cd $pkgname-$pkgver

  maturin build "${maturin_options[@]}"
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
