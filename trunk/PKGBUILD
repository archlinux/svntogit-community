# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-ipywidgets
_pipname=ipywidgets
pkgver=8.0.5
pkgrel=1
pkgdesc='IPython HTML widgets for Jupyter'
arch=(any)
url='https://github.com/ipython/ipywidgets'
license=(BSD)
depends=(python-ipykernel python-jsonschema python-ipython-genutils)
makedepends=(python-build python-installer python-setuptools python-wheel)
optdepends=('jupyterlab-widgets: widgets for Jupyter lab' 'jupyter-widgetsnbextension: widgets for the legacy notebook')
checkdepends=(python-pytest python-pytz)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('89a1930b9ef255838571a2415cc4a15e824e4316b8f067805d1d03b98b6a8c5f')

build() {
  cd ipywidgets-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ipywidgets-$pkgver
  pytest -v
}

package() {
  cd ipywidgets-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
