# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinuc.org>
# Contributor: Johannes Pohl <johannes.pohl90@gmail.com>

pkgname=urh
pkgver=2.8.8
pkgrel=1
pkgdesc='Universal Radio Hacker: investigate wireless protocols like a boss'
arch=('x86_64')
url='https://github.com/jopohl/urh'
license=('GPL3')
depends=('python' 'python-pyqt5' 'python-numpy' 'python-psutil' 'cython')
makedepends=('gcc' 'python-setuptools' 'airspy' 'hackrf' 'libuhd' 'limesuite' 'rtl-sdr' 'bladerf' 'libiio')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'xorg-server-xvfb')
optdepends=('airspy: Airspy backend'
            'cython: compiling native modules inside URH'
            'hackrf: HackRF backend'
            'libuhd: USRP backend'
            'limesuite: LimeSDR backend'
            'rtl-sdr: RTL-SDR backend'
            'gnuradio: GNURadio backend'
            'bladerf: BladeRF backend'
            'libiio: IIO backend (PlutoSDR)'
            'gnuradio-osmosdr: OsmoSDR backend, via GNURadio'
            'python-pyaudio: Soundcard backend')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jopohl/urh/archive/v$pkgver.tar.gz")
sha512sums=('499df6e15fbf73b0b7ecda3ea65c5246b3fd7e3da510b3602ea2f41390ad98c672cc9228394f779178daf545ade3783545f462436f295c06f5e37c0a5dc69179')

build() {
  cd $pkgname-$pkgver

  python setup.py build build_ext
  python src/urh/cythonext/build.py
}

check() {
  cd $pkgname-$pkgver

  xvfb-run pytest --boxed
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" -O1 --skip-build

  install -Dm644 ./data/urh.desktop "$pkgdir"/usr/share/applications/urh.desktop
  install -Dm 644 data/icons/appicon.png "$pkgdir"/usr/share/pixmaps/urh.png
  install -Dm 644 README.md "$pkgdir"/usr/share/doc/$pkgname/README
}

