# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi
pkgver=2021.05
pkgrel=4
pkgdesc='Editor for audio/video projects using the GStreamer framework'
arch=('x86_64')
url='http://pitivi.org/'
license=('LGPL')
depends=('gsound' 'gst-editing-services' 'gst-plugin-gtk' 'gst-plugins-bad' 'gst-plugins-good' 'gst-python'
         'gtk3' 'libnotify' 'libpeas' 'python-cairo' 'python-gobject' 'python-matplotlib' 'python-numpy')
makedepends=('appstream-glib' 'intltool' 'itstool' 'meson')
optdepends=('frei0r-plugins: additional video effects, clip transformation feature'
            'gst-libav: additional multimedia codecs'
            'gst-plugins-ugly: additional multimedia codecs')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        $pkgname-meson-0.61.patch::https://gitlab.gnome.org/GNOME/pitivi/-/commit/ddf2369d1fc6.patch
        $pkgname-add-icons-removed-from-adwaita.patch::https://gitlab.gnome.org/GNOME/pitivi/-/commit/ac64d971fd47.patch
        python310.patch)
sha256sums=('cf5693c46c42ab0da14a2e59bfcf4bc880604b41e9cd3aa8d2ebdc60827b75c7'
            'bbf4d3c329572f8e4e2c735bd9514c5bd9c2ee2df3aadece3f1a85b4d214e8fd'
            'af822fccf021241fc68762acb8d90fb8298cca5c9af78185dfe9c6ccad49e477'
            'bebb9d3308a1ff5f1bd47cbab23b534e72b41b59f7366fa6333999aad0d2ab7f')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../$pkgname-meson-0.61.patch
  patch -Np1 --no-backup-if-mismatch -i ../$pkgname-add-icons-removed-from-adwaita.patch
  patch -Np1 --no-backup-if-mismatch -i ../python310.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
