# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Sial <sial@cpan.org>
# Contributor: Martin Herndl <martin.herndl@gmail.com>

pkgname=guayadeque
pkgver=0.3.7
pkgrel=1
pkgdesc='Lightweight music player'
arch=('i686' 'x86_64')
url='http://guayadeque.org/'
license=('GPL3')
depends=('curl' 'desktop-file-utils' 'libgpod' 'taglib' 'wxgtk2.8')
makedepends=('cmake')
optdepends=('gstreamer0.10-good-plugins: Support for additional file formats'
            'gvfs: Support for external devices')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('d23eb1247add0bef8d5fef834294d7cb3d7c5fc20db9022b86f618a13d359938')

prepare() {
  cd ${pkgname}-${pkgver}/src

  xgettext -d guayadeque -o guayadeque.pot -C --keyword=_ *.cpp
  mv guayadeque.pot ../po
}

build() {
  cd ${pkgname}-${pkgver}

  rm -rf build
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8' -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8'
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
