# Maintainer: David Runge <dvzrv@archlinux.org>

_name=falcon
pkgname=python-falcon
pkgver=3.0.1
pkgrel=1
pkgdesc="Web API framework for building fast and reliable microservices, proxies, and app backends in Python"
arch=('x86_64')
url="https://falconframework.org/"
license=('Apache')
depends=('python')
makedepends=('cython' 'python-setuptools')
# TODO: add python-daphne
checkdepends=('gunicorn' 'hypercorn' 'python-aiofiles' 'python-cbor2'
'python-httpx' 'python-jsonschema' 'python-mimeparse' 'python-msgpack'
'python-mujson' 'python-orjson' 'python-pecan' 'python-pytest'
'python-pytest-asyncio' 'python-pytest-runner' 'python-rapidjson'
'python-requests' 'python-testtools' 'python-ujson' 'python-websockets'
'python-yaml' 'uvicorn')
optdepends=('python-django: for falcon-bench'
            'python-flask: for falcon-bench'
            'python-pecan: for falcon-bench'
            'python-bottle: for falcon-bench'
            'python-pprofile: for falcon-bench'
            'python-vmprof: for falcon-bench')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c59c9868aad1536a6872790ca658b47546f64db02a3c5bcacce55d6091f2f29707bf052940b8dee530ddd19a99c286d6c18f31517a9c83417a550deab082026f')
b2sums=('34325b756695eaa05eb05940c256cd663c652779e6c1070187b0638dc9572db19b6f29519366b40f96b4c03c6160407c926453c9f0ff3c9f5f47e0355c3c3ddd')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-$CARCH-${python_version}:$PWD:${PYTHONPATH}"
  pytest -v --ignore "tests/asgi/test_cythonized_asgi.py" tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
