# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=zenith
pkgver=0.14.0
pkgrel=2
pkgdesc="Terminal system monitor with histograms, written in Rust"
arch=('x86_64')
url="https://github.com/bvaisvil/zenith"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo' 'clang')
optdepends=('nvidia-utils')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('2cbcea2625cfa97c161b974ad412a47e330f7fd31bec0479e329ed3606cfc569')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 "README.md" -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 "LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "assets/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm 644 "assets/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}

# vim: ts=2 sw=2 et:
