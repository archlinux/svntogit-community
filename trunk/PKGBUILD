# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kernelshark
pkgver=2.1.1
pkgrel=2
pkgdesc="GUI frontend for trace-cmd based Linux kernel Ftrace captures"
arch=(x86_64)
url="https://kernelshark.org/"
license=(GPL2)
depends=(freeglut gcc-libs glibc glu qt5-base)
makedepends=(boost cmake json-c libglvnd libtracefs libtraceevent trace-cmd ttf-fira-sans)
provides=(libkshark.so libkshark-plot.so libkshark-gui.so)
source=($pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/utils/trace-cmd/kernel-shark.git/snapshot/kernel-shark-$pkgname-v$pkgver.tar.gz)
sha512sums=('8fd9d117401961bf892a46890a7fd97c17a2d9839704b43730a5bf90f5e2cf73c68da2453a2e208b1732055abfaf5ddd7532efe8de9256b4232abd172a04f89c')
b2sums=('d686a0c19b9daeb355fa3c57104b9147cd0b4e492a3b0b57e5fdbd450d265f963bb35c99b896039007ea47c983d86649381fd81ec9402e4482a39e962322de3a')

prepare() {
  cd kernel-shark-$pkgname-v$pkgver

  # replace FreeSans with ttf-fira-sans
  sed -e 's/FreeSans/FiraSans-Regular/g' \
      -i CMakeLists.txt
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -D_INSTALL_PREFIX=/usr \
        -D_LIBDIR=/usr/lib \
        -Wno-dev \
        -B build \
        -S kernel-shark-$pkgname-v$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(
    json-c libjson-c.so
    libglvnd libGL.so
    libtraceevent libtraceevent.so
    libtracefs libtracefs.so
    trace-cmd libtracecmd.so
  )

  make DESTDIR="${pkgdir}" prefix=/usr VERBOSE=1 install -C build
}
