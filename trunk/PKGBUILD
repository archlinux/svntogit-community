# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=fop
pkgver=2.1
pkgrel=1
pkgdesc='XSL-FO implementation in Java.'
arch=('any')
url='http://xmlgraphics.apache.org/fop/'
license=('APACHE')
depends=('java-xmlgraphics-commons' 'java-commons-logging' 'java-commons-io' 'java-batik'
         'java-avalon-framework')
makedepends=('apache-ant' 'java-environment')
source=("https://ftp-stud.hs-esslingen.de/pub/Mirrors/ftp.apache.org/dist/xmlgraphics/$pkgname/source/$pkgname-$pkgver-src.tar.gz"
        'fop.conf')
md5sums=('13b5b497dfb0ba8a01093dfc1a89d106'
         '3dc3325bf77afc787024f33e7d6ab99e')
backup=('etc/fop.conf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ant package 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0644 "build/fop.jar" "$pkgdir/usr/share/java/fop/fop.jar"
  install -Dm0644 "../fop.conf" "$pkgdir/etc/fop.conf"
  install -Dm0755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
}
