# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=photoflare
pkgver=1.6.9
pkgrel=1
pkgdesc='Quick, simple but powerful Qt-based image editor inspired by PhotoFiltre'
arch=('x86_64')
url='https://photoflare.io/'
license=('GPL3')
depends=('graphicsmagick' 'hicolor-icon-theme' 'qt5-base' 'qt5-imageformats')
makedepends=('qt5-tools')
source=("https://github.com/PhotoFlare/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('4040efa21d630af4e2d6102001dbe46f0527ad0a49fbd8617fb65b4b313efff3')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/pixmaps \\/d' Photoflare.pro
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 Photoflare.pro PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
