# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-visualization-spectrum
epoch=1
pkgver=19.0.2
_codename=Matrix
pkgrel=3
pkgdesc="Spectrum visualizer for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/visualization.spectrum'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-visualization')
makedepends=('cmake' 'glm' 'kodi-dev')
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/visualization.spectrum/archive/$pkgver-$_codename.tar.gz")
sha512sums=('da7b17147544d8f6471fc1831b3166471f2cc22a27d802ce41ee10e01fabdbf7fe8d846909c13550b39c69cf01c74e296f48ecd36f16d76bd1f0f4134df2aa93')

build() {
    cd "visualization.spectrum-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'glm')
    cd "visualization.spectrum-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

