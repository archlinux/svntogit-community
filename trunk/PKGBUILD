# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gilles CHAUVIN <gcnweb at gmail dot com>

pkgname=recordmydesktop
pkgver=0.3.8.1
pkgrel=7
pkgdesc="Produces a OGG encapsulated Theora/Vorbis recording of your desktop"
arch=('x86_64')
url="http://recordmydesktop.sourceforge.net"
license=('GPL')
depends=('alsa-lib' 'zlib' 'libvorbis' 'libxdamage' 'libxext' 'libtheora' 'libsm' 'jack')
makedepends=('jack')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	shmstr-to-shmproto.patch
	recordmydesktop-bitrate.patch)
sha256sums=('33a2e208186ae78e2db2a27b0f5047b50fb7819c47fe15483b0765200b9d738c'
            'e75fbfc3c9c8d8175511857d27e888771f0dda4dd85a2b526969571fba44c85f'
            '8acb56e9408d99f8c9f83ae4d4fd982faa55e1e128641d92e41ecee58893e284')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  patch -p0 < "$srcdir"/shmstr-to-shmproto.patch
  patch -p1 < "$srcdir"/recordmydesktop-bitrate.patch
  ./configure --prefix=/usr --enable-jack=yes LIBS="`pkg-config --libs jack`" \
	CFLAGS="`pkg-config --cflags jack` -DHAVE_LIBJACK"
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
