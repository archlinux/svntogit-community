# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
_helper_pkgname=budgie-helper
pkgver=10.4+34+gd6f3519f
_helper_gitrev=3b52434b6d1cfe00c5bac1046d323813ce531a55
pkgrel=1
pkgdesc="Modern desktop environment from the Solus Project"
arch=('x86_64')
url="https://budgie-desktop.org/"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=d6f3519f04d8c619f7d1797ad24a26357e578ba9  # master
source=("git+https://github.com/budgie-desktop/budgie-desktop.git#commit=$_commit"
        "git+https://git.gnome.org/browse/libgnome-volume-control"
        "$_helper_pkgname-$_helper_gitrev.tar.gz::https://codeload.github.com/City-busz/$_helper_pkgname/tar.gz/$_helper_gitrev"
	gnome3.28.patch
        no-nautilus-desktop.patch)
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            '3a4d7dd7c95ccba4e2916adf4a14769ffe54e8f86ed302d0268cd312b2a85c0e'
            '86a43069171e7949a66ac2eed7513cd7f102151e1734e5e54333edb92447ff45'
            'f553b4f14e8f64ee472ad7c923f2ac2d9fba5efa62adb11ee5ee9d14fb4278ac')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
	mkdir build
	cd $pkgname
	patch -p1 -i ../gnome3.28.patch
	patch -p1 -i ../no-nautilus-desktop.patch

	git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
	git submodule update --init

	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	arch-meson $pkgname  build
	ninja -C build

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	DESTDIR="$pkgdir" ninja -C build install

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	make DESTDIR="$pkgdir" install
}
