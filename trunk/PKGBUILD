# $Id$
pkgname=wlc
pkgver=0.0.8
pkgrel=1
pkgdesc="Wayland Compositor Library"
url="https://github.com/Cloudef/wlc"
arch=("i686" "x86_64")
license=("MIT")
depends=(
	"libdrm" "pixman" "xcb-util-image" "xcb-util-wm"
	"wayland" "wayland-protocols" "libxkbcommon" "libinput" "libgl"
)
makedepends=("cmake")
source=(
	"https://github.com/Cloudef/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"
	"https://github.com/Cloudef/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2.asc"
)
sha256sums=(
	"1a7d7758028b0caa75418268f59e2290420850d0f3226ecf74316405d2c5ace5"
	"SKIP"
)
validpgpkeys=("C2AA3C3A6F9B1F932014879EF769BB961AF6D26A")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

check() {
	cd build
	make test
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
