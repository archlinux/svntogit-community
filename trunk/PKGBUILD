# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: philefou <tuxication AT gmail DOT com>
# Contributor: lindquist <tomas@famolsen.dk>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Mihai Militaru <mihai.militaru@ephemeros.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgbase=glfw
pkgname=('glfw-x11' 'glfw-wayland' 'glfw-doc')
pkgver=3.2.1
pkgrel=1
arch=('x86_64')
url="http://www.glfw.org/"
license=('custom:ZLIB')
makedepends=('mesa' 'cmake' 'doxygen' 'vulkan-headers' 'vulkan-icd-loader'
             'extra-cmake-modules' 'wayland-protocols' 'libxi' 'libxrandr'
             'libxcursor' 'libxkbcommon' 'libxinerama')
source=("https://github.com/glfw/glfw/archive/${pkgver}.tar.gz")
md5sums=('91b8250b6edcc26c9f5205555070a504')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  [[ -d build-x11 ]] && rm -r build-x11
  mkdir build-x11 && cd build-x11

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON
  make docs

  cd ..

  [[ -d build-wayland ]] && rm -r build-wayland
  mkdir build-wayland && cd build-wayland

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON \
      -DGLFW_USE_WAYLAND=ON
}

package_glfw-x11() {
  pkgdesc="A free, open source, portable framework for graphical application development (x11)"
  depends=('libxi' 'libxrandr' 'libxinerama' 'libxcursor' 'libgl')
  replaces=('glfw')
  conflicts=('glfw')
  provides=('glfw')

  cd "$srcdir/$pkgbase-$pkgver"/build-x11

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_glfw-wayland() {
  pkgdesc="A free, open source, portable framework for graphical application development (wayland)"
  depends=('wayland' 'libxkbcommon' 'libgl')
  conflicts=('glfw')
  provides=('glfw')

  cd "$srcdir/$pkgbase-$pkgver"/build-wayland

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_glfw-doc() {
  pkgdesc="Set of HTML documentation for GLFW"

  mkdir -p "${pkgdir}/usr/share/doc/glfw/"
  cp -r "${srcdir}/glfw-${pkgver}/build-x11/docs/html" "${pkgdir}/usr/share/doc/glfw"
}
