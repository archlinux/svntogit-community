# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=37.2
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('gcc-libs' 'openssl')
makedepends=('systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz")
sha512sums=('16276cc2d1922e6f8657aa86a7bfe6b05d35cd0663bb6934620b5cc45db614594341cc6fa3acfa7d3d59fdd262b56f3bdc0839d97ffde6540c05c54d5c7a40d8')

build() {
  cd smartdns-Release$pkgver/src
  make
}

package() {
  cd smartdns-Release$pkgver
  make DESTDIR="$pkgdir" RUNSTATEDIR=/run SBINDIR=/usr/bin install

  rm -r "$pkgdir"/etc/init.d
}
