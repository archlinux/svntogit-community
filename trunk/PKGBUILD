# Maintainer:  Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer:  apophys <admin AT kubikmilan DOT sk>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Renzo Carbonara <gnuk0001/gmail.com>

pkgname=lzip
pkgver=1.19
pkgrel=1
pkgdesc="A lossless file compressor based on the LZMA algorithm"
arch=('x86_64')
url="http://www.nongnu.org/lzip/lzip.html"
license=('GPL3')
depends=('gcc-libs')
source=("https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"
	"https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz.sig")
sha512sums=('513525c2731334fae99f7a17b1211e7a018446dc211e6ba52a95054e771c9577333a131608cb06a5279da16a4e4877076402b97e97ff6a145c3626fde22d3392'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742')  # Antonio Diaz Diaz <antonio@gnu.org>

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr CXXFLAGS="$CXXFLAGS"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install{,-man}
}
