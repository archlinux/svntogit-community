# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=lucky-commit
_pkgname=lucky_commit
pkgver=2.1.0
pkgrel=1
pkgdesc="Customize your git commit hashes"
arch=('x86_64')
url="https://github.com/not-an-aardvark/lucky-commit"
license=('MIT')
depends=('gcc-libs' 'ocl-icd')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0d830d49535c641a77d446d46a5a0fede941b03bbdf7bacff3bc44a2438aff9f349c8c657fb3642ffbdaea88c254e8fc516405a964f44591452067cfe0988c69')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
