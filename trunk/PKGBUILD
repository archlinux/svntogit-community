# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Audet <ichimonji10@gmail.com>
# Contributor: Dominik Kozaczko <dominik@kozaczko.info>
# Contributor: Alper Kanat <alperkanat@raptiye.org>
# Contributor: Apkawa <apkawa@gmail.com>
# Contributor: Schnouki <thomas.jost@gmail.com>

pkgname=python-django-extensions
pkgver=3.0.4
pkgrel=1
pkgdesc='A collection of custom extensions for the Django Framework'
arch=(any)
url=https://github.com/django-extensions/django-extensions
license=(MIT)
depends=(
  python-django
  python-six
)
makedepends=(
  git
  python-setuptools
)
optdepends=(
  'graphviz: graph_models extension'
  'ipython: shell_plus extension'
  'python-werkzeug: runserver_plus extension'
)
_tag=b5140165c6dcb2714a8f30e02389900711c532ee
source=(git+https://github.com/django-extensions/django-extensions.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd django-extensions

  git describe --tags
}

build() {
  cd django-extensions

  python setup.py build
}

package() {
  cd django-extensions

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-django-extensions/
}

# vim: ts=2 sw=2 et:
