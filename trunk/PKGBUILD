# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spyder-kernels
pkgname=python-${_pkg}
pkgver=1.10.2
pkgrel=1
pkgdesc="Jupyter Kernels for the Spyder console"
arch=(any)
url="https://github.com/spyder-ide/spyder-kernels/"
license=(MIT)
makedepends=(python python-setuptools)
depends=(
    python-cloudpickle
    python-ipykernel
    python-jupyter_client
    python-pyzmq
    python-wurlitzer
)
checkdepends=(
    cython
    python-dask
    python-distributed
    python-flaky
    python-matplotlib
    python-mock
    python-numpy
    python-pandas
    python-pillow
    python-pytest
    python-scipy
    python-xarray
)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('28120272e994a3c8f55e46e68072941d86293c23f03c8837871da5c7ab724d1c')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest spyder_kernels
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
