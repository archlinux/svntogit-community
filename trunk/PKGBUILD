# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-package-deprecationmanager
pkgver=0.18
pkgrel=1
pkgdesc="Manage deprecation warnings for your distribution"
arch=('any')
license=('Artistic2.0')
depends=('perl-params-util' 'perl-sub-install' 'perl-package-stash'
         'perl-module-implementation' 'perl-namespace-autoclean')
checkdepends=('perl-test-fatal' 'perl-test-warnings')
options=('!emptydirs')
url="https://metacpan.org/release/Package-DeprecationManager"
source=(https://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Package-DeprecationManager-$pkgver.tar.gz)
sha256sums=('b68d3f0ced55b7615fddbb6029b89f92a34fe0dd8c6fd6bceffc157d56834fe8')

build() {
  cd "$srcdir"/Package-DeprecationManager-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir"/Package-DeprecationManager-$pkgver
  make test
}

package() {
  cd "$srcdir"/Package-DeprecationManager-$pkgver
  make install DESTDIR="$pkgdir"
}
