# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesmith
pkgver=0.2.1
pkgrel=2
pkgdesc="Hypothesis strategies for generating Python programs, something like CSmith"
url="https://github.com/Zac-HD/hypothesmith"
license=('MPL')
arch=('any')
depends=('python-hypothesis' 'python-lark-parser' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-black' 'python-parso' 'python-pytest')
source=("https://pypi.io/packages/source/h/hypothesmith/hypothesmith-$pkgver.tar.gz")
sha512sums=('3a09ab033b13b6ed5e043cced42df2e320f2c5949c4eb946bcf0ecf3aec07d544d91591beda3a8e6cc0ba92b8d1bed30d030bc069222e7a0ce3f3e247fc8adac')

prepare() {
  cd hypothesmith-$pkgver
  sed -e '/--cov/d' -i tox.ini
}

build() {
  cd hypothesmith-$pkgver
  python setup.py build
}

check() {
  cd hypothesmith-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd hypothesmith-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
