# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-bottle
pkgname=("python-bottle" "python2-bottle")
pkgver=0.12.7
pkgrel=2
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="http://bottlepy.org"
license=('MIT')
options=(!emptydirs)
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-mako' 'python2-mako' 'python-jinja' 'python2-jinja'
              'python-cherrypy' 'python2-cherrypy' 'python-twisted'
              'python2-twisted' 'python-tornado' 'python2-tornado'
              'python2-paste' 'python2-gevent')
source=("git+https://github.com/defnull/bottle.git#tag=${pkgver}")

prepare() {
  cp -r "bottle" "python2-bottle"
  sed -i "1s/python/python2/" python2-bottle/bottle.py
}

check() {
  cd bottle
  python test/testall.py

  cd ../python2-bottle
  python2 test/testall.py
}

package_python-bottle() {
  depends=('python3')

  cd bottle
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-bottle/LICENSE"
}

package_python2-bottle() {
  depends=('python2')

  cd python2-bottle
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-bottle/LICENSE"

  # deal with conflict with python-bottle's /usr/bin/bottle.py
  mv "$pkgdir/usr/bin/bottle.py"{,2}
}

sha512sums=('SKIP')
