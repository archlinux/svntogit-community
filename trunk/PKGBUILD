# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgname=lib32-libnm-glib
pkgver=1.4.0
pkgrel=2
pkgdesc='NetworkManager library'
arch=('x86_64')
url='http://www.gnome.org/projects/NetworkManager/'
license=('GPL2' 'LGPL2.1')
depends=('lib32-dbus-glib' 'lib32-glib2' 'lib32-glibc' 'lib32-jansson'
         'lib32-dbus' 'lib32-libgudev' 'lib32-nspr' 'lib32-nss'
         'lib32-util-linux'
         'libnm-glib')
makedepends=('dhclient' 'gcc-multilib' 'git' 'gtk-doc' 'intltool' 'iproute2'
             'iptables' 'lib32-libmm-glib' 'lib32-libndp' 'lib32-libnewt'
             'lib32-libsoup' 'lib32-libteam' 'lib32-polkit' 'modemmanager'
             'perl-yaml' 'ppp=2.4.7' 'python-gobject' 'rp-pppoe' 'systemd'
             'vala' 'wpa_supplicant')
_commit='93b4119a81af57c274ac58210a8776afecf2a252'
source=("git://anongit.freedesktop.org/NetworkManager/NetworkManager#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd NetworkManager

  git describe | sed 's/-dev/dev/;s/-/+/g'
}

prepare() {
  cd NetworkManager

  2to3 -w libnm src tools

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd NetworkManager

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/networkmanager' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --enable-modify-system \
    --disable-doc \
    --disable-introspection \
    --disable-more-warnings \
    --disable-static \
    --disable-wimax \
    --with-crypto='nss' \
    --with-dhclient='/usr/bin/dhclient' \
    --with-dnsmasq='/usr/bin/dnsmasq' \
    --with-iptables='/usr/bin/iptables' \
    --with-modem-manager-1 \
    --with-pppd='/usr/bin/pppd' \
    --with-pppd-plugin-dir='/usr/lib32/pppd/2.4.7' \
    --with-pppoe='/usr/bin/pppoe' \
    --with-kernel-firmware-dir='/usr/lib/firmware' \
    --with-resolvconf='/usr/bin/resolvconf' \
    --with-systemdsystemunitdir='/usr/lib/systemd/system' \
    --with-udev-dir='/usr/lib32/udev' \
    --with-session-tracking='systemd' \
    --without-dhcpcd

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' \
      -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' \
      -i libtool

  make
}

package() {
  cd NetworkManager

  make DESTDIR="${pkgdir}" -C libnm install
  make DESTDIR="${pkgdir}" -C libnm-util install
  make DESTDIR="${pkgdir}" -C libnm-glib install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
