# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
pkgver=8.048.02
pkgrel=8
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
makedepends=('linux-lts-headers')
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/r8168-$pkgver.tar.gz)
sha256sums=('0f209762fa37b90c6ba347a815fd083c9d342e38dbec14273059a419e671df70')

build() {
	cd "r8168-$pkgver"
	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/src/linux-lts M="$PWD/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	depends=('glibc' 'linux-lts')

	local extradir=/usr/lib/modules/$(</usr/src/linux-lts/version)/extramodules
	cd "r8168-$pkgver"
	install -Dt "$pkgdir$extradir" -m644 src/*.ko
	find "$pkgdir" -name '*.ko' -exec xz {} +

	echo "blacklist r8169" | \
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/modprobe.d/$pkgname.conf"
}

# vim:set sw=0 noet:
