# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=libfm
pkgver=1.1.2.2
pkgrel=2
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
groups=('lxde')
install=libfm.install
depends=('gtk2' 'menu-cache' 'shared-mime-info' 'udisks' 'desktop-file-utils')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/pcmanfm/$pkgname-$pkgver.tar.gz)
md5sums=('ea3d09b23ef4c37cb84ae57ea16b8f08')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-udisks \
    --with-gnu-ld 
  make LDFLAGS+=' -lgobject-2.0 -lglib-2.0'
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Temporary fix to FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm 
}
