# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=2.1.4
pkgrel=1
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/elementpath/archive/v$pkgver.tar.gz")
sha512sums=('a843379abba9d1e66ddbb28048442beb6a640c91197284d41c2446cffbd4e90256dd93b14726560c998d0e294820b3f07620642b831f5b111c5d8e19ebfc64f4')

build() {
  cd elementpath-$pkgver
  python setup.py build
}

check() {
  cd elementpath-$pkgver
  python -m pytest
}

package() {
  cd elementpath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
