# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_hkgname=timezone-series
pkgname=haskell-${_hkgname}
pkgver=0.1.9
pkgrel=8
pkgdesc='Enhanced timezone handling for Data.Time'
url='http://projects.haskell.org/time-ng/'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base' 'haskell-deepseq' 'haskell-time')
makedepends=('ghc')
source=(https://hackage.haskell.org/package/${_hkgname}-${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha256sums=('e5d35df5dc2408803120602b0a66ed63439e36b38dd0895f3e2159fcbd7d9cae')
sha512sums=('9a716223270dfcb0ed39e2dcfb460e3540c0f2444f703c466cbd48ad5664e689aa3f6595f32e59b96fef7891aafabc03c9925367a26c2c043cb6adca2dad7a88')

build() {
  cd ${_hkgname}-${pkgver}
    runhaskell Setup configure -O \
      --enable-shared \
      --enable-executable-dynamic \
      --disable-library-vanilla \
      --prefix=/usr \
      --docdir=/usr/share/doc/$pkgname \
      --dynlibdir=/usr/lib \
      --libsubdir=\$compiler/site-local/\$pkgid \
      --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
      --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd ${_hkgname}-${pkgver}
  runhaskell Setup test
}

package() {
  cd ${_hkgname}-${pkgver}
  install -Dm 744 register.sh "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm 755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
  runhaskell Setup copy --destdir="$pkgdir"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
}

# vim: ts=2 sw=2 et:
