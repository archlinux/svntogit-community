# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-process
pkgver=1.3.1
pkgrel=2
pkgdesc='Asynchronous process spawning'
arch=(any)
url='https://github.com/socketry/async-process'
license=(MIT)
depends=(ruby-async ruby-async-io)
checkdepends=(ruby-async-rspec ruby-covered ruby-rspec)
options=(!emptydirs)
source=(https://github.com/socketry/async-process/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('925f89b1fa5e9780098327a56b9210882c638fa4295a96079fd16f28cd25ee5a')

prepare() {
  cd async-process-$pkgver
  sed -r -e 's|~>|>=|g' -i async-process.gemspec
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-process-$pkgver
  gem build async-process.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-process-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-process-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd async-process-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
