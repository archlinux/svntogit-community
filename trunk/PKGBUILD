# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>

pkgname=nbd
pkgver=3.13
pkgrel=1
pkgdesc="tools for network block devices, allowing you to use remote block devices over TCP/IP"
arch=('i686' 'x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz
	config
	nbd.service
	sysusers_nbd.conf)
backup=('etc/nbd-server/config')
depends=('glib2')
install=nbd.install
md5sums=('784be37497cc2f9a53c67c8c77d1676d'
         'a5b890148b80c5951dbe9daa2f487d63'
         '5d1acc143018117174f79eff15afa038'
         'd969703f3d161935bb160a5c7afd468f')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-syslog

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m644 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service
  install -D -m644 "${srcdir}"/sysusers_nbd.conf "${pkgdir}"/usr/lib/sysusers.d/nbd.conf

  install -D -m644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}
