# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=unshield
pkgver=0.6
pkgrel=3
pkgdesc="Extracts CAB files from InstallShield installers"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/synce/files/"
license=('custom')
depends=('zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/synce/${pkgname}-${pkgver}.tar.gz
        libunshield.c.patch)
md5sums=('31a829192a255160d1f71cda4c865c9c'
         'aa731ca6fd7c7dfb03f8b07e232b37d3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # See http://www.mail-archive.com/synce-devel@lists.sourceforge.net/msg01796.html
  patch -Np0 -i "${srcdir}/libunshield.c.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/unshield/LICENSE
}

