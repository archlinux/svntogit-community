# $Id: PKGBUILD 160945 2012-06-07 12:04:14Z juergen $
# Maintainer: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=1.9.0
pkgrel=3
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl2' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2')
makedepends=('mesa')
source=("https://scummvm.org/frs/scummvm/${pkgver}/scummvm-${pkgver}.tar.xz")
sha512sums=('c81248e8474ad5b354a46741ab2e40b8ddfb99e196b0c837424e9ccced285d03d5f12fc41be57034be53bdb8254a61ca0c9d9cd9a2340875d32e1bffa31699c2')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure \
    --enable-all-engines \
    --enable-c++11 \
    --enable-release \
    --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 dists/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}
