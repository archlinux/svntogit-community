# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=minder
pkgver=1.14.0
pkgrel=1
pkgdesc='Mind-mapping application designed for Pantheon'
arch=('x86_64')
url='https://github.com/phase1geo/Minder'
license=('GPL3')
depends=('discount' 'granite' 'gtksourceview4' 'libarchive' 'libhandy')
makedepends=('meson' 'vala')
source=("https://github.com/phase1geo/Minder/archive/$pkgver/Minder-$pkgver.tar.gz")
sha256sums=('7561084a0ee51b51a1e25753dbdd34f48632cc340c89cd78c591a539d8cef653')

build() {
  arch-meson Minder-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s com.github.phase1geo.minder "$pkgdir/usr/bin/$pkgname"
}
