# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: quininer <quininer@live.com>
pkgname='alacritty'
pkgdesc="A cross-platform, GPU-accelerated terminal emulator"
pkgver=0.10.1
pkgrel=2
arch=('x86_64')
url="https://github.com/alacritty/alacritty"
license=('Apache')
makedepends=('auth-tarball-from-git' 'git' 'rust' 'cargo' 'cmake' 'ncurses' 'desktop-file-utils' 'gdb' 'libxcb' 'libxkbcommon')
depends=('freetype2' 'fontconfig' 'libxi' 'libxcursor' 'libxrandr')
checkdepends=('ttf-dejavu') # for monospace fontconfig test
optdepends=('ncurses: for alacritty terminfo database')
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
        chrisduerr.pgp # Christian DÃ¼rr <contact@christianduerr.com>
        kchibisov.pgp) # Kirill Chibisov <contact@kchibisov.com>
sha256sums=('e48d4b10762c2707bb17fd8f89bd98f0dcccc450d223cade706fdd9cfaefb308'
            '19573dc0ba7a2f003377dc49986867f749235ecb45fe15eb923a74b2ab421d74'
            '5b866e6cb791c58cba2e7fc60f647588699b08abc2ad6b18ba82470f0fd3db3b')

prepare() {
  cd "$pkgname-$pkgver"

  auth-tarball-from-git --keyring ../chrisduerr.pgp --keyring ../kchibisov.pgp \
    --tag v$pkgver --prefix $pkgname-$pkgver \
    $url ../$pkgname-$pkgver.tar.gz

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build(){
  cd "$pkgname-$pkgver"
  cargo build --release --locked --offline
}

check(){
  cd "$pkgname-$pkgver"
  cargo test --locked --offline
}

package() {
  cd "$pkgname-$pkgver"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "extra/linux/Alacritty.desktop"
  install -D -m755 "target/release/alacritty" "$pkgdir/usr/bin/alacritty"
  install -D -m644 "extra/alacritty.man" "$pkgdir/usr/share/man/man1/alacritty.1"
  install -D -m644 "extra/alacritty-msg.man" "$pkgdir/usr/share/man/man1/alacritty-msg.1"
  install -D -m644 "extra/linux/io.alacritty.Alacritty.appdata.xml" "$pkgdir/usr/share/appdata/io.alacritty.Alacritty.appdata.xml"
  install -D -m644 "alacritty.yml" "$pkgdir/usr/share/doc/alacritty/example/alacritty.yml"
  install -D -m644 "extra/completions/alacritty.bash" "$pkgdir/usr/share/bash-completion/completions/alacritty"
  install -D -m644 "extra/completions/_alacritty" "$pkgdir/usr/share/zsh/site-functions/_alacritty"
  install -D -m644 "extra/completions/alacritty.fish" "$pkgdir/usr/share/fish/vendor_completions.d/alacritty.fish"
  install -D -m644 "extra/logo/alacritty-term.svg" "$pkgdir/usr/share/pixmaps/Alacritty.svg"
}

