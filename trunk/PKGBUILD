# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: napa3um <napa3um@gmail.com>
# Contributor: Scott Furry <scott.wl.furry@gmail.com>

pkgname=wxsqlite3
pkgver=4.4.1
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('x86_64')
url="https://utelle.github.io/wxsqlite3"
license=('custom:wxWindows')
depends=('wxgtk3')
source=($pkgname-$pkgver.tar.gz::https://github.com/utelle/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('4cadb1f99558bdffa00090d146d78d7add24e5f2289807dbde6f816555ee57f0')

build() {
  cd $pkgname-$pkgver

  autoreconf
  ./configure --with-wx-config=/usr/bin/wx-config-gtk3 --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# license
  install -Dm644 LICENCE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENCE
}
