# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-initial-setup
pkgver=3.16.2
pkgrel=1
pkgdesc="Simple, easy, and safe way to prepare a new system"
arch=('i686' 'x86_64')
url="http://www.gnome.org/"
license=('GPL')
depends=('libnm-gtk' 'accountsservice' 'cheese' 'gnome-desktop' 'libgweather'
         'gnome-online-accounts' 'libgdm' 'libpwquality' 'libibus' 'libgnomekbd')
makedepends=('intltool')
optdepends=('gdm: GDM Initial Setup mode'
            'gnome-getting-started-docs: GNOME Welcome Tour')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('4f3270ac1cc5eab53b03736cfb3dbff1b3ae0c5bef8956877366d5bea2aac123')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/var/lib/gnome-initial-setup"

  # Fix a warning
  chown 102:0 "$pkgdir/usr/share/polkit-1/rules.d"
  chmod 700 "$pkgdir/usr/share/polkit-1/rules.d"
}
