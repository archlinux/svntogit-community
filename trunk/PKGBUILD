# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=2.6.0
pkgrel=2
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl2' 'sdl2_net' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2' 'curl' 'a52dec' 'giflib' 'glew' 'libspeechd' 'gtk3' 'sndio')
makedepends=('mesa')
source=("https://downloads.scummvm.org/frs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('6bb2b22f9b11a8de2eaa48c520d63579f4907fa49d8b02d58702ece86945b38ea911376f5ae0dc6f3b6ed794ff32ca51fbf690e341401b4a03276b32e9bd7b69')
b2sums=('38cdce3e9b7954347d3ba05946c607aa5a62aa8fa1c2145030c1667632a4da43afd3f14ed19ebaf664bbf521610b2cfccc739c82dae95650572bfab81495cb80')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --enable-release \
    --enable-all-engines \
    --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vDm644 dists/${pkgname}.desktop -t "${pkgdir}"/usr/share/applications/
}
