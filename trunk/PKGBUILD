# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-milc
pkgver=1.0.10
pkgrel=3
pkgdesc="Opinionated Batteries-Included Python 3 CLI Framework."
arch=("any")
url="https://milc.clueboard.co/#/"
license=("MIT")
depends=("python-appdirs" "python-argcomplete" "python-colorama")
makedepends=("python-setuptools")
checkdepends=("python-semver" "python-nose2")
source=("$pkgname-$pkgver.tar.gz::https://github.com/clueboard/milc/archive/${pkgver}.tar.gz")
sha256sums=('09cd6c4839b1a25ccc426250ee75252fec8f9adced640e87f1081f03e7d87216')

build() {
    cd "${pkgname#python-}-${pkgver}"
    python setup.py build
}

check() {
    cd "${pkgname#python-}-${pkgver}"
    nose2
}

package() {
    cd "${pkgname#python-}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
