# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=waylock
pkgver=0.6.1
pkgrel=2
pkgdesc="A simple screenlocker for wayland compositors"
arch=(x86_64)
url="https://github.com/ifreund/waylock"
license=(MIT)
depends=(gcc-libs glibc)
makedepends=(libxkbcommon pam scdoc wayland wayland-protocols zig)
source=("https://github.com/ifreund/waylock/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('4d3fc46b0f48c964362fdfceff27314a87b28cb3c40fbf782d24ca755bfa8c55'
            'SKIP')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>

build() {
  cd $pkgname-$pkgver

  DESTDIR="build" zig build \
    --prefix /usr \
    --search-prefix /usr \
    -Dtarget=native-linux.5.15-gnu \
    -Dcpu=baseline \
    -Drelease-safe \
    -Dpie
}

package() {
  depends+=(
    pam libpam.so
    wayland libwayland-client.so
    libxkbcommon libxkbcommon.so
  )

  cd $pkgname-$pkgver

  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
