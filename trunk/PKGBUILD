# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=0.9.7
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=(x86_64)
url="https://qmidinet.sourceforge.io"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(alsa-lib cmake jack2 qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
options=(debug)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('50d83581d38fa19d7dc94e2e4ac4fa73a55cc061b9293a19e3177ed350e358f7539efb0c3ce02b6c572b6add5d173a3050b0b625a08b30d663b31f3b0645dff7')
b2sums=('d47332bde42a6bb5d386610a4e08616bd5032367b5a45b6d36ad39fb4cc8c05d13d0e43ab3e36f22d60e8a2c719e93747d66cbe8843c773333e7518eeeb2727b')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libjack.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
