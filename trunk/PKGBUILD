# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-tinycss2
pkgver=1.0.2
pkgrel=1
pkgdesc="Low-level CSS parser for Python"
url="https://pypi.python.org/pypi/tinycss2"
license=('BSD')
arch=('any')
depends=('python-webencodings')
makedepends=('python-setuptools')
checkdepends=('python-pytest-flake8' 'python-pytest-cov' 'python-pytest-isort'
              'python-pytest-runner')
source=("https://pypi.io/packages/source/t/tinycss2/tinycss2-$pkgver.tar.gz")
sha512sums=('69bdf52898f9e704c01fe5fd11170e0efc9a6aca14646f5331fc06ebbdf941f9b6c798ded7d93636c7b810b35c659750c3ece3917c5c9cef4092e3e17b382dd1')

build() {
  cd tinycss2-$pkgver
  python setup.py build
}

check() {
  cd tinycss2-$pkgver
  python setup.py pytest
}

package() {
  cd tinycss2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
