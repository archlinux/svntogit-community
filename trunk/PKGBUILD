# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.24.33
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit'
arch=(x86_64)
license=(LGPL)
url=https://www.gtk.org/
depends=(
  gtk3
  lib32-at-spi2-atk
  lib32-colord
  lib32-dbus
  lib32-mesa
  lib32-gdk-pixbuf2
  lib32-json-glib
  lib32-libcups
  lib32-libepoxy
  lib32-librsvg
  lib32-libxcomposite
  lib32-libxcursor
  lib32-libxinerama
  lib32-libxkbcommon
  lib32-libxrandr
  lib32-pango
  lib32-rest
  lib32-libcanberra
  lib32-wayland
  lib32-libxdamage
)
makedepends=(
  gtk-doc
  git
  glib2-docs
  meson
  ninja
  sassc
  wayland-protocols
)
install=lib32-gtk3.install
_tag=8ff9b2f83ff491cbfcbf9b30c706bd917679e7cc
source=(
  git+https://gitlab.gnome.org/GNOME/gtk.git#tag=${_tag}
  gtk-query-immodules-3.0-32.hook
)
sha256sums=(
  SKIP
  04255db75cc626e122df4710b350fb656778d01968d56e598b496b71db335989
)

pkgver() {
  cd gtk
  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX=/bin/false
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config
  arch-meson gtk build \
    --libdir=/usr/lib32 \
    -Dbroadway_backend=true \
    -Dcloudproviders=false \
    -Dcolord=yes \
    -Dgtk_doc=false \
    -Dintrospection=false \
    -Dman=false
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -r "${pkgdir}"/{etc,usr/{include,share}}
  find "${pkgdir}"/usr/bin -type f -not -name gtk-query-immodules-3.0 -delete
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  install -Dm 644 gtk-query-immodules-3.0-32.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
}

# vim: ts=2 sw=2 et:
