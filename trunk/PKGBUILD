# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.4.1
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('imagemagick' 'libxslt')
makedepends=('gtk-doc')
license=("LGPL")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.asc})
sha512sums=('f0bd1911fd544f93afadbb2105f151dc77e31a2f3af4c7f8441a06f547a7ebe0674664721b18c3a35a7c4e745dda666713adc1dfbe5e97dfdc54856a13bd5063'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
