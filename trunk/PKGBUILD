# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-hookable' 'python2-zope-hookable')
pkgver=4.0.4
pkgrel=2
pkgdesc="Represents the core of the Zope hookable Architecture"
arch=('i686' 'x86_64')
url="https://github.com/zopefoundation/zope.hookable"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/zopefoundation/zope.hookable.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.hookable{,-py2}
}

build() {
  cd "$srcdir/zope.hookable"
  python setup.py build

  cd "$srcdir/zope.hookable-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.hookable"
  nosetests3

  cd "$srcdir/zope.hookable-py2"
  nosetests2
}

package_python-zope-hookable() {
  depends=('python-setuptools')

  cd "$srcdir/zope.hookable"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-hookable() {
  depends=('python2-setuptools')

  cd "$srcdir/zope.hookable"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
