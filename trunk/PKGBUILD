# Maintainer: Anatol Pomozov

pkgname=osquery
pkgver=5.3.0
pkgrel=1
pkgdesc='SQL powered operating system instrumentation, monitoring, and analytics'
arch=(x86_64)
url='https://osquery.io'
license=(Apache GPL2)
depends=(zlib)
makedepends=(cmake ninja clang python gcc-libs git libunwind)
options=(!strip)
# following commit represents osquery $pkgver with some Arch-specific patches
_commit=4261097abb7505403966a6e45a2526f2abb2aac9
source=(git+https://github.com/anatol/osquery.git#commit=$_commit
        fix_pthread_stack.patch::https://github.com/boostorg/thread/commit/74fb0a26099bc51d717f5f154b37231ce7df3e98.patch
        libaudit.patch)
sha256sums=('SKIP'
            '4a677ebdcebcfecba1bb83ec04ed8c81d8f8decf904e745a1459c84aecb638ca'
            '96218ef5b7d6d6deb3a7b4b3dfed8068b7e4d10acd5b19372b9882f89d4478a8')

build() {
  cd osquery

  CC=clang CXX=clang++ cmake \
      -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DOSQUERY_VERSION=$pkgver \
      -DOSQUERY_IGNORE_CMAKE_MAX_VERSION_CHECK=TRUE \
      -G Ninja -S . -B build

  # submodules are checked out by cmake, patch it after it were checked out
  (cd $srcdir/osquery/libraries/cmake/source/libaudit/src && git reset --hard && patch -p1 < $srcdir/libaudit.patch)
  (cd $srcdir/osquery/libraries/cmake/source/boost/src/libs/thread && git reset --hard && patch -p1 < $srcdir/fix_pthread_stack.patch)

  ninja -C build
}

package() {
  cd osquery

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 tools/deployment/osquery.example.conf "$pkgdir/etc/osquery/osquery.conf"
  install -Dm644 tools/deployment/linux_packaging/osqueryd.sysconfig "$pkgdir/etc/sysconfig/osqueryd"
  install -Dm644 tools/deployment/linux_packaging/rpm/osqueryd.service "$pkgdir/usr/lib/systemd/system/osqueryd.service"
  rm -r $pkgdir/control
}
