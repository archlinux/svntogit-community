# $Id$
# Maintainer: 
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=1.3
pkgrel=1
pkgdesc='KDE media player'
license=(GPL)
arch=(i686 x86_64)
url="http://kaffeine.kde.org"
depends=(kdebase-runtime vlc)
makedepends=(cmake automoc4)
install=kaffeine.install
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz)
md5sums=('4b20f3caeae716494f777e37d7a6571c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
