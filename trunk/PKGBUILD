# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.251
pkgrel=1
pkgdesc='An extremely fast Python linter, written in Rust'
arch=(x86_64)
url="https://github.com/charliermarsh/$pkgname"
license=(MIT)
depends=(gcc-libs glibc)
makedepends=(cargo)
options=(!lto)
_archive="$pkgname-$pkgver"
source=($url/archive/refs/tags/v$pkgver/$_archive.tar.gz)
sha512sums=('4979580d5e009d57108d3ad2021b48d4e54bbe9a7745cf19c25ed26bfbdb564a536db951137597f508086c439263ce7880e563eb94baaf717ba5b439eeea21e2')
b2sums=('e258305eeb5519a1eb258ec1f3462ac4a23965c1a11bc17622ebb0d27046273df16ecf7401c6507390039010f9d0767a541a99b1df9c369a82601a74adce9aed')

prepare() {
  cd "$_archive"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_archive"
  cargo build -p ruff_cli --frozen --release --all-features
}

check() {
  cd "$_archive"
  cargo test -p ruff_cli --frozen --all-features
}

package() {
  cd "$_archive"
  local _target="target/release/$pkgname"
  install -Dm0755 -t "$pkgdir/usr/bin/" "$_target"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  $_target --generate-shell-completion bash | install -Dm0644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$pkgname.bash"
  $_target --generate-shell-completion fish | install -Dm0644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  $_target --generate-shell-completion zsh  | install -Dm0644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
