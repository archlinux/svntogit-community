# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Anton Kudelin <kudelin at protonmail dot com>

_pyname=cylp
pkgname=python-$_pyname
pkgver=0.91.6
pkgrel=1
pkgdesc='A Python interface to CLP, CBC, and CGL to solve LPs and MIPs'
arch=(x86_64)
url='https://github.com/coin-or/CyLP'
license=(EPL)
depends=(python-scipy coin-or-cbc)
makedepends=(python-build python-installer python-setuptools python-wheel cython)
checkdepends=(python-pytest)
source=(https://github.com/coin-or/CyLP/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('e05a4db7f86646a0103235d2b9620ec3bb1d88ca34e11319f9feb98ed11e3156')

build() {
  cd CyLP-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd CyLP-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
# Prevent from loading the uncompiled cylp
  mv cylp/tests .
  rm -r cylp
# Many tests segfault https://github.com/coin-or/CyLP/issues/105
# test-env/bin/python -m pytest -v'
}
  
package() {
  cd CyLP-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm755 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
