# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async-dns
pkgname=ruby-${_gemname}
pkgver=1.2.4
pkgrel=3
pkgdesc='Asynchronous DNS resolver and server'
url='https://github.com/socketry/async-dns'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-async-io')
options=('!emptydirs')
source=(https://github.com/socketry/async-dns/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f288e22aadee0488829cceacc9badaab1aaca07623a54aaf9a0c95aff61997ef')
sha512sums=('c2fc6cf2367c63bb72fb9c9934d261cf793043678d1f6ad9f6a327934e39645cfb7ab4f8dd98ab6739627fa63c58f573538edefd18186a7dbc87f89336354628')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/README.md "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
