# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=persistent-test
pkgname=haskell-persistent-test
pkgver=2.13.1.2
pkgrel=37
pkgdesc="Tests for Persistent"
url="https://github.com/yesodweb/persistent"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-persistent' 'haskell-aeson' 'haskell-blaze-html' 'haskell-conduit'
         'haskell-hspec' 'haskell-hspec-expectations' 'haskell-hunit' 'haskell-monad-control'
         'haskell-monad-logger' 'haskell-path-pieces' 'haskell-quickcheck'
         'haskell-quickcheck-instances' 'haskell-random' 'haskell-resourcet'
         'haskell-transformers-base' 'haskell-unliftio' 'haskell-unliftio-core'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-hspec' 'haskell-http-api-data' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('658d46e6e0b4a147cff9e28a689c258d7967205db226111e31d26de396e55ca1f87ef052010edfb70400a29d582b03d7959f4b5a677fcb7385b8813fe89f870e')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
