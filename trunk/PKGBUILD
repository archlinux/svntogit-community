# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=3.36.0
pkgrel=1
epoch=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(GPL3)
depends=(
  cairo
  enchant
  folks
  gcr
  gdk-pixbuf2
  glib2
  gmime3
  org.freedesktop.secrets
  gnome-online-accounts
  gspell
  gtk3
  iso-codes
  libcanberra
  libgee
  libhandy
  libnotify
  libpeas
  libsecret
  libsoup
  libxml2
  libytnef
  pango
  sqlite
  webkit2gtk
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
source=(git+https://gitlab.gnome.org/GNOME/geary.git#tag=bb907070cd3b296cff7f121e1b48b846e8757e30)
sha256sums=(SKIP)

pkgver() {
  cd geary
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson geary build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
