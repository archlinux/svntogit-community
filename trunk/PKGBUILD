# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-orphans
pkgname=haskell-th-orphans
pkgver=0.13.13
pkgrel=2
pkgdesc="Orphan instances for TH datatypes"
url="https://github.com/ekmett/th-orphans/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-compat' 'haskell-th-lift' 'haskell-th-lift-instances'
         'haskell-th-reify-many')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('94747e89c8471bd927810180af7572c5150ba7daabc4f45b41afda3210a42889b1fda05fd7fcecee2b1c2a92ea2ad195f924b44d22f0d269de2c94ade57fd335')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
