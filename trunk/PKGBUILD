# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgbase=python-jsonpointer
pkgname=(python2-jsonpointer python-jsonpointer)
pkgver=2.0
pkgrel=2
pkgdesc="Identify specific nodes in a JSON document (RFC 6901)"
arch=('any')
url="https://github.com/stefankoegl/python-json-pointer"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.io/packages/source/j/jsonpointer/jsonpointer-$pkgver.tar.gz")
sha512sums=('e8e2b03c12c3fc5f65816fc71d1c07135ef74a0c912286f5b8bef2d9e21b8ae6e766d1aa2104b3eeb0e53928034321f25b203386ac3f5641b54b0641f6a08137')

prepare(){
    cp -a jsonpointer-$pkgver jsonpointer-$pkgver-2
    find jsonpointer-$pkgver-2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
    cd "$srcdir"/jsonpointer-$pkgver
    python setup.py build

    cd "$srcdir"/jsonpointer-$pkgver-2
    python2 setup.py build
}

check() {
    cd "$srcdir"/jsonpointer-$pkgver
    python tests.py

    cd "$srcdir"/jsonpointer-$pkgver-2
    python2 tests.py
}

package_python-jsonpointer() {
    depends=('python')

    cd jsonpointer-$pkgver
    python setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
package_python2-jsonpointer() {
    depends=('python2')

    cd jsonpointer-$pkgver-2
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    mv "$pkgdir"/usr/bin/jsonpointer{,2}
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
