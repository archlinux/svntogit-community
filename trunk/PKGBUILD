# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=snes9x
pkgname=(
  snes9x
  snes9x-gtk
)
pkgver=1.61
pkgrel=3
pkgdesc='Portable Super Nintendo Entertainment System (TM) emulator'
arch=(x86_64)
url=http://www.snes9x.com/
license=(custom)
makedepends=(
  alsa-lib
  cairo
  clang
  gdk-pixbuf2
  git
  glib2
  glslang
  gtk3
  gtkmm3
  intltool
  libepoxy
  libpng
  libpulse
  libx11
  libxext
  libxml2
  libxrandr
  libxv
  meson
  minizip
  nasm
  portaudio
  sdl2
  zlib
)
_tag=7e97bb59a1a65a95ad47549f52d301cc09dd8db5
source=(
  git+https://github.com/snes9xgit/snes9x.git#tag=${_tag}
  git+https://github.com/KhronosGroup/SPIRV-Cross.git
  git+https://github.com/KhronosGroup/glslang.git
)
b2sums=('SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd snes9x
  for submodule in shaders/{SPIRV-Cross,glslang}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
  cd unix
  autoreconf -fiv
}

pkgver() {
  cd snes9x
  git describe --tags
}

build() {
  export CC=clang
  export CXX=clang++
  pushd snes9x/unix
  ./configure \
    --prefix='/usr' \
    --enable-netplay
  make
  popd
  arch-meson snes9x/gtk build
  ninja -C build
}

package_snes9x() {
  depends=(
    alsa-lib
    libpng
    libx11
    libxext
    libxinerama
    libxv
    minizip
    zlib
  )

  install -Dm 755 snes9x/unix/snes9x -t "${pkgdir}"/usr/bin/
  install -Dm 644 snes9x/docs/{control-inputs.txt,controls.txt,snapshots.txt} -t "${pkgdir}"/usr/share/doc/snes9x
  install -Dm 644 snes9x/LICENSE "${pkgdir}"/usr/share/licenses/snes9x/LICENSE
}

package_snes9x-gtk() {
  depends=(
    alsa-lib
    cairo
    gdk-pixbuf2
    glib2
    gtk3
    gtkmm3
    hicolor-icon-theme
    libepoxy
    libpng
    libpulse
    libx11
    libxext
    libxml2
    libxrandr
    libxv
    minizip
    portaudio
    sdl2
    zlib
  )

  DESTDIR="${pkgdir}" ninja install -C build
  install -Dm 644 snes9x/docs/{control-inputs.txt,controls.txt,snapshots.txt} -t "${pkgdir}"/usr/share/doc/snes9x-gtk
  install -Dm 644 snes9x/LICENSE "${pkgdir}"/usr/share/licenses/snes9x-gtk/LICENSE
}

# vim: ts=2 sw=2 et:
