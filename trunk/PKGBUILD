# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=python-sphobjinv
_name=${pkgname#python-}
pkgver=2.3.1
pkgrel=2
pkgdesc="Manipulate and inspect Sphinx objects.inv files"
arch=('any')
url="https://github.com/bskinn/sphobjinv"
license=('MIT')
depends=('python' 'python-attrs' 'python-certifi' 'python-jsonschema')
makedepends=('python-setuptools')
source=(https://github.com/bskinn/sphobjinv/archive/v$pkgver/$_name-$pkgver.tar.gz)
sha256sums=('dab4abb840c8d2a5b3532da4318243c70ab0d18f6a87596eae16c3ef178265b1')

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

package() {
  cd "$_name-$pkgver"
  python setup.py install --root="$pkgdir" -O1
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}

# vim:set ts=2 sw=2 et:
