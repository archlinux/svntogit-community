# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=autopep8
pkgver=1.5.6
pkgrel=2
epoch=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="https://github.com/hhatto/autopep8"
license=('MIT')
depends=('python-pycodestyle' 'python-toml')
source=("https://github.com/hhatto/autopep8/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ffd560bdd97bd228e2833727f8e1ca5f2e618726d8a0f90324c5235ae91af8c868e3d183fe25c9f8fde7c1104823e7bc7e1e913201aa992bd15f307d9f5a71f0')

build() {
  cd autopep8-$pkgver
  python setup.py build
}

check() {
  cd autopep8-$pkgver
  python setup.py test
}

package() {
  cd autopep8-$pkgver
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
