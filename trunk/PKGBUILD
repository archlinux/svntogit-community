# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Yann Lopez <arch at lonewulf dot net>

pkgname=babeld
pkgver=1.8.2
pkgrel=1
pkgdesc="A loop-avoiding distance-vector routing protocol"
arch=('x86_64')
url="https://www.irif.fr/~jch/software/babel/"
license=('MIT')
depends=('glibc')
backup=('etc/babeld.conf')
source=("https://www.irif.fr/~jch/software/files/$pkgname-$pkgver.tar.gz"{,.asc}
        'babeld.conf' 'babeld.service')
validpgpkeys=('F8E70AAD74D32CAC27B9AFD62652C881EADB5526')
sha512sums=('08ef0747a672e7a3f2e06d4082da0d021378cea94633fdddf7de38178552bc5a587852fdb5c8bfdeeb6897ba4fd61c78e6ee4d19523a6c3e60d0e3c150418319'
            'SKIP'
            '1232684447b4409d6e3813041f4ab1158faa844b5ac65441b5f299f1e353f48679aacbce9cc79e6714e9a3e1b17d2cfa4963c1388dba225ee342ea3cdb7f1194'
            '01e69d19a6176a6bf26ee703939c5bdea3f1245464ed5d3863dc5867a76b8b6a6fc7324912aee17458a0c6275ccdfa5abe63b88f7503c8d4dfd37d3f15f42700')

build() {
  cd "$pkgname-$pkgver"
  make PREFIX="/usr"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX="/usr" TARGET="$pkgdir" install

  install -Dm644 ../babeld.conf "$pkgdir"/etc/babeld.conf
  install -Dm644 ../babeld.service "$pkgdir"/usr/lib/systemd/system/babeld.service
  install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
