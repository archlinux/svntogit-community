# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Ryan Corder <ryanc@greengrey.org>

pkgname=pam-krb5
pkgver=4.5
pkgrel=2
pkgdesc='A PAM module providing Kerberos v5 support.'
arch=('i686' 'x86_64')
url='http://www.eyrie.org/~eagle/software/pam-krb5/'
license=('custom')
options=('!libtool')
depends=('krb5' 'pam')
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz")
md5sums=('5736795225cf7f84718af42a15570fe0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --enable-reduced-depends \
    --libdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
