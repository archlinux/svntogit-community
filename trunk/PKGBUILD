# Maintainer:
# Contributor: Judd Vinet <jvinet@zeroflux.org>

_pkgbasename=libldap
pkgname=lib32-$_pkgbasename
pkgver=2.4.55
pkgrel=1
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries (32-bit)"
arch=('x86_64')
license=('custom')
url="https://www.openldap.org/"
depends=('lib32-openssl' 'lib32-libxcrypt' $_pkgbasename)
makedepends=(gcc-multilib)
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
        'ntlm.patch')
sha512sums=('c58296cc9dfecf0830ec1e79feeac14e0c5a76a03f2a9a17ff39886d2cbf05e695dd8bdb748a5927a256476cb5340d6ecbdd8781f98a3a9ed6b1ce53ee90bd38'
            '46e1d119231176b043947669e2043dfcf550a23ad370d03ce0ebd0a46483f9d55d692a88020cf89840a1e6bf896a06a8f77a30877994473925b3cf4d74df0c4d')

prepare() {
  cd openldap-${pkgver}
  patch -Np1 -i ../ntlm.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd openldap-${pkgver}

  ./configure --prefix=/usr \
              --libexecdir=/usr/sbin \
              --sysconfdir=/etc \
	      --mandir=/usr/share/man \
              --localstatedir=/var/lib/openldap \
              --enable-crypt --enable-dynamic \
              --with-threads --disable-wrappers \
	      --disable-spasswd --without-cyrus-sasl \
	      --disable-bdb --disable-hdb --libdir=/usr/lib32

  cd include
  make

  cd ../libraries
  make depend
  make

}

package() {
  cd openldap-${pkgver}

  cd include
  make DESTDIR="${pkgdir}" install

  cd ../libraries
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/etc"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
