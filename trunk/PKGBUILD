# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-bsddb
pkgname=('python2-bsddb' 'python-bsddb')
pkgver=6.2.5
pkgrel=1
pkgdesc="Python interface for BerkeleyDB"
license=('MIT')
arch=('x86_64')
url="http://www.jcea.es/programacion/pybsddb.htm"
makedepends=('python2-setuptools' 'python-setuptools')
source=(https://pypi.python.org/packages/ba/a7/131dfd4e3a5002ef30e20bee679d5e6bcb2fcc6af21bd5079dc1707a132c/bsddb3-$pkgver.tar.gz
	LICENSE)
sha256sums=('784bf40ad935258507594a89b32ea11f362cde120751c8b96de163955ced7db8'
            '84cfc62eb4ecb52241dac8bd422e2b9d760ed56e55bcb3cc13c1ef26a9637aed')

prepare() {
  cp -r bsddb3-${pkgver}{,-python2}
  cd bsddb3-${pkgver}-python2
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    Lib{,3}/bsddb/{dbshelve.py,test/test_dbtables.py}
}

build () {
  # Build python 3 module
  cd bsddb3-${pkgver}
  python3 setup.py --berkeley-db=/usr build

  # Build python 2 module
  cd ../bsddb3-${pkgver}-python2
  python2 setup.py --berkeley-db=/usr build
}

package_python2-bsddb() {
  depends=('db' 'python2>=2.7')
  cd bsddb3-${pkgver}-python2

  python2 setup.py --berkeley-db=/usr install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-bsddb() {
  depends=('db' 'python>=3.3')
  cd bsddb3-${pkgver}

  python3 setup.py --berkeley-db=/usr install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
