# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=trace-cmd
pkgver=2.9.5
pkgrel=1
pkgdesc="Interact with Ftrace Linux kernel internal tracer"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/trace-cmd/trace-cmd.git"
license=('GPL2' 'LGPL2.1')
depends=('glibc')
makedepends=('asciidoc' 'audit' 'bash-completion' 'libtraceevent' 'libtracefs' 'python' 'swig' 'xmlto')
optdepends=('python: for Python plugins')
provides=('libtracecmd.so')
source=("https://git.kernel.org/pub/scm/utils/trace-cmd/trace-cmd.git/snapshot/trace-cmd-v${pkgver}.tar.gz"
        "${pkgname}-2.9-make_install.patch"
        "${pkgname}-2.9.3-python3.patch"
        "${pkgname}-2.9.3-docs_makefile.patch"
        "${pkgname}-2.9.5-full_relro.patch")
sha512sums=('504508a8cf09056fc60288f7d9c2263e884cbd560a44dfc79584c747bed7c1050da834608bda9c9e325a1f6eede6be8ed68d0cd5c72ac667254ece46924a1e91'
            'c9321b2b885d88557a057d7064690274799ab1d67a8babb78b1c54c4d81546fdea7313286cfd5fb9efbbcf87cf8394dcb17202ac88434a351aa652e7109e1db6'
            'a1834b382cbee920697d08c7eb7292c4a5ea606e4f32459a60e0b236ad057536c987f3e162c9c99affa46396248e98c8f4ecd21031141f0221dbadf0bd33737d'
            '1d43fc0fec3b386a741467966bcecdcacdfbe36e21c63d644ec281ff9577332e2d6cff95cef22bab85d2209017eb39d36def35f24b86e54720d6913a44fc3cbd'
            '56f2c7e0e3e277770670af58dee7fce3910342cd792a2d8c3da311bad5dfb75b72f1456b1124c2f0ee74a84601434ed9dd6cce98464acfebd17551490f0d3061')
b2sums=('9acc29871c6b0f41fc50cd763a05fa05512b47b285d3d8ff4aaec010ee185eecc03ce4962d0ed4fa6933df1d08890dcadec073fa1cae1ee43bcfc5d6173e6a8f'
        '8a644be295417b1fd5fb807285a7176ef0ceb737352bbb4c7a88441f9b955a90befe974d890a58f2b8fb6de8a68a1b3c5d207f057c4b08db79caf6e97fa66f50'
        'dac368fc62d99eba6d36f2c7d81475c28211dfe5d1d59ca5e468daff31d36710637943dc822fb266784feb5355ca0bfb525819bb96095c8e0c0fa91f32558877'
        '86104406eb8694a96cd6ed34f2dcdf06039d4342ebacbb492759945b7b156d41806986fc96e421557a7a224f18a5786ec999c5c07f2a9ffff051351911cc8095'
        '5d2a4b6f0f1b7f085dbcfcc53b96ef30e9e3d6b676e23c79776a73685ae813d5c92fae2e9b0fd1cf203947c1a06391f518ef462be780e07b5cce2c89c41b46d5')

prepare() {
  mv -v "${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "../${pkgname}-2.9-make_install.patch"
  patch -Np1 -i "../${pkgname}-2.9.3-docs_makefile.patch"
  patch -Np1 -i "../${pkgname}-2.9.3-python3.patch"
  patch -Np1 -i "../${pkgname}-2.9.5-full_relro.patch"
}

build() {
  cd "$pkgname-$pkgver"
  export PYTHON_VERS=python3
  make prefix='/usr' libdir_relative='lib' all libs
  make -C Documentation
}

package() {
  depends+=('libaudit.so' 'libtraceevent.so' 'libtracefs.so')

  cd "${pkgname}-${pkgver}"
  make prefix="/usr" \
       libdir_relative=lib \
       DESTDIR="${pkgdir}/" \
       BASH_COMPLETE_DIR="$(pkgconf --variable=completionsdir bash-completion)" \
       install_cmd install_libs
  make prefix="/usr" DESTDIR="${pkgdir}/" install -C Documentation
  install -vDm 644 tracecmd/${pkgname}.bash \
    "${pkgdir}/usr/share/bash/bash-completion/completions/${pkgname}"
}
