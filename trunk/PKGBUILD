# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.22.1
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=(any)
url="https://tmuxp.git-pull.com/en/latest/"
license=(MIT)
depends=(python-click python-colorama python-kaptan python-libtmux python-typing-extensions)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-rerunfailures)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('45ab89b0853d1f0e4e427e6d458904db8281f358e00a9df7b9a793f389e83985a41a0693eefdf37482dab2417a1d03ef7eae49729874f47d2fb4d4e25834b45b')
b2sums=('36acae29db1e3bd5081540fc4ec120956600c526c82b9f65201f33d8deb50e3beea27d8b04abffbef6fb580f8be8dc2269091eee695d96943ee91d8664c93646')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _deselected=(
    # https://github.com/tmux-python/tmuxp/issues/855
    --deselect tests/workspace/test_builder.py::test_window_shell
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv "${_deselected[@]}" tests
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 examples/*.{json,yaml} -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}

# vim:set ts=2 sw=2 et:
