# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc
pkgver=3.0
pkgrel=0
pkgdesc='Conversion between markup formats'
url='https://pandoc.org'
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-glob' 'haskell-juicypixels' 'haskell-sha' 'haskell-aeson'
         'haskell-aeson-pretty' 'haskell-attoparsec' 'haskell-base64' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-case-insensitive' 'haskell-citeproc' 'haskell-commonmark'
         'haskell-commonmark-extensions' 'haskell-commonmark-pandoc' 'haskell-connection'
         'haskell-data-default' 'haskell-doclayout' 'haskell-doctemplates' 'haskell-emojis'
         'haskell-file-embed' 'haskell-gridtables' 'haskell-haddock-library' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-ipynb' 'haskell-jira-wiki-markup'
         'haskell-mime-types' 'haskell-network' 'haskell-network-uri' 'haskell-pandoc-types'
         'haskell-pretty-show' 'haskell-random' 'haskell-safe' 'haskell-scientific'
         'haskell-skylighting' 'haskell-skylighting-core' 'haskell-split' 'haskell-syb'
         'haskell-tagsoup' 'haskell-temporary' 'haskell-texmath' 'haskell-text-conversions'
         'haskell-unicode-collation' 'haskell-unicode-transforms' 'haskell-xml'
         'haskell-xml-conduit' 'haskell-xml-types' 'haskell-yaml' 'haskell-zip-archive'
         'haskell-zlib')
optdepends=('pandoc-crossref: for numbering figures, equations, tables and cross-references to them with pandoc-crossref filter'
            'texlive-core: for pdf output')
conflicts=('haskell-pandoc')
replaces=('haskell-pandoc')
makedepends=('ghc' 'bubblewrap' 'haskell-diff' 'haskell-tasty' 'haskell-tasty-golden'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('be25f6fcf141da0c9b5a46f568387789139b728553ed710fdef986f62deebb97ed73aad9c4d59ca9e0b176ecf12f2a6bd881247a80b6f2c79fc01f5e40510097')

prepare() {
    cd $pkgname-$pkgver
    # TODO: find a better solution
    sed -i "s|let env' = dynlibEnv ++ |let env' = dynlibEnv ++ [(\"LD_LIBRARY_PATH\", \"$PWD/dist/build\")] ++ |" test/Tests/Command.hs
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-embed_data_files
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm "${pkgdir}/usr/share/doc/${pkgname}/COPYING.md"
    install -Dm644 man/pandoc.1 -t "${pkgdir}"/usr/share/man/man1/

    LD_LIBRARY_PATH="$PWD/dist/build" bwrap --ro-bind / / --tmpfs /usr/share --ro-bind "$pkgdir"/usr/share/pandoc /usr/share/pandoc -- \
        dist/build/pandoc/pandoc --bash-completion > pandoc-completion.bash
    install -Dm644 pandoc-completion.bash "$pkgdir"/usr/share/bash-completion/completions/pandoc
}
