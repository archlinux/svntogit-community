# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesis-fspaths
pkgver=0.1.0
pkgrel=9
pkgdesc="Hypothesis extension for generating filesystem paths"
url="https://github.com/lazka/hypothesis-fspaths"
license=('MIT')
arch=('any')
depends=('python-hypothesis')
makedepends=('python-setuptools' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lazka/hypothesis-fspaths/archive/v$pkgver.tar.gz")
sha512sums=('26c6b347e2f4123239f2f1e64346b9a5162b8a3ae993b498d9f5f7edf5dbc2007906b7fcb3423b5906423ed7e3fd2d7e67049e7e1319e9f6646d80c06d7941c4')

build() {
  cd hypothesis-fspaths-$pkgver
  python setup.py build
}

check() {
  cd hypothesis-fspaths-$pkgver
  python setup.py pytest
}

package() {
  cd hypothesis-fspaths-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
