# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contrinutor: shining <shiningxc@gmail.com>
# Contributor: phillid <dbphillipsnz _at_thingy_that_swirly_a_symbol gmaildott comm>

pkgbase=python-pydot
pkgname=(python-pydot python2-pydot)
pkgver=1.4.1
pkgrel=2
pkgdesc="Python interface to Graphviz's Dot"
arch=('any')
url="https://github.com/erocarrera/pydot"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing'
             'graphviz')
checkdepends=('python-chardet' 'python2-chardet')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/erocarrera/pydot/archive/v$pkgver.tar.gz")
sha512sums=('b5f16b59908586df61cd415cb1a3e242fb69c39674a82b171862beff1f3727f47cba39bdd806901cb56bc8a6888783a53d112b1b70044e124ea992789d77f938')

prepare() {
  cp -a pydot-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pydot-$pkgver
  python setup.py build

  cd "$srcdir"/pydot-$pkgver-py2
  python2 setup.py build
}

check() {
  # https://github.com/pydot/pydot/issues/204

  cd "$srcdir"/pydot-$pkgver/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python pydot_unittest.py || warning "Tests failed"

  cd "$srcdir"/pydot-$pkgver-py2/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python2 pydot_unittest.py || warning "Tests failed"
}

package_python-pydot() {
  depends=('python-pyparsing' 'graphviz')

  cd pydot-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pydot() {
  depends=('python2-pyparsing' 'graphviz')

  cd pydot-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
