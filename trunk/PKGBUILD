# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=monero-gui
pkgver=0.16.0.3
_commit=3c690231350c32c7a364ce6729759547ef939cbe
pkgrel=2
pkgdesc="QT GUI wallet for Monero: the secure, private, untraceable peer-to-peer currency"
license=('BSD')
arch=('x86_64')
url="https://getmonero.org/"
depends=('monero' 'boost-libs' 'libunwind' 'openssl' 'readline' 'zeromq' 'pcsclite' 'hidapi' 'unbound' 'hicolor-icon-theme'
    'qt5-base' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-svg' 'qt5-xmlpatterns')
makedepends=('git' 'cmake' 'boost' 'qt5-tools')
source=(
    "git+https://github.com/monero-project/monero-gui#commit=${_commit}"
    "git+https://github.com/monero-project/monero"
    "monero-gui.desktop"
)
sha512sums=('SKIP'
            'SKIP'
            '8aa10a47c3217e5bf12bb40a02828bb75cb38e4b5b124a8b05e487e7d3212c132911d5025193d4a7d984aebef94177dee42acf958c11a18b99fd13e872a771e8')

prepare() {
  cd "${pkgname}"
  git submodule init
  git config submodule.monero.url "$srcdir/monero"
  git submodule update
}

build() {
  cd "${pkgname}"
  ARCH=default QT_SELECT=5 ./build.sh
}

package() {
  cd "${pkgname}"
  install -Dm755 build/release/bin/monero-wallet-gui -t "${pkgdir}/usr/bin"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm644 ../monero-gui.desktop -t "${pkgdir}/usr/share/applications"
  for x in 16 24 32 48 64 96 128 256; do
    install -Dm644 "images/appicons/${x}x${x}.png" "${pkgdir}/usr/share/icons/hicolor/${x}x${x}/apps/monero-gui.png"
  done
}

# vim: ts=2 sw=2 et:
