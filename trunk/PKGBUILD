# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=1.21
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache)
depends=(android-tools ffmpeg sdl2)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('c7228ada86c41f2ba460759ad0701a08a11a71992b517366afa5130a3c69fc01d5aabf6710e289e47a7042bd11bfb58482305214ee937191daa43ffb6c12788a'
        '92d90606a04adb67ac956feffb4ff2e658d294304d0bade42d08660bac312cb447a713f052d353ba9fa454a55fac5334a08fe7a847f758c27bf50589ab0926b5')

build() {
  mkdir -p build
  arch-meson build $pkgname-$pkgver \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server="../$pkgname-server-v$pkgver"
  ninja -v -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-$pkgver/LICENSE \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}
