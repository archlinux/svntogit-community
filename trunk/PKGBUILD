# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Roman Mikhayloff <rimf@inbox.ru>

pkgname=codeblocks
pkgver=17.12
pkgrel=10
pkgdesc='Cross-platform C/C++ IDE'
arch=('x86_64')
url='http://codeblocks.org/'
license=('GPL3')
depends=('boost-libs' 'wxgtk3' 'hunspell')
makedepends=('boost' 'imagemagick' 'zip')
source=("https://downloads.sourceforge.net/codeblocks/${pkgname}_$pkgver.tar.xz"
        "fix-crash.patch")
sha256sums=('13881a0a72769694e82e531b8e7814d51fbf1fa122c73c5004e186560fbc57e0'
            '5c01a81734a075c8809ca6d3c0171e53133ccb1a89d60a6a551f15a53e4df3fa')

prepare() {
  cd $pkgname-$pkgver
  convert src/mime/codeblocks.png +set date:create +set date:modify -background none -extent 64x64 src/mime/codeblocks.png
  sed -i 's|$(datadir)/pixmaps|$(datadir)/icons/hicolor/64x64/apps|' src/mime/Makefile.{am,in}
  sed -i 's|$(datarootdir)/appdata|$(datarootdir)/metainfo|' Makefile.{am,in} src/plugins/contrib/appdata/Makefile.{am,in}

  # wx3-gtk3: Fix crash when doing Control-A, Control-V (FS#62148)
  # https://sourceforge.net/p/codeblocks/code/11320/
  patch -Np2 -i ../fix-crash.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-contrib-plugins=all,-FileManager --with-wx-config=/usr/bin/wx-config-gtk3
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
