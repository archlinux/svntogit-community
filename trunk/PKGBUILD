# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent B. <vb@luminar.eu.org>

pkgname=opam
pkgver=2.0.1
pkgrel=1
pkgdesc='OCaml package manager'
arch=('x86_64')
url='https://opam.ocaml.org/'
license=('GPL')
depends=('bubblewrap' 'ocaml-compiler-libs' 'unzip')
optdepends=('darcs: For downloading packages with darcs'
            'git: For downloading packages with git'
            'mercurial: For downloading packages with mercurial'
            'rsync: For downloading packages with rsync')
source=("https://github.com/ocaml/opam/releases/download/$pkgver/opam-full-$pkgver.tar.gz")
sha256sums=('81f7f1b661a0c1e04642fe02d0bea5524b32aa2cbed0ecf9b18d7145324ed97c')

build() {
  cd "opam-full-$pkgver"
  ./configure --prefix=/usr
  make \
    OCAMLC='ocamlc -unsafe-string' \
    OCAMLOPT='ocamlopt -unsafe-string' \
    lib-ext all -j1
}

package() {
  make -C "opam-full-$pkgver" DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/doc"
  mv "$pkgdir/usr/doc/opam-installer" "$pkgdir/usr/share/doc/opam"
}

# getver: opam.ocaml.org
# vim: ts=2 sw=2 et:
