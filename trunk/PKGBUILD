# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-suds
pkgver=1.4.2.0
pkgrel=2
pkgdesc="A lightweight SOAP python client"
arch=('any')
url="https://github.com/cackharot/suds-py3"
license=('LGPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/cackharot/suds-py3/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b4aa5235b7615c4ef6bd763be8b72f60c86d0c22ba6ea86cb927e4f4425f6c40c55533140e5e9dd47e3684986c574746fdde8187e3f70b35a6cacbb1bfbc3b49')

build() {
  cd suds-py3-$pkgver
  python setup.py build
}

check() {
  cd suds-py3-$pkgver
  ./run_tests.sh
}

package() {
  cd suds-py3-$pkgver
  python setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
