# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Mitch Bigelow <ipha00@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>

pkgname=pianobar
pkgver=2016.06.02
pkgrel=2
pkgdesc="Console-based frontend for Pandora"
arch=('i686' 'x86_64')
url="https://6xq.net/pianobar/"
license=('MIT')
depends=('libao' 'ffmpeg' 'curl')
source=(http://6xq.net/projects/pianobar/$pkgname-$pkgver.tar.bz2)
sha256sums=('c6efb0d0fe36013c6506dadf3718e8ed9471b7839654c97027e036ed16212559')

build() {
  make -C ${pkgname}-${pkgver}
}

package() {
  make -C ${pkgname}-${pkgver} DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 $pkgname-$pkgver/contrib/headless_pianobar "$pkgdir/usr/bin/pianoctl"
}
