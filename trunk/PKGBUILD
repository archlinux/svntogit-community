# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=0.13
pkgrel=2
pkgdesc="Qt5 Configuration Utility"
arch=(i686 x86_64)
url="http://opendesktop.org/content/show.php?content=168066"
license=(BSD)
depends=(qt5-base)
makedepends=(qt5-tools)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2" 'COPYING.BSD')
install=$pkgname.install
sha256sums=('752338080244c64d7354b30c83cb7a680a7e22a817fe2610acf0e4b018882bd1'
            'b1742f842de71738501a471e4ce1cac38a02d3240a91848fbd32429bc20aa69b')

build() {
    cd $pkgname-$pkgver
    qmake-qt5 $pkgname.pro 
    make
}

package() {
    cd $pkgname-$pkgver
    make INSTALL_ROOT="$pkgdir" install

    install -Dm644 "$srcdir"/COPYING.BSD "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

