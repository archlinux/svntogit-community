# Maintainer: Anatol Pomozov
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=2.0.0
_tag=cpp-$pkgver
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=(i686 x86_64)
url='http://msgpack.org/'
license=(Apache)
depends=(glibc)
checkdepends=(gtest)
makedepends=(cmake)
source=($pkgname-$pkgver.zip::https://github.com/msgpack/msgpack-c/archive/$_tag.zip)
sha1sums=('6da4b438ce24397553737be7ace4c994d5ec1ee5')

build() {
  cd msgpack-c-$_tag 
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

check() {
  cd msgpack-c-$_tag
  make test 
}

package() {
  cd msgpack-c-$_tag
  make DESTDIR="$pkgdir" install
}
