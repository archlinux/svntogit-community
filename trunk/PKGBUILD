# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-client-tls
pkgname=haskell-http-client-tls
pkgver=0.3.5.1
pkgrel=26
pkgdesc="http-client backend using the connection package and tls library"
url="https://github.com/snoyberg/http-client"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-case-insensitive" "haskell-connection" "haskell-cryptonite"
         "haskell-data-default-class" "haskell-exceptions" "haskell-http-client"
         "haskell-http-types" "haskell-memory" "haskell-network" "haskell-network-uri"
         "haskell-text" "haskell-tls")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('76e0a6081edb1658c62e3ba9ff0b0cbcddd657982755ac2de6676a3c688fad4255da9fa0ee80c2101d0e46320a2696d75cb845a17112d3795c0f4c8708611091')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
