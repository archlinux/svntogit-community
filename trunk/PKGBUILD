# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: karolina.lindqvist@kramnet.se

pkgname=('algol68g' 'algol68g-doc')
pkgver=2.8.4
pkgrel=3
pkgdesc='Algol 68 compiler and interpreter'
arch=('x86_64' 'i686')
url='https://jmvdveer.home.xs4all.nl/algol.html'
license=('GPL')
depends=('gsl' 'plotutils')
optdepends=('postgresql-libs: for postgresql support')
source=("https://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
        'plotutils.patch'
        'ftp://ftp.stu.edu.tw/FreeBSD/distfiles/a68g-doc.pdf')
sha256sums=('5823ccd0c18fe10a368a117cc3924748c4a5d0fe8dff0d9d818ff73c342565f0'
            '50afcc7812d117e5f0a2f5240c9cd58f8bfcc04b3da0634b9aa0ab06183ed0aa'
            '064b6761207d937aa704d245fc871a072f5a33ea23a346cb136ec8b2078ea321')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../plotutils.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" \
    docdir="/usr/share/doc/$pkgname" install
}

package_algol68g-doc() {
  install -Dm644 a68g-doc.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}

# vim:set ts=2 sw=2 et:
