# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Aaron Bull Schaefer <aaron@elasticdog.com>
# Contributor: Felix Hanley <felix@seconddrawer.com.au>

pkgname=spawn-fcgi
pkgver=1.6.4
pkgrel=2
pkgdesc='A FastCGI process manager for web applications'
arch=('i686' 'x86_64')
url='http://redmine.lighttpd.net/projects/spawn-fcgi/'
license=('BSD')
depends=('glibc')
source=("https://www.lighttpd.net/download/${pkgname}-${pkgver}.tar.bz2"{,.asc})
md5sums=('bf269849995ab8b3a8989779fa5818aa'
         'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
