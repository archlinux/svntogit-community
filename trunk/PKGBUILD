# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pynitrokey
pkgname=python-pynitrokey
pkgver=0.4.16
pkgrel=3
pkgdesc="A command line interface for the Nitrokey FIDO2 and Nitrokey Start"
arch=(any)
url="https://github.com/Nitrokey/pynitrokey"
license=(Apache)
depends=(python-cbor python-cffi python-click python-cryptography
python-dateutil python-ecdsa python-fido2 python-intelhex python-nkdfu
python-pyusb python-requests python-tqdm python-urllib3)
makedepends=(git python-build python-flit python-installer)
source=(
  "git+https://github.com/nitrokey/pynitrokey#tag=v${pkgver}.nitrokey?signed"
  $pkgname-0.4.16-remove_spsdk.patch
)
sha512sums=('SKIP'
            '4f5abd730bf6c23f69e8d2252b97e729273a0d48fe85708f9ea623afc10ef0d9ac07b25d17479edc3e7da70af0fe3956b4ecf93f3af69392f57974c6b3f76d94')
b2sums=('SKIP'
        '133d8770ec99db1ead4e9d175ec84ad229dd8e94c0772f2406e63d6cabf372446ba76814f7f71fc10ba3128ae0be6b5a68e859bf768be44c70ed8b87d12014b9')
validpgpkeys=(868184069239FF65DE0BCD7DD9BAE35991DE5B22)  # Szczepan Zalega (Nitrokey) <szczepan@nitrokey.com>

prepare() {
  cd $_name
  patch -Np1 -i ../$pkgname-0.4.16-remove_spsdk.patch
}
build() {
  cd $_name
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
