# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape
pkgver=2.7.0
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=(x86_64)
url='https://duktape.org/'
license=(MIT)
makedepends=(setconf)
source=("https://duktape.org/duktape-$pkgver.tar.xz"
        duktape.pc)
b2sums=('da5eea88758201177f32c5611a3757433ef1b0173d6430eff1f067abd7535f3e4b9c3717290fbd8475827a53f7a2a74818287a65e563f14e91761f5cd0fe30d5'
        '0a2a709ac0630aa47083cd3a54f57bc54ab9cda609d249559140c4cab323e740e3da1aa0bb3c125e16f8d91108912512caccb0ed9234a5583a196082bc7e36dd')

prepare() {
  cd $pkgname-$pkgver
  mv Makefile.sharedlibrary Makefile
  sed 's/-Wall -Wextra/$(CFLAGS)/g' -i Makefile
  setconf Makefile INSTALL_PREFIX "$pkgdir/usr"
  setconf "$srcdir/duktape.pc" Version "$pkgver"
}

build() {
  CFLAGS="$CFLAGS -D DUK_USE_FASTINT -w" make -C $pkgname-$pkgver
}

package() {
  install -d "$pkgdir/usr/include"
  install -Dm644 duktape.pc "$pkgdir/usr/lib/pkgconfig/duktape.pc"
  cd $pkgname-$pkgver
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  make install
}

# getver: github.com/svaarala/duktape
