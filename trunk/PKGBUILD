# $Id$
# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
# Contributor: Daenyth <Daenyth+Arch at gmail dot com>
# Contributor: Chris Winter <twidds at gmail dot com>

pkgname=fdupes
pkgver=1.51
pkgrel=1
pkgdesc="a program for identifying or deleting duplicate files residing within specified directories"
arch=('i686' 'x86_64')
url="http://code.google.com/p/fdupes/"
license=('MIT')
depends=(glibc)
source=(http://fdupes.googlecode.com/files/$pkgname-$pkgver.tar.gz LICENSE)
sha256sums=('87dbc85b7b9cdb9626e713dd8078bd7487bceb58d47ceaff5404a9e6fd062881'
            '6300e5a838d8a0f08b412742cb755c22708970aef420dbad5dc65ca7de677e39')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make  PREFIX=/usr
}

package(){
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir"/usr/{share/man/man1,bin}
  make \
    PREFIX="$pkgdir/usr" \
    DESTDIR="$pkgdir/usr/bin" \
    INSTALLDIR="$pkgdir"/usr/bin \
    MAN_DIR="$pkgdir"/usr/share/man/man1 \
    install
  install -D -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: set ts=2 sw=2 et:
