# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-imageviewer
_pkgname=ImageViewer
pkgver=63
pkgrel=2
pkgdesc="Image viewer for Sugar"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Image_Viewer"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2
        fix-choose-image-button.patch)
sha256sums=('336fa7c214711d2f65eb4e9bdd0b575004a01d5fc67f35a9a5d5d55ef823dc40'
            '22bee7f1db29a058fc267e19198b458270d22d435696ec491a5b5448bc610253')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
  patch -Np1 -i ../fix-choose-image-button.patch
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
