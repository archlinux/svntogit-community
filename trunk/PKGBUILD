# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=lua-luautf8
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
pkgver=0.1.3
_rockrel=1
pkgrel=3
pkgdesc='Add UTF-8 support to Lua, compatible with Lua string module'
arch=(x86_64)
url="https://github.com/starwing/$_rockname"
license=(MIT)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$_rockname-$pkgver"
source=("$_archive.tgz::$url/archive/$pkgver.tar.gz")
sha256sums=('208b3423a03a6c2822a2fa6b7cc8092ed7d3c0d792ec12c7cd28d6afaa442e0b')

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}")
	provides=("${pkgname/-lua/-}=$pkgver")
	replaces=("${pkgname/-lua/-}")
	luarocks --lua-version="$1" --tree="$pkgdir/usr/" \
		CFLAGS="$CPPFLAGS $CFLAGS -fPIC" \
		LIBFLAG="$LDFLAGS -shared" \
		make --deps-mode=none --no-manifest "rockspecs/$_rockname-$pkgver-$_rockrel.rockspec"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua-luautf8() {
	_package 5.4
}

package_lua51-luautf8() {
	_package 5.1
}

package_lua52-luautf8() {
	_package 5.2
}

package_lua53-luautf8() {
	_package 5.3
}
