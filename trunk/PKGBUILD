# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=sqlite3
pkgname=ruby-${_gemname}
pkgver=1.4.2
pkgrel=2
pkgdesc='Ruby bindings for the SQLite3 embedded database'
url='https://github.com/luislavena/sqlite3-ruby'
arch=('x86_64')
license=('BSD')
depends=('ruby' 'sqlite')
options=('!emptydirs')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rake-compiler' 'ruby-hoe' 'ruby-mini_portile' 'ruby-minitest')
source=(https://github.com/sparklemotion/sqlite3-ruby/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha256sums=('7e876104c0feb6e0e69befc3f6055fb7f20f37b13a667c8f9782d22272f37479')
sha512sums=('c30938f0ad835e23f9e857323ea654d111d1ef5d6f055471ca5f8f080bcdcbb56ac773be9aab34b6432cdb53d69a842c99dcebbd3ea9808967f04907863abc89')

prepare() {
  cd ${_gemname}-ruby-${pkgver}
  sed 's|~>|>=|g' -i Gemfile
}

build() {
  cd ${_gemname}-ruby-${pkgver}
  rake compile
  rake gem
}

check() {
  cd ${_gemname}-ruby-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-ruby-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem

  install -Dm 644 README.rdoc CHANGELOG.rdoc API_CHANGES.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/${_gemname}-${pkgver}/ext
  rm extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
