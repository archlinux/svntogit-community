# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.92
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache' 'MIT')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('9d7dd61ed2e369699e31af341209a0ede372e5b39a91de2ad46c7c249cab7eed'
            '9a51c9403da397f9d8eda069ca3fa7d1b588a4715dbce024b3c186e6c069b381')
b2sums=('cdf4233b2ecc0b53ddb49c7d2d091f82c80f0fe83ec931beb7c23ac2e3535bb1a9046e25f1922b4a434fd1df5e97b7f6c66f31e1bd0477472d6358d204cfdcfc'
        '73dfce362e5bccdee9ed9629a7fd80ad2e663d7a9f849902f4354e6337434b856132beb086fda055147ee66c890320be64c7ab7bee43b9605d803137b58aa213')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
