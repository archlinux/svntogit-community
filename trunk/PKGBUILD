# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=fortune-mod
pkgver=3.12.0
pkgrel=1
pkgdesc="The Fortune Cookie Program from BSD games"
arch=('x86_64')
url="https://www.shlomifish.org/open-source/projects/fortune-mod/"
license=('BSD')
depends=('glibc' 'recode')
makedepends=('cmake' 'rinutils')
source=("https://github.com/shlomif/fortune-mod/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz"
        'not-a-game.patch')
sha512sums=('3a25d276219bb4f8a3015cd24b43462844d3be481815b587a365acd31dde31afee110dc5a658dd4df43bc6ba7d19e85afeffb215bd8c8f34640d658edb335089'
            'f12bfed15648bf7e53143bfa5c626e0e614d64c64c4329119e7b1ad8a5d79c89d3e8f2c1336d489abecbf6a8ec0ad9acf3080b142d498f8b3f36cea77c82a23d')
b2sums=('3a3b0c28a0f7c297294b1788c57e01eb78d5083578aba877b896f41b854d2c34f65c9b46a1efe8bfef669f7ced6bb5e8ba667e3df1391a3cd3e5fed22e9058b6'
        '3af374f348d42bf649199a79afc18a8f0dace50d2b4c8aa090a1a54ccffcb65bf069d26f0d1d417a395c5a3574f7376c5ae1ec8b97dd2385d6b4b944643b1215')


prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../not-a-game.patch
}

build() {
  cmake \
    -B build \
    -S "$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_OFFENSIVE=OFF \
    -DLOCALDIR=/usr/share/fortune \
    -DLOCALODIR=/usr/share/fortune/off \
    -DCOOKIEDIR=/usr/share/fortune \
    -DOCOOKIEDIR=/usr/share/fortune/off

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  rm -f "$pkgdir/usr/share/fortune/"{,off/}*.u8

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/COPYING.txt"
}
