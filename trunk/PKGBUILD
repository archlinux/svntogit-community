# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-debtcollector
pkgname=('python-debtcollector' 'python2-debtcollector')
pkgver=1.19.0
pkgrel=1
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='http://docs.openstack.org/developer/debtcollector'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-wrapt' 'python2-wrapt' 'python2-funcsigs')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/debtcollector/archive/$pkgver.tar.gz")
sha512sums=('90e11584c22bc0947599fc5b7ecd44eae9cb82f6a26b32aa6a56a1698dde47ad1a00fe90d10dc46ddb816c19445a4563c22d2360ae1688c76ffb5695f6048b59')

prepare() {
  cp -a debtcollector-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py build

  cd "$srcdir"/debtcollector-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py testr

  cd "$srcdir"/debtcollector-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-debtcollector() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')

  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-debtcollector() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-wrapt' 'python2-funcsigs')

  cd debtcollector-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
