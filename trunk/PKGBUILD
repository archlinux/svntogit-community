# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=libad9361
_pkgname=$pkgname-iio
pkgver=0.2
pkgrel=1
pkgdesc='IIO AD9361 library for filter design and handling, multi-chip sync, etc.'
arch=('x86_64')
url='https://github.com/analogdevicesinc/libad9361-iio'
license=('LGPL2.1')
depends=('libiio')
makedepends=('cmake' 'doxygen' 'graphviz')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f95fa955531198ab33c16d41685d23140e875b2eafa1da10cdf60a7bf1ae760e86e2d3d28c9ab36a3a7b7a90e36bb53456dec1d6e537a1574a5505b7595e764c')

build() {
  mkdir -p $_pkgname-$pkgver/build
  cd $_pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_PACKAGING=OFF

  make
}

package() {
  cd $_pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}

