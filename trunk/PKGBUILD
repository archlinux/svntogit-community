# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cliff
pkgver=3.1.0
pkgrel=1
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="https://docs.openstack.org/cliff/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
         'python-stevedore' 'python-yaml')
makedepends=('git')
checkdepends=('python-testrepository' 'python-mock' 'python-sphinx' 'python-testscenarios')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz"
        new-cmd2.patch)
sha512sums=('e65af5ec08df4d7586a1d6821acd283e568227bffee2e5ed8bb40d9f6d4fb17d83fcafc0f1a8ef4293ff7cd2f9e204db35198e58ff7e35d566ffcc03eed5740b'
            '9998d90b3b07f624340feca9ab38cd13db789006ff5d658ceeb9b7fe8c74180ddce4e91c57c8696cff997aed390ab17df53a4840e1f02a629c3e824c5ba25c3d')

export PBR_VERSION=$pkgver

prepare() {
  cd cliff-$pkgver
  patch -p1 -i ../new-cmd2.patch
}

build() {
  cd cliff-$pkgver
  python setup.py build
}

check() {
  cd cliff-$pkgver
  python setup.py testr
}

package() {
  cd cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
