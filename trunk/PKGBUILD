# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-django
pkgname=('python-pytest-django' 'python2-pytest-django')
pkgver=2.8.0
pkgrel=2
pkgdesc="A Django plugin for py.test"
arch=('any')
license=('BSD')
url="https://github.com/pytest-dev/pytest-django"
makedepends=('python-pytest' 'python2-pytest' 'git')
checkdepends=('python-django' 'python2-django' 'python-pytest-xdist' 'python2-pytest-xdist'
              'twine' 'python2-twine' 'python-south' 'python2-south')
source=("git+https://github.com/pytest-dev/pytest-django.git#tag=v$pkgver"
        remove-assertion-in-django_settings_is_configured.patch)
sha512sums=('SKIP'
            '3f94b06e83c31a949b0bee5ec9220d137ee4c594e4428f6b22f9e73bb2dbdeb6c2e92561343b940acfda4408b5dc756d3d4c5828818f3554bca644b9bdca9781')

prepare() {
  # https://github.com/pytest-dev/pytest-django/issues/146
  (cd pytest-django; patch -p1 -i ../remove-assertion-in-django_settings_is_configured.patch)

  cp -a pytest-django{,-py2}
}

build() {
  cd "$srcdir/pytest-django"
  python setup.py build

  cd "$srcdir/pytest-django-py2"
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir/pytest-django"
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.4/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test tests || warning "Tests failed"

  cd "$srcdir/pytest-django-py2"
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test2 tests
}

package_python-pytest-django() {
  depends=('python-pytest')

  cd pytest-django
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest-django() {
  depends=('python2-pytest')

  cd pytest-django-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
