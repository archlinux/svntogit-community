# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Colin Pitrat <colin.pitrat@gmail.com>

pkgname=mediaproxy
pkgver=2.5.2
pkgrel=4
pkgdesc="Open-source media proxy for OpenSER"
url="http://mediaproxy.ag-projects.com/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('python2' 'libnetfilter_conntrack' 'iptables'
	 'python2-application' 'python2-cjson' 'python2-gnutls')
#backup=('opt/mediaproxy/config.ini')
source=("http://download.ag-projects.com/MediaProxy/mediaproxy-$pkgver.tar.gz"
	"mediaproxy.init"
	"mediaproxy-dispatcher.service"
	"mediaproxy-relay.service"
	"mediaproxy.tmpfiles")
md5sums=('840de8f52e656991be728c15ec30bb5e'
         '66f4417f40dda0dfa31569ee4ccc32c7'
         '4a1fa0b6ce0a8bd44bf0f1d9d36a6154'
         '23c1a584835de00a958ef963cdd40040'
         '60a25f2cb6429a0a7d62d45d0c75a0ea')

build() {
  cd $srcdir/mediaproxy-$pkgver
  sed -i "s|libraries = get_link_libraries(),|libraries = ['netfilter_conntrack', 'ip4tc'],|g" setup.py
}

package() {
  cd $srcdir/mediaproxy-$pkgver

  python2 setup.py install --root=$pkgdir

  install -D -m0755 $srcdir/mediaproxy.init $pkgdir/etc/rc.d/mediaproxy
  install -Dm0644 $srcdir/$pkgname-dispatcher.service $pkgdir/usr/lib/systemd/system/$pkgname-dispatcher.service
  install -Dm0644 $srcdir/$pkgname-relay.service $pkgdir/usr/lib/systemd/system/$pkgname-relay.service
  install -Dm0644 $srcdir/$pkgname.tmpfiles $pkgdir/usr/lib/tmpfiles.d/$pkgname.conf
}
