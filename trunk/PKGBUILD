# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgbase=open-vm-tools
pkgname=('open-vm-tools' 'open-vm-tools-dkms')
epoch=5
pkgver=2013.09.16
_pkgsubver=1328054
pkgrel=2
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('LGPL')
depends=('libdnet' 'icu' 'procps-ng' 'uriparser' 'libsigc++' 'libxss' 'iproute2')
makedepends=('chrpath' 'doxygen' 'gtkmm' 'fuse' 'libxtst')
optdepends=('gtkmm' 'libnotify' 'libxtst' 'fuse' 'libsm'
	    'open-vm-tools-modules: for linux kernel'
	    'open-vm-tools-dkms: for linux-lts and other kernels'
	    'netctl: suspend-resume network state'
	    'networkmanager: suspend-resume network state')
backup=('etc/pam.d/vmware-guestd')
options=('docs')
install=$pkgname.install
#source=("http://downloads.sourceforge.net/project/open-vm-tools/open-vm-tools/Development%20Snapshots/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz"
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-${_pkgsubver}.tar.gz
	network-script
	tools.conf
	vmware-guestd
	xautostart.conf
	modprobe.conf
	vmtoolsd.service)
md5sums=('66c63d2d447b3a91303f996372ebda71'
         'e307f5201b2b3fe79c9e2b410d02b989'
         'b55d15e2c4be396aad709aeca91033d3'
         '1b9ae908fce4c623269f100ee9bdfdef'
         '75a25d83417e683957321f97a00f8465'
         'bc5518489077e91655489bd04b868584'
         'a6c53243d31c765580f6dded7d5fa98f')

build() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"
  export CFLAGS="-DGLIB_DISABLE_DEPRECATION_WARNINGS ${CFLAGS}"
  export CUSTOM_PROCPS_NAME="procps"
  sed -i 's|-Werror||g' configure{,.ac}
  ./configure --prefix=/usr --without-kernel-modules --sbindir=/usr/bin
  make
}

package_open-vm-tools() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"

  make install DESTDIR="$pkgdir"
  install -Dm0644 vmware-user-suid-wrapper/vmware-user.desktop $pkgdir/usr/share/applications/vmware-user.desktop

  # vmware-user XDG autostart
  mkdir -p $pkgdir/etc/xdg/autostart
  ln -s /usr/share/applications/vmware-user.desktop $pkgdir/etc/xdg/autostart/vmware-user.desktop

  install -D -m 755 scripts/common/vmware-xdg-detect-de "$pkgdir"/usr/bin/vmware-xdg-detect-de
  chmod 07755 "$pkgdir"/usr/bin/vmware-user-suid-wrapper

  install -Dm0755 "$srcdir"/network-script $pkgdir/etc/vmware-tools/scripts/vmware/network
  install -Dm0644 "$srcdir"/tools.conf "$pkgdir"/etc/vmware-tools/tools.conf
  install -Dm0644 "$srcdir"/xautostart.conf "$pkgdir"/etc/vmware-tools/xautostart.conf
  install -Dm0644 "$srcdir"/vmware-guestd "$pkgdir"/etc/pam.d/vmware-guestd
  rm -rf "$pkgdir"/usr/etc

  # We don't want a symlink in /sbin
  rm "$pkgdir"/sbin/mount.vmhgfs
  rmdir "$pkgdir"/sbin

  cd "$pkgdir" && find -type f -exec sh -c "file {} | grep ELF >/dev/null && echo {} && chrpath -d {}" \;
  install -Dm644 ${srcdir}/vmtoolsd.service ${pkgdir}/usr/lib/systemd/system/vmtoolsd.service

  rm -f $pkgdir/etc/vmware-tools/scripts/vmware/*.orig
}

package_open-vm-tools-dkms() {
  pkgdesc="kernel modules for the open source implementation of VMware Tools"
  depends=('dkms')
  provides=('open-vm-tools-modules')
  conflicts=('open-vm-tools-modules')
  optdepends=()
  backup=()
  install=open-vm-tools-dkms.install

  install -d -m755 ${pkgdir}/usr/src/
  sh ${srcdir}/open-vm-tools-${pkgver}-${_pkgsubver}/modules/linux/dkms.sh \
	${srcdir}/open-vm-tools-${pkgver}-${_pkgsubver}/ \
	${pkgdir}/usr/src/
  install -Dm644 ${srcdir}/modprobe.conf ${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf
}
