# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=waylock
pkgver=0.5.0
_commit=0bb019937c9a5d21cb205526493559deedfdeace  # refs/tags/v0.5.0^{}
pkgrel=2
pkgdesc="A simple screenlocker for wayland compositors"
arch=(x86_64)
url="https://github.com/ifreund/waylock"
license=(MIT)
depends=(gcc-libs glibc)
makedepends=(chrpath git libxkbcommon pam scdoc wayland wayland-protocols zig)
source=(
  git+https://github.com/ifreund/waylock#commit=$_commit?signed
  git+https://github.com/ifreund/zig-wayland
  git+https://github.com/ifreund/zig-xkbcommon
  zig_0.10.0.patch
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'b123ddf515e2fccff1c4fa20193e2a946a3bda8807f15a1445fbbbc04255b142')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <ifreund@ifreund.xyz>

prepare() {
  cd $pkgname
  git submodule init
  git config submodule."deps/zig-wayland".url "${srcdir}/zig-wayland"
  git config submodule."deps/zig-xkbcommon".url "${srcdir}/zig-xkbcommon"
  git -c protocol.file.allow=always submodule update

  # zig 0.10.0 compatibility
  patch -Np1 < ../zig_0.10.0.patch
  cd deps/zig-wayland
  git checkout 71d21959b4671a848f1d198f6bb919f54d541f41
  cd ../zig-xkbcommon
  git checkout bfd1f97c277c32fddb77dee45979d2f472595d19
}

build() {
  cd $pkgname
  DESTDIR="build" zig build -Dpie --prefix /usr install
  chrpath --delete build/usr/bin/waylock
}

package() {
  depends+=(libpam.so libwayland-client.so libxkbcommon.so)

  cd $pkgname
  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
