# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-rbtree
_pkgname="${pkgname#ruby-}"
pkgver=0.4.5
pkgrel=2
pkgdesc='A sorted associative collection that is implemented with a Red-Black Tree'
arch=('x86_64')
url='https://rubygems.org/gems/rbtree'
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=("$pkgname-$pkgver.gem::https://rubygems.org/downloads/$_pkgname-$pkgver.gem")
noextract=("$pkgname-$pkgver.gem")
b2sums=('a2e4763f99a3cecb4b7525ed7637fcb5fe9efd45f61125164804fd6a883fad2ed688357af9264317898500bf8f9d31b5797cb93d88982c06b356a9de0b5b74e1')

package() {
  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname-$pkgver.gem"

  # delete unnecessary files/folders
  rm -rf "$pkgdir/$_gemdir/cache"
  find "$pkgdir" -type f -name '*.o' -delete
  rm -rf "$pkgdir/$_gemdir/gems/$_pkgname-$pkgver/$_pkgname.so"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
