# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-perl-critic
pkgver=1.146
pkgrel=1
pkgdesc="Critique Perl source code for best-practices"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Perl-Critic"
depends=('perl-b-keywords' 'perl-config-tiny' 'perl-exception-class' 'perl-file-homedir'
         'perl-file-which' 'perl-list-someutils' 'perl-module-build' 'perl-module-pluggable'
         'perl-ppi' 'perl-ppix-quotelike' 'perl-ppix-regexp' 'perl-ppix-utilities' 'perl-pod-spell'
         'perl-tidy' 'perl-readonly' 'perl-string-format')
makedepends=('perl-test-deep')
checkdepends=('perl-test-memory-cycle')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PE/PETDANCE/Perl-Critic-$pkgver.tar.gz")
sha512sums=('632b64a3f0ee546f37283ce5ef178d2cec92480fb77a5b5b9b1d7392a3be224fcf4e24084dffb81a40d20fdfeb41ed735f02d9c4785ac611144a948d88f18782')

build() {
  cd Perl-Critic-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Perl-Critic-$pkgver
  perl Build test
}

package() {
  cd Perl-Critic-$pkgver
  perl Build install destdir="$pkgdir"
}
