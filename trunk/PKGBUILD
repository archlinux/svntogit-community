# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=authenticate-oauth
pkgname=haskell-authenticate-oauth
pkgver=1.7
pkgrel=134
pkgdesc="Library to authenticate with OAuth for Haskell web applications."
url="https://github.com/yesodweb/authenticate"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-http-client' 'haskell-crypto-pubkey-types' 'haskell-rsa'
         'haskell-data-default' 'haskell-base64-bytestring' 'haskell-sha' 'haskell-random'
         'haskell-http-types' 'haskell-blaze-builder' 'haskell-transformers-compat')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('c216d05051971715a834f2831278833fb685d91b7c0c8c1cf9732a3b39795a2dcf596bf7e5670df25ee3713c64496599043a7ec0092c538c759f2965f1230e57')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
