# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=2.4.3
pkgrel=1
pkgdesc='Captive Portal Assistant'
arch=(x86_64)
url=https://github.com/elementary/capnet-assist
license=(GPL3)
groups=(pantheon)
depends=(
  gcr
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  libsoup
  networkmanager
  webkit2gtk-4.1
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=47dd7038f812a00ed15dea3a43a9b7001e0d7242
source=(git+https://github.com/elementary/capnet-assist.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd capnet-assist
  git describe --tags
}

build() {
  arch-meson capnet-assist build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
