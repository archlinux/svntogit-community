# Generated by gem2arch (https://github.com/anatol/gem2arch)
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Christoph Scholz <christoph.scholz@gmail.com>

_gemname='sys-proctable'
pkgname="ruby-${_gemname}"
pkgver=1.2.6
pkgrel=2
pkgdesc='An interface for providing process table information'
arch=('any')
url='https://github.com/djberg96/sys-proctable'
license=('Apache')
depends=('ruby' 'ruby-ffi')
makedepends=('ruby-rake' 'ruby-rdoc' 'ruby-rake-compiler' 'ruby-yard' 'ruby-rspec' 'ruby-bundler')
checkdepends=('ruby-test-unit')
options=('!emptydirs')
source=("${url}/archive/${_gemname}-${pkgver}.tar.gz")
sha512sums=('3243f0dd21518799665fd36eaf6a4a749d9a407c6f8b269141c70518080a713055d6e2d6c127e78af458bbc7f34aff9b8274f10eed364513e1d5a3f64090c0b6')

prepare() {
  cd "${srcdir}/${_gemname}-${_gemname}-${pkgver}"

  # we won't sign our output gemfile (i.e. we won't upload to rubygems)
  sed -i '/spec.signing_key/d' Rakefile

  # we're running in a limited chroot and won't reach the default 10 procs for `top` output
  # just disable this test and move on
  sed -i '/described_class.top.size/d' spec/sys_top_spec.rb
}

build() {
  cd "${srcdir}/${_gemname}-${_gemname}-${pkgver}"
  rake gem:create
}

check() {
  cd "${srcdir}/${_gemname}-${_gemname}-${pkgver}"
  rake spec
}

package() {
  cd "${srcdir}/${_gemname}-${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  find . -iname '*.gem'
  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin"  \
    "${_gemname}-${pkgver}.gem"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache
}

# vim: ts=2 sw=2 et:
