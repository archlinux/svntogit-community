# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=libfm-qt
pkgver=1.2.0
pkgrel=2
pkgdesc="Core library of PCManFM-Qt (Qt binding for libfm)"
arch=("x86_64")
url="https://github.com/lxqt/$pkgname"
license=("LGPL2.1")
depends=("menu-cache" "libexif" "qt5-x11extras")
makedepends=("lxqt-build-tools")
optdepends=(
  "org.freedesktop.secrets: allow to remember mount passwords"
)
provides=("libfm-qt.so")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	"https://github.com/lxqt/libfm-qt/commit/6927f6536bda3f23cb2bad80f5ffa0dd7bed00d0.patch"
)
sha512sums=('941943b9d1275e2306689e19375d5d5589a638446319c275ebdf211d25cdb90116b9359daa36b53dc5688ba09219d66dc79e312752ce56559e485ecdbfd895c0'
            'SKIP'
            '6af20d975cd49ead63c38e5747a166784a9e88ad390a9b3b3c057d6b85ae1272ebfb506bc1b3e08ce44923914479b1d154312e4bd7e0f9179b2b7582defd738f')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
	cd $pkgname-$pkgver
	# https://bugs.archlinux.org/task/76705
	patch -Np1 -i ../6927f6536bda3f23cb2bad80f5ffa0dd7bed00d0.patch
}

build() {
	cmake -B build -S "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make -C build
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
