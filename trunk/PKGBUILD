# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=10.5.0
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(x86_64)
url="http://www.shoup.net/ntl/"
license=(LGPL)
depends=(gf2x gmp)
makedepends=()
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('b90b36c9dd8954c9bc54410b1d57c00be956ae1db5a062945822bbd7a86ab4d2')

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NATIVE=off TUNE=generic
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
