# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.38
pkgrel=2
pkgdesc='Plugins for Geany'
arch=(x86_64)
url='https://plugins.geany.org/'
license=(GPL)
depends=(ctpl "geany>=$pkgver" gpgme gtkspell3 libgit2 lua51 python vte3 webkit2gtk)
makedepends=(cppcheck intltool)
source=("$url$pkgname/$pkgname-$pkgver.tar.bz2"{,.sig})
b2sums=('a2fd9e59218e4b5359b910ccff8d0a6504845081f827d2f1c798ea6401914e8e01222c784a76ab0a126f5fe07c8aa86018cef4c71bb5e2891193f6c8965a3e47'
        'SKIP')
validpgpkeys=('986FA7E80256D3D16F30FB7A01380DF54FD09D02') # Frank Lanitz <frank@geany.org>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi

  # don't redefine bool
  sed -i 's/typedef unsigned int bool/#include <stdbool.h>/g' \
    pretty-printer/src/PrettyPrinter.h
}

build() {
  cd $pkgname-$pkgver
  export CFLAGS+=' -w'
  export PYTHON=/usr/bin/python
  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
