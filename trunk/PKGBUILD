# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: djanku <djanku (at) email (dot) cz>

_pipname=flask-babel
pkgname=python-flask-babel
pkgver=3.0.1
pkgrel=1
pkgdesc="Adds i18n/l10n support to Flask applications with the help of the Babel library."
arch=(any)
url="https://pypi.python.org/pypi/flask-babel"
license=(BSD)
depends=(python-flask python-babel python-pytz python-speaklater python-jinja)
makedepends=(python-setuptools)
source=("https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/${_pipname/-/_}-$pkgver.tar.gz")
sha256sums=('d408cace25514bea8b92e898fd7e55877fbac79b71bc230e266ff515408eba38')

build() {
  cd ${_pipname/-/_}-$pkgver 
  python setup.py build
}

package() {
  cd ${_pipname/-/_}-$pkgver 
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
