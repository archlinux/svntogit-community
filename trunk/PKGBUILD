# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-berkeleydb
pkgver=0.65
pkgrel=2
pkgdesc="Interface to Berkeley DB version 2, 3 or 4"
arch=('x86_64')
url="https://search.cpan.org/dist/BerkeleyDB/"
license=('GPL' 'PerlArtistic')
depends=('perl' 'db')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/P/PM/PMQS/BerkeleyDB-$pkgver.tar.gz)
sha256sums=('410aa89e7232941d49106c9e048d6337475543ec1d233ea7cdb725cb5b963504')

build() {
  cd  "$srcdir"/BerkeleyDB-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  _dbver1=`pacman -Q db | cut -d\  -f2 | cut -d- -f1 | cut -f1 -d.`
  _dbver2=`pacman -Q db | cut -d\  -f2 | cut -d- -f1 | cut -f2 -d.`
  depends=('perl' "db>=${_dbver1}.${_dbver2}" "db<${_dbver1}.$(expr $_dbver2 + 1)")

  cd  "$srcdir"/BerkeleyDB-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
