# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=dante
pkgver=1.3.1
pkgrel=1
pkgdesc="SOCKS v4 and v5 compatible proxy server and client"
url="http://www.inet.no/dante"
arch=(i686 x86_64)
license=('custom')
depends=('tcp_wrappers' 'pam')
backup=('etc/socks.conf'
	'etc/sockd.conf')
options=('!libtool')
source=(ftp://ftp.inet.no/pub/socks/${pkgname}-${pkgver}.tar.gz
	sockd.rc)
md5sums=('19b8a15860ce6b9e96453ddd7f1cbfe2'
         '5110dfd78a2b38fff27a886ee88b58a6')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Config files
  mkdir -p ${pkgdir}/etc/conf.d
  cp example/{socks,sockd}.conf ${pkgdir}/etc

  # License
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # rc-script
  install -D -m755 ${srcdir}/sockd.rc ${pkgdir}/etc/rc.d/sockd
  echo 'SOCKD_OPTS="-D"' >${pkgdir}/etc/conf.d/sockd.conf
}
