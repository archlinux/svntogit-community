# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Max Meyer <dev@fedux.org>

pkgbase=pacparser
pkgname=('pacparser' 'python2-pacparser')
pkgver=1.3.6
pkgrel=1
arch=(i686 x86_64)
url="http://pacparser.googlecode.com"
license=('LGPL')
makedepends=('python2' 'git')
source=("git+https://github.com/pacparser/pacparser.git#tag=$pkgver")
sha512sums=('SKIP')

build() {
  cd $pkgbase
  PYTHON=python2 make all pymod -C src
}

package_pacparser() {
  pkgdesc="Library to parse proxy auto-config (PAC) files"

  cd $pkgbase
  make -C src DESTDIR="$pkgdir/" install
}

package_python2-pacparser() {
  depends=('python2')
  pkgdesc="Python 2.x pacparser module"

  cd $pkgbase
  PYTHON=python2 make -C src DESTDIR="$pkgdir/" install-pymod
}

# vim:set ts=2 sw=2 et:
