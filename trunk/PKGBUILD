# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Aaron, phrakture, Griffin <aaron@archlinux.org>
# Contributor: erkaN kaplaN <erkan@linux-sevenler.org>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=gwget
pkgver=1.0.4
pkgrel=5
pkgdesc='Download manager for GNOME'
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gwget/"
license=('GPL')
depends=('gtk2' 'gconf' 'libnotify')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        gwget-1.0.4-notify-0.7.patch
        gwget-1.0.4-glib-2.32.patch
        drop-libgnomeui.patch)
sha256sums=('a65b2b2b3befb6cf34a697a13111b35498a6d63f9a9b048a22b8654f35ef643f'
            '84efbbea9c2a91c60288edb67264bbdb4ec2c579cf95b3baa8282040a1b65c76'
            '319c4795d0034c4adacf302db697d966ca228fc1bbe9778af9dadcfb0c2531c8'
            '72506677886deb5fc2436c667e2f79ed868ce9384cad77b9a97ec68e41ae45c3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.gnome.org/show_bug.cgi?id=634170
  patch -Np1 -i "$srcdir/gwget-1.0.4-notify-0.7.patch"

  # https://bugzilla.gnome.org/show_bug.cgi?id=665286
  patch -Np1 -i "$srcdir/gwget-1.0.4-glib-2.32.patch"

  # https://bugzilla.gnome.org/show_bug.cgi?id=671759
  patch -Np1 -i "$srcdir/drop-libgnomeui.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
