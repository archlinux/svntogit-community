# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=mediawiki
pkgver=1.20.2
_mathver=110614
pkgrel=1
pkgdesc="MediaWiki engine"
arch=('any')
url="http://www.mediawiki.org/wiki/MediaWiki"
# + http://www.mediawiki.org/wiki/Extension:Math
license=("GPL")
depends=(php)
optdepends=('texvc: for math rendering'
	    'python2')
backup=(etc/webapps/mediawiki/.htaccess)
install=mediawiki.install
options=(!strip)
source=(http://download.wikimedia.org/mediawiki/1.20/mediawiki-$pkgver.tar.gz
	http://upload.wikimedia.org/ext-dist/Math-MW1.19-${_mathver}.tar.gz
	apache.example.conf)
md5sums=('0b3f7a30e217e794138e52b276eb1e4f'
         '8a44b503eb91b2b78482985d249101ed'
         '99c25f04d4093f3333db1be7624fe4e7')

package() {
  cd $srcdir
#  sed -i -e 's#is_executable#is_file#' -e 's#is_executable#is_file#' $pkgname-$pkgver/includes/Math.php
  install -vdm0755 $pkgdir/usr/share/webapps
  install -vdm0755 $pkgdir/etc/webapps/mediawiki
  cp -a $pkgname-$pkgver $pkgdir/usr/share/webapps/mediawiki
  cp -a Math $pkgdir/usr/share/webapps/mediawiki/extensions/Math
  ln -s /usr/bin/texvc $pkgdir/usr/share/webapps/mediawiki/extensions/Math/math/texvc
  echo "deny from all" >> $pkgdir/etc/webapps/mediawiki/.htaccess
  ln -sf /etc/webapps/mediawiki/.htaccess $pkgdir/usr/share/webapps/mediawiki/.htaccess
  install -vDm0644 $srcdir/apache.example.conf $pkgdir/etc/webapps/mediawiki/apache.example.conf

  # move cache and images to /var
  install -vdm0755 -o http -g http $pkgdir/var/cache/mediawiki
  install -vdm0755 -o http -g http $pkgdir/var/lib/mediawiki

  cd $pkgdir/usr/share/webapps/mediawiki

  mv cache/.htaccess $pkgdir/var/cache/mediawiki/
  rmdir cache
  ln -sf /var/cache/mediawiki cache

  mv images/* $pkgdir/var/lib/mediawiki/
  mv images/.htaccess $pkgdir/var/lib/mediawiki/
  rmdir images
  ln -sf /var/lib/mediawiki images
}
