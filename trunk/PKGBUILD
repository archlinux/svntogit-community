# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=atril
pkgver=1.22.2
pkgrel=2
pkgdesc="MATE document viewer"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('djvulibre' 'libspectre' 'libgxps' 'mathjax2' 'poppler-glib' 'webkit2gtk' 'caja' 'texlive-bin')
makedepends=('gobject-introspection' 'intltool' 'itstool')
groups=('mate-extra')
conflicts=('atril-gtk3')
replaces=('atril-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('dc692b9c2a2b8bdd7c36631a9f50fb19335f9ce36156464e967a7fdde7478786')

prepare() {
	cd "$pkgname-$pkgver"

	# Fix mathjax path
	sed -i 's|/usr/share/javascript/mathjax|/usr/share/mathjax2|' backend/epub/epub-document.c
}

build() {
	cd "$pkgname-$pkgver"
	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${_pkgbase} \
        	--enable-djvu \
        	--enable-dvi \
        	--enable-epub \
        	--enable-t1lib \
        	--enable-pixbuf \
        	--enable-comics \
        	--enable-xps \
        	--enable-introspection

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "$pkgname-$pkgver"
    	make DESTDIR="$pkgdir" install
}
