# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alessandro Sagratini

pkgname=inotify-tools
pkgver=3.22.6.0
pkgrel=2
pkgdesc="inotify-tools is a C library and a set of command-line programs for Linux providing a simple interface to inotify."
arch=('x86_64')
url="https://github.com/rvoicilas/inotify-tools"
license=('GPL')
depends=()
makedepends=('gcc' 'make' 'doxygen')
options=('docs')
source=($pkgname-$pkgver.tar.gz::https://github.com/rvoicilas/inotify-tools/archive/$pkgver.tar.gz)
sha256sums=('c6b7e70f1df09e386217102a1fe041cfc15fa4f3d683d2970140b6814cf2ed12')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i 's|-Werror||g' src/Makefile.*
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --enable-fanotify
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
