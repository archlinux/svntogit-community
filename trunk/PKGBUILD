# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-redcarpet
pkgver=3.5.1
pkgrel=1
pkgdesc='A fast, safe and extensible Markdown to (X)HTML parser'
arch=(x86_64)
url='https://github.com/vmg/redcarpet'
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rake-compiler ruby-test-unit)
checkdepends=(tidy)
options=(!emptydirs)
source=(https://github.com/vmg/redcarpet/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('95393dffd8d6b2a6db968f488486e45d0774ceb430bae6f080f619d876812ed8')

prepare() {
  cd redcarpet-$pkgver
  sed -i -e "/task 'test:unit' => :compile/d" -e 's/=> :compile//' Rakefile
  echo gemspec > Gemfile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd redcarpet-$pkgver
  gem build redcarpet.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    redcarpet-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
  ls -lR tmp_install
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd redcarpet-$pkgver
  GEM_HOME="$PWD/tmp_install/$_gemdir" rake test
}

package() {
  cd redcarpet-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
