# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=svelte-language-server
pkgver=0.15.8
pkgrel=2
pkgdesc='Language server for Svelte'
url=https://github.com/sveltejs/language-tools/tree/master/packages/language-server
license=('MIT')
arch=('any')
depends=('nodejs')
makedepends=('yarn')
conflicts=("nodejs-$pkgname")
replaces=("nodejs-$pkgname")
source=("https://github.com/sveltejs/language-tools/archive/language-server-$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('bf6fc5b9b85066d56d78581badc6d7f3c8b3ec2e081aa4ac00fb916fb7e18740e511fcc112024738846874f85e6e4ca8199c99f343d79185d884ea34827271de')

build() {
  cd language-tools-language-server-$pkgver
  yarn --frozen-lockfile
  yarn workspace svelte2tsx build
  cd packages/language-server
  yarn build
}

check() {
  cd language-tools-language-server-$pkgver/packages/language-server
  yarn test
}

package() {
  install -d "$pkgdir"/usr/{bin,lib/node_modules/$pkgname}
  ln -s ../lib/node_modules/$pkgname/bin/server.js \
    "$pkgdir"/usr/bin/svelteserver

  cd language-tools-language-server-$pkgver
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  # Emulate `npm prune --production`
  mv package.json{,.bak}
  cd packages/language-server
  yarn --frozen-lockfile --production
  mv ../../package.json{.bak,}

  cp -r bin dist node_modules package.json \
    "$pkgdir"/usr/lib/node_modules/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
}
