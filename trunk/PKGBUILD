# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=vit
pkgver=2.1.0
pkgrel=1
pkgdesc='Terminal interface for Taskwarrior with Vim key bindings and colorization'
url='https://github.com/vit-project/vit'
arch=('any')
license=('MIT')
depends=('task' 'python' 'python-pytz' 'python-tzlocal' 'python-urwid' 'python-tasklib')
makedepends=('python-setuptools')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('56676651164fb71878a0c1e4bb369cfa336d46e6fd0de7e6167b57fca7eaf662')
b2sums=('2d4242399c399808783a670b853b31586471bd12fad2f9109d760569239d1ebcc5ddead4eb6803300d803fcb34b73c3dd42c091d7a619357f26a7bb10801702c')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  # TODO: requires python-terminal
  # ./run-tests.sh
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 scripts/bash/vit.bash_completion "${pkgdir}/usr/share/bash-completion/completions/vit"
  install -Dm 644 *.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
