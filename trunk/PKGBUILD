# Maintainer: Jonathan Steel <jsteel@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=dvgrab
pkgver=3.5
_commit=e46042e0c7b3523b6854ee547b0534e8310b7460
pkgrel=13
pkgdesc="Saves audio and video data from an IEEE (FireWire) digital source"
arch=('x86_64')
url="http://www.kinodv.org/"
license=('GPL')
depends=('gcc-libs' 'glibc' 'libdv' 'libavc1394' 'libjpeg-turbo' 'libiec61883' 'libraw1394')
makedepends=('git')
source=("git+https://github.com/ddennedy/dvgrab.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
