# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Bob Finch <w9ya@arrl.net>

pkgname=dosemu
pkgver=1.4.0
pkgrel=4
pkgdesc="DOS emulator"
arch=('i686' 'x86_64')
url="http://www.dosemu.org/"
license=('GPL' 'custom')
depends=('gpm' 'slang' 'sdl' 'libxxf86vm' 'libsndfile' 'sh' 'xorg-mkfontdir' 'xorg-bdftopcf')
backup=('etc/dosemu/dosemu.conf'
	'etc/dosemu/dosemu.users'
	'etc/dosemu/global.conf')
source=(http://downloads.sourceforge.net/sourceforge/dosemu/$pkgname-$pkgver.tgz
	http://downloads.sourceforge.net/sourceforge/dosemu/dosemu-freedos-1.0-bin.tgz)
md5sums=('0bba530637266f99d404ba15e3f118d4'
         '2e09774fe91cff4372cb4a393eb467f5')
build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --with-fdtarball=$srcdir/dosemu-freedos-1.0-bin.tgz --mandir=/usr/share/man
  make 
}
package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install 
  install -D -m 644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING && \
  install -D -m 644 COPYING.DOSEMU $pkgdir/usr/share/licenses/$pkgname/COPYING.DOSEMU
}
