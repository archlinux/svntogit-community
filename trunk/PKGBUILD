# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tuxbubling <tuxbubling@jabber.fr>

pkgname=cacti
pkgver=1.1.30
pkgrel=1
pkgdesc="Complete network graphing solution designed to harness the power of RRDTool's data storage and graphing functionality."
arch=('any')
url="https://www.cacti.net"
license=('GPL2')
depends=('rrdtool' 'mariadb' 'php-gd' 'net-snmp' 'xorg-fonts-100dpi')
backup=('etc/webapps/cacti/.htaccess'
        'etc/webapps/cacti/config.php')
source=("https://www.${pkgname}.net/downloads/${pkgname}-${pkgver}.tar.gz"
        'tmpfiles.conf')
sha512sums=('2516deffcbd5a2394b2fdde0359f9a52eeabe019d340ebd7c9f26de454984243760b54733fd4d0e63e3d04fc2b511d3e0066bfc8ed9aaa6374c809705d06e828'
            '272a5e666e5f1c61b3165dcf1b229a88e8c1bb9c75e857721f65961795abf9cecf691d6291cba9401ce70733183191dfdfb8e4e941426cfdb06f2c9e0b129b0a')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove unneeded executable bits
  find . -executable -type f -and -not -path "*scripts*" -exec chmod 644 {} \;
}

package() {
  # create directories
  install -dm0755 "${pkgdir}/usr/share/webapps/${pkgname}" \
    "${pkgdir}/etc/webapps/${pkgname}" \
    "${pkgdir}/var/log/" "${pkgdir}/var/lib/${pkgname}/"

  # move sources
  mv "$srcdir/${pkgname}-${pkgver}"/* \
    "${pkgdir}/usr/share/webapps/${pkgname}"

  # move and symlink configuration
  echo "Require all denied" >> "${pkgdir}/etc/webapps/${pkgname}/.htaccess"
  mv "${pkgdir}/usr/share/webapps/${pkgname}/include/config.php" \
    "${pkgdir}/etc/webapps/${pkgname}"
  ln -s "/etc/webapps/${pkgname}/.htaccess" \
    "${pkgdir}/usr/share/webapps/${pkgname}/.htaccess"
  ln -s "/etc/webapps/${pkgname}/config.php" \
    "${pkgdir}/usr/share/webapps/${pkgname}/include/config.php"

  # move and symlink var directory
  mv "${pkgdir}/usr/share/webapps/${pkgname}/rra" \
    "${pkgdir}/var/lib/${pkgname}/rra"
  ln -s "/var/lib/${pkgname}/rra" \
    "${pkgdir}/usr/share/webapps/${pkgname}/rra"

  # move and symlink log directory
  mv "${pkgdir}/usr/share/webapps/${pkgname}/log" \
    "${pkgdir}/var/log/${pkgname}"
  ln -s "/var/log/${pkgname}" "${pkgdir}/usr/share/webapps/${pkgname}/log"

  # tmpfiles.d
  install -Dm644 "${srcdir}/tmpfiles.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
