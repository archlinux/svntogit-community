# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-proxy.py
pkgver=2.3.1
_commit=82d562b7d6b068ea706634f97e6a086f5209df61 # git rev-parse "v$pkgver"
pkgrel=1
pkgdesc='Lightweight HTTP, HTTPS, HTTP2 and WebSockets proxy server'
arch=('any')
url='https://github.com/abhinavsingh/proxy.py'
license=('BSD')
depends=('python' 'python-typing_extensions')
makedepends=('git' 'python-setuptools')
checkdepends=('python-paramiko')
optdepends=('python-paramiko: proxy over SSH tunnel support')
source=("git+$url#commit=$_commit")
sha512sums=('SKIP')

prepare() {
	cd "${pkgname#python-}"
	# Remove strict version dependency for typing_extensions
	sed -i 's/==.*//' requirements.txt
}

pkgver() {
	cd "${pkgname#python-}"
	git describe --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}	

build() {
	cd "${pkgname#python-}"
	python setup.py build
}

check() {
	cd "${pkgname#python-}"
	python setup.py test
}

package() {
	cd "${pkgname#python-}"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
