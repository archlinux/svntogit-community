# Maintainer: David Runge <dvzrv@archlinux.org>

_name=findpython
pkgname=python-findpython
pkgver=0.2.3
pkgrel=1
pkgdesc="A utility to find python versions on your system"
arch=(any)
url="https://github.com/frostming/findpython"
license=(MIT)
depends=(python-packaging)
makedepends=(python-build python-installer python-pdm-pep517 python-wheel)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('251ec820f965f61296f593edc3bf8adcacd95d9395b3c140650d0f1cf92a682e6c7739ecb682f23930362dcd31ca5cce8802188e246dffd6c54cf1d8c97c743d')
b2sums=('ccc304b770c3085a38a3f7af8e8907efbda3613da1bf05c3308a5828a97ff5642f9fc6357af343a238cc097902e9975c905512e6eab8dbffe2ea463fcc719244')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="test_dir" dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
