# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=graphviz
pkgname=python-graphviz
pkgver=0.17
pkgrel=2
pkgdesc="Simple Python interface for Graphviz"
arch=('any')
url="https://github.com/xflr6/${_pkgname}"
license=('MIT')
depends=('graphviz' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.zip")
sha256sums=('ef6e2c5deb9cdcc0c7eece1d89625fd07b0f2208ea2bcb483520907ddf8b4e12')
b2sums=('f2f5ad728157d60c48964d2cfdc76559310af6099a9eae16279b0f9e8a41c08d02d5f255be62222c1f0808750a26e67822b5fbb930f7ada5c6a614c3607494cd')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run python-coverage in unittests :/
    sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
    # don't add pointless dependency on python-mock needed on python 3.5
    sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
