# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-compat
pkgname=python-ansible-compat
pkgver=2.2.4
pkgrel=2
pkgdesc="Functions that help interacting with various versions of Ansible"
arch=(any)
url="https://github.com/ansible-community/ansible-compat"
license=(MIT)
# python-packaging is a direct dependency: https://github.com/ansible/ansible-compat/issues/169
depends=(python-jsonschema python-packaging python-pyyaml python-subprocess-tee)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm python-wheel)
checkdepends=(ansible python-flaky python-pytest python-pytest-mock)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-2.2.4-dependencies.patch
)
sha256sums=('6a2c3ade5005530cdfdd8e961c784b1718f17ad480a1be5a8014bff89c9c9c2e'
            'c35e029bba2dd65cc7b6881abf3c3c7dccfc5075a6cbaf8bd87e5b54716b71a3')
b2sums=('a4874675086956e11de4df4068e3b17b65b4f9f7610bb88bfba308d880fe478e39a4450a362bb06a555a36a41731f336c51756893ebe65715fc022421c51a293'
        '2f9144e6bbc56d85bb7ee24c4f81c68554c9057b003d45b26ea35634352284ddcb430f26475bce8d4e24f6da3d2dc0c221da87ea245e892168f0d26a3febe12e')

prepare() {
  # remove python-setuptools-scm-git-archive from makedepends: https://github.com/ansible/ansible-compat/pull/186
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-2.2.4-dependencies.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
