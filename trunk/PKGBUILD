# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-reports-scheduler-plugin
pkgver=2.4.1.0
pkgrel=1
_opensearchver=2.4.1
_jdkver=11
pkgdesc="OpenSearch Reports Scheduler Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/dashboards/reporting"
license=('Apache')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/dashboards-reporting/archive/${pkgver}.tar.gz"
)
sha256sums=('1e812221185347c6baf3c7e6d8df2c6a5f67fc0999d4dd1d065cd821803ec9be')

build() {
  cd "dashboards-reporting-${pkgver}/reports-scheduler"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  unzip "${srcdir}/dashboards-reporting-${pkgver}/reports-scheduler/build/distributions/opensearch-reports-scheduler-${pkgver}.zip"

  install -Dm644 "${srcdir}/dashboards-reporting-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
