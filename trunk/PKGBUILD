# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrew Belitsky <belitsky.a@gmail.com>
pkgname=mygui
pkgver=3.0.1
pkgrel=3
pkgdesc="A multilayer and overlappable GUI System for OGRE"
arch=('i686' 'x86_64')
url="http://mygui.info/"
license=('LGPL')
depends=('boost-libs' 'ogre' 'ois')
makedepends=('boost' 'cmake' 'doxygen' 'graphviz')
install=mygui.install
source=("http://downloads.sourceforge.net/project/my-gui/MyGUI/MyGUI_3.0.1/MyGUI_3.0.1_source.zip")
md5sums=('e9e82bf60081be58fea3f64d32e2ab3a')

build() {
  cd $srcdir/MyGUI3.0

  # change CMake config so demos install to /opt  
  sed -i 's:"bin:"../opt/MYGUI:' CMake/Utils/MyGUIConfigTargets.cmake
  sed -i -e 's:../share:/usr/share:' \
         -e 's:"bin":"../opt/MYGUI":' \
         CMake/InstallResources.cmake

  # get a clean build dir
  [[ -d build ]] && rm -rf build
  mkdir build
  cd build

  # generate CMake Makefile
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYGUI_INSTALL_SAMPLES=TRUE \
    -DMYGUI_INSTALL_TOOLS=TRUE \
    -DMYGUI_INSTALL_DOCS=TRUE \
    -DMYGUI_INSTALL_MEDIA=TRUE \
    -DCMAKE_BUILD_TYPE=Release # set=Debug for debugging version

  # compile
  make || return 1

  # generate docs
  if [[ $(which dot) && $(which doxygen) ]]; then
    make api-docs 2>/dev/null || return 1
  fi
}

package() {
  cd $srcdir/MyGUI3.0/build

  # install the whole bunch
  make DESTDIR="$pkgdir" install

  # manually install docs
  install -d $pkgdir/usr/share/doc/MYGUI
  cp -r ../Docs/html $pkgdir/usr/share/doc/MYGUI

  # make demos work
  chown root:users $pkgdir/opt/MYGUI
  chmod 775 $pkgdir/opt/MYGUI
}

# vim:set ts=2 sw=2 et:
