# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Diego Galizzi <dgalizzi at gmail dot com>
# Contributor: zoulnix <http://goo.gl/HQaP>

pkgname=cimg
pkgver=2.1.5
pkgrel=1
pkgdesc="Open-source C++ toolkit for image processing"
arch=('any')
url="http://cimg.eu/"
license=('CeCiLL-C' 'CeCiLL')
depends=('fftw' 'graphicsmagick')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dtschump/CImg/archive/v.${pkgver//./}.tar.gz")
sha256sums=('c97e7d268d9832d71cfee0f45ed5d965441b7c8cb7566570531db41e9556391d')

package() {
  cd "${srcdir}"/CImg-v.${pkgver//./}
  install -d "${pkgdir}"/usr/include/CImg/plugins ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 CImg.h "${pkgdir}"/usr/include/
  install -m644 plugins/*.h "${pkgdir}"/usr/include/CImg/plugins/
  install -m644 Licence_CeCILL*_V*-en.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
