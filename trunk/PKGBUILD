# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Yardena Cohen <yardenack at gmail dot com>

pkgname=amavisd-new
pkgver=2.11.0
pkgrel=5
pkgdesc="High-performance interface between mailer (MTA) and content checkers"
arch=('any')
url="https://www.ijs.si/software/amavisd/"
license=('GPL')
depends=(
    'perl>=5.8.2'
    'perl-archive-zip>=1.14'
    'perl-convert-tnef'
    'perl-convert-uulib>=1.4-5'
    'perl-mime-tools'
    'perl-mailtools>=1.58'
    'perl-net-libidn'
    'perl-net-server>=0.88'
    'perl-io-socket-inet6'
    'perl-io-stringy'
    'perl-unix-syslog>=1.1-4'
    'perl-mail-dkim>=0.31'
    'perl-berkeleydb>=0.42'
    'bzip2'
    'gzip'
)
optdepends=(
    'perl-file-libmagic'
    'arc:        Decoder for: .arc'
    'arj:        Decoder for: .arj .exe'
    'lrzip:      Decoder for: .lrz'
    'lz4:        Decoder for: .lz4'
    'lzo:        Decoder for: .lzo'
    'p7zip:      Decoder for: .7z'
    'unrar:      Decoder for: .rar'
    'rpmextract: Decoder for: .rpm'
)
backup=('etc/amavisd/amavisd.conf' 'etc/amavisd/amavisd-custom.conf')
source=("https://www.ijs.si/software/amavisd/amavisd-new-${pkgver}.tar.xz"
        "amavisd.service"
        "amavisd.sysusers"
        "amavisd.tmpfiles"
        "fix-dkim-for-originating-mails.patch")
sha512sums=('a33292c976abf54db9475392069658c926e7a6f11a4970bbe353b34b3343388bc83b40eda4729f8efa735a3a6e23fd1ed83487f6f7ccf1e9f0903220e6d26957'
            '5028913479808c02a74682987ed5289d094c1ae2e37874b8c45a6abcc825f2e9d9a46acf9ee010897a5a41e5f9256507cc4d7b9e535c46b03cc06b560dddd908'
            '8933d8457989c949845c0dabd2bed58680adbf87c920f05a59fd136acdd6da84b4d5b48c4c71dfbc0b047be005a70f665138d6397c1b09e0c746903c1d19fc11'
            '1320d2416151e3340dc9a40b2b516835a5a4966c7db58268a005ee3f5701790851cdf3a2897c6fb9d4e95847d8c0c170e64f8a668ae999fd143de02f6fae8b52'
            '0ea12ca769eb58eacbcbb234d1ff56ba06c06149230d7f6614cf52847ba2971fc5179f1a3cc88da114d9f6c1421108b4adfbe1b2d7e0f6be1be4ac70cec21988')

prepare() {
    sed -i \
        -e "s/# @bypass_virus_checks_maps = (1)/@bypass_virus_checks_maps = (1)/g" \
        -e "s/# @bypass_spam_checks_maps  = (1)/@bypass_spam_checks_maps = (1)/g" \
        -e "s/\\\$daemon_user  = 'vscan'/\\\$daemon_user  = 'amavis'/g" \
        -e "s/\\\$daemon_group = 'vscan'/\\\$daemon_group = 'amavis'/g" \
        -e "sX# \\\$MYHOME = '/var/amavis'X\\\$MYHOME = '/var/spool/amavis'Xg" \
        -e "sX\\\$QUARANTINEDIR = '/var/virusmails'X\\\$QUARANTINEDIR = \"\\\$MYHOME/virus\"Xg" \
        -e "sX# \\\$pid_file  = \"\\\$MYHOME/var/amavisd.pid\"X\\\$pid_file = \"/run/amavis/pid\"Xg" \
        -e "sX# \\\$lock_file = \"\\\$MYHOME/var/amavisd.lock\"X\\\$lock_file = \"/run/amavis/lock\"Xg" \
        -e "sX\\\$unix_socketname = \"\\\$MYHOME/amavisd.sock\"X\\\$unix_socketname = \"/run/amavis/sock\"Xg" \
        "${srcdir}/${pkgname}-${pkgver}/amavisd.conf"

    # fix ORIGINATING detection for DKIM signing
    # see: https://bugzilla.redhat.com/show_bug.cgi?id=1364730#c1
    # and: https://lists.amavis.org/pipermail/amavis-users/2016-July/004428.html
    patch -p0 "${srcdir}/${pkgname}-${pkgver}/amavisd" < fix-dkim-for-originating-mails.patch
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m755 JpegTester.pm               "${pkgdir}/usr/share/perl5/vendor_perl/JpegTester.pm"
    install -D -m644 LDAP.ldif                   "${pkgdir}/etc/openldap/schema/amavisd-new.ldif"
    install -D -m644 LDAP.schema                 "${pkgdir}/etc/openldap/schema/amavisd-new.schema"
    install -D -m750 -o 333 -g 333 -d            "${pkgdir}/var/spool/amavis/"
    install -D -m750 -o 333 -g 333 -d            "${pkgdir}/var/spool/amavis/db"
    install -D -m750 -o 333 -g 333 -d            "${pkgdir}/var/spool/amavis/tmp"
    install -D -m750 -o 333 -g 333 -d            "${pkgdir}/var/spool/amavis/var"
    install -D -m750 -o 333 -g 333 -d            "${pkgdir}/var/spool/amavis/virus"

    install -D -m644 -t "${pkgdir}/etc/amavisd"             amavisd{.conf,-custom.conf,.conf-default}
    install -D -m644 -t "${pkgdir}/usr/share/doc/amavis"    amavisd-new-{qmqpqq,courier}.patch
    install -D -m755 -t "${pkgdir}/usr/bin"                 p0f-analyzer.pl amavisd{,-{agent,nanny,release,signer,snmp-subagent{,-zmq}}}

    install -D -m644 "${srcdir}/amavisd.service"  "${pkgdir}/usr/lib/systemd/system/amavisd.service"
    install -D -m644 "${srcdir}/amavisd.sysusers" "${pkgdir}/usr/lib/sysusers.d/amavisd.conf"
    install -D -m644 "${srcdir}/amavisd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/amavisd.conf"
}
