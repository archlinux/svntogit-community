# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=6.6.0
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=(any)
url="https://github.com/openapitools/openapi-generator/"
license=(Apache)
depends=(
  bash
  java-runtime=8)
makedepends=(
  maven
  java-environment=8
  strip-nondeterminism
)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('deafc6e8d858b86f588232f5f4f4ddb04bc35f31faab01fc09333a8afc45bf1f5bd3349b71f6027124156e1e8a4ea6b1d4c36672d8901776021a0e59c30bc251'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')
b2sums=('81a558c2eedba48baf78f6c036b5f445c71426ccfb79b29886018e306a49056ef8ead1ee89839f2aa1d7792dbded78585f71d920b37c4deff0099640944af163'
        'd2f38df8fd23a32e95e9e09624283ca4927dce8cf6dbdbfc11547396ae2bd9c6c1ff65082447f1e30caba3654fd6e50558272d524242ca6727df7024729a1123')

build() {
  cd $pkgname-$pkgver
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  install -vDm 644 $pkgname-$pkgver/modules/$pkgname-cli/target/openapi-generator-cli.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/scripts/$pkgname-cli-completion.bash "$pkgdir/usr/share/bash/bash-completion/completions/openapi-generator"
  install -vDm 755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  ln -svf "$pkgname" "$pkgdir/usr/bin/$pkgname-cli"
}
