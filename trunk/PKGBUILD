# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-camel-converter
pkgver=3.0.0
pkgrel=1
pkgdesc='Converts a string from snake case to camel case or camel case to snake case'
arch=(any)
license=('MIT')
url='https://github.com/sanders41/camel-converter'
depends=(python)
makedepends=(python-poetry-core)
makedepends=(python-build python-installer python-wheel python-poetry-core)
checkdepends=(python-pytest python-pydantic)
optdepends=('python-pydantic: provides a base class to automatically convert between snake and camel case')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sanders41/camel-converter/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('aef04545966c5fcee5e2ce370dc42b12516d452fdc70127e023b9fa5b389616412749620422addfb481a2d5d6ebd2944f7baa209f970fea15e63a28998038f28')

build() {
  cd "camel-converter-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "camel-converter-${pkgver}"
  python -m unittest discover -vs tests
}

package() {
  cd "camel-converter-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
