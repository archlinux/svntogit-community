# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pkgbasename=expat
pkgname=lib32-${_pkgbasename}
pkgver=2.1.1
pkgrel=3
pkgdesc="An XML Parser library written in C (32 bit)"
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
depends=('lib32-glibc' "${_pkgbasename}")
source=(http://downloads.sourceforge.net/sourceforge/expat/${_pkgbasename}-${pkgver}.tar.bz2
        CVE-2015-1283-refix.patch
        CVE-2016-0718-v2-2-1.patch
		cve-2012-6702-plus-cve-2016-5300-v1.patch)
md5sums=('7380a64a8e3a9d66a9887b01d0d7ea81'
         'd54dd69a14bedb86fc6f6e0c0be5c4a4'
         'beb1b2dc1f0d988ed85a5dd30a3b322a'
         '0d0df76777dc43221e4fc4522601d803')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -p2 < "${srcdir}/CVE-2015-1283-refix.patch"
  patch -p2 < "${srcdir}/CVE-2016-0718-v2-2-1.patch"
  patch -p2 < "${srcdir}/cve-2012-6702-plus-cve-2016-5300-v1.patch"
}

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  ./configure --prefix=/usr --libdir=/usr/lib32 --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share/man}
}
