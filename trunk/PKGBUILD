# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=pypy
pkgver=5.6.0
pkgrel=2
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'tk' 'sqlite' 'mercurial')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
source=("hg+http://bitbucket.org/pypy/pypy#tag=release-pypy2.7-v${pkgver}")
md5sums=('SKIP')

build() {
  cd pypy/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone
}

package() {
  cd pypy

  python2 pypy/tool/release/package.py --archive-name pypy --targetdir .
  mkdir unpacked
  tar xf pypy.tar.bz2 -C unpacked

  cd unpacked
  install -Dm755 pypy/bin/pypy "${pkgdir}"/opt/pypy/bin/pypy
  install -Dm755 pypy/bin/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so
  cp -r pypy/include pypy/lib_pypy pypy/lib-python pypy/site-packages "${pkgdir}"/opt/pypy/
  cd ..

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy "${pkgdir}"/usr/bin/pypy

  install -Dm644 LICENSE "${pkgdir}"/opt/pypy/LICENSE
  install -Dm644 README.rst "${pkgdir}"/opt/pypy/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE

  # Compile binary modules
  #export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
  #for module in _curses syslog gdbm sqlite3 _tkinter; do
  #   "${pkgdir}"/opt/pypy/bin/pypy-c -c "import ${module}"
  #done

  # Generate bytecode (they apparently haven't intended for this to happen yet)
  #"${pkgdir}"/opt/pypy/bin/pypy-c -m compileall "${pkgdir}"/opt/pypy
  #"${pkgdir}"/opt/pypy/bin/pypy-c -O -m compileall "${pkgdir}"/opt/pypy
}
# vim: ts=2 sw=2 et:
