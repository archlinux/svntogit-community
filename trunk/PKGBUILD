# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=soundkonverter
pkgver=0.3.11
pkgrel=2
pkgdesc="Front-end to various audio converters"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=29024"
license=('GPL')
depends=('cdparanoia' 'kdelibs3' 'taglib')
optdepends=('cdrkit: cdda2wav backend'
	    'faac: faac backend'
	    'faad2: faad backend'
	    'ffmpeg: ffmpeg backend'
	    'flac: flac backend'
	    'lame: lame backend'
	    'mplayer: mplayer backend'
	    'vorbis-tools: oggenc, oggdec backends'
	    'speex: speexenc, speexdec backends'
	    'vorbisgain: vorbisgain backend'
	    )
options=('libtool')
source=(http://hessijames.googlepages.com/soundkonverter-${pkgver}.tar.bz2
	gcc.patch)
md5sums=('96bde488573fa66421e346a6dc64b18f'
         '99d41e0d9c443278adfb3610997aca95')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  source /etc/profile.d/qt3.sh
  source /etc/profile.d/kde3.sh
  patch -p1 < ../gcc.patch
  make -f admin/Makefile.common
  ./configure --prefix=/opt/kde --disable-debug --without-arts
  make || ( aclocal && make )
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/opt/kde/share/doc
}
