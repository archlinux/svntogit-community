# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-prctl
pkgname=('python-prctl' 'python2-prctl')
pkgver=1.6.1
pkgrel=1
pkgdesc='Python(ic) interface to the linux prctl syscall'
arch=('x86_64')
license=('GPL')
url='https://github.com/seveas/python-prctl'
makedepends=('python' 'python2' 'libcap' 'git')
checkdepends=('iputils' 'procps-ng')
source=("git+https://github.com/seveas/python-prctl.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a python-prctl{,-py2}
}

build() {
  cd "$srcdir"/python-prctl
  python setup.py build

  cd "$srcdir"/python-prctl-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-prctl
  python -m unittest discover || warning "Tests failed"

  cd "$srcdir"/python-prctl-py2
  python2 -m unittest discover || warning "Tests failed"
}

package_python-prctl() {
  depends=('python' 'libcap')

  cd python-prctl
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-prctl() {
  depends=('python2' 'libcap')

  cd python-prctl-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
