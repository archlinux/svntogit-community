# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=1.9
pkgrel=2
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/about/"
license=('GPL2')
groups=('realtime')
depends=('glibc')
makedepends=('numactl' 'python')
optdepends=('bash: for determine_maximum_mpps.sh and get_cpuinfo_mhz.sh'
            'numactl: for cyclictest'
            'python: for get_cyclictest_snapshot and hwlatdetect')
source=("https://git.kernel.org/pub/scm/utils/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('27ecf3dee9ea8e986b12082b1d7a704ffe382422baa403d8cb9f1dfe4ce1b10ced1328c3e7a532bb2f963f138defac8fdfd39745c8b520a3fe5128e878ba621d')
b2sums=('905e20eca5440b121f789799163ea8298ff8ae15e4f36623f970621e6b5c6ca143b699c63bf24be931a2244d2a1f8bdbe6b63a31ed7c1a24d856374c39f386f2')

build() {
  cd "${pkgname}-${pkgver}"
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -vDm 644 {MAINTAINERS,README.markdown} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
