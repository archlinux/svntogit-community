# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: TheHoff <forums>

pkgname=devil
pkgver=1.7.8
pkgrel=16
pkgdesc="Library for reading several different image formats"
arch=('i686' 'x86_64')
url="http://openil.sourceforge.net/"
depends=('libpng' 'libmng' 'jasper' 'lcms' 'openexr')
install=devil.install
options=('!libtool' '!docs' '!emptydirs')
license=('GPL')
source=(http://downloads.sourceforge.net/openil/DevIL-$pkgver.tar.gz libpng14.patch)
md5sums=('7918f215524589435e5ec2e8736d5e1d'
         '0f839ccefd43b0ee8b4b3f99806147fc')

build() {
  cd ${srcdir}/devil-$pkgver

  patch -Np1 -i ${srcdir}/libpng14.patch

  if [[ $CARCH == x86_64 ]]; then
    ./configure --prefix=/usr --enable-ILU
  else
    ./configure --prefix=/usr --enable-ILU --disable-sse3
  fi

  make
}

package() {
  cd ${srcdir}/devil-$pkgver

  make prefix=${pkgdir}/usr install
}
