# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname='radcli'
pkgver=1.2.8
pkgrel=1
pkgdesc='Client library for writing Radius clients in C or C++'
arch=('x86_64')
url='http://radcli.github.io/radcli/'
license=('BSD')
depends=('nettle' 'gnutls')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('cf4b7c8f62651c01a7408cc57e97edcd71a3ee0353368e571469ae2a13b7b2066f400a4fe0d49fe75b32e03473c02924d4cc61c432e3bd9d5eb4b11df67bf7a1'
            'SKIP')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') # Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix='/usr' \
    --sysconfdir='/etc'
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
