# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=galculator
pkgname=(galculator galculator-gtk2)
pkgver=2.1.2
pkgrel=2
pkgdesc='GTK+ based scientific calculator'
arch=('i686' 'x86_64')
url='http://galculator.sourceforge.net/'
license=('GPL')
depends=('gtk3' 'gtk2')
makedepends=('intltool')
install=$pkgname.install
source=(http://downloads.sourceforge.net/galculator/galculator-$pkgver.tar.bz2)
sha256sums=('23e7e8d02ec49bf06e061e7fa56c779fd0f353ec294c30fd88858786d8e4a79b')

build() {
  # GTK3 version
  cd $pkgname-$pkgver
  [ -e gtk3 ] || mkdir gtk3
  cd gtk3
  ../configure --prefix=/usr
  make

  # GTK2 version
  cd $srcdir/$pkgname-$pkgver
  [ -e gtk2 ] || mkdir gtk2
  cd gtk2
  ../configure --prefix=/usr --disable-gtk3
  make
}

package_galculator() {
  depends=('gtk3')

  cd $pkgbase-$pkgver/gtk3
  make DESTDIR="$pkgdir" install
}

package_galculator-gtk2() {
  pkgdesc+=' (GTK2 version)'
  depends=('gtk2')
  conflicts=('galculator')

  cd $pkgbase-$pkgver/gtk2
  make DESTDIR="$pkgdir" install
}
