# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.7.0
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG and JPEG file types, both lossless and lossy compression mode'
arch=('any')
url='https://github.com/Huluti/Curtail'
license=('GPL3')
depends=('gtk4' 'libadwaita' 'jpegoptim' 'oxipng' 'pngquant' 'python-gobject')
makedepends=('appstream-glib' 'meson')
source=("https://github.com/Huluti/Curtail/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('fc5b846999268eb3ad0f33812c8ce467174fdb9c1a600563fbb19569657c2828')

build() {
  arch-meson Curtail-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir "$pkgdir"
}
