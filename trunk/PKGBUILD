# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=python-beautifulsoup4
pkgname=('python-beautifulsoup4' 'python2-beautifulsoup4')
pkgver=4.7.1
pkgrel=1
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping"
arch=('any')
url="http://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=('python-setuptools' 'python2-setuptools' 'python-soupsieve' 'python2-soupsieve')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://www.crummy.com/software/BeautifulSoup/bs4/download/${pkgver%.*}/${pkgbase#*-}-$pkgver.tar.gz")
sha512sums=('7aa77bc6008bbcbbbe91b0a850007ab237d2832b63a787fbd94b7cbf47d4276b185e0c61c134df73221406458edff2b75b6b8c2b53b543aa3bb1b0e2202dac5a')

prepare() {
  cp -a beautifulsoup4-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/beautifulsoup4-$pkgver
  python setup.py build

  cd "$srcdir"/beautifulsoup4-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/beautifulsoup4-$pkgver/build
  py.test

  cd "$srcdir"/beautifulsoup4-$pkgver-py2/build
  py.test2
}

package_python-beautifulsoup4() {
  depends=('python-soupsieve')
  optdepends=('python-chardet: to autodetect character encodings'
              'python-lxml: alternative HTML parser'
              'python-html5lib: alternative HTML parser')

  cd beautifulsoup4-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-beautifulsoup4() {
  depends=('python2-soupsieve')
  optdepends=('python2-chardet: to autodetect character encodings'
              'python2-lxml: alternative HTML parser'
              'python2-html5lib: alternative HTML parser')

  cd beautifulsoup4-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
