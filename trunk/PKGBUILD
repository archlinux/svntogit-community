# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-debtcollector
pkgname=('python-debtcollector' 'python2-debtcollector')
pkgver=1.21.0
pkgrel=1
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='https://docs.openstack.org/developer/debtcollector'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-wrapt' 'python2-wrapt' 'python2-funcsigs')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/debtcollector/archive/$pkgver.tar.gz")
sha512sums=('ff0288fcb9010d29a75c7a008d6d28fc19088d7a5934eef8b0d262609a1eee385394e4763dcefae1a4d2e30977fa6122a62cde5ecd618ee12ad4b8e68a98f16e')

prepare() {
  cp -a debtcollector-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py build

  cd "$srcdir"/debtcollector-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/debtcollector-$pkgver
  stestr run

  cd "$srcdir"/debtcollector-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-debtcollector() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')

  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-debtcollector() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-wrapt' 'python2-funcsigs')

  cd debtcollector-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
