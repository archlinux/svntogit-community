# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Nathan Baum <n@p12a.org.uk>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=at
pkgver=3.1.23
pkgrel=1
pkgdesc='AT and batch delayed command scheduling utility and daemon'
arch=('x86_64')
url='http://blog.calhariz.com/tag/at'
license=('GPL')
depends=('pam' 'flex')
makedepends=('smtp-forwarder' 'git')
backup=('etc/at.deny' 'var/spool/atd/.SEQ')
options=('!makeflags')
validpgpkeys=('464BC7CD439FEE5E8B4098A0348A778D6885EF8F') # Jose M Calhariz (Técnico) <jose.calhariz@tecnico.ulisboa.pt>
source=("http://blog.calhariz.com/public/sft/$pkgname/${pkgname}_${pkgver}.orig.tar.gz"{,.asc})
sha256sums=('97450aa954aaa8a70218cc8e61a33df9fee9f86527e9f861de302fb7a3c81710'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-jobdir=/var/spool/atd \
    --with-atspool=/var/spool/atd \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  CFLAGS="$CFLAGS -w" make
}

package() {
  make -C "$pkgname-$pkgver" IROOT="$pkgdir" docdir=/usr/share/doc install
}

# vim:set ts=2 sw=2 et:
