# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Modified from aur/python2-pytrie; original maintainership info:
# Contributor: dseg

pkgname=python-pytrie
pkgver=0.3.1
pkgrel=4
pkgdesc="A pure Python implementation of the trie data structure."
url="https://github.com/gsakkis/pytrie/"
depends=(python python-sortedcontainers)
makedepends=(python-setuptools)
# Tests in this package imports tests from CPython :/
checkdepends=(python-tests)
license=(BSD)
arch=(any)
source=("https://github.com/gsakkis/pytrie/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-py39.patch::https://github.com/gsakkis/pytrie/commit/62564b0c14c66b2e5fb2fcad43246bd8d56d51a8.patch)
sha256sums=('9c51f5f50d22a829dee6d566a34a7f9ec33e434d08bfc9a4346fe8acaf2c1418'
            'cfc68a3df41009390a5646180ea1952981e5cf922eb9dfb4f2a6b501391a4c08')

prepare() {
  cd pytrie-$pkgver
  patch -Np1 -i ../$pkgname-py39.patch
}

build() {
  cd pytrie-$pkgver
  python setup.py build
}

check() {
  cd pytrie-$pkgver
  python -m unittest discover -v ./tests
}

package() {
  cd pytrie-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
