# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgname=rng-tools
pkgver=5
pkgrel=4
pkgdesc="Random number generator related utilities"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/gkernel/"
license=('GPL')
depends=('libgcrypt')
backup=(etc/conf.d/rngd)
source=(https://downloads.sourceforge.net/sourceforge/gkernel/$pkgname-$pkgver.tar.gz
        rngd.conf
        rngd.service
        rngd-test-signal-in-loop.patch)

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -p1 -i "$srcdir"/rngd-test-signal-in-loop.patch
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	install -Dm0644 "$srcdir"/rngd.service "$pkgdir"/usr/lib/systemd/system/rngd.service
	install -Dm0644 "$srcdir"/rngd.conf "$pkgdir"/etc/conf.d/rngd
}

sha256sums=('60a102b6603bbcce2da341470cad42eeaa9564a16b4490e7867026ca11a3078e'
            'f4fd197dd682cdab07ddeabe8d3b73b1bc2e1cf322237be238a26b49a7dcb047'
            'd3e38ac1460e3072792def73281faf5f59dffe9ac43b00916252cc69f7c7d1b5'
            'fec42e65a2f48a4b1a96d839b45d5890651877016f4767f63a5186424dd61a2d')
