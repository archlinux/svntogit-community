# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-requestsexceptions
pkgname=(python-requestsexceptions python2-requestsexceptions)
pkgver=1.3.0
pkgrel=1
pkgdesc="Import exceptions from potentially bundled packages in requests."
arch=('any')
url="https://github.com/openstack-infra/requestsexceptions"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-requests'
             'python2-requests')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack-infra/requestsexceptions/archive/$pkgver.tar.gz")
sha512sums=('acae76c2dca030db0eb89f6fc45bf14d61646610551c7694cd406c7fc466bcd9c229631f324dccfb06475aa1c7100bd909d2e38a2b7eb91755a814bd8e216f97')

prepare() {
  cp -a requestsexceptions-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/requestsexceptions-$pkgver
  python setup.py build

  cd "$srcdir"/requestsexceptions-$pkgver-py2
  python2 setup.py build
}

package_python-requestsexceptions() {
  depends=('python-requests')

  cd "$srcdir"/requestsexceptions-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-requestsexceptions() {
  depends=('python2-requests')

  cd "$srcdir"/requestsexceptions-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
