# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.7.1
pkgrel=1
epoch=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt5-base
  qt5-declarative
  qt5-graphicaleffects
  qt5-networkauth
  qt5-quickcontrols2
  qt5-svg
  qt5-websockets
)
makedepends=(help2man meson jack python-jinja python-yaml rtaudio)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('cd26c4ef5735567baf159a3389d6eb94b9a1ad4fa44354ffa8efa462a1aa84da112b960fef756b5c7d8a37e31598df16b13935167aeccb85c261fc6782101658')
b2sums=('05c6e917c73abce0528ed8fb01180bbdb92c2eb3dd14fd77a51dc24b1d15551052a47eeffd509bb92daf102fa2e342bd40e9e7c614aff62479c373ef2a0c8a45')

build() {
  local meson_options=(
    -D noupdater=true
  )

  arch-meson "${meson_options[@]}" build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(
    jack libjack.so
    rtaudio librtaudio.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
