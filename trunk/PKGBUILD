# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgbase=python-xapian
pkgname=('python-xapian' 'python2-xapian')
_realname=xapian-bindings
pkgver=1.4.11
epoch=1
pkgrel=1
pkgdesc="Python bindings for Xapian"
arch=('x86_64')
url="https://xapian.org/docs/bindings/python/"
license=('GPL2')
makedepends=('python-sphinx' 'python2-sphinx' "xapian-core=$epoch:$pkgver")
source=("https://oligarchy.co.uk/xapian/${pkgver}/${_realname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('326b08b9959143b92e6c10bd9b6b107ce2779e6ef1a3b5570abd07802a5988dd7df4e890d820f7da355f9f9e64df9a3ae08a0664d70d10ba6f876fca3dc44ecd'
            'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${_realname}-${pkgver}"
  autoreconf -vfi
  cd ..
  mv -v "${_realname}-$pkgver" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-$pkgver" "${pkgname[1]}-${pkgver}"
  cd "${pkgname[1]}-${pkgver}"
  # fix shebang of examples
  sed -i '1s/python/python2/' python/docs/examples/*.py
  export XAPIAN_CONFIG=/usr/bin/xapian-config
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  ./configure --prefix=/usr \
              --with-python3
  make
  cd ../"${pkgname[1]}-${pkgver}"
  PYTHON2=/usr/bin/python2
  ./configure --prefix=/usr \
              --with-python
  make
}

package_python-xapian() {
  pkgdesc="Python 3.x bindings for Xapian"
  depends=('python' "xapian-core=$epoch:$pkgver")
  cd "${pkgname[0]}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO}
}

package_python2-xapian() {
  pkgdesc="Python 2.x bindings for Xapian"
  depends=('python2' "xapian-core=$epoch:$pkgver")
  cd "${pkgname[1]}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO}
}

# vim:set ts=2 sw=2 et:
