# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jeff Mickey <j@codemac.net>
# Contributor: Henrik Ronellenfitsch <henrik at searinox dot de>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=mdf2iso
pkgver=0.3.1
pkgrel=1
pkgdesc='Converts Alcohol 120% .mdf files to .iso files'
arch=('x86_64')
# Previously located at http://mdf2iso.berlios.de/
url='https://anonscm.debian.org/cgit/collab-maint/mdf2iso.git/'
license=('GPL')
depends=('glibc')
makedepends=('git')
source=("git://anonscm.debian.org/collab-maint/mdf2iso.git#tag=upstream/$pkgver")
sha256sums=('SKIP')

build() {
  cd "$pkgname"

  ./configure --prefix=/usr
  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# getver ok
# vim:set ts=2 sw=2 et:
