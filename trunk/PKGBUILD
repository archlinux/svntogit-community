# Maintainer: David Runge <dvzrv@archlinux.org>

_name=xkbcommon
pkgname=python-xkbcommon
pkgver=0.5
pkgrel=1
pkgdesc="Python bindings for libxkbcommon using cffi"
arch=(x86_64)
url="https://github.com/sde1000/python-xkbcommon"
license=(MIT)
depends=(python-cffi libxkbcommon)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
options=(debug)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('6bf7008d3dd665a6d448a7c1e939e0432e15d5b9aa3a57cee0b4c85de59ad18086420ffed19726dceeb4283c188ce4098bc1a8432535f9ce0d8570abb9bb5089')
b2sums=('c50c09d67005737cd8c4eb1fce3452c6df5fd0b27a0cc2ae4c64312ad6c86550db684d5798d21e73710d21fef514ad68f1706f9e26a3681f5c50210a93e7d288')

build() {
  cd $_name-$pkgver
  python $_name/ffi_build.py
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
