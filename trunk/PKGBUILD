# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=algebraic-graphs
pkgname=haskell-algebraic-graphs
pkgver=0.5
pkgrel=120
pkgdesc="A library for algebraic graph construction and transformation"
url="https://github.com/snowleopard/alga"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'uusi' 'haskell-extra' 'haskell-inspection-testing' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        ghc9.patch)
sha256sums=('89b9fecf8245476ec823355125fcb95decf41fd9784e807d7bd0d09f0a79c50b'
            'cdc1730dffc2365dee644e8457f2bcc210674bf07c4b5bb4c73f1cb4f7667357')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u QuickCheck $_hkgname.cabal
  sed -i '/instance Arbitrary a => Arbitrary (Tree a) where/,+13d' test/Algebra/Graph/Test/Arbitrary.hs
  patch -p1 -i ../ghc9.patch
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
