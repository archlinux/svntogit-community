# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-notiffany
pkgver=0.1.3
_commit=c726a05138e55c7247bd7e370aa21b1586d86ea5
pkgrel=2
pkgdesc='Wrapper libray for most popular notification libraries such as Growl, Libnotify, Notifu'
arch=(any)
url='https://github.com/guard/notiffany'
license=(MIT)
depends=(ruby-nenv ruby-shellany)
makedepends=(git)
checkdepends=(ruby-rake ruby-rspec tmux)
options=(!emptydirs)
source=(git+https://github.com/guard/notiffany.git#commit=$_commit)
sha256sums=('SKIP')

prepare() {
  cd notiffany
  sed -i -e 's|~>|>=|' notiffany.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd notiffany
  gem build notiffany.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    notiffany-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd notiffany
  # https://github.com/guard/notiffany/issues/44
  GEM_HOME="tmp_install/$_gemdir" rake || echo "Tests failed"
}

package() {
  cd notiffany
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
