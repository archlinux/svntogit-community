# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Andrews <christophersimonandrews [at] gmail [dot] com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Serge Victor <arch@random.re>
# Contributor: Krzysztof Wloch <wloszekk [at] gmail [dot] com>
# Contributor: Luke McCarthy <luke@iogopro.co.uk>

pkgname=fasm
pkgver=1.72
pkgrel=1
pkgdesc='Fast and efficient self-assembling x86 assembler for DOS, Windows and Linux operating systems'
url='https://flatassembler.net/'
arch=('i686' 'x86_64')
license=('custom')
depends_i686=('glibc')
depends_x86_64=('lib32-glibc')
source=(${pkgname}-${pkgver}.tgz::https://flatassembler.net/${pkgname}-${pkgver}.tgz)
sha256sums=('31d40019be410a5127734c23751b0b1e8ad0c279938351175a4d0fd9a58662a8')
sha512sums=('0569955ededc75c94fb669ec76e201f150453f7e5212a0c7bb181c7ac37cf42347ef4c841c832885d7a4e529254afd2d8c5484f56d0b48a7f2ba3b810960ccf2')

build() {
  cd ${pkgname}

  if [ "${CARCH}" == "x86_64" ]; then
    SRCDIR=Linux/x64
    ./fasm.x64 source/${SRCDIR}/fasm.asm
  elif [ "${CARCH}" == "i686" ]; then
    SRCDIR=Linux
    ./fasm source/${SRCDIR}/fasm.asm
  fi
  mv source/${SRCDIR}/fasm ./fasm

  # tools
  local s
  for s in listing prepsrc symbols; do
    ./fasm tools/libc/${s}.asm ${s}.o
    gcc -m32 ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -o ${s} ${s}.o
  done
}

package() {
  cd ${pkgname}

  install -Dm 755 fasm -t "${pkgdir}/usr/bin"
  # tools
  local s
  for s in listing prepsrc symbols; do
    install -Dm 755 ${s} "${pkgdir}/usr/bin/fasm-${s}"
  done

  # docs
  install -Dm 644 ${pkgname}.txt whatsnew.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/elfexe/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe"
  install -Dm 644 examples/elfexe/dynamic/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe/dynamic"
  install -Dm 644 examples/elfexe/dynamic/*.inc -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe/dynamic"
  install -Dm 644 examples/elfobj/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfobj"
  install -Dm 644 examples/libcdemo/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/libcdemo"
  install -Dm 644 examples/libcdemo/*.inc -t "${pkgdir}/usr/share/doc/${pkgname}/examples/libcdemo"
  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
