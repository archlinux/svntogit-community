# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch at gmail dot com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Alexander Mieland (dma147) <dma147@linux-stats.org>
# Contributor: slubman <slubman@slubman.net>

pkgname=iftop
pkgver=1.0pre4
pkgrel=3
pkgdesc="Display bandwidth usage on an interface"
arch=('x86_64')
url="http://www.ex-parrot.com/~pdw/iftop/"
license=('GPL')
depends=('libpcap' 'ncurses')
source=("http://www.ex-parrot.com/~pdw/$pkgname/download/$pkgname-$pkgver.tar.gz")

build() {
  cd $pkgname-$pkgver

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

sha512sums=('abd74e8025bb82fef9ebab4997b1d018201a523d47c0128128ca37797490046538d74758dc4471735c22b890e5bd238ad6b2a30776d465138ede367cdd263d22')
