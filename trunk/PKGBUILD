# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Abdelhakim Qbaich <abdelhakim@qbaich.com>
# Contributor: korjjj <korjjj+aur@gmail.com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Adam Ehlers Nyholm Thomsen <adament@gmail.com>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=3.1.3
pkgrel=1
pkgdesc='Double-entry accounting system with a command-line reporting interface'
arch=('i686' 'x86_64')
url='https://ledger-cli.org'
license=('BSD')
depends=('python2' 'boost' 'libedit')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ledger/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('b248c91d65c7a101b9d6226025f2b4bf3dabe94c0c49ab6d51ce84a22a39622b')

build() {
  cd "$pkgname-$pkgver"

  cmake . \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_INSTALL_LIBDIR:PATH=lib \
    -DUSE_PYTHON:BOOL=TRUE \
    -DDISABLE_ASSERTS:BOOL=TRUE \
    -DBUILD_EMACSLISP:BOOL=TRUE
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
