# Maintainer: Anatol Pomozov

pkgname=aws-sdk-cpp
pkgver=1.11.32
pkgrel=1
pkgdesc='AWS SDK for C++'
arch=(x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache)
depends=(openssl curl zlib util-linux-libs aws-c-common aws-c-event-stream libpulse aws-crt-cpp)
makedepends=(cmake ninja)
source=(aws-sdk-cpp-$pkgver.zip::https://github.com/aws/aws-sdk-cpp/archive/$pkgver.zip)
sha256sums=('6f23d29e9659dd6ad3d30114d087367a4f0caeb99c2e8c190f4e3f67ff6132b5')

build() {
  cd aws-sdk-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCUSTOM_MEMORY_MANAGEMENT=OFF -DENABLE_TESTING=OFF -DBUILD_DEPS=OFF \
      -DCMAKE_MODULE_PATH=/usr/lib/cmake \
      -S . -B build -G Ninja
  ninja -C build
}

package() {
  cd aws-sdk-cpp-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
