# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Luca Fulchir <luca@fulchir.it>

pkgname=dnssec-tools
pkgver=2.2
pkgrel=12
pkgdesc="libval & dnssec management tools"
arch=('x86_64')
url="http://www.dnssec-tools.org"
license=('BSD')
conflicts=('libval' 'libval_shim' 'libsres')
backup=('etc/dnssec-tools/dnssec-tools.conf' 'etc/dnssec-tools/resolv.conf' 'etc/dnssec-tools/root.hints' 'etc/dnssec-tools/dnsval.conf')
depends=('dnsutils' 'perl' 'perl-timedate' 'perl-net-dns' 'perl-net-dns-sec' 'perl-mailtools' 'bind' 'openssl-1.0' 'glibc' 'pth')
optdepends=('perl-tk: for GUI components'
            'perl-graphviz: for GUI components')
options=('!emptydirs')
source=(http://www.dnssec-tools.org/download/${pkgname}-${pkgver}.tar.gz
        makefile-dependency.patch
        dnssec-tools-link.patch
        glibc-2.25.patch)
sha256sums=('4f45033a7a27382fe5284de705a1792277dfbe9a149d369b95d787865a44ef66'
            'fb23565ff9afabf85dc812ab500a3652add4537e9519a49214e329de3d2171f9'
            'c499ce75b27d89b456c9b79d7051d63bdfeb35835e95e0cd312f1a55b1881c85'
            'd5485691e4d1604bc2e0d06a66068f9f930c94cb2012e4d89ffee5615542d296')
install="dnssec-tools.install"

prepare() {
  # patch for race condition with -j 2+
  cd "$srcdir/${pkgname}-${pkgver}"
  patch -p0 -i "$srcdir/makefile-dependency.patch"

  # patch for linking errors
  patch -p1 -i "$srcdir/dnssec-tools-link.patch"

  # patch for glibc 2.25 compatibility
  patch -p1 -i "$srcdir/glibc-2.25.patch"

  sed -e '/^maninstall:/,+3s:$(MKPATH) $(mandir)/$(man1dir):$(MKPATH) $(DESTDIR)/$(mandir)/$(man1dir):' \
      -i Makefile.in
  sed -e 's:/usr/local/etc:/etc:g' \
      -e 's:/usr/local:/usr:g' \
      -i tools/donuts/donuts \
      -i tools/etc/dnssec-tools/dnssec-tools.conf \
      -i tools/scripts/genkrf
  sed -e 's:/usr/bin/genkrf:/usr/bin/vendor_perl/genkrf:' \
      -e 's:/usr/bin/keyarch:/usr/bin/vendor_perl/keyarch:' \
      -e 's:/usr/bin/rollchk:/usr/bin/vendor_perl/rollchk:' \
      -e 's:/usr/bin/rollctl:/usr/bin/vendor_perl/rollctl:' \
      -e 's:/usr/bin/zonesigner:/usr/bin/vendor_perl/zonesigner:' \
      -e 's:/usr/sbin/dnssec-keygen:/usr/bin/dnssec-keygen:' \
      -e 's:/usr/sbin/rndc:/usr/bin/rndc:' \
      -e 's:/usr/sbin/named-checkzone:/usr/bin/named-checkzone:' \
      -e 's:/usr/sbin/dnssec-signzone:/usr/bin/dnssec-signzone:' \
      -i tools/etc/dnssec-tools/dnssec-tools.conf

}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -I/usr/include/openssl-1.0"
  ./configure \
    --exec_prefix=/usr \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --with-ipv6 \
    --with-nsec3 \
    --with-dlv \
    --with-perl-build-args=INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  cp validator/etc/dnsval.conf validator/etc/root.hints "${pkgdir}"/etc/dnssec-tools/

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cd "${pkgdir}/etc/dnssec-tools"
  mkdir KEY-SAFE
  touch KEY-SAFE/.pacmankeep

  ## add support for ISC DLV checking
  cat >> dnsval.conf <<EOF
##################################
# ISC DLV root anchor
##################################
: dlv-trust-points
    .   dlv.isc.org.
;
EOF
}
