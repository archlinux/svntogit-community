# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.1.3
pkgrel=2
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('ad75adc08e5d8daa985cca6b8e57a93f05e6465f629da676a305ee73a772618a432297a190695dd4b722cf4cc7f4affc87ad7bbb608fd2fd1e9b040d24a273cb')
b2sums=('2a2717417d42b389f0c4c7d9d474f35e44aafc902b1c650d446f40afd1d5e9b3d03ca7530ac4bc652e1514bf00cc399c6e96ed96bdb4c26d2c731714edd37979')

build() {
  local cmake_options=(
    -B build
    -S $pkgname-$pkgver
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
