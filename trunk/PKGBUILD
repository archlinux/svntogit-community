# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: FigoDaPaura <ffigoDaPaura>
# Contributor: Andres Jimenez

pkgname=virtualgl
pkgver=3.0.1
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card"
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('libxv' 'glu' 'libjpeg-turbo' 'xcb-util-keysyms' 'opencl-driver' 'ocl-icd' 'xorg-xhost')
makedepends=('cmake' 'mesa' 'libxtst' 'opencl-headers' 'ninja')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz"{,.sig}
        https://github.com/VirtualGL/virtualgl/commit/46d09529563127fef0d42a4fb2681ae8d65a856b.patch)
sha512sums=('d953b9c31ec3608a05da0069d83e7115a42e2c98bf169dcf97bf2d271cf7a5673e32d66247f0184aa40741597ca0732edb5f7992a82c5bc48bac25f1cb1889ef'
            'SKIP'
            'f17715b21f91439675981d2e6dad635cccbb36ddc012f037ff9d0859a21179f6881aac1ad7f5630e2311146bec7b10284bad982986b1e2f43beb3b24a0a8796b')
validpgpkeys=('AE1A7BA4EFFF9A9987E1474C4BACCAB36E7FE9A1') # The VirtualGL Project (Signing key for official binaries) <information@VirtualGL.org>

prepare() {
  cd "VirtualGL-$pkgver"

  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"

  patch -Np1 -i "$srcdir"/46d09529563127fef0d42a4fb2681ae8d65a856b.patch
}

build() {
  cd "VirtualGL-$pkgver"

  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DCMAKE_INSTALL_DOCDIR=/usr/share/doc/${pkgname} \
      -DVGL_FAKEXCB=1
  ninja -C build
}

package() {
  cd "VirtualGL-$pkgver"
  DESTDIR="$pkgdir" ninja -C build install

  mv "$pkgdir"/usr/bin/glxinfo "$pkgdir"/usr/bin/vglxinfo
  mv "$pkgdir"/usr/bin/eglinfo "$pkgdir"/usr/bin/veglinfo
}
