# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=zint
pkgname=(zint zint-qt)
pkgver=2.8.0
pkgrel=1
pkgdesc="Barcode encoding library supporting over 50 symbologies"
arch=('x86_64')
url="http://zint.org.uk/"
license=('GPL')
depends=('hicolor-icon-theme' 'qt5-base')
makedepends=('cmake' 'qt5-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        zint-qt.appdata.xml)
sha256sums=('0abb92df28e61dd3efb2bcaf324588ff77e5037cac7bb7098b8da814e622639c'
            '6c99514fb9079b6e6322b3b11fed5ff2c43cdcdd24c15e3cbf2663b884fc2f0c')

build() {
	cd $pkgbase-$pkgver
	cmake -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_zint() {
	depends=('libpng')

	cd $pkgbase-$pkgver
	make DESTDIR="$pkgdir" install

	rm "$pkgdir/usr/bin/zint-qt" \
	   "$pkgdir/usr/include/qzint.h" \
	   "$pkgdir/usr/lib/libQZint.so"*
}

package_zint-qt() {
	pkgdesc="Zint Barcode Studio GUI"
	depends=('hicolor-icon-theme' 'qt5-base' 'zint')

	cd $pkgbase-$pkgver
	make -C backend_qt DESTDIR="$pkgdir" install
	make -C frontend_qt DESTDIR="$pkgdir" install
	install -Dm644 zint.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/zint.png"
	install -Dm644 zint-qt.desktop "$pkgdir/usr/share/applications/zint-qt.desktop"
	install -Dm644 ../zint-qt.appdata.xml "$pkgdir/usr/share/metainfo/zint-qt.appdata.xml"
}
