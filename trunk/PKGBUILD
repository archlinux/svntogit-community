# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
pkgver=1.1.2
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
arch=(i686 x86_64)
url="http://freerdp.github.io/Remmina/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3' 'libgcrypt' 'libxdmcp')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
             'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib'
             'libvncserver')
optdepends=('libxkbfile'
            'freerdp'
            'telepathy-glib'
            'gnome-keyring'
            'nxproxy'
            'xorg-server-xephyr: XDMCP support'
            'libvncserver: VNC client/server support')
replaces=('remmina-plugins')
provides=('remmina-plugins')
install=remmina.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/Remmina/archive/v$pkgver.tar.gz"
	"freerdp-1.2.patch")
md5sums=('825b4dbee7053a059ac314ca11714219'
         '509ded89b6a2858b3091698d380da831')

prepare() {
  cd ${srcdir}/Remmina-$pkgver/
  for i in "${source[@]}"; do
    if [[ $i == *.patch ]]; then
      patch -F100 -p1 -i "${srcdir}/${i}"
    fi
  done
}

build() {
  cd ${srcdir}/Remmina-$pkgver/
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/Remmina-$pkgver/
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/build
}
