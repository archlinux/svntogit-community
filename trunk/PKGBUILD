# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>

pkgname=xmldiff
pkgver=0.6.10
pkgrel=4
pkgdesc="A python tool that figures out the differences between two similar XML files, in the same way the diff utility does it"
arch=('x86_64')
url="https://www.logilab.org/project/xmldiff/"
license=('GPL2')
depends=('python2' 'libxslt' 'jade')
optdepends=('docbook-xsl: render revision info')
#source=(https://ftp.logilab.org/pub/$pkgname/$pkgname-$pkgver.tar.gz)
source=(https://arch.p5n.pp.ru/~sergej/dl/2012/xmldiff-$pkgver.tar.gz)
sha512sums=('b183a6e94258c4d693fa97e759868dae2dca6088e3cf12cf08b40e82d18a375f02b12d62d0cfc03fb74a5138f32f6614f555b95a3062ba768d1cdaf3391cc95b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' main.py
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -m755 -d "$pkgdir/usr/share/man/man1"
  install -m644 man/*.1 "$pkgdir/usr/share/man/man1"
}
