# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>
# Contributor: rabyte <gmail.com: rabyte>

pkgname=freebasic
pkgver=1.08.0
_bootstrapver=1.07.2
pkgrel=1
pkgdesc="A free BASIC compiler"
arch=('x86_64')
url="https://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('ncurses')
makedepends=('freebasic' 'libxpm' 'gpm' 'libffi' 'libxrandr' 'mesa')
source=("https://downloads.sourceforge.net/fbc/FreeBASIC-$pkgver-source.tar.xz")
#source_x86_64=("https://downloads.sourceforge.net/fbc/FreeBASIC-${_bootstrapver}-linux-x86_64.tar.xz")
md5sums=('ca303315ce8707ca4a4abd4a361973c2')
sha512sums=('6d2732f6587935352e16db35dae081e897b046ec9d402b98ddce0a2e1e0b316503579e0ce6913ebb3b508063fdbe21e3bfaaca14cbf4356042bf57c7d544c439')
#md5sums_x86_64=('e327951b74992d18d2414cc08301c7e7')
#sha512sums_x86_64=('4da46df1763590e3dda739dc2f5c7d1f0e2779227c655210ac0335ec87e64be889b855fff7e82df087ade898825b5074239f33867aba7337112f950dbe76fc3c')

bootstrap() {
  [[ "$CARCH" = "x86_64" ]] && _arch=x86_64

  mkdir -p bootstrap
  cd "${srcdir}/FreeBASIC-${_bootstrapver}-linux-$_arch"
  ./install.sh -i "${srcdir}/bootstrap"
  # Hack!
  ln -s /usr/lib/libncursesw.so "${srcdir}/bootstrap/lib/libtinfo.so.5"
}

prepare() {
  #bootstrap
  :
}

build() {
  export PATH="${srcdir}/bootstrap/bin:$PATH"
  export LD_LIBRARY_PATH="${srcdir}/bootstrap/lib"

  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make CFLAGS="$CFLAGS $(pkg-config --cflags libffi)"
}

package() {
  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make install prefix="${pkgdir}/usr"
}
