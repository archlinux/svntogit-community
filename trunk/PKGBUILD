# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Nikita Volodin <volodin.n at gmail dot com>

pkgname=keycloak
pkgver=8.0.2
_postgresql_jdbc=42.2.10
pkgrel=1
pkgdesc="Open Source Identity and Access Management For Modern Applications and Services"
arch=('any')
url="https://www.keycloak.org/"
license=('Apache')
depends=('java-runtime>=8' 'grep' 'bash' 'coreutils' 'util-linux')
makedepends=('maven' 'git' 'java-environment=8')
backup=(
  opt/keycloak/standalone/configuration/application-roles.properties
  opt/keycloak/standalone/configuration/application-users.properties
  opt/keycloak/standalone/configuration/logging.properties
  opt/keycloak/standalone/configuration/mgmt-groups.properties
  opt/keycloak/standalone/configuration/mgmt-users.properties
  opt/keycloak/standalone/configuration/standalone-ha.xml
  opt/keycloak/standalone/configuration/standalone.xml
)
options=(emptydirs)
source=(https://github.com/keycloak/keycloak/archive/$pkgver/$pkgname-$pkgver.tar.gz
        keycloak.service
        keycloak.sysusers
        keycloak.tmpfiles
        layers.conf
        postgresql-module.xml
        https://jdbc.postgresql.org/download/postgresql-${_postgresql_jdbc}.jar)
sha512sums=('c035ef49531a87d27876ed828ff35a1238952f45f27c7df63cde5f304bb24d3234491f1e25695fbd89e7f0a0c7bda967e91ae9422b7640e6e846c3eccee4a291'
            '34d3fda501e0616bf5e4631f8dbe3a7f4c2b7566ffedc63e4f6ea49acbc6c1f3f75aabec22bb4b42d2aad586468ef5c7fddf29ca94253fcf141b71d88806a71a'
            'c53b734598ccf675930754514bd2af628355025197e77541e5acb8718629a8ece763c814e8373278758a30475e6a716febeff10bdebf0847d914bc193e732c79'
            '2f51757761d0444820e5624cdd061062d125a1ea74faf51c224603cae11583f99ff9ffbb79bb1834ae45dd6439b5d2db0f36f3a84c625df85fa9549e95308fea'
            'd54a04c120edb933c4f7168460bd813ff92a4e3771cedcabfeeb76c5d3e057b3cd0d0486dc11ddbd91e8d87ca90193e4c74cc3414e45106dd556d455ac7e60b2'
            '5eece4f3eb57372969f0e5319804198b546455828e938ff7f37bbcfb58be08efde43a5b0376c98f359c4bc5eb069e0cd9410daefe19da1c03e9e9c57d3c1b75b'
            '91638625fe7c95ce5199ed47cc6d2a9ad25c01f3f6fea4196116d304f21fcc330a2d8550eff66568690ba8bca52f253a6329c0a0e1c70c8bda155fdf9b750ee7')

build() {
  cd $pkgname-$pkgver

  export PATH="/usr/lib/jvm/java-8-openjdk/bin:$PATH"
  mvn -Pdistribution \
    -pl distribution/server-dist \
    -am \
    -Dmaven.test.skip \
    clean install
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/opt"
  cp -r "distribution/server-dist/target/keycloak-$pkgver" "$pkgdir/opt/keycloak"
  install -Dm 644 "$srcdir"/layers.conf -t "$pkgdir/opt/keycloak/modules"
  install -d "$pkgdir"/opt/keycloak/standalone/{data,log}

  install -Dm644 "$srcdir"/postgresql-${_postgresql_jdbc}.jar "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/postgresql-${_postgresql_jdbc}.jar
  sed "s/@JDBC_POSTGRESQL_JAR@/postgresql-${_postgresql_jdbc}.jar/g" "$srcdir"/postgresql-module.xml > "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/module.xml

  install -d "$pkgdir/"{etc,var/log}
  ln -s /opt/keycloak/standalone/configuration "$pkgdir/etc/keycloak"
  ln -s /opt/keycloak/standalone/log "$pkgdir/var/log/keycloak"

  install -Dm644 "$srcdir"/keycloak.service "$pkgdir"/usr/lib/systemd/system/keycloak.service
  install -Dm644 "$srcdir"/keycloak.sysusers "$pkgdir"/usr/lib/sysusers.d/keycloak.conf
  install -Dm644 "$srcdir"/keycloak.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/keycloak.conf
}

# vim: ts=2 sw=2 et:
