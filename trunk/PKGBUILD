# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: jsteel <mail at jsteel dot org>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Tdy  <tdy@gmx.com>
# Contributor: Biginoz <biginoz at free.fr>
# Contributor: Archan Paul <paul.archan@gmail.com>

pkgname=sweethome3d
pkgver=5.4
pkgrel=2
pkgdesc="An interior design application to draw the plan of your house in a 3D environment"
arch=('i686' 'x86_64')
url="http://www.sweethome3d.com/"
license=('GPL' 'custom')
depends=('java-runtime>=8' 'shared-mime-info' 'desktop-file-utils' 'libxext' 'libgl' 'libxrender')
source=("${pkgname}.sh"
        "${pkgname}.desktop"
        "${pkgname}.xml")
source_i686=("SweetHome3D-5.4-linux-x86.tgz"::"http://downloads.sourceforge.net/${pkgname}/SweetHome3D-${pkgver}-linux-x86.tgz")
source_x86_64=("SweetHome3D-5.4-linux-x64.tgz"::"http://downloads.sourceforge.net/${pkgname}/SweetHome3D-${pkgver}-linux-x64.tgz")
sha256sums=('c1a76d4131ddf04375bab3e98d646543b4967af623a6aee0e19bac7d40d7ecef'
            '5eea3337d956d773b05ddef69fe9d34b940ff550370dc92bf307f1b9a3957f9e'
            'ec0ad1a0671f708af68ced46bec1f4ab377e24ca1a0a9984867ee5fe484f57c5')
sha256sums_i686=('f5778277c11be1b8f069be1775964a9eae1f20e3d20eb030cffe16f8f5252fdf')
sha256sums_x86_64=('4f2f69495aac1cb2fd9a7c7235b596b1916fee4f77f348e8381cf136c56e5ae4')
install="${pkgname}.install"

package() {
  # Desktop and launchers
  cd "${srcdir}"
  install -m 755 -D "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -m 755 -D "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -m 644 -D "${pkgname}.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
  # Install licenses
  cd "SweetHome3D-${pkgver}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 755 -t "${pkgdir}/usr/share/licenses/${pkgname}" THIRDPARTY-LICENSE-*
  # Install icon
  install -m 644 -D "SweetHome3DIcon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # Install javaws.jar from jre8
  install -m 755 -d "${pkgdir}/usr/share/java/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgname}" jre8/lib/javaws.jar
  # Install main program and libraries
  cd "lib"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgname}" *.jar
  # Java3D 1.5 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/java3d-1.5"
  mv -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.5" \
    "${pkgdir}"/usr/share/java/${pkgname}/{j3dcore.jar,j3dutils.jar,vecmath.jar}
  install -m 755 -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.5" libj3dcore-ogl.so
  # Java3D 1.6 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/java3d-1.6"
  install -m 644 -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.6" java3d-1.6/*.jar
  install -m 755 -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.6" java3d-1.6/*.so
}
