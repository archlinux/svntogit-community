# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: wenLiangcan <boxeed at gmail dot com>

pkgbase=lua-linenoise
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
pkgver=0.9
pkgrel=8
_rockrel=1
pkgdesc='A binding for the linenoise command line library'
arch=(x86_64)
url="https://github.com/hoelzro/$pkgbase"
license=(MIT)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$pkgbase-$pkgver"
_rock="$_rockname-$pkgver-$_rockrel.linux-$CARCH.rock"
_rockspec="$_rockname-$pkgver-$_rockrel.rockspec"
source=("$_archive.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('cc1cdb4047edd056a10dcdeec853dbaf5088e2202941d579e4592584d733f09c')

build() {
	cd "$_archive"
	for LUAVER in 5.{1,2,3,4}; do
		luarocks --lua-version "$LUAVER" \
			make --pack-binary-rock --deps-mode none -- "$_rockspec"
		install -Dm0644 -t "lua-$LUAVER/" "$_rock"
	done
}

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}")
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		install --deps-mode none --no-manifest -- "lua-$1/$_rock"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING
}

package_lua-linenoise() {
  _package 5.4
}

package_lua51-linenoise() {
  _package 5.1
}

package_lua52-linenoise() {
  _package 5.2
}

package_lua53-linenoise() {
  _package 5.3
}
