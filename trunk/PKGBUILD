# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Denis A. Altoe Falqueto <denisfalqueto@gmail.com>

pkgbase=projectm
pkgname=('projectm' 'projectm-libvisual' 'projectm-pulseaudio' 'projectm-qt' 'projectm-test')
pkgver=2.1.0
pkgrel=3
arch=('x86_64' 'i686')
url='http://projectm.sourceforge.net/'
license=('LGPL')
makedepends=('mesa' 'qt' 'cmake' 'ftgl' 'glew' 'gtkglext' 'libvisual' 'sdl' 'libxext' 'pulseaudio')
source=("http://downloads.sourceforge.net/$pkgname/projectM-complete-$pkgver-Source.tar.gz"
        'projectm-test-opengl.patch'
        'projectm-install-vera-ttf.patch')
sha256sums=('513204f033006bd3dcdf8aada196d816d6b7187266ddcbb1594d0285cc9406ee'
            'c577d8356be011a3b3ee9f9b389db55f47804d100f690d8ea12f2920cdd432d1'
            '7d67aad0b210edf25a527274504c9efdf3e9d5b737235b938fec361ac5a8b110')
_projectmdir="projectM-complete-$pkgver-Source"

build() {
  cd "$srcdir/projectM-complete-$pkgver-Source"
  patch -p1 -i "$srcdir/projectm-test-opengl.patch"
  patch -p1 -i "$srcdir/projectm-install-vera-ttf.patch"

  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  cmake \
    -Wno-dev \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    "../projectM-complete-$pkgver-Source"
  make
}

package_projectm() {
  pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering'
  depends=('ftgl' 'glew')

  cd "$srcdir/build"
  
  for dirs in src/{NativePresets,libprojectM}; do
    make -C "$dirs" DESTDIR="$pkgdir" install
  done
}

package_projectm-libvisual() {
  pkgdesc='ProjectM plugin for XMMS'
  depends=('projectm' 'libvisual')
  
  cd "$srcdir/build/src/projectM-libvisual"
  make DESTDIR="$pkgdir" install
}

package_projectm-pulseaudio() {
  pkgdesc='ProjectM support for Pulseaudio'
  depends=('projectm' 'projectm-qt' 'pulseaudio')
  
  cd "$srcdir/build/src/projectM-pulseaudio"
  make DESTDIR="$pkgdir" install
}

package_projectm-qt() {
  pkgdesc='Qt bindings for ProjectM'
  depends=('projectm' 'qt')
  
  cd "$srcdir/build/src/projectM-qt"
  make DESTDIR="$pkgdir" install
}

package_projectm-test() {
  pkgdesc='ProjectM test applications'
  depends=('projectm' 'sdl')
  
  cd "$srcdir/build/src/projectM-test"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
