# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jo Christian Bergskås <jcberg@gmail.com>

pkgbase=fceux
pkgname=('fceux' 'gfceux')
pkgver=2.1.2
pkgrel=1
arch=('i686' 'x86_64')
url="http://fceux.com/web/htdocs/index.php"
license=('GPL')
depends=('sdl' 'zlib' 'zenity' 'lua' 'mesa')
makedepends=('scons')
source=(http://downloads.sourceforge.net/fceultra/fceux-${pkgver}.src.tar.bz2)
md5sums=('dbb9332e03c380de181e18ef3a75b6d1')
sha1sums=('4c784848c6386d5bb8f16dffeb7230bd5d211354')

build() {
  cd "${srcdir}/fceu"
  scons || return 1
}

package_fceux() {
  pkgdesc="A fast and ultra-compatible NES/Famicom emulator with SDL, OpenGL and SVGALIB support"
  optdepends=('xchm: for viewing the help manual')
  provides=('fceu')
  replaces=('fceu')

  cd "${srcdir}/fceu"
  sed -i "s|/usr/local|${pkgdir}/usr|" SConstruct || return 1
  scons install || return 1
  install -D -m644 bin/fceux.chm "${pkgdir}/usr/share/doc/fceux/fceux.chm" || return 1
}

package_gfceux() {
  pkgdesc="A GUI for the popular Nintendo Emulator, FCEUX"
  depends=('pygtk' 'fceux')
  provides=('gfceu')
  replaces=('gfceu')

  cd "${srcdir}/gfceux"
  python setup.py install --root="${pkgdir}" || return 1
}
