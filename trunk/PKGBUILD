# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Chris Brannon <cmbrannon (at) cox.net>

pkgname=epic4
pkgver=2.10.5
pkgrel=4
pkgdesc="It's a new direction in ircII development"
arch=('i686' 'x86_64')
url="http://www.epicsol.org/"
license=('custom')
depends=('openssl' 'ncurses' 'tcl' 'perl')
options=('!emptydirs')
source=(ftp://ftp.epicsol.org/pub/epic/EPIC4-PRODUCTION/$pkgname-$pkgver.tar.gz)
sha256sums=('411b5811728276af490d431938ed5e5984b90b0949947998143f6a70375a379c')

build() {
 cd "${srcdir}"/$pkgname-$pkgver

#to avoid build fail
  unset CPPFLAGS
 
  ./configure --prefix=/usr \
       --mandir=/usr/share/man \
       --with-ssl \
       --with-ipv6 \
       --with-perl \
       --with-tcl
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix="${pkgdir}"/usr \
       libexecdir="${pkgdir}"/usr/bin \
       mandir="${pkgdir}"/usr/share/man install

#license
  install -D -m644 COPYRIGHT "${pkgdir}"/usr/share/licenses/$pkgname/COPYRIGHT
}
