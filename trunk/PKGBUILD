# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Raffaele Zamorano
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Nikolay Korotkiy <sikmir@gmail.com>

pkgname=gpxsee
_pkgname=GPXSee
pkgver=5.9
pkgrel=1
pkgdesc='GPS log file viewer and analyzer'
arch=(x86_64)
url='http://www.gpxsee.org/'
license=(GPL3)
depends=(qt5-imageformats)
makedepends=(qt5-tools)
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('28b39c6f2730a4003788c6696a5b4b94494bf2f6e0d6b68842ede0e02d25d398')

build() {
  cd $_pkgname-$pkgver
  lrelease-qt5 gpxsee.pro
  qmake gpxsee.pro
  make
}

package() {
  cd $_pkgname-$pkgver
  install -dm755 "$pkgdir"/usr/share/$pkgname/{csv,maps,translations}
  install -Dm755 GPXSee "$pkgdir"/usr/bin/$pkgname
  install -Dm644 pkg/csv/* "$pkgdir"/usr/share/$pkgname/csv
  install -Dm644 pkg/maps/* "$pkgdir"/usr/share/$pkgname/maps
  install -Dm644 lang/*.qm "$pkgdir"/usr/share/$pkgname/translations
  install -Dm644 icons/gpxsee.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 pkg/gpxsee.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 pkg/gpxsee.xml "$pkgdir"/usr/share/mime/packages/$pkgname.xml
  install -Dm644 pkg/appdata.xml "$pkgdir"/usr/share/metainfo/$pkgname.appdata.xml
}
