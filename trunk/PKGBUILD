# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-yaspin
pkgver=1.5.0
pkgrel=1
pkgdesc="Yet Another Terminal Spinner"
url="https://github.com/pavdmyt/yaspin"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-dephell')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pavdmyt/yaspin/archive/v$pkgver.tar.gz")
sha512sums=('532de0b781e515835d088dbdac44b132c2cef8596d503ccec271f00245a19195c76fdabe0ba121f269d26d2686942fffcfac65402c3eb95ccfd90741f6f298a9')

prepare() {
  cd yaspin-$pkgver

  # poetry-generated setup.py is badly broken in several ways, including
  # distribution of tests in the built package as well as using distutils for
  # bad metadata. See https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd yaspin-$pkgver
  python setup.py build
}

check() {
  cd yaspin-$pkgver
  python setup.py pytest
}

package() {
  cd yaspin-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
