# Maintainer:  Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer:  Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jens Staal <staal1978@gmail.com>

pkgname=kernel-headers-musl
pkgver=3.12.6_6
pkgrel=1
pkgdesc="Linux kernel headers sanitized for use with musl libc"
arch=('x86_64')
url="https://github.com/sabotage-linux/kernel-headers"
license=('LGPL')
depends=('musl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sabotage-linux/kernel-headers/archive/v${pkgver/_/-}.tar.gz")
sha512sums=('866f01e5fc3f03eb191c0ce8e8a021b21d57bf5b38bdaf0fc75629f17409817358916177f48a90192dcd2678fc1ed3c917f1176ea3bd42b1ccfffe1a7eb2f8ba')

_CARCH=$CARCH
[[ $CARCH = i?86 ]] && _CARCH=x86

build() {
  cd "$srcdir"/kernel-headers-${pkgver/_/-}
  make ARCH=${_CARCH} prefix=/usr/lib/musl
}

package() {
  cd "$srcdir"/kernel-headers-${pkgver/_/-}
  make ARCH=${_CARCH} prefix=/usr/lib/musl DESTDIR="$pkgdir" install
}
