# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.38
pkgrel=4
pkgdesc='Deepin Window Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'deepin-menu' 'granite' 'deepin-mutter'
         'gnome-desktop' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz"
        vala.patch::https://github.com/linuxdeepin/deepin-wm/pull/65.patch)
sha512sums=('3eb4e47856611e0dc257ab079eff3166ed643aa8b55686519c917c3184cd5f238d01c3c8960d715a81264ff1337aa5d7c809a6559eab82e9decb1b9b8a33917d'
            '7cdfbbd0b42f8a132565d9e0187c821707d85fddc7664fc3f3cf48b292772289867fac72e0c3f8d55b20c143a220e0523ba01332fa18fba1bea543e5cc8aa97e')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../vala.patch
  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' src/Background/BackgroundSource.vala
}

build() {
  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"

  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
