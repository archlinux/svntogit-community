# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime
pkgver=3.0.3
pkgrel=1
pkgdesc='Bible study tool'
arch=('x86_64')
url='http://www.bibletime.info/'
license=('GPL2')
depends=('sword' 'clucene' 'qt5-webengine' 'qt5-svg')
makedepends=('cmake' 'docbook-xsl' 'expac' 'fop' 'ninja' 'po4a' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bibletime/bibletime/archive/v$pkgver.tar.gz")
sha256sums=('a2cecfd7a1096d42c6062849291d58c4ae320f77a2988c512925f7e0e28cfc81')

prepare() {
  mkdir -p build
}

build() {
  cd build
  local _xsl_shared=/usr/share/xml/docbook/xsl-stylesheets-$(expac %v docbook-xsl | cut -d - -f 1)
  cmake -GNinja ../bibletime-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBT_DOCBOOK_XSL_HTML_CHUNK_XSL=$_xsl_shared/html/chunk.xsl \
    -DBT_DOCBOOK_XSL_PDF_DOCBOOK_XSL=$_xsl_shared/fo/docbook.xsl
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
