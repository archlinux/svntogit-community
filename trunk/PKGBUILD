# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Contributor: Jonas Hahnfeld <hahnjo@hahnjo.de>

pkgname=lilypond
pkgver=2.24.0
pkgrel=1
pkgdesc="Music engraving program, devoted to producing the highest-quality sheet music possible"
arch=(x86_64)
url="https://lilypond.org"
license=(FDL1.3 GPL3 custom:OFL)
groups=(pro-audio)
depends=(gcc-libs
         ghostscript
         glibc
         gsfonts)
makedepends=(fontconfig
             fontforge
             freetype2
             glib2
             guile2.2
             imagemagick
             pango
             python
             rsync
             t1utils
             texinfo
             texlive-core
             texlive-langcyrillic
             zip)
optdepends=(
  'python: for lilypond-book and other scripts'
  'tex-gyre-fonts: for extra fonts'
  'ttf-dejavu: for extra fonts'
)
source=(https://lilypond.org/downloads/sources/v${pkgver%.*}/$pkgname-$pkgver.tar.gz)
sha512sums=('fc00736ae899519408781a7f1bf83065e493a857073d6f56cdadabc5817d4f41092dad12bfe210ec8a866f454b70f87ede6c7f1ba7aedc901134913b22c1431b')
b2sums=('2915d44e0e61a5756f515d31d11d29c657d763b7d7d84701ba67cb67c8e84faad74b8743a9703c4fe64ea4ef5f7c4ff76ed9675d62bd670aa6ea81b15385d348')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --disable-texi2html
  make all bytecode
}

package() {
  depends+=(libfontconfig.so libfreetype.so libglib-2.0.so libgobject-2.0.so
  libguile-2.2.so libpangoft2-1.0.so libpango-1.0.so)

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" vimdir="/usr/share/vim/vimfiles" install install-bytecode
  install -vDm 644 LICENSE.OFL -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,NEWS}.txt README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
