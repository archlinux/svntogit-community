# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=prettyprinter-ansi-terminal
pkgname=haskell-prettyprinter-ansi-terminal
pkgver=1.1.3
pkgrel=110
pkgdesc="ANSI terminal backend for the prettyprinter package."
url="https://github.com/quchen/prettyprinter/tree/master/prettyprinter-ansi-terminal"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-prettyprinter')
makedepends=('ghc' 'haskell-doctest')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('985e965f78c767234b9f30e9cc1590d037e00d211385ebfa833b96f9b9aa092840e2587236d848c66ff1c58c97748f6ae9bd5cc02ea5175aa66d2654392ea1b6')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-buildReadme
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
