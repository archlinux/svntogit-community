# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgname=python-apsw
pkgver=3.35.4
pkgrel=1
pkgdesc="Python wrapper for SQLite"
arch=('x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('python' 'sqlite')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
sha512sums=('4a566e90b7faf6a42b95fc496f76535a92a872e58e395d299cf8f0e3498d5659ba9f0a27970562f61fafa1ffae349201edd0fd1daa06d793a715b7eb5f561ecb')
b2sums=('44f41546949c4e53e28d9ffbd5d31848594748b1fe159f96b02162a44e36aa16b359251b2311bb610ba736080ee241bc758b3268c17aa81a8dea210f6aae64b3')

build() {
  cd apsw-${pkgver}-r1

  python setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw-${pkgver}-r1

  gcc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -fPIC -shared -o testextension.sqlext src/testextension.c
  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3*)
  PYTHONPATH="${pythonpaths[0]}" python setup.py test
}

package() {
  cd "$srcdir"/apsw-${pkgver}-r1

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
