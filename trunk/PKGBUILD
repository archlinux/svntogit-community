# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=stubby
pkgver=0.2.4
pkgrel=1
pkgdesc="DNS Privacy stub resolver"
arch=('x86_64')
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=('BSD')
backup=('etc/stubby/stubby.yml')
depends=('getdns')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/getdnsapi/stubby/archive/v${pkgver}.tar.gz")
sha256sums=('40816d403584aaa8ce72d6276b47e9d91b96294418af7a031dc48a3515398540')

prepare() {
    cd ${pkgname}-${pkgver}
    sed -e 's/^# appdata_dir:/appdata_dir:/g' -e 's:/etc/unbound/getdns-root.key:/etc/trusted-key.key:g' -i stubby.yml.example
}

build() {
    cd ${pkgname}-${pkgver}
    autoreconf -vfi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
    rmdir "${pkgdir}"/var{/run,}

    install -Dm644 systemd/${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
