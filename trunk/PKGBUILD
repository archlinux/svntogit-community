# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

pkgname=musescore
pkgver=3.0.2
pkgrel=1
pkgdesc='Create, play and print beautiful sheet music'
arch=(x86_64)
url='http://musescore.org/'
license=(GPL)
depends=(
  alsa-lib
  libpulse
  libsndfile
  libvorbisfile.so
  portaudio
  portmidi
  qt5-base
  qt5-declarative
  qt5-svg
  qt5-tools
  qt5-webengine
  qt5-xmlpatterns
  zlib
)
makedepends=(
  cmake
  doxygen
  git
  lame
  qt5-script
  texlive-core
)
optdepends=('lame: MP3 export')
source=(git+https://github.com/musescore/MuseScore.git#tag=v${pkgver})
sha256sums=('SKIP')

build() {
  make PREFIX='/usr' -C MuseScore release
}

package() {
  make DESTDIR="${pkgdir}" -C MuseScore/build.release install
}

# vim: ts=2 sw=2 et:
