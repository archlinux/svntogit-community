# $Id$
# Maintainer: Mihails Strasuns <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=('ldc' 'liblphobos')
groups=('dlang' 'dlang-ldc')
pkgver=1.1.0
epoch=1
pkgrel=1
pkgdesc="A D Compiler based on the LLVM Compiler Infrastructure including D runtime and libphobos2"
arch=('i686' 'x86_64')
url="https://github.com/ldc-developers/ldc"
license=('BSD')
depends=('libconfig')
makedepends=('cmake' 'llvm' 'dmd')
source=("https://github.com/ldc-developers/ldc/releases/download/v$pkgver/ldc-$pkgver-src.tar.gz"
        "ldc2.conf"
       )
sha256sums=('3b95216cd664e140dca321a6364c2238c442c972d6ccca8b9a65cb02d2e47112'
            '2ef3b1090e25187305f18ce6fbbbc45527dcbb33570afbe30e177790813948db')

build() {
    mkdir -p $srcdir/tmp_install_dir
    cd ldc-$pkgver-src

    mkdir -p build && cd build

    # workaround for wrong ld flag format generated by cmake:
    export LDFLAGS="-L-O1 -L--sort-common -L--as-needed -L-z -Lrelro"

    cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DINCLUDE_INSTALL_DIR=/usr/include/dlang/ldc \
    -DBUILD_SHARED_LIBS=ON \
    ..
    make 
    make install DESTDIR=$srcdir/tmp_install_dir
}

package_ldc() {
    depends=('liblphobos' 'libconfig' 'llvm-libs')
    backup=("etc/ldc2.conf")
    provides=("d-compiler")

    cd tmp_install_dir

    # binaries
    install -D -m755 ./usr/bin/ldmd2 $pkgdir/usr/bin/ldmd
    ln -s /usr/bin/ldmd $pkgdir/usr/bin/ldmd2
    install -D -m755 ./usr/bin/ldc2 $pkgdir/usr/bin/ldc
    ln -s /usr/bin/ldc $pkgdir/usr/bin/ldc2

    # supplementaries
    install -D -m644 $srcdir/ldc-$pkgver-src/bash_completion.d/ldc2 $pkgdir/usr/share/bash-completion/completions/ldc

    # licenses
    install -D -m644 $srcdir/ldc-$pkgver-src/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    

    # default configuration files
    install -D -m644 $srcdir/ldc2.conf $pkgdir/etc/ldc2.conf
}

package_liblphobos() {
    provides=("d-runtime" "d-stdlib")
    replaces=("liblphobos-devel")
    conflicts=("liblphobos-devel")
    depends=("curl")

    # licenses
    install -D -m644 $srcdir/ldc-$pkgver-src/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    

    cd tmp_install_dir

    # libraries
    install -D -m644 ./usr/lib/libphobos2-ldc.so $pkgdir/usr/lib/liblphobos2.so
    install -D -m644 ./usr/lib/libdruntime-ldc.so $pkgdir/usr/lib/libldruntime.so
    install -D -m644 ./usr/lib/libphobos2-ldc-debug.so $pkgdir/usr/lib/liblphobos2-debug.so
    install -D -m644 ./usr/lib/libdruntime-ldc-debug.so $pkgdir/usr/lib/libldruntime-debug.so

    # imports
    mkdir -p $pkgdir/usr/include/dlang
    cp -r ./usr/include/dlang/ldc $pkgdir/usr/include/dlang/ldc
}
