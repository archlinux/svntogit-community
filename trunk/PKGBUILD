# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=bmon
pkgver=3.8
pkgrel=2
pkgdesc="Portable bandwidth monitor and rate estimator"
arch=('i686' 'x86_64')
url="http://github.com/tgraf/bmon/"
license=('MIT' 'BSD')
depends=('ncurses' 'libnl' 'confuse')
makedepends=('autoconf')
source=($pkgname-$pkgver.tar.gz::http://github.com/tgraf/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('6bb2bbdd619f279132e54f1fa83fc98c0f75003766f82d6b7cd36c8a46900453')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# Licenses
  install -d "${pkgdir}"/usr/share/licenses/$pkgname
  cp LICENSE.{BSD,MIT} "${pkgdir}"/usr/share/licenses/$pkgname
}
