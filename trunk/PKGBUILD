# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-annotation' 'python2-zope-annotation')
pkgver=4.4.1
pkgrel=2
pkgdesc="Object annotation mechanism"
arch=('any')
url="https://github.com/zopefoundation/zope.annotation"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-component' 'python2-zope-component'
             'python-zope-location' 'python2-zope-location' 'python-zope-proxy' 'python2-zope-proxy'
             'git')
checkdepends=('python-nose' 'python2-nose' 'python-zope-configuration' 'python2-zope-configuration'
              'python-zope-testing' 'python2-zope-testing')
source=("git+https://github.com/zopefoundation/zope.annotation.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.annotation{,-py2}
}

build() {
  cd "$srcdir"/zope.annotation
  python setup.py build

  cd "$srcdir"/zope.annotation-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.annotation
  nosetests3

  cd "$srcdir"/zope.annotation-py2
  nosetests2
}

package_python-zope-annotation() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-component'
           'python-zope-location' 'python-zope-proxy')

  cd "$srcdir/zope.annotation"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-annotation() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-component'
           'python2-zope-location' 'python2-zope-proxy')

  cd "$srcdir/zope.annotation-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
