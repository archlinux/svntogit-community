# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=oidentd
pkgver=2.0.8
pkgrel=9
pkgdesc='An RFC1413 compliant ident daemon'
arch=('i686' 'x86_64')
url="http://dev.ojnk.net/"
license=('GPL')
depends=('glibc' 'bash')
source=(http://downloads.sourceforge.net/sourceforge/ojnk/$pkgname-$pkgver.tar.gz{,.asc}
        service socket)

md5sums=('c3d9a56255819ef8904b867284386911'
         'SKIP'
         'f54c5ed2a532a559c837af18735a28f1'
         '4442fb9fc10754914568e6ffc3d5fc42')

validpgpkeys=('7A4533F19780BF32793C1820DAD25608075C69BC') # Ryan McCabe

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/oidentd@.service"
	install -D -m644 "$srcdir/socket" "$pkgdir/usr/lib/systemd/system/oidentd.socket"
}

