# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: shild <sxp@bk.ru>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

_hkgname=X11
pkgname=haskell-x11
pkgver=1.9.2
pkgrel=26
pkgdesc="A Haskell binding to the X11 graphics library."
arch=(x86_64)
url="https://hackage.haskell.org/cgi-bin/hackage-scripts/package/X11"
license=('BSD')
depends=('ghc-libs' "libx11" "libxinerama" "libxrandr" "libxss" "haskell-data-default")
conflicts=(haskell-x11-extras)
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/X11/$pkgver/X11-$pkgver.tar.gz)
sha512sums=('e9658c33b6dcbb6a42f1cc4a8976365506af94ddc15c32f73205b453934ef3232f3b383f8365b6aa6e69b51acfda4a0f3a8038f3164523b33781139a80582a41')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
