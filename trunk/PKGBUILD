# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Alexander Courtis

pkgname=interception-dual-function-keys
pkgver=1.4.0
pkgrel=1
pkgdesc="Interception plugin for dual-function keys: Tap for one key, hold for another"
arch=('x86_64')
url="https://gitlab.com/interception/linux/plugins/dual-function-keys"
license=('MIT')
depends=('interception-tools')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/interception/linux/plugins/dual-function-keys/-/archive/$pkgver/dual-function-keys-$pkgver.tar.gz")
sha256sums=('81d400780ee8c38abe62261499e36efeb71ca18563b4cecd96355bbed95d422f')

build() {
    cd "dual-function-keys-$pkgver"
    make VERSION="$pkgver"
}

package() {
    cd "dual-function-keys-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir/" install
    install -Dm644 {README,doc/examples}.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
