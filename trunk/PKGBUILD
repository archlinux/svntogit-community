# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hoogle
pkgver=4.2.43
pkgrel=29
pkgdesc="Haskell API Search"
url="http://www.haskell.org/hoogle/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=7.10.3" "haskell-quickcheck" "haskell-aeson" "haskell-blaze-builder"
             "haskell-case-insensitive" "haskell-cmdargs" "haskell-conduit"
             "haskell-src-exts" "haskell-http-types" "haskell-old-locale" "haskell-parsec"
             "haskell-random" "haskell-resourcet" "haskell-safe" "haskell-shake"
             "haskell-tagsoup" "haskell-text" "haskell-uniplate" "haskell-vector"
             "haskell-vector-algorithms" "haskell-wai" "haskell-warp")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('eb30df565d363cd5d98821c51b0daf93493dec3bfe95c016922c95a20efa7c17')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "docs/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -r "$pkgdir"/usr/{lib,share/doc}
}
