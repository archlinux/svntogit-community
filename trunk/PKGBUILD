# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=svt-vp9
pkgver=0.1.0
pkgrel=2
pkgdesc='Scalable Video Technology VP9 encoder'
arch=(x86_64)
url=https://github.com/OpenVisualCloud/SVT-VP9
license=(BSD)
depends=(glibc)
makedepends=(
  cmake
  git
  nasm
)
source=(git+https://github.com/OpenVisualCloud/SVT-VP9.git#tag=4d7f6c6eb9501bfd1f58d4063cae050408b26552)
md5sums=(SKIP)

pkgver() {
  cd SVT-VP9

  git describe --tags | sed 's/^v//'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  sed '/CMAKE_BUILD_TYPE Release/d' -i SVT-VP9/CMakeLists.txt
}

build() {
  cd build

  export LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

  cmake ../SVT-VP9 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DNATIVE=OFF
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../SVT-VP9/LICENSE.md -t "${pkgdir}"/usr/share/licenses/svt-vp9/
}

# vim: ts=2 sw=2 et:
