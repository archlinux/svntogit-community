# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cor
pkgver=0.1.17
pkgrel=3
pkgdesc="Just another C++/C library"
arch=('x86_64')
url="https://git.merproject.org/mer-core/cor"
license=('LGPL')
depends=('systemd')
makedepends=('git' 'cmake' 'tut')
source=("git+https://git.merproject.org/mer-core/cor.git#tag=$pkgver" cor-gcc7.patch)
sha256sums=('SKIP'
            '1aaaf4b404b57b0b93d63211d1fe5919efb1441a7958a208bf07115cec55a4e2')

prepare() {
  mkdir build

  cd cor
  patch -p1 -i ../cor-gcc7.patch # Fix build with GCC 7 
}

build() {
  cd build

  cmake -DVERSION=$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ../cor
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
