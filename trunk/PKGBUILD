# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgbase=python-pillow
pkgname=(python-pillow python2-pillow)
pkgver=6.0.0
pkgrel=1
_appname=Pillow
_py2basever=2.7
_py3basever=3.7m
pkgdesc="Python Imaging Library (PIL) fork. Python3 version."
arch=('x86_64')
url="https://python-pillow.github.io/"
license=('BSD')
depends=('lcms2' 'libtiff' 'openjpeg2' 'libimagequant')
optdepends=('freetype2: for the ImageFont module'
            'libraqm: for complex text scripts'
            'libwebp: for webp images'
            'tk: for the ImageTK module')
makedepends=('python-setuptools' 'python2-setuptools' 'freetype2' 'libraqm' 'libwebp' 'tk')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/P/$_appname/$_appname-$pkgver.tar.gz")
md5sums=('9da5d2db0d7604ba553ece1ab7ca53a3')

prepare() {
  cd "$srcdir/${_appname}-$pkgver"
}

build() {
  cd "$srcdir/${_appname}-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$srcdir/${_appname}-$pkgver"

  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-${_py3basever%m}"
  python selftest.py
  pytest
  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-$_py2basever"
  python2 selftest.py
  pytest2
}

package_python-pillow() {
  depends+=('python')
  optdepends+=('python-olefile: OLE2 file support'
               'python-pyqt5: for the ImageQt module')
  cd "$srcdir/$_appname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py3basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py3basever/" src/libImaging/*.h
}

package_python2-pillow() {
  pkgdesc="Python Imaging Library (PIL) fork. Python2 version."
  depends+=('python2')
  optdepends+=('python2-olefile: OLE2 file support'
               'python2-pyqt5: for the ImageQt module')
  provides=('python-imaging' 'python2-imaging')
  conflicts=('python-imaging' 'python2-imaging')
  cd "$srcdir/${_appname}-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py2basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py2basever/" src/libImaging/*.h
}
