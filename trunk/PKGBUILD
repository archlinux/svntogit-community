# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-debtcollector
pkgname=('python-debtcollector' 'python2-debtcollector')
pkgver=1.4.0
pkgrel=1
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='http://docs.openstack.org/developer/debtcollector'
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel' 'python-wrapt' 'python2-wrapt' 'python2-funcsigs')
checkdepends=('python-testrepository' 'python2-testrepository')
source=("git+https://git.openstack.org/openstack/debtcollector#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a debtcollector{,-py2}
}

build() {
  cd "$srcdir"/debtcollector
  python setup.py build

  cd "$srcdir"/debtcollector-py2
  python2 setup.py build
}

check() {
  # Disabled for now, as it brings in too many dependencies

  cd "$srcdir"/debtcollector
  # python setup.py testr

  cd "$srcdir"/debtcollector-py2
  # python2 setup.py testr
}

package_python-debtcollector() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')

  cd debtcollector
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-debtcollector() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-wrapt' 'python2-funcsigs')

  cd debtcollector-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
