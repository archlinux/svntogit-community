# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>

pkgname=gbrainy
pkgver=2.06
pkgrel=1
pkgdesc="A brain teaser game and trainer to have fun and to keep your brain trained"
arch=('any')
url="http://live.gnome.org/gbrainy"
license=('GPL2')
depends=('gtk-sharp-2' 'mono-addins' 'librsvg' 'hicolor-icon-theme')
makedepends=('intltool' 'gettext' 'gnome-doc-utils')
install=gbrainy.install
source=(http://gent.softcatala.org/jmas/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('19d0eafcc50935fd711cc86c43ce7e2396847fd4516fb9fd54cb9f8402127c1f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export MONO_SHARED_DIR="$srcdir"
  ./configure --prefix=/usr --disable-gnome
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
