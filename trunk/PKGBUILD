# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Christoph Zeiler <arch at moonblade dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

_stopver=0.3.0
pkgname=aeolus
pkgver=0.9.5
pkgrel=1
pkgdesc="Synthesised pipe organ emulator"
arch=('x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio/aeolus/"
license=('GPL3')
depends=('clthreads' 'clxclient' 'jack' 'zita-alsa-pcmi')
source=(${url}../downloads/{$pkgname-$pkgver,stops-$_stopver}.tar.bz2 $pkgname.conf)
md5sums=('a190e3238a205289c3e0757110b84f5c'
         '2a7b1cae820408fa1cc655800d08d88f'
         'b9cbca97ac4184f0b207faf4f58f72a9')

prepare() {
  cd "$pkgname-$pkgver/source"
  # strip ldconfig and -march=native
  sed -i '/ldconfig\|-march=native/d' Makefile
}

build() {
  cd "$pkgname-$pkgver/source"
  make PREFIX=/usr LIBDIR=lib
}

package(){
  cd "$pkgname-$pkgver/source"
  make DESTDIR="$pkgdir/" PREFIX=/usr LIBDIR=lib install

  # system config file
  install -Dm644 "$srcdir/$pkgname.conf" "$pkgdir/etc/$pkgname.conf"

  # stops
  cd "$srcdir/stops-$_stopver"
  find . -name definition -exec install -Dm644 {} ${pkgdir}/usr/share/$pkgname/stops/{} \;
  find . -name presets -exec install -Dm644 {} ${pkgdir}/usr/share/$pkgname/stops/{} \;
  find . -name '*.ae0' -exec install -Dm644 {} ${pkgdir}/usr/share/$pkgname/stops \;
}
