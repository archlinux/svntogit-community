# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=2.9
pkgrel=3
pkgdesc="Lightweight and fast web browser"
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/"
license=('GPL')
depends=('libmng' 'curl' 'libglade' 'librsvg' 'desktop-file-utils' 'libnsbmp' 'libnsgif' 'libcss>=0.1.2' 'hubbub>=0.1.2' 'libwebp>=0.1.99')
makedepends=('re2c' 'lcms' 'gendesk>=0.4.2')
provides=('netsurf')
conflicts=('netsurf')
install=netsurf.install
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://download.$pkgname-browser.org/$pkgname/releases/source/$pkgname-$pkgver-src.tar.gz")
sha256sums=('f0dbcc5d80bf03d706aa8b28a322aa7f169a40813848c2d1505691f6e2c7ef00'
            'f74767161fd7f3186e483b438a183f2ea3cae160020ad5dd63c75a4d78f5c797')
_exec=('netsurf %U')
_genericname=('Web Browser')
_comment=('Lightweight web browser')
_mimetype=('text/html;application/xhtml+xml;x-scheme-handler/http;x-scheme-handler/https;x-scheme-handler/geo')

build() {
  cd "$srcdir"

  gendesk
  cd "$pkgname"
  make PREFIX=/usr TARGET=gtk NETSURF_USE_WEBP=YES
}

package() {
  cd "$srcdir/$pkgname"

  make install PREFIX=/usr DESTDIR="$pkgdir"
  install -Dm644 "../$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
