# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-initial-setup
pkgver=3.36.4
pkgrel=1
pkgdesc="Simple, easy, and safe way to prepare a new system"
arch=(x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(nm-connection-editor accountsservice cheese gnome-desktop libgweather
         gnome-online-accounts libgdm libpwquality libibus libgnomekbd
         libpackagekit-glib geoclue malcontent)
makedepends=(meson)
optdepends=('gdm: GDM Initial Setup mode'
            'gnome-getting-started-docs: GNOME Welcome Tour')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('badc7e1dab82dfb05f53608ee923750bbc8d1c8f35422bf93cf1f2a2c4ff5f9f')

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # Setup system user and group
  echo 'u gnome-initial-setup - "GNOME Initial Setup" /run/gnome-initial-setup' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /run/gnome-initial-setup 0700 gnome-initial-setup gnome-initial-setup -' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
