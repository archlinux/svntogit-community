# Maintainer: Anatol Pomozov
# Contributor: Alad Wenter <alad (at) archlinux.info>
# Contributor: Jochen Schalanda <jochen+aur (at) schalanda.name>
# Contributor: C. Dominik BÃ³di <dominik.bodi@gmx.de>
# Contributor: Pierre Carrier <pierre@spotify.com>
# Contributor: Thomas Dziedzic <gostrc (at) gmail>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Contributor: seblu <seblu+arch (at) seblu.net>
# Contributor: squiddo <squiddo (at) intheocean.net>
# Contributor: dront78 <dront78 (at) gmail.com>
# Contributor: hugelgupf <ckoch (at) cs.nmt.edu>

pkgname=dpkg
pkgver=1.21.1
pkgrel=1
pkgdesc='The Debian Package Manager tools'
arch=('x86_64')
url="https://tracker.debian.org/pkg/dpkg"
license=('GPL')
options=('emptydirs')
depends=('xz' 'zlib' 'bzip2' 'perl')
makedepends=('perl-io-string' 'perl-timedate' 'git')
checkdepends=('perl-io-string' 'perl-test-pod')
install=dpkg.install
source=(https://salsa.debian.org/dpkg-team/dpkg/-/archive/$pkgver/dpkg-$pkgver.tar.bz2
        origin.archlinux)
sha256sums=('a2d63470df5f8280608c8346d39994b28ce80bf24b0bed2040dc96bae4e5107b'
            '193ac1929ea93157c0173db3c871d2f7fe53385391f48078190b03904c5b1f2d')

prepare() {
    cd dpkg-$pkgver
    echo "$pkgver" > .dist-version
    ./autogen
}

build() {
    cd dpkg-$pkgver
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib \
        --disable-start-stop-daemon
    make
}

check() {
    cd dpkg-$pkgver
    # make check
}

package() {
    cd dpkg-$pkgver
    make DESTDIR="$pkgdir" install
    install -Dm644 ../origin.archlinux "$pkgdir"/etc/dpkg/origins/archlinux
    ln -sf archlinux "$pkgdir"/etc/dpkg/origins/default

    install -d "$pkgdir/var/dpkg"/updates/
    touch "$pkgdir/var/lib/dpkg"/{status,available}
}
