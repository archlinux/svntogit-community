# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=stlink
pkgrel=1
pkgver=1.1.0
pkgdesc='Firmware programmer for STM32 STLINK v1/v2 protocol'
arch=(i686 x86_64)
url='https://github.com/texane/stlink'
license=(BSD)
depends=(libusb)
install=stlink.install
source=($pkgname-$pkgver.zip::https://github.com/texane/stlink/archive/$pkgver.zip)
sha256sums=('a95ee75b67068649f6557cd886c4940cae024532202b2dc141cc558ebde03f4d')

build() {
  cd stlink-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd stlink-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 stlink_v1.modprobe.conf  "$pkgdir"/usr/lib/modprobe.d/stlink_v1.modprobe.conf
  install -Dm644 49-stlinkv1.rules        "$pkgdir"/usr/lib/udev/rules.d/49-stlinkv1.rules
  install -Dm644 49-stlinkv2.rules        "$pkgdir"/usr/lib/udev/rules.d/49-stlinkv2.rules
  install -Dm644 LICENSE                  "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 README                   "$pkgdir"/usr/share/doc/$pkgname/README
}
