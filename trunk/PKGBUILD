# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=vivid
pkgver=0.8.0
pkgrel=1
pkgdesc='LS_COLORS manager with multiple themes'
arch=('x86_64')
url=https://github.com/sharkdp/vivid
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=("$url/archive/v$pkgver/vivid-$pkgver.tar.gz")
b2sums=('e5820e22c384157ab9d5862c4423805ca33365164133f25bf6933b03c8f77c413e89a85934312f3b6544d414c811c67ed2374abc5ebebc65bf570010feba5571')

prepare() {
  cd vivid-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd vivid-$pkgver
  cargo build --release --locked --offline
}

package() {
  cd vivid-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/vivid
  install -Dm644 -t "$pkgdir"/usr/share/licenses/vivid LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
