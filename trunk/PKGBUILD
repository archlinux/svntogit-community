# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-mgba
pkgver=6706
pkgrel=1
pkgdesc='Nintendo Game Boy Advance core'
arch=(x86_64)
url=https://github.com/libretro/mgba
license=(custom:MPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
source=(libretro-mgba::git+https://github.com/libretro/mgba.git#commit=7ad318f5f683b447db7bd8ef4a7a8f3570e8eca9)
sha256sums=(SKIP)

pkgver() {
  cd libretro-mgba

  git rev-list --count HEAD
}

build() {
  make -C libretro-mgba -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mgba/mgba_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mgba/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mgba/
}

# vim: ts=2 sw=2 et:
