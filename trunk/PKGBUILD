# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Streetwalrus <streetwalrus@codewalr.us>

pkgname=shotgun
pkgver=2.3.0
pkgrel=1
pkgdesc='Minimal X screenshot utility'
arch=('x86_64')
url=https://github.com/neXromancers/shotgun
license=('MPL2')
depends=('gcc-libs' 'libxrandr')
makedepends=('git' 'rust')
optdepends=('hacksaw: for area/window selection'
            'slop: for area/window selection')
source=("shotgun-$pkgver.tar.gz::https://static.crates.io/crates/shotgun/shotgun-$pkgver.crate")
b2sums=('410a88cf3750b695a59c1079a345d57c3d32c3e83f6cc2aa3f04079198aa20fe9dbf94604bb1aac0714d5cfce39bff74ae0f2a0da1c44a24a5ac6c14e203aa1d')

prepare() {
  cd shotgun-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd shotgun-$pkgver
  cargo build --release --locked --offline
}

package() {
  cd shotgun-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/shotgun
}
