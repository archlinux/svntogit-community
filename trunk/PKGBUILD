# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: djgera
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.26
pkgrel=1
pkgdesc='Plugins for Geany'
arch=('x86_64' 'i686')
url='http://plugins.geany.org/'
license=('GPL')
depends=("geany>=$pkgver" 'gtkspell' 'webkitgtk2' 'ctpl' 'gpgme' 'vte'
         'desktop-file-utils' 'gtk-update-icon-cache' 'lua51' 'libgit2')
makedepends=('intltool' 'vala' 'gdb' 'cppcheck' 'libgit2')
optdepends=('hspell: hebrew spell checker')
install="$pkgname.install"
source=("http://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('b6ea0795da34e05f245c48b8868c023814c82f71aeb05ddd4b8e3a73a976e1bd')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib
  make 
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: -u 6 raw.githubusercontent.com/geany/geany-plugins/master/NEWS
# vim:set ts=2 sw=2 et:
