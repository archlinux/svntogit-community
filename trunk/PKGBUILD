# Maintainer: T.J. Townsend <blakkheim@archlinux.org>

pkgbase=nerd-fonts
pkgname=(otf-aurulent-nerd
         otf-cascadia-code-nerd
         otf-codenewroman-nerd
         otf-droid-nerd
         otf-firamono-nerd
         otf-hasklig-nerd
         otf-hermit-nerd
         otf-opendyslexic-nerd
         otf-overpass-nerd
         ttf-3270-nerd
         ttf-agave-nerd
         ttf-anonymouspro-nerd
         ttf-arimo-nerd
         ttf-bigblueterminal-nerd
         ttf-bitstream-vera-mono-nerd
         ttf-cousine-nerd
         ttf-daddytime-mono-nerd
         ttf-dejavu-nerd
         ttf-fantasque-nerd
         ttf-firacode-nerd
         ttf-go-nerd
         ttf-hack-nerd
         ttf-heavydata-nerd
         ttf-iawriter-nerd
         ttf-ibmplex-mono-nerd
         ttf-inconsolata-go-nerd
         ttf-inconsolata-lgc-nerd
         ttf-inconsolata-nerd
         ttf-iosevka-nerd
         ttf-jetbrains-mono-nerd
         ttf-lekton-nerd
         ttf-liberation-mono-nerd
         ttf-lilex-nerd
         ttf-meslo-nerd
         ttf-monofur-nerd
         ttf-monoid-nerd
         ttf-mononoki-nerd
         ttf-mplus-nerd
         ttf-noto-nerd
         ttf-profont-nerd
         ttf-proggyclean-nerd
         ttf-roboto-mono-nerd
         ttf-sharetech-mono-nerd
         ttf-sourcecodepro-nerd
         ttf-space-mono-nerd
         ttf-terminus-nerd
         ttf-tinos-nerd
         ttf-ubuntu-mono-nerd
         ttf-ubuntu-nerd
         ttf-victor-mono-nerd)
pkgdesc="Iconic font aggregator, collection, and patcher"
pkgver=2.3.1
pkgrel=2
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
license=('MIT')
options=('!strip')
provides=('ttf-font-nerd')
source=("3270-${pkgver}.zip::${url}/releases/download/v${pkgver}/3270.zip"
        "agave-${pkgver}.zip::${url}/releases/download/v${pkgver}/Agave.zip"
        "anonymouspro-${pkgver}.zip::${url}/releases/download/v${pkgver}/AnonymousPro.zip"
        "arimo-${pkgver}.zip::${url}/releases/download/v${pkgver}/Arimo.zip"
        "aurulent-${pkgver}.zip::${url}/releases/download/v${pkgver}/AurulentSansMono.zip"
        "bigblueterminal-${pkgver}.zip::${url}/releases/download/v${pkgver}/BigBlueTerminal.zip"
        "bitstreamveramono-${pkgver}.zip::${url}/releases/download/v${pkgver}/BitstreamVeraSansMono.zip"
        "cascadiacode-${pkgver}.zip::${url}/releases/download/v${pkgver}/CascadiaCode.zip"
        "codenewroman-${pkgver}.zip::${url}/releases/download/v${pkgver}/CodeNewRoman.zip"
        "cousine-${pkgver}.zip::${url}/releases/download/v${pkgver}/Cousine.zip"
        "daddytimemono-${pkgver}.zip::${url}/releases/download/v${pkgver}/DaddyTimeMono.zip"
        "dejavu-${pkgver}.zip::${url}/releases/download/v${pkgver}/DejavuSansMono.zip"
        "droid-${pkgver}.zip::${url}/releases/download/v${pkgver}/DroidSansMono.zip"
        "fantasque-${pkgver}.zip::${url}/releases/download/v${pkgver}/FantasqueSansMono.zip"
        "fira-${pkgver}.zip::${url}/releases/download/v${pkgver}/FiraCode.zip"
        "firamono-${pkgver}.zip::${url}/releases/download/v${pkgver}/FiraMono.zip"
        "go-${pkgver}.zip::${url}/releases/download/v${pkgver}/Go-Mono.zip"
        "hack-${pkgver}.zip::${url}/releases/download/v${pkgver}/Hack.zip"
        "hasklig-${pkgver}.zip::${url}/releases/download/v${pkgver}/Hasklig.zip"
        "heavydata-${pkgver}.zip::${url}/releases/download/v${pkgver}/HeavyData.zip"
        "hermit-${pkgver}.zip::${url}/releases/download/v${pkgver}/Hermit.zip"
        "iawriter-${pkgver}.zip::${url}/releases/download/v${pkgver}/iA-Writer.zip"
        "ibmplexmono-${pkgver}.zip::${url}/releases/download/v${pkgver}/IBMPlexMono.zip"
        "inconsolata-${pkgver}.zip::${url}/releases/download/v${pkgver}/Inconsolata.zip"
        "inconsolatago-${pkgver}.zip::${url}/releases/download/v${pkgver}/InconsolataGo.zip"
        "inconsolatalgc-${pkgver}.zip::${url}/releases/download/v${pkgver}/InconsolataLGC.zip"
        "iosevka-${pkgver}.zip::${url}/releases/download/v${pkgver}/Iosevka.zip"
        "jetbrainsmono-${pkgver}.zip::${url}/releases/download/v${pkgver}/JetBrainsMono.zip"
        "lekton-${pkgver}.zip::${url}/releases/download/v${pkgver}/Lekton.zip"
        "liberationmono-${pkgver}.zip::${url}/releases/download/v${pkgver}/LiberationMono.zip"
        "lilex-${pkgver}.zip::${url}/releases/download/v${pkgver}/Lilex.zip"
        "meslo-${pkgver}.zip::${url}/releases/download/v${pkgver}/Meslo.zip"
        "monofur-${pkgver}.zip::${url}/releases/download/v${pkgver}/Monofur.zip"
        "monoid-${pkgver}.zip::${url}/releases/download/v${pkgver}/Monoid.zip"
        "mononoki-${pkgver}.zip::${url}/releases/download/v${pkgver}/Mononoki.zip"
        "mplus-${pkgver}.zip::${url}/releases/download/v${pkgver}/MPlus.zip"
        "noto-${pkgver}.zip::${url}/releases/download/v${pkgver}/Noto.zip"
        "opendyslexic-${pkgver}.zip::${url}/releases/download/v${pkgver}/OpenDyslexic.zip"
        "overpass-${pkgver}.zip::${url}/releases/download/v${pkgver}/Overpass.zip"
        "profont-${pkgver}.zip::${url}/releases/download/v${pkgver}/ProFont.zip"
        "proggyclean-${pkgver}.zip::${url}/releases/download/v${pkgver}/ProggyClean.zip"
        "robotomono-${pkgver}.zip::${url}/releases/download/v${pkgver}/RobotoMono.zip"
        "sharetechmono-${pkgver}.zip::${url}/releases/download/v${pkgver}/ShareTechMono.zip"
        "sourcecodepro-${pkgver}.zip::${url}/releases/download/v${pkgver}/SourceCodePro.zip"
        "spacemono-${pkgver}.zip::${url}/releases/download/v${pkgver}/SpaceMono.zip"
        "terminus-${pkgver}.zip::${url}/releases/download/v${pkgver}/Terminus.zip"
        "tinos-${pkgver}.zip::${url}/releases/download/v${pkgver}/Tinos.zip"
        "ubuntu-${pkgver}.zip::${url}/releases/download/v${pkgver}/Ubuntu.zip"
        "ubuntumono-${pkgver}.zip::${url}/releases/download/v${pkgver}/UbuntuMono.zip"
        "victormono-${pkgver}.zip::${url}/releases/download/v${pkgver}/VictorMono.zip")
sha256sums=('525612b687be4b23331d7a396ffaa305f23dabd8d5b7fba0248e9d78e2c399c5'
            '615f18e3c2a3dc02440c32d64b81e8979f9c987072c4f547851974bd3403252b'
            '158a5a05174d8d134372ca723e17ddcd389612f4fe8cf0d4255e56aafb9341c7'
            'dd1cac458aa3893b8f174bc0ea0c3ab52e762360ac26c60a7f8f9f68a923ba28'
            '8582a21593c8019850fc3f6bbe8b6174e6dda98071ec561f82ada190f3964f81'
            'fa9883d49624bbc45dcb15233a1a1d9945ed778e3a91d29837a68a35b211178c'
            'f02fcc637c994bffd9980d6895b8ee3c10cc47f3b946aaf9b2233f757487bc09'
            'cf6cbed1f52fe7f9085df4e9cc2d5da5e56d82b3c048a39832a5b5dbfd636022'
            '28676e4e035324913296b88e6a9d838b4a5968bfa05e1ba39eb48ce8d3f6c21f'
            'e3887cf52e515d08a9c4a98cc8d0be9b914da4f20d029893e34147eb8ba021b3'
            '706725c95d7281736758a2721d662f3c25eb616d3cd52dac852769ad327641e2'
            'de6900a7c679957fe6b24bccc62d4c56d8a6b2ebac477677241989f02005e80f'
            '7a941f762663796a6a46d5ed5d9a6bbe8483b8ae208d46d318df6dad345b7b9e'
            '65ddcff3c1a3b29b20d3d5f6ae02758952fe51ff5612be93708487d9009d1272'
            '9d0018e5a299b582c42d6e3e80cd4f3b0a3489e14e0c8ad126869248fa11c172'
            '27cf672a21a7c653b1d00debef8fabe0b578180044e80a3fa9bc79ea9bd68432'
            'f7626dacb59acf77cc88474433afafeced2de3f54848da410765774b0f282af1'
            'f292638c838de6502031e7627721a409119e25416839714db6eec555c2187717'
            '86d6630ae872706d248ac19c6b2c3d6d1533f3f3d257e32b6e218ee95e383f20'
            'b65e7b617987e4fad8379d5893d1b0e6e9e996bb12f3a283315a16ecbab05cb9'
            '4d914aaa3ff721f644fd3b8010fdb3597689caceadb9c0037992aafb28dce63f'
            'ea8c78b736668dd1a49883a8057cf89198a5abc0ad7d1a47b711cb0fd300a426'
            '129b8bc4fb7f9ad68413932281c5c6c211ca0c5c34dc1987818e6bb181c7996f'
            'ac44777674779b786d5d4b0b522e016e9e582c965cb1f5d34a01c4e1f3c29658'
            '9e7b3924b570e549d76902a182bbcbb9ad3f2e673f481643302e349625c04de0'
            'd32989cb851df7ce372e706e7d1f009c272e3e5a9d22c540dc52f431f7f14978'
            '68e4575f0831b9c32f8add5e3842a4ee4c1d4f980362ced83fbac8149ef4083a'
            'f10282c2c8857e80f5b6c589944a0b8f17c88ec4865acf3309e2066066691f68'
            'be478aef1707ce11ffc3d9d70d886d8a98f24a4b892b21358529044bc4acade8'
            '598959cb069aee2f14dfc0a9ee19b6229ac269290766421a8b0ec112dcc4b11a'
            '7f03611fb726bafaec882fc0f057290cfe7282659589022b2ccbe206849c6f7d'
            '15cb5b4c6acc0cda2d5daf5e8963787cacade7e0012aca3825f2598a5fa424b5'
            'f20649010a54065dd30932e3c4de373674dcf6142322cec2f707162263d483f4'
            '642aac75f57735379ffd9f21eaf4e0b445c5e720b0937ce312121d775f6878a0'
            '8c605d7159e32e2b86cd2c5e6d8346ab8fa3c596f191142935e8b4dface0dd9b'
            '35559c4634e406bf493c4864ce2fe8f900eb6f00cbae2b75d30b0db81b59f7a8'
            'c9a14978112274aca6c49d75c20b7a3a06d97631fc5eef4e7bffb5605e44e49e'
            'b7ebd1ed6a1ae5d899ae51a55efc48e1329fe0d543406e340fed6d0020f4ff7a'
            '70f12bdf12ed0aad8a74b16169427d9aea592ab2b2e852962835b4f8929c68a6'
            '76e0d6a68de99ecc40690b847c2bb93813c4992fd8697bd4e2cfaf89a58135f3'
            '8192edd15f2bf94ccec1db6e29c63a82d7e73d2b9caaa6d7363d6c18db17d318'
            'ed39bddfc400222b81caa43d9c9a3cce6ad1fadfcc2fe46429bae703c7d798be'
            'd11d6f402b6122be39352cf8703fdd149bb2fcbb02c59323b67074eadd8a7948'
            '9f0d196e9edec1b9fa6f11edc67b4600ab3e3ebd008d641a3135a9297247be7d'
            'f3f74571a685b045b2dbf39c778576c227b45030a219cd282745ba2e81c413db'
            '3a89ee018ac71678f66a5f037f720eed530a2a7ffed6456c0749a9b365df1e03'
            '8cb947caff0eb80bda25b01aa235259c0f7dc8dba1f0bde695ca6c1e1e123186'
            'c43e8571b0c2e120de418240f1d3b7b092f250b7195b355a2752aa8dafcb886d'
            'f8dfd23f9f214f083d7ecdeb68b04bf5eb038a984c9c40000b6eef3c8b30ed67'
            '714ca4e2bf238c3538ee78288d30b5b24536baf5cd348ef17e7ba617bf00ce26')
noextract=("${source[@]%%::*}")

prepare() {
  # zip files don't include a font-specific directory, so we have to make them
  for _font in 3270 agave anonymouspro arimo aurulent bigblueterminal \
               bitstreamveramono cascadiacode codenewroman cousine \
               daddytimemono dejavu droid fantasque fira firamono go hack \
               hasklig heavydata hermit iawriter ibmplexmono inconsolata \
               inconsolatago inconsolatalgc iosevka jetbrainsmono lekton \
               liberationmono lilex meslo monofur monoid mononoki mplus noto \
               opendyslexic overpass profont proggyclean robotomono \
               sharetechmono sourcecodepro spacemono terminus tinos ubuntu \
               ubuntumono victormono; do
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.zip -C ${_font}
  done
  find "${srcdir}" -name '*Windows*' -delete
}

package_ttf-arimo-nerd() {
  pkgdesc="Patched font Arimo from nerd fonts library"
  conflicts=(nerd-fonts-arimo)
  replaces=(nerd-fonts-arimo)

  install -Dm644 arimo/*.ttf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 arimo/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-aurulent-nerd() {
  pkgdesc="Patched font Aurulent Sans Mono from nerd fonts library"

  install -Dm644 aurulent/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 aurulent/"SIL Open Font License.txt" \
          -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-firacode-nerd() {
  pkgdesc="Patched font Fira (Fura) Code from nerd fonts library"
  conflicts=(nerd-fonts-fira-code)
  replaces=(nerd-fonts-fira-code)

  install -Dm644 fira/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 fira/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-firamono-nerd() {
  pkgdesc="Patched font Fira (Fura) Mono from nerd fonts library"
  conflicts=(nerd-fonts-fira-mono)
  replaces=(nerd-fonts-fira-mono)

  install -Dm644 firamono/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 firamono/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-hack-nerd() {
  pkgdesc="Patched font Hack from nerd fonts library"
  conflicts=(nerd-fonts-hack)
  replaces=(nerd-fonts-hack)

  install -Dm644 hack/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 hack/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-iosevka-nerd() {
  pkgdesc="Patched font Iosevka from nerd fonts library"
  conflicts=(nerd-fonts-iosevka)
  replaces=(nerd-fonts-iosevka)

  install -Dm644 iosevka/*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
  install -Dm644 iosevka/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-terminus-nerd() {
  pkgdesc="Patched font Terminus (Terminess) from nerd fonts library"
  conflicts=(nerd-fonts-terminus)
  replaces=(nerd-fonts-terminus)

  install -Dm644 terminus/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 terminus/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-ubuntu-nerd() {
  pkgdesc="Patched font Ubuntu from nerd fonts library"
  conflicts=(nerd-fonts-ubuntu)
  replaces=(nerd-fonts-ubuntu)

  install -Dm644 ubuntu/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ubuntu/LICENCE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-ubuntu-mono-nerd() {
  pkgdesc="Patched font Ubuntu Mono from nerd fonts library"
  conflicts=(nerd-fonts-ubuntu-mono ttf-ubuntumono-nerd)
  replaces=(nerd-fonts-ubuntu-mono ttf-ubuntumono-nerd)

  install -Dm644 ubuntumono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ubuntumono/LICENCE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-3270-nerd() {
  pkgdesc="Patched font IBM 3270 from nerd fonts library"

  install -Dm644 3270/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 3270/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-agave-nerd() {
  pkgdesc="Patched font Agave from nerd fonts library"
  conflicts=(nerd-fonts-agave)
  replaces=(nerd-fonts-agave)

  install -Dm644 agave/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 agave/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-bigblueterminal-nerd() {
  pkgdesc="Patched font Big Blue Terminal from nerd fonts library"

  install -Dm644 bigblueterminal/*.TTF -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 bigblueterminal/LICENSE.TXT \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}

package_ttf-anonymouspro-nerd() {
  pkgdesc="Patched font Anonymous Pro (Anonymice) from nerd fonts library"
  conflicts=(nerd-fonts-anonymous-pro)
  replaces=(nerd-fonts-anonymous-pro)

  install -Dm644 anonymouspro/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 anonymouspro/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-bitstream-vera-mono-nerd() {
  pkgdesc="Patched font Bitstream Vera Mono from nerd fonts library"
  conflicts=(nerd-fonts-bitstream-vera-mono)
  replaces=(nerd-fonts-bitstream-vera-mono)

  install -Dm644 bitstreamveramono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 bitstreamveramono/"Bitstream Vera License.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/Bitstream Vera License.txt"
}

package_otf-cascadia-code-nerd() {
  pkgdesc="Patched font Cascadia Code (Caskaydia) from nerd fonts library"
  conflicts=(nerd-fonts-cascadia-code)
  replaces=(nerd-fonts-cascadia-code)

  install -Dm644 cascadiacode/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 cascadiacode/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_otf-codenewroman-nerd() {
  pkgdesc="Patched font Code New Roman from nerd fonts library"

  install -Dm644 codenewroman/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 codenewroman/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

package_ttf-cousine-nerd() {
  pkgdesc="Patched font Cousine from nerd fonts library"

  install -Dm644 cousine/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 cousine/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-daddytime-mono-nerd() {
  pkgdesc="Patched font Daddy Time Mono from nerd fonts library"

  install -Dm644 daddytimemono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 daddytimemono/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-dejavu-nerd() {
  pkgdesc="Patched font Dejavu Sans Mono from nerd fonts library"
  conflicts=(nerd-fonts-dejavu-complete)
  replaces=(nerd-fonts-dejavu-complete)

  install -Dm644 dejavu/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 dejavu/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-droid-nerd() {
  pkgdesc="Patched font Droid Sans Mono from nerd fonts library"
  conflicts=(nerd-fonts-droid-sans-mono)
  replaces=(nerd-fonts-droid-sans-mono)

  install -Dm644 droid/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 droid/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-fantasque-nerd() {
  pkgdesc="Patched font Fantasque Sans Mono from nerd fonts library"
  conflicts=(nerd-fonts-fantasque-sans-mono)
  replaces=(nerd-fonts-fantasque-sans-mono)

  install -Dm644 fantasque/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 fantasque/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-go-nerd() {
  pkgdesc="Patched font Go Mono from nerd fonts library"
  conflicts=(nerd-fonts-go-mono)
  replaces=(nerd-fonts-go-mono)

  install -Dm644 go/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 go/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-hasklig-nerd() {
  pkgdesc="Patched font Hasklig (Hasklug) from nerd fonts library"
  conflicts=(nerd-fonts-hasklig)
  replaces=(nerd-fonts-hasklig)

  install -Dm644 hasklig/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 hasklig/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-heavydata-nerd() {
  pkgdesc="Patched font Heavy Data from nerd fonts library"

  install -Dm644 heavydata/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 heavydata/"Vic Fieger License.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/Vic Fieger License.txt"
}

package_otf-hermit-nerd() {
  pkgdesc="Patched font Hermit from nerd fonts library"
  conflicts=(nerd-fonts-hermit)
  replaces=(nerd-fonts-hermit)

  install -Dm644 hermit/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 hermit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-iawriter-nerd() {
  pkgdesc="Patched font iA Writer (iM Writing) from nerd fonts library"

  install -Dm644 iawriter/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 iawriter/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-ibmplex-mono-nerd() {
  pkgdesc="Patched font IBM Plex Mono (Blex) from nerd fonts library"
  conflicts=(nerd-fonts-ibm-plex-mono)
  replaces=(nerd-fonts-ibm-plex-mono)

  install -Dm644 ibmplexmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ibmplexmono/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-inconsolata-nerd() {
  pkgdesc="Patched font Inconsolata from nerd fonts library"
  conflicts=(nerd-fonts-inconsolata)
  replaces=(nerd-fonts-inconsolata)

  install -Dm644 inconsolata/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolata/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-inconsolata-go-nerd() {
  pkgdesc="Patched font Inconsolata Go from nerd fonts library"
  conflicts=(nerd-fonts-inconsolata-go)
  replaces=(nerd-fonts-inconsolata-go)

  install -Dm644 inconsolatago/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolatago/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-inconsolata-lgc-nerd() {
  pkgdesc="Patched font Inconsolata LGC from nerd fonts library"
  conflicts=(nerd-fonts-inconsolatalgc)
  replaces=(nerd-fonts-inconsolatalgc)

  install -Dm644 inconsolatalgc/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolatalgc/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-jetbrains-mono-nerd() {
  pkgdesc="Patched font JetBrains Mono from nerd fonts library"
  conflicts=(nerd-fonts-jetbrains-mono)
  replaces=(nerd-fonts-jetbrains-mono)

  install -Dm644 jetbrainsmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 jetbrainsmono/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-lekton-nerd() {
  pkgdesc="Patched font Lekton from nerd fonts library"

  install -Dm644 lekton/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 lekton/"SIL Open Font License.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/SIL Open Font License.txt"
}

package_ttf-liberation-mono-nerd() {
  pkgdesc="Patched font Liberation Mono from nerd fonts library"
  conflicts=(nerd-fonts-liberation-mono)
  replaces=(nerd-fonts-liberation-mono)

  install -Dm644 liberationmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 liberationmono/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-lilex-nerd() {
  pkgdesc="Patched font Lilex from nerd fonts library"

  install -Dm644 lilex/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 lilex/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-meslo-nerd() {
  pkgdesc="Patched font Meslo LG from nerd fonts library"
  conflicts=(nerd-fonts-meslo)
  replaces=(nerd-fonts-meslo)

  install -Dm644 meslo/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 meslo/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-monofur-nerd() {
  pkgdesc="Patched font Monofur from nerd fonts library"
  conflicts=(nerd-fonts-monofur)
  replaces=(nerd-fonts-monofur)

  install -Dm644 monofur/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 monofur/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-monoid-nerd() {
  pkgdesc="Patched font Monoid from nerd fonts library"
  conflicts=(nerd-fonts-monoid)
  replaces=(nerd-fonts-monoid)

  install -Dm644 monoid/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 monoid/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-mononoki-nerd() {
  pkgdesc="Patched font Mononoki from nerd fonts library"
  conflicts=(nerd-fonts-mononoki)
  replaces=(nerd-fonts-mononoki)

  install -Dm644 mononoki/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 mononoki/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-mplus-nerd() {
  pkgdesc="Patched font MPlus from nerd fonts library"
  conflicts=(nerd-fonts-mplus)
  replaces=(nerd-fonts-mplus)

  install -Dm644 mplus/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 mplus/LICENSE_E "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-noto-nerd() {
  pkgdesc="Patched font Noto from nerd fonts library"
  conflicts=(nerd-fonts-noto)
  replaces=(nerd-fonts-noto)

  install -Dm644 noto/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 noto/LICENSE_OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE_OFL.txt"
}

package_otf-opendyslexic-nerd() {
  pkgdesc="Patched font OpenDyslexic from nerd fonts library"

  install -Dm644 opendyslexic/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 opendyslexic/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-overpass-nerd() {
  pkgdesc="Patched font Overpass from nerd fonts library"
  conflicts=(nerd-fonts-overpass)
  replaces=(nerd-fonts-overpass)

  install -Dm644 overpass/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 overpass/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-profont-nerd() {
  pkgdesc="Patched font ProFont from nerd fonts library"
  conflicts=(nerd-fonts-profont)
  replaces=(nerd-fonts-profont)

  install -Dm644 profont/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 profont/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-proggyclean-nerd() {
  pkgdesc="Patched font ProggyClean from nerd fonts library"

  install -Dm644 proggyclean/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 proggyclean/Licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/Licence.txt"
}

package_ttf-roboto-mono-nerd() {
  pkgdesc="Patched font Roboto Mono from nerd fonts library"
  conflicts=(nerd-fonts-roboto-mono)
  replaces=(nerd-fonts-roboto-mono)

  install -Dm644 robotomono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 robotomono/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-sharetech-mono-nerd() {
  pkgdesc="Patched font Share Tech Mono (Shure Tech Mono) from nerd fonts library"

  install -Dm644 sharetechmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 sharetechmono/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-sourcecodepro-nerd() {
  pkgdesc="Patched font Source Code Pro from nerd fonts library"
  conflicts=(nerd-fonts-source-code-pro)
  replaces=(nerd-fonts-source-code-pro)

  install -Dm644 sourcecodepro/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 sourcecodepro/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-space-mono-nerd() {
  pkgdesc="Patched font Space Mono from nerd fonts library"
  conflicts=(nerd-fonts-source-space-mono)
  replaces=(nerd-fonts-source-space-mono)

  install -Dm644 spacemono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 spacemono/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-tinos-nerd() {
  pkgdesc="Patched font Tinos from nerd fonts library"
  license=('Apache')
  conflicts=(nerd-fonts-source-tinos)
  replaces=(nerd-fonts-source-tinos)

  install -Dm644 tinos/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 tinos/"Apache License.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-victor-mono-nerd() {
  pkgdesc="Patched font Victor Mono from nerd fonts library"
  conflicts=(ttf-nerd-fonts-victor-mono)
  replaces=(ttf-nerd-fonts-victor-mono)

  install -Dm644 victormono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 victormono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
