# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-videos
pkgver=0.2.4
pkgrel=2
pkgdesc='The Pantheon Video Player'
arch=('x86_64')
url='https://github.com/elementary/videos'
license=('GPL3')
groups=('pantheon')
depends=('clutter' 'clutter-gst' 'clutter-gtk' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'gtk3' 'libgee' 'libx11'
         'libgranite.so')
makedepends=('cmake' 'intltool' 'vala')
replaces=('audience')
provides=('audience')
conflicts=('audience')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries')
source=("pantheon-videos-${pkgver}.tar.gz::https://github.com/elementary/videos/archive/${pkgver}.tar.gz"
        remove-glib-application-set-default.patch)
sha256sums=('b79c995cd48b4c4a5064a66673449f981ca5d1085e690c939544939061513a1f'
            'eb95236f813defcd9b2cb4dc58299188d0a8556d77e20c253127d09af1759df9')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
  cd videos-$pkgver
  patch -Np1 -i ../remove-glib-application-set-default.patch
}

build() {
  cd build

  cmake ../videos-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
