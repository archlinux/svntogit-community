# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-kkc
pkgver=5.0.11
pkgrel=1
pkgdesc="KKC Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-kkc"
license=('GPL')
depends=('fcitx5-qt' 'libkkc' 'libkkc-data')
makedepends=('extra-cmake-modules')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('64c67d41465f7d2fbac3e60cd4513d2bf7efc535d48e178e16be3ebd802dab6083833892d6bac4df7623748931bab6c1b052558de82f87f86fdc20b86aa12c8e'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
