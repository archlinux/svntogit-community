# $Id$
# Contributor: William Rea <sillywilly@gmail.com>
# Previous Maintainer: Bjorn Lindeijer <bjorn lindeijer nl>
# Previous Maintainer: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=ruby-sqlite3
pkgver=1.3.0
pkgrel=1
pkgdesc='A Ruby interface for the SQLite3 database engine.'
arch=('i686' 'x86_64')
url='http://rubyforge.org/projects/sqlite-ruby'
license=('BSD')
depends=('ruby' 'sqlite3')
makedepends=('chrpath')
source=("http://rubyforge.org/frs/download.php/71085/sqlite3-ruby-${pkgver}.tgz")
md5sums=('e0514ba69433cefcb79c8ef0be33417c')

build() {
  cd sqlite3-ruby-${pkgver}

  ruby setup.rb config

  ruby setup.rb setup
}

package() {
  cd sqlite3-ruby-${pkgver}

  ruby setup.rb install --prefix=${pkgdir}

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # fix insecure RPATH
  chrpath -r /usr/lib ${pkgdir}/usr/lib/ruby/site_ruby/1.9.1/${CARCH}-linux/sqlite3/sqlite3_native.so
}
