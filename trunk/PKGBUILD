# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
pkgver=0.4.1
pkgrel=1
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-jsonschema')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-isodate' 'python-rfc3339-validator' 'python-strict-rfc3339')
optdepends=(
  'python-isodate: validate datetime with isodate'
  'python-rfc3339-validator: validate datetime with rfc3339_validator'
  'python-strict-rfc3339: validate datetime with strict_rfc3339'
)
source=("https://github.com/p1c2u/openapi-schema-validator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e437309a6e741eaefa6b49bb038bd5976288bd6559fc6957d7029d9c0fed36ee368ac5fb26133a0fe54366da77ec649b5e44eb6a7926d9d5c49573afdfa3309b')

prepare() {
  cd openapi-schema-validator-$pkgver
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd openapi-schema-validator-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd openapi-schema-validator-$pkgver
  python -m pytest
}

package() {
  cd openapi-schema-validator-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
