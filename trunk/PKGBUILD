# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.3.0
pkgrel=3
pkgdesc="A personal finances manager for KDE"
arch=('i686' 'x86_64')
url="http://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qt5-tools' 'qca-qt5' 'qt5-script' 'libofx' 'grantlee-qt5'
         'kross' 'knewstuff' 'krunner' 'knotifyconfig' 'kdelibs4support' 'sqlcipher')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'python')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz
        $pkgname-gcc-fix.patch)
sha256sums=('76f57e2cede320848f1f1bef41b3be128aec9d59a8e643701b30bcae61f8a11a'
            '9fce467043903e4e82b75cc95c96fa57facef99867bb08243adb910bbca990b8')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
#https://bugs.kde.org/show_bug.cgi?id=359679
  patch -Np1 -i "${srcdir}"/$pkgname-gcc-fix.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  export
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=`kf5-config --prefix` \
          -DKDE_INSTALL_QTPLUGINDIR=`kf5-config --qt-plugins` \
          -DKDE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
