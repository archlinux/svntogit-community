# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Maarten de Vries <maarten@de-vri.es>

_pkg=filter-dkimsign
pkgname=opensmtpd-${_pkg}
pkgver=0.5
pkgrel=1
pkgdesc="OpenSMTPD filter for signing mail with DKIM"
license=(BSD)
url="https://imperialat.at/dev/filter-dkimsign/"
arch=(x86_64)
depends=(libopensmtpd openssl)
makedepends=(gzip)
source=(https://distfiles.sigtrap.nl/${_pkg}-${pkgver}.tar.gz
        LICENSE)
sha512sums=('35293bc51c6026c6846952ddf3a0fc92c99c88e9a573676e3e46034761318f32fe86563c2607175909c331ff0c1e2e6603173e286574a922528eae92bf6a4064'
            '24ffeb515e767416728adf4f02aac9b6305447ff4ce8acdfb1ecb8321f76e261e5a536b20df9acec90542e3c3f30e259c0d11cd99d401eb154fd5faf89a6e125')

build() {
  cd ${_pkg}-${pkgver}
  export CFLAGS="${CFLAGS} -I/usr/include/libopensmtpd"
  make -f Makefile.gnu
  gzip -kf filter-dkimsign.8
}

package() {
  cd ${_pkg}-${pkgver}
  install -Dm755 filter-dkimsign -t "${pkgdir}"/usr/lib/smtpd/opensmtpd/
  install -Dm644 filter-dkimsign.8.gz -t "${pkgdir}"/usr/share/man/man8/
  install -Dm644 ../LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
