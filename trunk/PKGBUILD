# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Hinrich Harms <arch@hinrich.de>
# Contributor: sylvain alborini <sylvain.alborini@gmail.com>

pkgname=gpscorrelate
pkgver=1.6.2
_gitrev=365f6e1b3fbf7015f80f0a19334f701d0482e095
pkgrel=1
pkgdesc="Correlate (geotagging) digital camera photos with GPS data in GPX format"
arch=('x86_64' 'i686')
url="https://github.com/freefoote/gpscorrelate"
license=('GPL2')
depends=('exiv2' 'gtk3')
makedepends=('libxslt')
source=($pkgname-$_gitrev.tar.gz::https://github.com/freefoote/$pkgname/archive/$_gitrev.tar.gz
        gtk3-port.patch)
sha256sums=('449023f21f5dd0a2149592754d7f94635697653b6dcfcd4432986e85c1155a4f'
            '19a707fae49d83e30cd71b3eead9345ca316fde8a1c3c960b90a05228a2765f8')

prepare() {
  cd $pkgname-$_gitrev
  patch -Np1 -i ../gtk3-port.patch
}

build() {
  cd $pkgname-$_gitrev
  make prefix=/usr
}

package() {
  cd $pkgname-$_gitrev
  make prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 gpscorrelate.desktop "$pkgdir/usr/share/applications/gpscorrelate.desktop"
  install -Dm644 gpscorrelate-gui.svg "$pkgdir/usr/share/pixmaps/gpscorrelate-gui.svg"
}
