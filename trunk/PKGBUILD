# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=3.3.3
_pkgver=3.3.3
pkgrel=6
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu' 'graphicsmagick' 'pango' 'zlib')
makedepends=('boost' 'cmake' 'postgresql-libs' 'fcgi' 'sqlite' 'mysql++' 'qt4' 'doxygen' 'mesa' 'glu')
optdepends=('openssl: for SSL support in built-in webserver'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'mysql++: for the hangman example'
	    'qt4: for the Wt/Qt interopability example (wtwithqt)')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/$pkgname-${_pkgver}.tar.gz
        boost-wt.patch::http://pastie.org/pastes/9478732/download?key=q3jk2mfp0ovfipigt8qsaw)
md5sums=('90211e80d0e14ef7ff2e3152380bd05d'
         '2d5d39f230c99355be7c20a5cc9aa409')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}

  patch -Np1 < $srcdir/boost-wt.patch

  [[ -d build ]] && rm -r build
  mkdir -p build && cd build
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DWT_CPP_11_MODE=-std=c++11 \
      -DWT_WRASTERIMAGE_IMPLEMENTATION=GraphicsMagick \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      ..
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}/build
  make DESTDIR=${pkgdir} install

  install -Dm644 ${pkgdir}/usr/cmake/FindWt.cmake ${pkgdir}/usr/share/cmake-3.1/Modules/FindWt.cmake
  rm -r ${pkgdir}/usr/cmake
  rm -rf $pkgdir/var/run
}
