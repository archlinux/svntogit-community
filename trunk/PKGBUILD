# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=3.18.4
pkgrel=1
pkgdesc='Golang bindings for dde-daemon'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill'
         'poppler-glib' 'xcur2png' 'blur-effect')
makedepends=('git' 'deepin-gettext-tools' 'golang-deepin-gir' 'golang-deepin-lib'
             'golang-github-linuxdeepin-go-dbus-factory' 'golang-github-linuxdeepin-go-x11-client'
             'golang-github-nfnt-resize' 'bzr' 'go-pie')
provides=('golang-deepin-dde-api')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-api/archive/$pkgver.tar.gz"
        deepin-api.sysusers)
sha512sums=('6125615a7c61f403ce8bd3128ca56810a0bd0fbbf8cd6af36092c88bdd6ed677d28d7451523e04ac1025070dc2871eb99c193532599e9d69369b343af2eee9f9'
            '548b4c8d06a10f0103d84fe6b68532ca71aae53347a6ad85dad1d274844146c9861d1bf6ce8e5518ea1b043aa082d705c534f0a4c0feb16a031537cee6befb08')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p build/src/pkg.deepin.io/dde/api
  cp -a dde-api-$pkgver/* build/src/pkg.deepin.io/dde/api/

  # golang-deepin-lib's dependency, remove when go packaging resumes
  go get github.com/cryptix/wav

  go get github.com/disintegration/imaging github.com/fogleman/gg
}

build(){
  cd dde-api-$pkgver
  make
}

package() {
  cd dde-api-$pkgver
  make DESTDIR="$pkgdir" SYSTEMD_LIB_DIR=/usr/lib install

  install -Dm644 ../deepin-api.sysusers "$pkgdir"/usr/lib/sysusers.d/deepin-api.conf
}
