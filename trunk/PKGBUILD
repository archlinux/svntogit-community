# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-simple
pkgver=1.302194
pkgrel=1
pkgdesc="Basic utilities for writing tests"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test-Simple"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test-Simple-$pkgver.tar.gz")
sha512sums=('c6543dfc14b6bd93d8e846a8bc38991a92b3c62389f5caaa2a4e4745277d393ada99ce085d4ba706704c66c6197c354dae6deede1cb9a3d6626035f15b5f1d9b')

build() {
  cd Test-Simple-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Simple-$pkgver
  make test
}

package() {
  cd Test-Simple-$pkgver
  make DESTDIR="$pkgdir" install
}
