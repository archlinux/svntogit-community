# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-maxitest
pkgver=4.4.0
pkgrel=1
pkgdesc='Minitest + all the features you always wanted'
arch=(any)
url='https://github.com/grosser/maxitest'
license=(MIT)
depends=(ruby-minitest)
makedepends=(ruby-rake ruby-rspec)
checkdepends=(procps-ng ruby-byebug)
options=(!emptydirs)
source=(https://github.com/grosser/maxitest/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ca9a1d3af2cd94cbe5eb8b72762bc755bd90ea72c1747446fae6b40570eafd70')

prepare() {
  cd maxitest-$pkgver
  sed -e 's/, "< 5.18.0"//' -e 's|git ls-files lib/ bin/ MIT-LICENSE Readme.md|find lib/ bin/ MIT-LICENSE Readme.md -type f|' -i maxitest.gemspec
  sed -i '/bump/d' Gemfile
  rm Gemfile.lock
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd maxitest-$pkgver
  gem build maxitest.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    maxitest-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd maxitest-$pkgver
  PATH="tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd maxitest-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
