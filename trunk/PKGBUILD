# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=backuppc
pkgver=3.2.1
pkgrel=1
pkgdesc='Enterprise-grade system for backing up Linux, Windows and MacOS PCs'
url='http://backuppc.sourceforge.net/'
license=('GPL2')
arch=('any')
depends=(
  'net-tools'
  'perl-archive-zip'
  'perl-time-modules'
  'perl-xml-rss'
  'start-stop-daemon'
)
optdepends=(
  'smbclient: used by samba transfert method'
  'rsync: used by rsync transfert method'
  'perl-file-rsyncp: used by rsync transfert method'
  'openssh: used by ssh transfert method'
  'par2cmdline: used by archive'
  'perl-io-dirent'
)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/BackupPC-$pkgver.tar.gz"
        'backuppc.conf'
        'backuppc.rc')
install=$pkgname.install
backup=("etc/$pkgname/config.pl"
        "etc/$pkgname/hosts")
md5sums=('2334fafb8e03284225a9b8a7fb230012'
         'bc7402cb4ec4b5f646e2794be67827d1')

package() {
  # install rc script
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 755 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"

  cd BackupPC-$pkgver
  #install -d -m 755 ${pkgdir}/var/opt
  perl configure.pl \
    --batch \
    --no-set-perms \
    --uid-ignore \
    --hostname __HOSTNAME__ \
    --compress-level 5 \
    --backuppc-user=backuppc \
    --dest-dir "$pkgdir" \
    --fhs \
    --install-dir /usr \
    --config-dir /etc/$pkgname \
    --cgi-dir /srv/http/$pkgname/cgi-bin \
    --html-dir /srv/http/$pkgname \
    --html-dir-url /backuppc \
    --data-dir /var/lib/$pkgname \
    --log-dir /var/log/$pkgname

  # set correct permissions
  chown 91 -R "$pkgdir"/etc/$pkgname
  chown 91 -R "$pkgdir"/var/{log,lib}/$pkgname
  chmod 640 "$pkgdir/etc/$pkgname/config.pl"
  chmod 754 "$pkgdir/srv/http/$pkgname/cgi-bin/BackupPC_Admin"
  chmod 755 -R "$pkgdir/usr/bin/" "$pkgdir/var/"
}

# vim:set ts=2 sw=2 ft=sh et:
