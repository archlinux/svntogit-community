# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.1.0.4
pkgrel=2
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
groups=('deepin')
depends=('deepin-qt5integration' 'deepin-wallpapers' 'kwin')
makedepends=('cmake' 'expac' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        $pkgname-tabbox-chameleon-rename.patch::https://github.com/linuxdeepin/dde-kwin/pull/106.patch
        $pkgname-build-fix.patch::https://github.com/linuxdeepin/dde-kwin/pull/109.patch
        $pkgname-unload-blur.patch::https://github.com/linuxdeepin/dde-kwin/pull/112.patch
        deepin-kwin-qt5.15.patch
        kwin-5.19.patch)
sha512sums=('0c997631bdfbafd6d34a472c6a3354b6f7f37bc0616ed45513ebea91593cbad32c7238ebfaef6fe578162402ff70eb6dcf39d3e3e19f9c4ae56dcfe3a3fe7e46'
            '7ac1a577868b73314274d336287334acc5bf4ac06dde046c93e4c2d3f68a112389c8b0644d1cbc3fde5ba1c01f368676f4f34b4a1b7b6c97a5b73d66507d134d'
            '3e10241050959b51bdd6bf752253af61a2b5899c3df5d84716bd1688d9292ff65d2b7d3aea8f61e896081a9bee013cb3f4c2f42f7915038f7f476120a44cf029'
            '800bb8770b4a6d835e27f33226f81dce92ddf10c295e02015419788e667e7d46b014d69bde73d03af8f0480c938526b48498368d2f16619689352f0e8e183912'
            '8fe73f833e2b24fe9bbaf12220a9d41d5e4808c833d7e7878635a85429e6c474ae79189d75d1d4e77c78bafbe2855db37ded24c85768f9a9415a00a0456c9dce'
            '834ca1372c4e17ec62edecf81d30814ddd063bdb7d4362da8260a6ac23c19600819cb9c62dd7070fc57ff20546667ad84db7cde6de543358298348ce18781aaf')

prepare() {
  cd dde-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' plugins/kwineffects/multitasking/background.cpp
  patch -p1 -i ../$pkgname-tabbox-chameleon-rename.patch
  patch -p1 -i ../$pkgname-build-fix.patch
  patch -p1 -i ../$pkgname-unload-blur.patch
  patch -p1 -i ../deepin-kwin-qt5.15.patch # Fix build with Qt 5.15
  patch -p1 -i ../kwin-5.19.patch
}

build() {
  cd dde-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DKWIN_VERSION=$(expac %v kwin | cut -d - -f 1)
  ninja
}

package() {
  cd dde-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/kwin_no_scale
}
