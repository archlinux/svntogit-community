# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=masscan
pkgver=1.0.5
pkgrel=1
pkgdesc='TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes'
url='https://github.com/robertdavidgraham/masscan'
arch=('x86_64')
license=('AGPL3')
depends=('libpcap')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/robertdavidgraham/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('55d58234994ebea10bcb3f45a24cdb51b020350faa116aee3a48861a119f66c339d1f533c978f52e37ccf5266be6fd2802765f13461d94eb28ec8d18c68e2f88')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|(CFLAGS )=|\1+=|g' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make CC=gcc
}

check() {
  cd ${pkgname}-${pkgver}
  make regress
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 doc/masscan.8 -t "${pkgdir}/usr/share/man/man8"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/masscan.8.markdown -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
