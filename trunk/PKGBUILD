# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Amish <contact at via dot aur>
# Contributor: Justin Davis <jrcd83@gmail.com>
# Contributor: Tony Sokhon <tonyskn@gmail.com>

pkgname=perl-par-packer
pkgver=1.051
pkgrel=1
pkgdesc='PAR Packager'
arch=('x86_64')
url='https://metacpan.org/release/PAR-Packer'
license=('GPL')
# This package links agains libperl.so
depends=('perl=5.32.0' 'perl-archive-zip' 'perl-getopt-argvfile' 'perl-ipc-run3' 'perl-module-scandeps'
          'perl-par' 'perl-par-dist')
makedepends=('perl-extutils-makemaker')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/R/RS/RSCHUPP/PAR-Packer-${pkgver}.tar.gz")
b2sums=('85f3f42db8eb37fe45b5227964b8f9adaebdeaa191a93da775fcfdd1f5c318eb81b056716b811bb6b66cc2f18483de7df7d8ab0c116e1068dd6adb3a8ca69ac9')

build() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  #remove conflicting and empty man page
  rm "${pkgdir}"/usr/share/man/man1/pp.1
}
