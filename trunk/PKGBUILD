# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zsolt Varadi <sysop_xxl@fibermail.hu>

pkgname=netbrake
pkgver=0.2
pkgrel=4
pkgdesc="Netbrake is an utility to limit the bandwidth used by a process"
arch=(i686 x86_64)
url="http://www.hping.org/netbrake/"
license=('GPL')
depends=(glibc)
makedepends=()
source=($url/$pkgname-$pkgver.tar.gz
	arch-x86_64.patch)
md5sums=('42f61481cdb910bddd1105d48367bdd2'
         'fe9de3428ff2a2ec8766d1b2bf8acfb5')

build() {
  cd $srcdir/$pkgname

  [ $CARCH == "x86_64" ] && patch -Np1 <../arch-x86_64.patch

  cp Makefile.in Makefile
  sed -i -e 's|LIBPATH=@LIBPATH@|LIBPATH=/usr/lib|g' Makefile
  sed -i -e 's|BINPATH=@BINPATH@|BINPATH=/usr/bin|g' Makefile
  sed -i -e 's|COMPILE_TIME=@COMPILE_TIME@|COMPILE_TIME=-DNB_HTTPFS|g' Makefile

  cp netbrake.h.in netbrake.h
  sed -i -e 's|#define LIBPATH "@LIBPATH@"|#define LIBPATH "/usr/lib"|g' netbrake.h

  make
}

package() {
  cd $srcdir/$pkgname

  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/lib

  install -m 755 netbrake $pkgdir/usr/bin
  install -m 755 libnetbrake.so.0.1 $pkgdir/usr/lib
}
