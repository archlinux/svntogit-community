# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=6.0.1
pkgrel=1
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL3)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  gtk3
  libgee
  libgranite.so
  libgtop
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=8e0cf8101b1e5e85c38b49f562a060de5b79e8d1
source=(git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-about

  git describe --tags
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
