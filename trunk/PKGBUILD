# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.2
pkgrel=2
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('x86_64')
url='http://goaccess.io'
license=('GPL')
depends=('ncurses' 'libmaxminddb')
optdepends=('geoip2-database: for geoip support')
backup=('etc/goaccess.conf')
source=("http://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('bc31ef1e4c92608635856fa73e69b58267c161a7e3c6f2e3ca8860b862ed1c47575b93943dd975a5f8503774ee90ee0649d380563639e30908d876416638f889')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip=mmdb --enable-utf8
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}

