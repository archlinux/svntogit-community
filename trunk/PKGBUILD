# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=10.2.1
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL3)
depends=(glibc lua)
makedepends=(cmake flac jq libid3tag openssl pcre2 perl)
source=($pkgname-$pkgver.tar.gz::https://github.com/jcorporation/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('d5d123c73d5e7537646734a2f1a6cfa9dfbecd9b53a3a1f51cb51cb82dfe1bb33d1c1ee901911e1966f0b3b50d6db2e00068c688c959017a13003e94078f11a0')
b2sums=('80a15d58e3137c65414d755a83ce864ca803c614bdb4fd61d59b6399dd7e2883d0954eef9a553a08add7342b2d30bb6f9800389b40cf04350236ea226801626b')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_FLAC=ON
    -S $_name-$pkgver
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # move service file to correct location: https://github.com/jcorporation/myMPD/issues/955
  mv -v "$pkgdir/"{lib,usr/lib}
}
