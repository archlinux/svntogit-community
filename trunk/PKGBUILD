# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonpickle
pkgver=3.0.1
pkgrel=1
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="https://jsonpickle.github.io/"
license=('BSD')
depends=('python')
optdepends=("python-numpy: for serializing sklearn models, numpy arrays, and other numpy-based data"
            "python-gmpy2: for serializing ecdsa module's keys")
makedepends=('python-setuptools-scm')
checkdepends=('python-numpy' 'python-feedparser' 'python-simplejson' 'python-gmpy2'
              'python-pymongo' 'python-ujson' 'python-pandas' 'python-pytest')
source=("https://github.com/jsonpickle/jsonpickle/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3190716491c01de1e5ce16cd8fee3163c20831521503803a5282b43c6653756554fa6184de78755f3232ac6c9bc51baccb91305f9ac9462c8e22fc57fbcc52eb')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jsonpickle-$pkgver
  # do not run coverage in an integration test
  sed -i 's/--cov//' pytest.ini
}

build() {
  cd jsonpickle-$pkgver
  python setup.py build
}

check() {
  cd jsonpickle-$pkgver
  pytest
}

package() {
  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
