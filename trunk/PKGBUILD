# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=gufw
pkgver=15.10.0
pkgrel=1
pkgdesc="An uncomplicated way to manage your Linux firewall."
arch=('any')
url="http://gufw.org"
license=('GPL3')
depends=('gtk3' 'polkit' 'python2-gobject' 'python2-netifaces' 'ufw' 'webkitgtk')
makedepends=('intltool' 'python2-distutils-extra')
source=("https://launchpad.net/gui-ufw/15.10/15.10/+download/gui-ufw-${pkgver}.tar.gz")
sha256sums=('58fee07bc33f9be615e899b943535ca8755fdf9262010c2580a0176d2a1c2443')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/gui-ufw-${pkgver}"
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/env python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
              $(find . -name '*.py')
    sed -i "s|python ${LOCATIONS[${i}]}|python2 ${LOCATIONS[${i}]}|" bin/gufw
    sed -i "s|python ${LOCATIONS[${i}]}|python2 ${LOCATIONS[${i}]}|" bin/gufw-pkexec
}

package(){
    cd "${srcdir}/gui-ufw-${pkgver}"
    export XDG_RUNTIME_DIR="/tmp/${pkgname}"
    python2 ./setup.py install --root="${pkgdir}"
}
