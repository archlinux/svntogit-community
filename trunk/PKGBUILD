# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=0.13.1
pkgrel=1
pkgdesc="A minimally intrusive build tool for Scala projects"
arch=('any')
url='http://www.scala-sbt.org/'
license=('BSD')
depends=('java-runtime-headless' 'sh')
optdepends=('scala')
source=("http://scalasbt.artifactoryonline.com/scalasbt/sbt-native-packages/org/scala-sbt/${pkgname}/${pkgver}/${pkgname}.tgz"
        'LICENSE')
md5sums=('3e8dc95757f8c70378df638a3047ea9b'
         '0cc49331142988765ce73782a5839c57')

package() {
  cd ${pkgname}
  install -d "${pkgdir}"/usr/share/java/${pkgname}/{bin,conf}
  install -m755 bin/{sbt,sbt-launch.jar,sbt-launch-lib.bash} \
    "${pkgdir}"/usr/share/java/${pkgname}/bin
  install -m644 conf/{sbtconfig.txt,sbtopts} \
    "${pkgdir}"/usr/share/java/${pkgname}/conf

  install -d "${pkgdir}"/usr/bin/
  ln -s /usr/share/java/${pkgname}/bin/sbt "${pkgdir}"/usr/bin/sbt

  install -Dm644 "${srcdir}"/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

}
