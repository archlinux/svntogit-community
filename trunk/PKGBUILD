# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=csfml
pkgver=2.1
pkgrel=1
pkgdesc='C bindings for sfml'
arch=('i686' 'x86_64')
url='http://www.sfml-dev.org/'
license=('zlib')
depends=("sfml=${pkgver}")
makedepends=('cmake' 'doxygen')
source=("${pkgname}-${pkgver}::https://github.com/LaurentGomila/CSFML/archive/${pkgver}.tar.gz")
md5sums=('960085220e6978c720a62b02cc4a2ec4')

build() {
  cd "$srcdir"/CSFML-${pkgver}

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_DOC=true
  make
  make doc
}

package() {
  cd "$srcdir"/CSFML-${pkgver}/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

