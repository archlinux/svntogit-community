# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=opengv
pkgver=1.0.git1
_commit=91f4b19
pkgrel=2
pkgdesc="An efficient C++ library for calibrated camera pose computation using geometric computer vision algorithms"
arch=('x86_64')
url="https://laurentkneip.github.io/opengv/"
license=('BSD')
depends=('eigen' 'python-numpy')
makedepends=('cmake' 'git' 'ninja')
source=("git+https://github.com/laurentkneip/opengv.git#commit=$_commit"
        "git+https://github.com/pybind/pybind11.git")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd opengv

  git submodule init
  git config submodule.pybind11.url "${srcdir}/pybind11"
  git submodule update
}

build() {
  cd opengv

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=None \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DBUILD_SHARED_LIBS=ON \
  	-DBUILD_PYTHON=ON
  ninja -C build
}

package() {
  cd opengv

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

check() {
  cd opengv

  ninja -C build test
}
