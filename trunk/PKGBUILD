# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hedgehog
pkgname=haskell-hedgehog
pkgver=1.0.2
pkgrel=14
pkgdesc="A modern property-based testing system"
url="https://hedgehog.qa"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-concurrent-output'
         'haskell-erf' 'haskell-exceptions' 'haskell-lifted-async' 'haskell-mmorph'
         'haskell-monad-control' 'haskell-pretty-show' 'haskell-primitive' 'haskell-random'
         'haskell-resourcet' 'haskell-transformers-base' 'haskell-wl-pprint-annotated')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b34004210f4af480e10e6fd0a715ee0efb7d718a7c0d8b84355639064b105840e59be223e90cc4fa26a9fa564df6d9686e77b03d996a8820955b19a6ef42c1bb')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/semigroups/d' -e '/fail/d' -e 's/< *1.10/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
