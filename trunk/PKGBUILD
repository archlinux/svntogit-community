# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Jonathan Conder <jonno dot conder at gmail dot com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Nathan Jones <nathanj@insightbb.com>
# Contributor: Javier "Phrodo_00" Aravena <phrodo.00 at gmail dot com>
# Contributor: angvp <angvp at archlinux dot us>
# Contributor: Allan <mcrae_allan at hotmail dot com>
# Contributor: w0rm <w0rmtux at gmail dot com>
# Contributor: vEX <vex at niechift dot com>
# Contributor: Asher256 <achrafcherti at gmail dot com>

pkgname=desmume
pkgver=0.9.10
pkgrel=2
pkgdesc="Nintendo DS emulator"
arch=('i686' 'x86_64')
url="http://desmume.org/"
license=('GPL')
depends=('desktop-file-utils' 'libpcap' 'soundtouch' 'alsa-lib' 'glu' 'sdl' 'gtk2' 'zziplib')
makedepends=('intltool' 'mesa')
install=desmume.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.tar"
        "glx_3Demu.patch")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # http://sourceforge.net/p/desmume/code/4907/
  patch -Np2 < "$srcdir"/glx_3Demu.patch

  # See https://bugs.archlinux.org/task/35086 and
  # https://sourceforge.net/tracker/?func=detail&aid=3612768&group_id=164579&atid=832291
  sed -i 's/@GETTEXT_PACKAGE@/desmume/' po/Makefile.in.in

  ./configure --prefix=/usr --enable-wifi
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

md5sums=('a6aedfe5d6437d481aa9ac5fb5aebbea'
         '352ea0ffa109f68da2fb79f30d2c85e2')
