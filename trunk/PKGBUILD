# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Victor HÃ¤ggqvist <aur@snilius.com>

_name=pyfiglet
pkgname=python-pyfiglet
pkgver=0.8.post0
pkgrel=1
pkgdesc='Pure-python FIGlet implementation'
url=https://github.com/pwaller/pyfiglet
license=('GPL')
arch=('any')
depends=('python-setuptools')
checkdepends=('figlet')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('43f0497ae86139e8f64c45e41e575aa82eee574331739e96c555ca33e8f6900df6487d3220667188891eacd56e00d716d427479337fe5d6b2efdaa6aaaa64376')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  python pyfiglet/test.py
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
