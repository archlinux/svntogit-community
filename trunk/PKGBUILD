# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=1.6.0
pkgrel=4
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'laszip' 'jsoncpp' 'hexer-hobu')
makedepends=('cmake' 'python' 'python-numpy' 'laz-perf')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("http://download.osgeo.org/pdal/PDAL-$pkgver-src.tar.gz"
        gcc.patch)
sha512sums=('2e3bb90033d2248277bae241e4c9387f175e31abfeb3951d9ccaddca8d8f4cf58606d9a6c1730c69a5a0814f708366a271a38f80359d1f0db4572f45e102bc32'
            '4f71629c8ade61ca8266911b1d4ed6dd740bdf32a27e08f3a87d1c7d184036702deb61dc500ca41f734af8025285d9c9c59055a21044c9b3248632aa50ef5f95')

prepare() {
    cd "PDAL-$pkgver-src"

    patch -Np1 < $srcdir/gcc.patch

    [[ -d build ]] || mkdir build
}

build() {
    cd "PDAL-$pkgver-src/build"

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_PLUGIN_PYTHON=ON \
        -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
        -DBUILD_PLUGIN_SQLITE=ON \
        -DBUILD_PLUGIN_HEXBIN=ON \
        -DWITH_LAZPERF=ON \
        -DWITH_LASZIP=ON \
        -DWITH_COMPLETION=ON

    make
}

package() {
    cd "PDAL-$pkgver-src/build"

    make DESTDIR="$pkgdir/" install

    cd "$srcdir/PDAL-$pkgver-src"
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
