# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Christofer Bertonha <christoferbertonha at gmail dot com>
# Contributor: leepesjee <lpeschier at xs4all dot nl>
# Contributor: Olivier Medoc
# Contributor: ignotus
# Contributor: Fabian Moser
# Contributor: djscholl

pkgname=vtk
pkgver=5.8.0
pkgrel=1
pkgdesc='A software system for 3D computer graphics, image processing, and visualization which supports a wide variety of visualization algorithms and advanced modeling techniques.'
arch=('i686' 'x86_64')
url='http://www.vtk.org'
license=('BSD')
depends=('libpng' 'libtiff' 'freetype2' 'python2' 'java-runtime' 'tk' 'boost' 'ffmpeg' 'qt' 'lesstif' 'mysql')
optdepends=('gnuplot: plotting tools'
            'graphviz: drawing tools')
makedepends=('cmake' 'java-environment' 'doxygen' 'gnuplot' 'mesa')
source=("http://www.vtk.org/files/release/${pkgver:0:3}/vtk-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${pkgver:0:3}/vtkdata-${pkgver}.tar.gz"
        'fixkernelversioncheck.diff')
md5sums=('37b7297d02d647cc6ca95b38174cb41f'
         'a05295b81930e894fc0e31866f399151'
         '9e137af23701f76fc727222ebac23389')

build() {
  cd VTK

  # fix compilation error: http://vtk.org/Bug/view.php?id=12568
  # remove next pkgver bump
  patch -Np1 -i ${srcdir}/fixkernelversioncheck.diff

  cd ${srcdir}

  rm -rf build
  mkdir build
  cd build

  cmake \
    -Wno-dev \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DVTK_DATA_ROOT:FILEPATH=${srcdir}/VTKData \
    -DBUILD_DOCUMENTATION:BOOL=ON \
    -DDOCUMENTATION_HTML_HELP:BOOL=ON \
    -DDOCUMENTATION_HTML_TARZ:BOOL=ON \
    -DBUILD_EXAMPLES:BOOL=ON \
    -DVTK_USE_HYBRID:BOOL=ON \
    -DVTK_USE_PARALLEL:BOOL=ON \
    -DVTK_USE_PATENTED:BOOL=ON \
    -DVTK_USE_SYSTEM_EXPAT:BOOL=ON \
    -DVTK_USE_SYSTEM_FREETYPE:BOOL=ON \
    -DVTK_USE_SYSTEM_JPEG:BOOL=ON \
    -DVTK_USE_SYSTEM_PNG:BOOL=ON \
    -DVTK_USE_SYSTEM_TIFF:BOOL=ON \
    -DVTK_USE_SYSTEM_ZLIB:BOOL=ON \
    -DVTK_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DVTK_USE_BOOST:BOOL=ON \
    -DVTK_USE_INFOVIS:BOOL=ON \
    -DVTK_USE_GL2PS:BOOL=ON \
    -DVTK_USE_MYSQL:BOOL=ON \
    -DVTK_USE_FFMPEG_ENCODER:BOOL=ON \
    -DVTK_USE_TEXT_ANALYSIS:BOOL=ON \
    -DVTK_WRAP_JAVA:BOOL=ON \
    -DVTK_WRAP_PYTHON:BOOL=ON \
    -DVTK_WRAP_TCL:BOOL=ON \
    -DVTK_PYTHON_SETUP_ARGS:STRING="--root=${pkgdir}" \
    -DVTK_USE_QT:BOOL=ON \
    -DVTK_INSTALL_QT_PLUGIN_DIR:STRING="/usr/lib/qt/plugins" \
    -DVTK_USE_GUISUPPORT:BOOL=ON \
    -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
    ${srcdir}/VTK

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  # Move the vtk.jar to the arch-specific location
  install -dv ${pkgdir}/usr/share/java/vtk
  mv -v ${pkgdir}/usr/lib/vtk-${pkgver:0:3}/java/vtk.jar \
    ${pkgdir}/usr/share/java/vtk

  # Install license
  install -dv ${pkgdir}/usr/share/licenses/vtk
  install -m644 ${srcdir}/VTK/Copyright.txt \
    ${pkgdir}/usr/share/licenses/vtk

  # Put an entry in /etc/ld.so.conf.d
  install -dv ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/vtk-${pkgver:0:3}" > ${pkgdir}/etc/ld.so.conf.d/vtk.conf
}
