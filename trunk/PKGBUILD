# $Id: PKGBUILD 73684 2012-07-14 05:00:28Z dwallace $
# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Byron Clark <byron@theclarkfamily.name>

pkgname=udiskie
pkgver=1.6.0
pkgrel=1
pkgdesc="Removable disk automounter using udisks"
arch=("any")
url="https://pypi.python.org/pypi/udiskie"
license=("MIT")
depends=("udisks2" "python-gobject" "python-setuptools" "python-yaml" "python-docopt" "gtk3" "libnotify" "python-keyutils")
makedepends=("asciidoc")
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://codeload.github.com/coldfix/$pkgname/tar.gz/$pkgver")
md5sums=('787ce643de330e825d5533a817f08201')

package() {
	cd "$srcdir/$pkgname-$pkgver"
	export LC_ALL=en_US.utf8

	python setup.py install --root="$pkgdir" --optimize=1

	make -C doc
	install -m 0644 -D "doc/$pkgname.8" "$pkgdir/usr/share/man/man8/$pkgname.8"
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -dm755 "$pkgdir/usr/share/zsh/site-functions"
	install -m644 completions/* "$pkgdir/usr/share/zsh/site-functions"
}
