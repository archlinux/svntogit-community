# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.4.23
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('cmake' 'ninja' 'gtest' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('9a16f74311d87fbf64c85f68f14dedd4406aea79331d42e4598474300cb8989a76efb44619b0f81090712958641754180dd9e48fe222e1363aa053c0e7cfcee1')

prepare() {
  mkdir -p build
  sed -i 's|DESTINATION /lib/systemd/user|DESTINATION /usr/lib/systemd/user|' dde-clipboard-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
