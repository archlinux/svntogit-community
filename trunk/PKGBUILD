# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.10.1
pkgrel=4
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
depends=('qt6-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11'
         'qt6-declarative' 'qt6-quickcontrols2' 'qt6-svg' 'zstd')
makedepends=('mesa' 'qt6-tools' 'python' 'qbs')
optdepends=('python: Python plugin'
            'qt6-imageformats: Support for additional image formats (including WebP)')
source=($pkgname-$pkgver.tar.gz::https://github.com/mapeditor/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('3c0f268e943ddbfa49cb5b193994200985397aa0b3e255da32a414428c3f8bfb7a12d59d05bdbcb2f230dc1f4153b0dbae877fcbbd2a51da9b6cfd2c8c1e01ac')

prepare() {
  qbs setup-qt /usr/bin/qmake6 qt6
  qbs config defaultProfile qt6
}

build() {
  cd $pkgname-$pkgver
  qbs qbs.installPrefix:"/usr" projects.Tiled.useRPaths:false projects.Tiled.installHeaders:true
}

package() {
  cd $pkgname-$pkgver
  qbs install --install-root "$pkgdir"
}
