# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gilles CHAUVIN <gcnweb at gmail dot com>

pkgname=recordmydesktop
pkgver=0.4.0
pkgrel=3
pkgdesc="Produces a OGG encapsulated Theora/Vorbis recording of your desktop"
arch=('x86_64')
url="https://github.com/Enselic/recordmydesktop/"
license=('GPL')
depends=('alsa-lib' 'zlib' 'libvorbis' 'libxdamage' 'libxext' 'libtheora' 'libsm' 'jack' 'popt')
makedepends=('jack')
source=("https://github.com/Enselic/recordmydesktop/releases/download/v0.4.0/recordmydesktop-${pkgver}.tar.gz")
sha512sums=('4b0a141c68e10679cfce53efac518e254b0cd361b6769aaf22fdb25a2162e7cc255eb741dd671a908dafa370d464b0e298b7386b561a6a721e8419632982a1e0')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-jack=yes
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
