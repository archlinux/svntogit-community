# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=ansible
pkgver=2.10.2
pkgrel=2
pkgdesc='Official assortment of Ansible collections'
arch=('any')
url='https://pypi.org/project/ansible/'
license=('GPL3')
depends=('python' 'ansible-base')
provides=('python-ansible_collections')
optdepends=('python-pyopenssl: openssl modules'
            'python-dnspython: for dig lookup'
            'python-ovirt-engine-sdk: ovirt support'
            'python-boto3: aws_s3 module'
            'python-ldap: ldap support'
            'acme-tiny: openssl_certificate module')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/a/ansible/ansible-${pkgver}.tar.gz")
sha512sums=('43e4800f5627af28eb8bc49b3bd41903b4f28683fc94116797406f2a6038b4b3523d99888b46df25c3c1de4f5cee1364d23dcfe713a85c4d0187258853066a47')

build() {
  cd ansible-${pkgver}
  python setup.py build
}

package() {
  cd ansible-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING

  # Remove some extra data that shouldn't have been installed.
  # No -f so we know once upstream removes this from the package.
  rm -r "${pkgdir}"/usr/lib/python3.8/site-packages/ansible_collections/cisco/meraki/{tests,venv}
}
