# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-crossref
pkgver=0.3.2.0
pkgrel=41
pkgdesc="Pandoc filter for cross-references"
url="https://hackage.haskell.org/package/${pkgname}"
license=("GPL2")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-data-accessor' 'haskell-data-accessor-template'
         'haskell-data-accessor-transformers' 'haskell-file-embed'
         'haskell-open-browser' 'haskell-optparse-applicative' 'pandoc' 'haskell-pandoc-types'
         'haskell-roman-numerals' 'haskell-syb' 'haskell-utility-ht')
makedepends=('ghc' 'haskell-hspec')
conflicts=('haskell-pandoc-crossref')
replaces=('haskell-pandoc-crossref')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lierdakil/pandoc-crossref/archive/v$pkgver.tar.gz")
sha512sums=('945a9eb97f65c17b4303afba5fe3af4c0bee4934d41feaef5252134af20ae576aeef4bd406a707c3559a54f8a5ecec0aa8ec0d347fffe9895ed80f5077bf3043')

prepare() {
    cd $pkgname-$pkgver
    sed -i -e 's/<.*1.3/<2/' $pkgname.cabal

    # https://github.com/lierdakil/pandoc-crossref/issues/181
    sed -i -e '27,28d' \
           lib/Text/Pandoc/CrossRef/Util/PandocOrphans.hs
}

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
