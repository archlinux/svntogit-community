# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=fflas-ffpack
pkgver=2.3.2
pkgrel=1
pkgdesc="A library for dense linear algebra over word-size finite fields"
arch=(x86_64) # pkgconfig file is arch-specific
url="https://linbox-team.github.io/fflas-ffpack/"
license=(LGPL)
depends=(givaro cblas lapack)
source=("https://github.com/linbox-team/fflas-ffpack/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c0c7426a983d961d75c61638be9406a74415b429251e5d4295ee1110d1ee0e69')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-precompilation --with-blas-libs="-lblas -lcblas -llapack" \
    --disable-sse3 --disable-ssse3 --disable-sse41 --disable-sse42 --disable-avx --disable-avx2 --disable-fma
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
