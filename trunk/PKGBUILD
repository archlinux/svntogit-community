# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=2.6
pkgrel=1
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoc' 'source-highlight' 'cmake' 'pkg-config' 'perl' 'python'
             'lua' 'tcl' 'ruby' 'aspell' 'guile2.0' 'asciidoctor')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell' 'guile2.0')
source=("${pkgname}-${pkgver}.tar.xz::https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.xz.asc::https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz.asc")
sha512sums=('cf56692e12b2e034675d4c15a8822836b2fcf93d584948e338d263f5a7207edf4599cc72bfb940427146f5f75e7239207cf54f5ed07dd5b6119f68c445725a2d'
            'SKIP')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
