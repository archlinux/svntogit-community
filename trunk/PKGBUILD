# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=trailrunner
pkgname=python-$_pkgname
pkgver=1.4.0
pkgrel=2
pkgdesc='Walk paths and run things'
arch=('any')
url='https://github.com/omnilib/trailrunner'
license=('MIT')
depends=('python-pathspec')
makedepends=('python-build' 'python-installer' 'python-flit-core' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('9b73912895c663c14f7ab642512d00c82553378219a75c0cabb6082acdcd8c910ce89d4fd2275bd1d5f496702cc4fcfaa43fc0deeb81794e06e80601ae93522d')

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
