# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-i18n
pkgver=6.0.0
_commit=03605c2db7d034ad6776b860d4928939ae7e3594
pkgrel=1
pkgdesc="Oslo i18n library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.i18n/$pkgver"
license=('Apache')
depends=('python-pbr')
makedepends=('git')
checkdepends=('python-oslotest' 'python-oslo-config')
source=("git+https://github.com/openstack/oslo.i18n.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd oslo.i18n
  python setup.py build
}

check() {
  cd oslo.i18n
  stestr run
}

package() {
  cd oslo.i18n
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
