# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-email-validator
pkgname=(python-email-validator python2-email-validator)
pkgver=1.0.4
pkgrel=1
pkgdesc="A robust email syntax and deliverability validation library for Python 2.x/3.x."
arch=('any')
url="https://github.com/JoshData/python-email-validator"
license=('custom:CC0')
makedepends=('python-setuptools' 'python2-setuptools' 'python-dnspython' 'python2-dnspython'
             'python-idna' 'python2-idna')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/JoshData/python-email-validator/archive/v$pkgver.tar.gz")
sha512sums=('0c9fcee070578a5905562b807a2f849a02af17d10c6752dbd7f5826d348da8cd049dbc25760537201fc97fe22297149561a1df789362ebe14a8753e0daf21764')

prepare() {
  cp -a python-email-validator-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-email-validator-$pkgver
  python setup.py build

  cd "$srcdir"/python-email-validator-$pkgver-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/python-email-validator-$pkgver
  python email_validator/__init__.py --tests < test_pass.txt

  cd "$srcdir"/python-email-validator-$pkgver-py2
  python2 email_validator/__init__.py --tests < test_pass.txt
}

package_python-email-validator() {
  depends=('python-dnspython' 'python-idna')

  cd "$srcdir"/python-email-validator-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-email-validator() {
  depends=('python2-dnspython' 'python2-idna')

  cd "$srcdir"/python-email-validator-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/email_validator{,2}
}

# vim:set ts=2 sw=2 et:
