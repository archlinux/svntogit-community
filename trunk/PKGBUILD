# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-primitive
pkgver=0.6.1.0
pkgrel=2
pkgdesc="Wrappers for primitive operations"
url="http://hackage.haskell.org/package/primitive"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc")
source=("http://hackage.haskell.org/packages/archive/primitive/${pkgver}/primitive-${pkgver}.tar.gz")
sha256sums=('93731fa72eaf74e8e83453f080828e18cec9fbc82bee91b49ba8b61c043d38c8')

prepare() {
  sed -e 's/base >= 4.3 && < 4.9/base >= 4.3 \&\& < 4.10/' \
      -e 's/ghc-prim >= 0.2 && < 0.5/ghc-prim >= 0.2 \&\& < 0.6/' \
      -e 's/transformers >= 0.2 && < 0.5/transformers >= 0.2 \&\& < 0.6/' \
      -i "$srcdir"/primitive-${pkgver}/primitive.cabal
}

build() {
  cd "$srcdir"/primitive-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
  depends=("ghc=${_ghcver}")

  cd "$srcdir"/primitive-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm755 "$pkgdir"/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html "$pkgdir"/usr/share/doc/ghc/html/libraries/primitive
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE
}
