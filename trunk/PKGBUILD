# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor:  Bartłomiej Piotrowski <barthalion@gmail.com>
# Contributor: Maciej Sitarz <macieks@freesco.pl>

pkgname=hunspell-pl
pkgver=20190101
pkgrel=1
pkgdesc='Polish dictionary for Hunspell'
arch=('any')
url='https://www.sjp.pl/slownik/ort/'
license=('GPL')
optdepends=('hunspell: the spell checking libraries and apps')
source=(https://sources.archlinux.org/other/community/hunspell-pl/sjp-myspell-pl-$pkgver.zip)
md5sums=('9ee4a80641764c0c26627c83ec481e39')

prepare() {
  bsdtar -xf pl_PL.zip
}

package() {
  install -dm755 "$pkgdir"/usr/share/hunspell
  install -m644 pl_PL.aff "$pkgdir"/usr/share/hunspell
  install -m644 pl_PL.dic "$pkgdir"/usr/share/hunspell

  # the symlinks
  install -dm755 "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
  for file in "$pkgdir"/usr/share/hunspell/*; do
    ln -sv /usr/share/hunspell/$(basename $file) .
  done
  popd

  # docs
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README_pl_PL.txt "$pkgdir"/usr/share/doc/$pkgname
}
