# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

_pkgbasename=libxshmfence
pkgname=lib32-$_pkgbasename
pkgver=1.0
pkgrel=1
pkgdesc="a library that exposes a event API on top of Linux futexes (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('GPL')
#groups=()
depends=('lib32-glibc' ${_pkgbasename})
makedepends=('xorg-util-macros' 'gcc-multilib')
source=(${url}/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('625089553939baa20e665873a6c8f3d694cfde78fb3110aa59da92fd2b14f14f')

build() {
	cd $_pkgbasename-$pkgver
	
        export CC="gcc -m32"
        export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	# fix some libtools errors
	autoreconf -vfi
	
	./configure --prefix=/usr --libdir=/usr/lib32
	make
}

check() {
	cd $_pkgbasename-$pkgver
	make -k check
}

package() {
	cd $_pkgbasename-$pkgver
	make DESTDIR="$pkgdir/" install

        rm -r ${pkgdir}/usr/include
	
        install -m755 -d "${pkgdir}/usr/share/licenses/$_pkgbasename"
        ln -s ${_pkgbasename} "$pkgdir/usr/share/licenses/$_pkgbasename/$pkgname"

}

