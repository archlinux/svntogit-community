# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-desktop
pkgver=6.3.1
pkgrel=2
pkgdesc='Switchboard Desktop Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-pantheon-shell
license=(GPL3)
groups=(pantheon-unstable)
depends=(
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  libgee
  libgexiv2
  libgranite.so
  libhandy
  libswitchboard-2.0.so
  plank
  tumbler
)
makedepends=(
  git
  meson
  vala
)
_tag=ca815e955786d3a8adbc773190c25d7fd752cd95
source=(switchboard-plug-desktop::git+https://github.com/elementary/switchboard-plug-pantheon-shell.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-desktop

  git describe --tags
}

prepare() {
  sed 's|/usr/share/backgrounds|/usr/share/backgrounds/elementary|' -i switchboard-plug-desktop/src/Views/Wallpaper.vala
}

build() {
  arch-meson switchboard-plug-desktop build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
