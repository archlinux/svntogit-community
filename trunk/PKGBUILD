# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.6.2a
pkgrel=6
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('drumstick' 'hicolor-icon-theme' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'qt5-tools' 'libxkbcommon-x11')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('89ba161c3ca7ae260d02c175d1116bea7cdfdfddb0f303d8b9177eeb3d77ded8f6126bed43efa6c279daec0a987d0be30c280d59f42a7c145167fb54a7b8db16')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,ChangeLog,NEWS,README,TODO}
}
