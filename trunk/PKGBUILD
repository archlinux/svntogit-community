# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jackminimix
pkgver=0.2.1
pkgrel=3
pkgdesc="A simple mixer for the Jack Audio Connection Kit with an OSC based control interface"
arch=(x86_64)
url="https://www.aelius.com/njh/jackminimix/"
license=(GPL2)
groups=(pro-audio)
depends=(glibc libjack.so liblo.so)
source=(https://github.com/njh/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('f99bd0de60624b185a8f6d9c254f52385a74efbbad172ffcb5fe0225648e04bac28d75883c1fc5bba47457ce87ef819cbf1b57d3e758fd07048cc227f57024eb')
b2sums=('9f1c62801fc2bacfe3cb1184f62465aded6fd53c1bac5a6bd048a6d678f58700bec39ad16654436648b0849f1213ed9f339dd0be6670aefb1d66d067b3f3f843')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,{NEWS,README}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
