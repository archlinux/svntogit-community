# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.92
pkgrel=2
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
provides=(sml)
install=smlnj.install
depends_x86_64=(lib32-glibc)
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('e41efc6670b65e5000c44d829fc628909980549eb9d6570016f460d566056592'
            '9a9d0312d71dc4c4dcce5c3a933e50cb69d57f9f73c5bf62e6b9536de31214d3'
            '5d27dcd463e90f826fc27b4b8712a70f9349465e5c21130cff06828862a676b4'
            'bf470c76bc70efbca9a593fbc2399a5c448ce4d8ec451c8bbc7cfdba97c0ad3f'
            '1f982e4f09e1c9035d625c7ddd997e20c6e9089c5f6b67d476191a6006ee6e94'
            'f6c029e01d1d9aee1a74714078a725d5783034ad45a5da7ec7fb22aad6c0fcbe'
            'd7477a2fe89af5cc6306b8ca67c7ee1ab6f9b2d3eea17366cce7e5c43a98a260'
            '3ed86dd7dd39d679d25ce7851c97b95659bd6d47aa02004b65c8b1b889a95bfc'
            'b57f17e7f66faa94ade8e4b73b9ecbcc4c61ef80af8891558a7b7471dad37f03'
            'f81db40e8fa26e397b37a2a96933a86d711a7692945ed97635aadf2584e7fbda'
            'c492c3dc03390ca2da6f694e0174edd68a70b20392a88c9fb50cf9bba527676f'
            '9cef64f66801579018b5f2a87c1732e3ffda80040692d56a115f7189553f2a52'
            'def31fef458e74a29c1bd79c427c85f5a65d1fb6f0340e8faf9b2c0aaa44e53f'
            '512b1660ccfddeef1ee8f9e32d9ab5d33936a9d8aefa9eb757041f39fd398317'
            'b992c9852aec2592a672978ab05d0de96a54dff4bfdea0a73b877dbb39d851d5'
            '22e0975a2938251580ecd9dbcf141a79bc871cc74553ee09eed88280761f2088'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
