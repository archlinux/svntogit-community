# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-melonds
pkgver=541
pkgrel=2
pkgdesc='Nintendo DS core'
arch=('x86_64')
url='https://github.com/libretro/melonDS'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libretro-core-info')
makedepends=('git')
_commit='9440f3d5f02d3af049b4610f38f528ce9c57629c'
source=("libretro-melonds::git+https://github.com/libretro/melonDS.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd libretro-melonds

  git rev-list --count HEAD
}

build() {
  cd libretro-melonds

  export LDFLAGS="$LDFLAGS -lpthread"

  make
}

package() {
  cd libretro-melonds

  install -Dm 644 melonds_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
