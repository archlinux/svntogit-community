# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski at archlinux dot org>
# Contributor: Felipe Hommen <felibank at gmail dot com>
# Contributor: moostik <mooostik at gmail dot com>

pkgname=geogebra
pkgver=6.0.609.0
pkgrel=3
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet"
arch=('x86_64')
url='https://www.geogebra.org/'
license=('GPL3' 'CCPL:by-sa' 'CCPL:by-nc')
depends=('desktop-file-utils' 'electron6' 'shared-mime-info' 'xdg-utils')
makedepends=('gendesk')
source=("https://download.geogebra.org/installers/6.0/GeoGebra-Linux64-Portable-${pkgver//./-}.zip"
        "https://static.geogebra.org/images/geogebra-logo.svg"
        "geogebra"
        "geogebra-mime.xml")
changelog=ChangeLog
sha256sums=('2b91ae69181743b6aa2fe433c0a4f3fb880f93e26a809dde1108f5830a5eec67'
            '55ded6b5ec9ad382494f858d8ab5def0ed6c7d529481cd212863b2edde3b5e07'
            'db40c91786ae316f342a424f8635b330f6e193ad5bc172cb6a66f66d151b7c50'
            '100dd83e61057b9a104630ea39a84d967475d459ab38e29783a7587b3acfb6a4')

prepare() {
  gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" \
          --name="GeoGebra" \
          --categories="Education;Science;Math" \
          --mimetypes="application/vnd.geogebra.file;application/vnd.geogebra.tool"
}

package() {
  cd "GeoGebra-linux-x64"

  install -Dm755 "${srcdir}/geogebra" "${pkgdir}/usr/bin/geogebra"
  install -dm755 "${pkgdir}/usr/lib/geogebra"
  cp -dpr --no-preserve=ownership "resources" "${pkgdir}/usr/lib/geogebra"
  cp -dpr --no-preserve=ownership "locales" "${pkgdir}/usr/lib/geogebra"

  install -Dm644 "${srcdir}/geogebra.desktop" "${pkgdir}/usr/share/applications/geogebra.desktop"
  install -Dm644 "${srcdir}/geogebra-logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/geogebra.svg"
  install -Dm644 "${srcdir}/geogebra-mime.xml" "${pkgdir}/usr/share/mime/packages/geogebra.xml"
}
