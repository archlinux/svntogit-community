# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='kpeg'
# can't use version tag as it doesn't exist: https://github.com/evanphx/kpeg/issues/52
_commit='2c66601458e1012adf4b1ff21835d9208caa5298'
pkgname="ruby-${_gemname}"
pkgver=1.3.1
pkgrel=2
pkgdesc='A simple PEG library for ruby'
arch=('any')
url="https://github.com/evanphx/${_gemname}"
license=('MIT')
depends=('ruby')
makedepends=('ruby-hoe' 'ruby-rake')
options=('!emptydirs')
source=("${url}/archive/${_commit}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7f901cedf015d37e249d0db394a992e9d76d9c4248035477b8ac8cc8845de68e315bb790765c58622bb887ce59752b10e2d35ba122bc04d747890bc15465acc3')

prepare() {
  cd "${_gemname}-${_commit}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # remove non-existing files from gemspec: https://github.com/evanphx/kpeg/issues/51
  sed --in-place --regexp-extended 's|, ".gemtest"||g' "${_gemname}.gemspec"
  sed --in-place --regexp-extended 's|, ".travis.yml"||g' "${_gemname}.gemspec"

  # fix version in gemspec: https://github.com/evanphx/kpeg/issues/52
  sed --in-place --regexp-extended "s|1\.0\.0\.20140103162640|${pkgver}|g" "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${_commit}"

  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${_commit}"

  rake test
}

package() {
  cd "${_gemname}-${_commit}"

  local _gemdir="$(gem env gemdir)"
  local _platform="$(gem env platform | cut -d':' -f2)"
  local _extension_api_version="$(ruby -e 'puts Gem.extension_api_version')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  install --verbose --directory --mode=0755 \
    "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_platform}" \
    "${pkgdir}${_gemdir}/specifications/default" \
    "${pkgdir}/usr/share/vim/vimfiles/"

  mv --verbose "${pkgdir}${_gemdir}/specifications/${_gemname}-${pkgver}.gemspec" "${pkgdir}${_gemdir}/specifications/default/${_gemname}-${pkgver}.gemspec"
  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/lib/${_gemname}.rb" "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_gemname}.rb"
  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/lib/${_gemname}" "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_gemname}"
  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/lib/hoe" "${pkgdir}/usr/lib/ruby/${_extension_api_version}/hoe"
  mv --verbose vim/syntax_kpeg/* "${pkgdir}/usr/share/vim/vimfiles/"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}${_gemdir}/cache/" \
    "${pkgdir}${_gemdir}/build_info/" \
    "${pkgdir}${_gemdir}/extensions/" \
    "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/"{examples,lib,test,vim,.autotest,.*ignore,*.*,LICENSE,Rakefile} \
    "${pkgdir}${_gemdir}/plugins/" \
    "${pkgdir}${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  install --verbose -D --mode=0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.rdoc --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
