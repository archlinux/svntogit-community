# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=cuetools
pkgver=1.3.1
pkgrel=5
pkgdesc='Set of utilities for working with cue files and toc files'
arch=('i686' 'x86_64')
url="http://developer.berlios.de/projects/cuetools/"
license=('GPL')
depends=('glibc')
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz \
        cuetag.patch)
md5sums=('45575f7a1bdc6615599fa6cb49845cca'
         '38969241a7b6ac88e2f1b46c3da0ecc2')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # fix cuetag
  patch -Np0 < "$srcdir"/cuetag.patch
  sed -i -e 's/--import-vc-from/--import-tags-from/' \
    -e 's/--remove-vc-all/--remove-all-tags/' extras/cuetag.sh

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -m755 extras/cuetag.sh "$pkgdir"/usr/bin/cuetag.sh
}
