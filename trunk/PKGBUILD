# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

# TODO: move python-joblib from AUR (needed to accelerate distributed estimation for certain models)
# TODO: create X-12-ARIMA or X-13ARIMA-SEATS (can be used for time-series analysis)

pkgname='python-statsmodels'
pkgver=0.10.0
pkgrel=1
pkgdesc="Statistical computations and models for use with SciPy"
arch=('x86_64')
url="http://statsmodels.sourceforge.net/"
license=('BSD')
depends=('python-scipy' 'python-pandas' 'python-patsy')
makedepends=('python-setuptools' 'cython')
optdepends=('python-nose: needed for integrated test suite'
            'python-matplotlib: needed for plotting functions'
	    'python-joblib:  accelerate distributed estimation for certain models'
	    'jupyter: needed to run the notebooks')
source=("https://github.com/statsmodels/statsmodels/archive/v${pkgver}.tar.gz")
sha256sums=('3aae6454480f485821a8e5761a0456030bc7e41f01648b177d141899f06588e6')

build() {
  cd "$srcdir"/statsmodels-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/statsmodels-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
