# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-code-assistance
pkgver=3.16.1+14+gaad6437
pkgrel=2
pkgdesc="Code assistance services for GNOME"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/CodeAssistance"
license=(GPL3)
depends=(libgee python-dbus python-gobject python-pylint python-pyflakes python-pycodestyle
         python-lxml python-simplejson ruby-dbus ruby-sass)
makedepends=(intltool gobject-introspection llvm clang gjs vala go-pie gnome-common git)
optdepends=('clang: Assistance for C and C++'
            'gjs: Assistance for JavaScript'
            #'vala: Assistance for Vala'
            'go: Assistance for Go')
groups=(gnome-extra)
_commit=aad6437571461d35fe86c34a97f264e9bd7720c3  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-code-assistance.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i 's/libvala-0\.42/libvala-0.44/g' configure.ac
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-vala
  make
}

package(){
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
