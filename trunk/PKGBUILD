# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hasktags
pkgver=0.71.2
pkgrel=192
pkgdesc="Produces ctags \"tags\" and etags \"TAGS\" files for Haskell programs"
url="https://github.com/MarcWeber/hasktags"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-json' 'haskell-microlens-platform' 'haskell-optparse-applicative'
         'haskell-utf8-string')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('649f8e74202dc08b8c37535c2fb0fefe11c55bc9de300093b20e7b224fa6daccbc938988ef810a479e745b53270215fc847caa6e0b06310a7d1c7493fc37b1ce')

prepare() {
    cd $pkgname-$pkgver
    sed -i -e 's/< *0.4/<1/' -e 's/< *0.10/<1/' $pkgname.cabal
}

build() {
    cd $pkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-debug --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
