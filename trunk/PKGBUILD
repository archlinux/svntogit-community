# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgname=rng-tools
pkgver=4
pkgrel=2
pkgdesc="Random number generator related utilities"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/gkernel/"
license=('GPL')
depends=('glibc')
backup=(etc/conf.d/rngd)
source=(http://downloads.sourceforge.net/sourceforge/gkernel/$pkgname-$pkgver.tar.gz
        rngd.conf
        rngd.service)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm0644 "$srcdir/rngd.service" "$pkgdir/usr/lib/systemd/system/rngd.service"
	install -Dm0644 "$srcdir/rngd.conf" "$pkgdir/etc/conf.d/rngd"
}

sha256sums=('b71bdfd4222c05e8316001556be90e1606f2a1bac3efde60153bd84e873cc195'
            'a4a19dbe5403b9dcb2bc6b3699dcaba7a47ff3e3ccf58aa3bdbfe14eb1d16003'
            'd3e38ac1460e3072792def73281faf5f59dffe9ac43b00916252cc69f7c7d1b5')
