# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>

pkgname=givaro
pkgver=4.0.4
pkgrel=2
pkgdesc="C++ library for arithmetic and algebraic computations"
arch=(x86_64)
url="http://givaro.forge.imag.fr/"
license=(GPL)
depends=(gmp)
source=("https://github.com/linbox-team/givaro/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('65257ee07183b8cbd50add2e36b1bc757f1459e665ae02cd662777d4001ba1dd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-shared \
    --disable-sse3 --disable-ssse3 --disable-sse41 --disable-sse42 --disable-avx --disable-avx2 --disable-fma
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
