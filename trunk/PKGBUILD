# $Id$
# Maintainer: Chris Brannon <chris@the-brannons.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>
pkgname=ndisc6
pkgver=1.0.2
pkgrel=4
pkgdesc="A collection of IPv6 networking utilities - ndisc6, rdisc6, tcptraceroute6, tracert6, rltraceroute6, tcpspray6"
arch=('i686' 'x86_64')
url="http://www.remlab.net/ndisc6/"
license=("GPL")
depends=('glibc' 'perl')
makedepends=('glibc' 'gcc')
options=(!emptydirs)
install=ndisc6.install
source=(http://www.remlab.net/files/ndisc6/$pkgname-$pkgver.tar.bz2
        rdnssd.rc.d
	rdnssd.confd
	rdnssd.service)
md5sums=('50cb4c19606cf6ff2b7388e71832f579'
         '3421fd731c4da4f9e07fc6b6ff4e5084'
         'f1910902932e2622fc166960819db6c4'
         'beeb4f033e9eb951e2de962f609c7c2a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --localstatedir=/var --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/rdnssd.rc.d" "$pkgdir/etc/rc.d/rdnssd"
  install -Dm644 "$srcdir/rdnssd.confd" "$pkgdir/etc/conf.d/rdnssd"
  install -Dm644 "$srcdir/rdnssd.service" \
    "$pkgdir/usr/lib/systemd/system/rdnssd.service"

  install -d "$pkgdir/usr/lib/tmpfiles.d"
  echo "d /run/rdnssd 0755 nobody root" > "$pkgdir/usr/lib/tmpfiles.d/ndisc6.conf"
}
