# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=crane
_pkgname=go-containerregistry
pkgver=0.12.1
pkgrel=1
pkgdesc="Interacting with remote container images and registries."
arch=('x86_64')
url="https://github.com/google/go-containerregistry"
license=('Apache-2.0')
makedepends=('go')
source=(https://github.com/google/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6f8060933ace2acff468ce17359aa858b7ca3db049ed8d0ac5d4ae62359573c3')
b2sums=('95fa0acd295e64f10b01299122ea6446d93167437f68161dbdef9db946f0a2b4ccbbe7f730dbc42091f7a1f6866c2a5f8a567acfc3ce3a258f1e25bc729c5f7f')

build() {
  cd ${_pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build ./cmd/crane
  go build ./cmd/gcrane

  cd cmd/krane
  go build .
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    crane gcrane cmd/krane/krane
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
