# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hideaki Takahashi <mymelo+aur@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.pm>

pkgname=(python-docker-pycreds python2-docker-pycreds)
pkgver=0.2.1
pkgrel=2
pkgdesc="Python bindings for the docker credentials store API"
arch=('any')
license=('Apache')
url="https://github.com/shin-/docker-pycreds"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'flake8' 'python2-flake8'
              'python-pytest-cov' 'python2-pytest-cov')
source=("git+https://github.com/shin-/dockerpy-creds.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i 's/==.*$//' dockerpy-creds/test-requirements.txt

  cp -a dockerpy-creds{,-py2}
}

build() {
  cd "$srcdir"/dockerpy-creds
  python setup.py build

  cd "$srcdir"/dockerpy-creds-py2
  python2 setup.py build
}

# We don't have docker-credential-secretservice
check_disabled() {
  cd "$srcdir"/dockerpy-creds
  python setup.py pytest

  cd "$srcdir"/dockerpy-creds-py2
  python2 setup.py pytest
}

package_python-docker-pycreds() {
  depends=('python-six')

  cd dockerpy-creds
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-docker-pycreds() {
  depends=('python2-six')

  cd dockerpy-creds-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
