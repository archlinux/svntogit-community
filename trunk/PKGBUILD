# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-concurrency
pkgname=(python-oslo-concurrency python2-oslo-concurrency)
pkgver=3.30.0
pkgrel=3
pkgdesc="OpenStack library for all concurrency-related code"
arch=('any')
url="https://pypi.python.org/pypi/oslo.concurrency/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-oslo-config'
             'python2-oslo-config' 'python-oslo-i18n' 'python2-oslo-i18n' 'python-oslo-utils'
             'python2-oslo-utils' 'python-fasteners' 'python2-fasteners' 'python2-enum34')
checkdepends=('python-mock' 'python2-mock' 'python-oslotest' 'python2-oslotest' 'python-oslo-config'
              'python2-oslo-config' 'python-eventlet' 'python2-eventlet')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.concurrency/archive/$pkgver.tar.gz")
sha512sums=('28a8652c018025257fa6d13ce2ddf70154c4d22bb1e6e0af229ec876a33cfa3ab3c76283004a677c7a447d05d939a24e7d6d0a18cdb53aff6c87113dd4e25110')

prepare() {
  cp -a oslo.concurrency-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.concurrency-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.concurrency-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.concurrency-$pkgver
  stestr run || warning "1 failure about raising rlimit"

  cd "$srcdir"/oslo.concurrency-$pkgver-py2
  PYTHON=python2 stestr2 run || warning "1 failure about raising rlimit"
}

package_python-oslo-concurrency() {
  depends=('python-six' 'python-pbr' 'python-oslo-config' 'python-oslo-i18n' 'python-oslo-utils'
           'python-fasteners')

  cd "$srcdir"/oslo.concurrency-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-concurrency() {
  depends=('python2-six' 'python2-pbr' 'python2-oslo-config' 'python2-oslo-i18n'
           'python2-oslo-utils' 'python2-fasteners' 'python2-enum34')

  cd "$srcdir"/oslo.concurrency-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/lockutils-wrapper{,2}
}

# vim:set ts=2 sw=2 et:
