# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgname=python-faker
pkgver=1.0.7
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='https://faker.readthedocs.io/en/master/'
license=('MIT')
depends=('python-setuptools' 'python-dateutil' 'python-text-unidecode')
provides=("python-fake-factory=$pkgver")
conflicts=('python-fake-factory')
replaces=('python-fake-factory')
checkdepends=('python-ukpostcodeparser' 'python-mock' 'python-email-validator' 'python-pytest'
              'python-freezegun' 'python-random2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/joke2k/faker/archive/v$pkgver.tar.gz")
sha512sums=('e49d3298424e7ab552cdd3fe106d8ce63fc7d38b707a6093d2aee932507c13f75fe9794125e3ee99fcfe284cc61bf377899a7d3ebc55808df37007f7d01f3c51')

prepare() {
  sed -i -e 's/<3.9/<6/' -e 's/==/>=/' faker-$pkgver/setup.py
}

build() {
  cd faker-$pkgver
  python setup.py build
}

check() {
  cd faker-$pkgver
  python setup.py pytest
}

package() {
  cd faker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}
