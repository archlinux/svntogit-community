# Contributor: Herv√© Cauwelier <herve/oursours/net>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=jpegoptim
pkgver=1.5.2
pkgrel=1
pkgdesc="Jpeg optimisation utility"
arch=('x86_64')
url="https://www.kokkonen.net/tjko/projects.html"
license=('GPL')
depends=('glibc' 'libjpeg')
source=($pkgname-$pkgver.tar.gz::https://github.com/tjko/jpegoptim/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('e8701cc85c065e05747a15da72ebb403056ebecaa98e2806cf69cdd443397910')
#validpgpkeys=('9168CD7A2F0AAB0679D09BBA9D08A80CED908D6A')


build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
  make strip
}

package() {
  cd "${pkgname}-${pkgver}"

  make prefix="${pkgdir}/usr" install
  install -m 644 -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
