# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=zim-tools
pkgver=3.1.2
pkgrel=1
pkgdesc="Various ZIM command line tools"
license=('GPL3')
arch=('x86_64')
provides=('zimwriterfs')
conflicts=('zimwriterfs')
replaces=('zimwriterfs')
depends=('file' 'libzim>=8.0.1' 'docopt' 'gumbo-parser')
makedepends=('meson' 'mustache')
url='https://github.com/openzim/zim-tools'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('93af4a314d02d87facf5c24b9f595937db5ebf1563d22728dc8cc434b23d89c5')

build() {
	cd "${pkgname}-${pkgver}"
	arch-meson build
	ninja -C build
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
