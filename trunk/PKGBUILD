# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-glu
pkgver=9.0.2
pkgrel=1
pkgdesc="Mesa OpenGL utility library (32 bits)"
arch=('x86_64')
url="https://mesa.freedesktop.org/"
license=('LGPL')
depends=('lib32-libgl' 'glu')
makedepends=('gcc-multilib' 'lib32-libglvnd' 'meson')
source=(ftp://ftp.freedesktop.org/pub/mesa/glu/glu-$pkgver.tar.xz{,.sig})
sha512sums=('2517d7406bb643d12c017a95dcb5d8716f307344332638bcbdf274a90752a7c22165d34745f1b082ed916bb07d40e62d1d1d67d96426225be63166f3480d6f64'
            'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  arch-meson glu-$pkgver build \
    --libdir=/usr/lib32 \
    -D default_library=shared

  # Print config
  meson configure build

  ninja -C build
  meson compile -C build
}

check() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="i686-pc-linux-gnu-pkg-config"

  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -rf "${pkgdir}"/usr/include
}
