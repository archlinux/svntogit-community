# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=openFPGALoader
pkgname=openfpgaloader
pkgver=0.9.0
pkgrel=1
pkgdesc='Universal utility for programming FPGA'
arch=('x86_64')
url='https://github.com/trabucayre/openFPGALoader'
license=('Apache2')
depends=('libftdi' 'libftdi-compat' 'libhidapi-libusb.so' 'libudev.so')
makedepends=('cmake' 'libusb' 'systemd')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c4ed3b35b9893676e39c630631882be5f33e4ad25700b2608a8c3565c2bcad51ef733a7d187f435f58164be74bb9bc21afa5d6fad9dfeaaffe8d97645a233edb')

prepare() {
  sed -i 's|MODE="664", GROUP="plugdev"|MODE="666"|g' $_pkgname-$pkgver/99-openfpgaloader.rules
}

build() {
  mkdir -p $_pkgname-$pkgver/build
  cd $_pkgname-$pkgver/build

  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_UDEV=True \
    -DENABLE_CMSISDAP=True

  make
}

package() {
  cd $_pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 ../99-openfpgaloader.rules "$pkgdir"/usr/lib/udev/rules.d/99-openfpgaloader.rules
}

