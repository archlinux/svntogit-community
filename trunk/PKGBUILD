# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=0.10.7
pkgrel=3
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
depends=('python-pathtools' 'python-yaml' 'python-argh')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-timeout' 'python-pytest-runner')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('64b747f11691ddf976052ceb88f7978abef1789230102c418075ab2f41ede04af4df3dbd4bd5ef433b0616d80e3275853cf535da0707450e2bf908871cb70e8e')

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  python setup.py pytest
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
