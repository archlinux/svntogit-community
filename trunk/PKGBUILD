# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Rolinh <robinDOThahlingATgw-computingDOTnet>

pkgname=dfc
pkgver=3.1.1
pkgrel=1
pkgdesc="Display file system space usage using graphs and colors"
arch=('x86_64')
url="http://projects.gw-computing.net/projects/dfc"
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'gettext')
conflicts=('dfc-git')
source=("https://github.com/Rolinh/dfc/archive/v${pkgver}.tar.gz")
install='dfc.install'
sha512sums=('0dded1824e6e6734214999f898d253fab760bb69b0f152b44e47b54b30c6ebcf4ac04cec44a37eb655548d4dbaef9328a6b383957b5b8fe6e20cb550d51d46e2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DPREFIX=/usr -DSYSCONFDIR=/etc -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
