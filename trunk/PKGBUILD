# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=ruff
pkgver=0.0.173
pkgrel=1
pkgdesc="An extremely fast Python linter, written in Rust"
arch=(x86_64)
url="https://github.com/charliermarsh/ruff"
license=(MIT)
depends=(gcc-libs glibc python)
makedepends=(cargo maturin python-installer)
options=(!lto debug)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('08d2ab2ef07b2d89bd6853066b88e8f86c426227b95cf7814fa29a3bd2081756e03407eeb7e522a4ff6ff480ba0bce68850f570cb53ca5b3521819812a4dd771')
b2sums=('bc645ae99ab430882af6ad8c8f1c9625a6f09954f9a8ff0851bc057e6288534a8793553fadcdd4084ceca591929616056695b06765f03c3b59d626e215029365')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  local maturin_options=(
    --release
    --strip
    --locked
    --all-features
    --target $CARCH-unknown-linux-gnu
  )

  cd $pkgname-$pkgver

  maturin build "${maturin_options[@]}"
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
