# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_pkgname=furo
pkgname=python-sphinx-$_pkgname
pkgver=2023.03.27
pkgrel=2
pkgdesc='Clean customizable documentation theme for Sphinx'
arch=('any')
url='https://github.com/pradyunsg/furo'
license=('MIT')
depends=('python-sphinx-basic-ng' 'python-pygments' 'python-beautifulsoup4')
makedepends=('python-build' 'python-installer' 'python-sphinx-theme-builder' 'python-flit-core'
             'nodejs-lts-gallium' 'npm' 'expac' 'git')
source=("git+$url.git#tag=$pkgver?signed")
b2sums=('SKIP')
validpgpkeys=('E2FEFA4CA4BDD28171DB00F3FF99710C4332258E') # Pradyun Gedam (GitHub July 2020 Key) <pradyunsg@users.noreply.github.com>

prepare() {
  cd $_pkgname
  # force use of system nodejs
  local node_ver=$(expac %v nodejs-lts-gallium | cut -d - -f 1)
  sed -i "s/16.15.1/$node_ver/" pyproject.toml
}

build() {
  cd $_pkgname

  STB_USE_SYSTEM_NODE=true python -m build -nw

  # docs disabled for now to unblock Python 3.10 update

  # sphinx needs this theme installed because it is uses it.
  # simply injecting the package to sys.path (via PYTHONPATH or similar)
  # is not enough because sphinx looks for themes in the registered
  # entrypoints, this means we actually have to install the package.
  # for this we will create a venv with access to system packages and
  # install the theme there, then we will build the documentation.

  #python -m venv --system-site-packages doc-env
  #doc-env/bin/python setup.py install --skip-build

  #doc-env/bin/python /usr/bin/sphinx-build -b dirhtml -v docs build/docs/html
}

package() {
  cd $_pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  #install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  #cp -r -a --no-preserve=ownership build/docs/html "$pkgdir"/usr/share/doc/$pkgname
  #rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
