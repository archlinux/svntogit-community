# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-screensaver
pkgver=1.24.2
pkgrel=1
pkgdesc='Screensaver for MATE'
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libmatekbd' 'gettext' 'libnotify' 'libxss' 'mate-desktop' 'mate-menus' 'mate-session-manager')
makedepends=('systemd')
groups=('mate-extra')
conflicts=('mate-screensaver-gtk3')
replaces=('mate-screensaver-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5efbb05ac94a0d188d105f296d5bd96dd3a3797cf1c7a4edba8b399fe9831da2')

build() {
	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--with-xscreensaverdir=/usr/share/xscreensaver/config \
        	--with-xscreensaverhackdir=/usr/lib/xscreensaver \
        	--with-mit-ext \
        	--with-libnotify \
        	--enable-locking \
        	--without-console-kit
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
