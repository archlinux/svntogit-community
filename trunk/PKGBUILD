# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christopher Reimer <mail@c-reimer.de>

pkgname=pop-icon-theme
pkgver=2.1.0.r2434.28b598d347
pkgrel=1
pkgdesc='System76 Pop Icon Theme'
arch=(any)
url=https://github.com/pop-os/icon-theme
license=(CCPL)
depends=(adwaita-icon-theme)
makedepends=(
  git
  meson
)
_commit=28b598d34759c32714f85bdc75edf5fc780e3758
source=(pop-icon-theme::git+https://github.com/pop-os/icon-theme.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd pop-icon-theme
  echo 2.1.0.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  arch-meson pop-icon-theme build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
