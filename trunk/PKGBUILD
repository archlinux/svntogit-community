# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=uproot-methods
pkgname=python-uproot-methods
pkgver=0.7.1
pkgrel=2
pkgdesc="Pythonic mix-ins for ROOT classes"
arch=('any')
makedepends=('python-setuptools')
depends=('python-awkward' 'python-numpy')
url="https://github.com/scikit-hep/uproot-methods"
license=('BSD')

source=("${url}/archive/${pkgver}.zip")
sha256sums=('f5c8b41c1c820708c903bbfc66924eea8e9b8e852520cfd1db5e8a4c0e17a37b')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py test
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py install --root="${pkgdir}/" --optimize=1

    install -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -D README.rst "${pkgdir}/usr/share/${pkgname}/README.rst"
}
