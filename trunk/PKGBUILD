# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-kiwisolver
pkgver=1.4.3
pkgrel=1
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('PerlArtistic')
arch=('x86_64')
depends=('python')
makedepends=('python-cppy' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-runner')
source=("https://github.com/nucleic/kiwi/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('04205555da46e772f2d25a481c4d966dc7bab8a541434a7c183e1b4f41ccd6e06aec564f2a95dae649585d337c1bf818a7cf24727460ad14669a3b3d74c74274')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd kiwi-$pkgver
  python setup.py build
}

check() {
  cd kiwi-$pkgver
  PYTHONPATH="$PWD"/py \
  python setup.py pytest
}

package() {
  cd kiwi-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
