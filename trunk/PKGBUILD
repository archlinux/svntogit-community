# $Id$
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=uptimed
pkgver=0.3.17
pkgrel=2
pkgdesc="A system uptime record daemon"
arch=(i686 x86_64)
url="http://podgorny.cz/uptimed/"
license=('GPL')
depends=('glibc' 'bash')
backup=(etc/uptimed.conf)
options=('!libtool')
install=$pkgname.install
source=(http://podgorny.cz/$pkgname/releases/$pkgname-$pkgver.tar.bz2
        rc.d
        service)
md5sums=('528b62c33454b33537c3bf2366977bdb'
         '4ae90bee9fc78014a889a5072effb735'
         '657102b06613ddb9811427bee1973c6b')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  # Change log path to Arch standard
  for M in `ls M*`; do
    sed -i "s|/var/spool/uptimed|/var/log/uptimed|" $M
  done
  sed -i 's|spool|log|' libuptimed/urec.h

  autoreconf
  libtoolize --force

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=$pkgdir/usr sysconfdir=$pkgdir/etc install

  mv $pkgdir/etc/uptimed.conf-dist $pkgdir/etc/uptimed.conf
  install -Dm755 $srcdir/rc.d $pkgdir/etc/rc.d/uptimed
  install -Dm644 $srcdir/service $pkgdir/usr/lib/systemd/system/uptimed.service
}
