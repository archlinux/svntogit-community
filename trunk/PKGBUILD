# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Judd <jvinet@zeroflux.org>

_pkgbasename=libevent
pkgname=lib32-libevent
pkgver=2.1.12
pkgrel=1
pkgdesc="An event notification library (32-bit)"
arch=('x86_64')
url="http://libevent.org/"
license=('BSD')
depends=('lib32-openssl')
source=(https://github.com/libevent/libevent/releases/download/release-$pkgver-stable/$_pkgbasename-$pkgver-stable.tar.gz{,.asc})
sha256sums=('92e6de1be9ec176428fd2367677e61ceffc2ee1cb119035037a27d346b0403bb'
            'SKIP')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA'
              '9E3AC83A27974B84D1B3401DB86086848EF8686D')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}-stable
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --disable-libevent-regress --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbasename}-${pkgver}-stable
  make -j1 check
}

package() {
  cd ${_pkgbasename}-${pkgver}-stable
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include}

  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
