# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-term-table
pkgver=0.015
pkgrel=3
pkgdesc="Format a header and rows into a table"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Term-Table"
depends=('perl-importer')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Term-Table-$pkgver.tar.gz")
sha512sums=('2f5cbc28d0d0f76a6269892055871c103a65d9fd9836d3a0b7c7320cf1439d27cba596453f1d94bd324bd4d4dffdd0eb672801aa23f9a8609310a28eca15dd87')

build() {
  cd Term-Table-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Term-Table-$pkgver
  make test || echo "Expected failure due to Test2::Tools::Tiny being too old"
}

package() {
  cd Term-Table-$pkgver
  make DESTDIR="$pkgdir" install
}
