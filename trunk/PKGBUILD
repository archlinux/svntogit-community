# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-lib
pkgname=haskell-clash-lib
pkgver=1.6.4
pkgrel=41
pkgdesc="Clash: a functional hardware description language - As a library"
url="https://clash-lang.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-ansi-terminal' 'haskell-async'
         'haskell-attoparsec' 'haskell-base16-bytestring' 'haskell-clash-prelude'
         'haskell-concurrent-supply' 'haskell-cryptohash-sha256' 'haskell-data-binary-ieee754'
         'haskell-data-default' 'haskell-dlist' 'haskell-extra' 'haskell-ghc' 'haskell-glob'
         'haskell-hashable' 'haskell-src-meta' 'haskell-hint' 'haskell-interpolate' 'haskell-lens'
         'haskell-ordered-containers' 'haskell-pretty-show' 'haskell-prettyprinter'
         'haskell-primitive' 'haskell-stringsearch' 'haskell-temporary' 'haskell-terminal-size'
         'haskell-trifecta' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-vector-binary-instances' 'haskell-yaml')
makedepends=('ghc' 'uusi' 'haskell-ghc-typelits-knownnat' 'haskell-quickcheck-text' 'haskell-src-exts'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('9a53a16cf6bd1788c20b46698a086a397dcd4b76f9882b69a80ac53ed3062e02072655b2c714030a63587e336bcf5b0b025cdc33f469b29b38d4d845e5062e90')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u aeson $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
