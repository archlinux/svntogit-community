# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=doctest
pkgname=haskell-doctest
pkgver=0.13.0
pkgrel=5
pkgdesc="Test interactive Haskell examples"
url="https://github.com/sol/doctest"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-ghc' 'haskell-syb' 'haskell-code-page'
         'haskell-ghc-paths')
makedepends=('ghc' 'haskell-hunit' 'haskell-hspec' 'haskell-quickcheck' 'haskell-stringbuilder'
             'haskell-silently' 'haskell-setenv' 'haskell-with-location' 'haskell-mockery')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('6ae0cc74811501cc8d94c50a1de2ae241e298a3bdc56ef21a1b0c5f6ad05b3119efbafb1e3d286c1aee59c9884f364c04ecc8e4505ac54d9c8319b3402a92644')

prepare() {
    # Add -dynamic flag
    sed -i '/-packageghc/a \  , "-dynamic"' $_hkgname-$pkgver/test/doctests.hs
    sed -i 's/Example.hs"/Example.hs", "-dynamic"/' $_hkgname-$pkgver/src/Run.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
