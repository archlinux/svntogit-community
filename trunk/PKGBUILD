# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: <david@pako.st>

pkgname=chicken
pkgver=4.7.0
pkgrel=2
pkgdesc='Compiler for R5RS scheme, featuring handy extensions'
arch=('i686' 'x86_64')
url='http://www.call-with-current-continuation.org/'
license=('BSD')
depends=('gcc')
source=("http://code.call-cc.org/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('9389388fdf04c3c64de29633aae12539')

build() {
  cd ${pkgname}-${pkgver}

  # parallel builds are not supported..
  export MAKEFLAGS="${MAKEFLAGS/-j?/}"

  # some yummy make options there. check out README in the tarball.
  make PLATFORM=linux PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  
  # parallel builds are not supported..
  export MAKEFLAGS="${MAKEFLAGS/-j?/}"
  make PLATFORM=linux PREFIX=/usr DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
