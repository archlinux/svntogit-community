# Maintainer: Alad Wenter <alad at mailbox dot org>
# Contributor: William Gathoye <william at gathoye dot be>
# Contributor: Emil Vanherp <emil dot vanherp at hotmail dot com>
# Contributor: Xavier D. <magicrhesus at ouranos dot be>
# Contributor: Valere Monseur <valere dot monseur at ymail dot com>

pkgname=eid-mw
pkgver=4.2.12
pkgrel=1
pkgdesc='The middleware, viewer and Firefox extension for the Belgian electronic identity card (Belgian eID)'
arch=('x86_64')
url='http://eid.belgium.be/'
license=('LGPL3')
depends=('pcsclite' 'gtk3' 'libproxy')
optdepends=('firefox: extension for Belgian eid'
            'acsccid: ACS CCID smart card readers'
            'ccid: A generic USB Chip/Smart Card Interface Devices driver'
            'pcsc-tools: PC/SC smartcard tools')
conflicts=('eid-viewer')
replaces=('eid-viewer')
source=("https://dist.eid.belgium.be/continuous/sources/$pkgname-$pkgver-v$pkgver.tar.gz"
	"https://dist.eid.belgium.be/continuous/sources/$pkgname-$pkgver-v$pkgver.tar.gz.asc")
sha256sums=('93a6b560a062d749f67f67412a644f6476af43e26d4004881895a218fc76aeb0'
            'SKIP')
# Belgian eID Automatic Signing Key (continuous builds)
validpgpkeys=('D95426E309C0492990D8E8E2824A5E0010A04D46')

build() {
    cd "$pkgname-$pkgver-v$pkgver"
    ./configure --prefix=/usr --libexecdir=/usr/bin
    make
}

package() {
    cd "$pkgname-$pkgver-v$pkgver"
    make install DESTDIR="$pkgdir"
}
