# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=sratom
pkgver=0.6.0
pkgrel=1
pkgdesc="An LV2 Atom RDF serialisation library"
arch=('x86_64')
url="https://drobilla.net/software/sratom/"
license=('custom:ISC')
depends=('lv2' 'sord')
makedepends=('python')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('899bb01896e2b65b69482041c7e06edc9e45e9a3466392c82a58f16dfc71462a6c3e3e16a5aa50a6347c8366ea72898468910a59a084762f75eb8c7031ceacf8'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove local ldconfig call
  sed -i '/ldconfig/d' wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python waf configure --prefix=/usr
  python waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"
  # license
  install -vDm644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {NEWS,README}
}

# vim:set ts=2 sw=2 et:
