# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=hiredis
pkgver=0.12.1
pkgrel=1
pkgdesc='Minimalistic C client library for Redis'
arch=('x86_64' 'i686')
url="https://github.com/redis/hiredis/"
license=('BSD')
depends=('glibc')
checkdepends=('redis')
source=(hiredis-$pkgver.tar.gz::https://github.com/redis/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('194df55484a506e921da784913a07fbda86e2608b5aabd1ffe9bb1de330099a8')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -r 's|echo \\|echo -e \\|' -i Makefile
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make PREFIX="$pkgdir"/usr install

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
