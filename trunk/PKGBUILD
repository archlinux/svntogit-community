# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: val√®re monseur <valere dot monseur at ymail dot com>

pkgname=python-ghp-import
_pkgname=ghp-import
pkgver=0.5.4
pkgrel=2
pkgdesc='Copy your docs directly to the gh-pages branch'
arch=('any')
url="https://pypi.python.org/pypi/ghp-import"
license=(custom:Tumbolia)
depends=('python')
makedepends=('python-setuptools' 'git')
replaces=('ghp-import')
conflicts=('ghp-import')
source=("https://github.com/davisp/${_pkgname}/archive/${pkgver}.tar.gz")
#source=("${_pkgname}-${pkgver}::git+https://github.com/davisp/ghp-import.git#commit=ab2637fb7a84ed79d6baeb74db7f9e544a448a04")
sha512sums=('1607c679e2c8a9772e47472a35f4206f9e6bbcc3190d4ccc79de4d88a38396ec08badf030f0cda76a01e2d71f84f32d61c733fef361b8e3dbd52cd3e166932fc')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  LC_CTYPE=en_US.UTF-8 python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  LC_CTYPE=en_US.UTF-8 python setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
