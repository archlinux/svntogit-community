# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgbase=python-parse
pkgname=(python-parse python2-parse)
pkgver=1.8.0
pkgrel=1

pkgdesc='Parse strings using a specification based on the Python format() syntax.'
url='http://pypi.python.org/pypi/parse'
arch=('any')
license=('BSD')

makedepends=('python-setuptools' 'python2-setuptools')

source=("https://pypi.org/packages/source/p/parse/parse-$pkgver.tar.gz"{,.asc}
        LICENSE) # license from the bottom of parse.py

md5sums=('6ea7e32cb35810113137f6073fb30639'
         'SKIP'
         '68c10fa20482cc4328a1052695fb7e53')

validpgpkeys=('0145FD2B52E80A8E329A16C7AC68AC0441C6E930')


check() {
  cd parse-"$pkgver"
  python test_parse.py || warning 'test_too_many_fields fails on Python 3'
  python2 test_parse.py
}


package_python-parse() {
  depends=('python')

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

  cd parse-"$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}


package_python2-parse() {
  depends=('python2')

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

  cd parse-"$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
