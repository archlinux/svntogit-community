# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-system-monitor
pkgver=1.0.4
pkgrel=1
pkgdesc="Monitor for UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-system-monitor"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'hicolor-icon-theme' 'kwindowsystem' 'libgtop' 'qt5-svg'
         'qt5-x11extras')
makedepends=('qt5-tools')
source=("https://github.com/ukui/ukui-system-monitor/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8fb6eb3501e803efac303ceb2c5324d43e80c7161903861561943e0d02de036e15906e70793131da228295721489624ac7813ecd41f57e6f3cca9d47f02beb8e')

build() {
  cd ukui-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
