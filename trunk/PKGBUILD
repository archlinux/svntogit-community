# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=trace-cmd
pkgver=2.9.3
pkgrel=1
pkgdesc="Interact with Ftrace Linux kernel internal tracer"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/trace-cmd/trace-cmd.git"
license=('GPL2' 'LGPL2.1')
depends=('glibc')
makedepends=('asciidoc' 'audit' 'bash-completion' 'libtraceevent' 'libtracefs' 'python' 'swig' 'xmlto')
optdepends=('python: for Python plugins')
source=("https://git.kernel.org/pub/scm/utils/trace-cmd/trace-cmd.git/snapshot/trace-cmd-v${pkgver}.tar.gz"
        "${pkgname}-2.9-make_install.patch"
        "${pkgname}-2.9.3-python3.patch"
        "${pkgname}-2.9.3-docs_makefile.patch")
sha512sums=('3483e2682e39269c1f86387be74e18267e1352db99dc6748c67a25a5bd3c4d079c424c42fca69a0022e4b6496b43b831855904bc29645b4f02d7d1c23b673bf7'
            'c9321b2b885d88557a057d7064690274799ab1d67a8babb78b1c54c4d81546fdea7313286cfd5fb9efbbcf87cf8394dcb17202ac88434a351aa652e7109e1db6'
            'a1834b382cbee920697d08c7eb7292c4a5ea606e4f32459a60e0b236ad057536c987f3e162c9c99affa46396248e98c8f4ecd21031141f0221dbadf0bd33737d'
            '1d43fc0fec3b386a741467966bcecdcacdfbe36e21c63d644ec281ff9577332e2d6cff95cef22bab85d2209017eb39d36def35f24b86e54720d6913a44fc3cbd')
b2sums=('a62e625e13ffbe10280d330f3b40f636f156a001cbcefd3a6e5fabcf572c1a3cabcbde8288b6a5c69925c7b1f9f5a54e8ec21a02a3979dddb074f980cc0f511a'
        '8a644be295417b1fd5fb807285a7176ef0ceb737352bbb4c7a88441f9b955a90befe974d890a58f2b8fb6de8a68a1b3c5d207f057c4b08db79caf6e97fa66f50'
        'dac368fc62d99eba6d36f2c7d81475c28211dfe5d1d59ca5e468daff31d36710637943dc822fb266784feb5355ca0bfb525819bb96095c8e0c0fa91f32558877'
        '86104406eb8694a96cd6ed34f2dcdf06039d4342ebacbb492759945b7b156d41806986fc96e421557a7a224f18a5786ec999c5c07f2a9ffff051351911cc8095')

prepare() {
  mv -v "${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "../${pkgname}-2.9-make_install.patch"
  patch -Np1 -i "../${pkgname}-2.9.3-docs_makefile.patch"
  patch -Np1 -i "../${pkgname}-2.9.3-python3.patch"
}

build() {
  cd "$pkgname-$pkgver"
  export PYTHON_VERS=python3
  make prefix='/usr' libdir_relative='lib'
  make -C Documentation
}

package() {
  depends+=('libaudit.so' 'libtraceevent.so' 'libtracefs.so')

  cd "${pkgname}-${pkgver}"
  make prefix="/usr" \
       libdir_relative=lib \
       DESTDIR="${pkgdir}/" \
       BASH_COMPLETE_DIR="$(pkgconf --variable=completionsdir bash-completion)" \
       install
  make prefix="/usr" DESTDIR="${pkgdir}/" install -C Documentation
  install -vDm 644 tracecmd/${pkgname}.bash \
    "${pkgdir}/usr/share/bash/bash-completion/completions/${pkgname}"
}
