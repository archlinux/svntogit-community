# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.6.9.27
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'udisks2-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz")
sha512sums=('59ed213dd533ed7d6187fef44e8f5edde95016494d562adb16b15002e837cfcfd5291d476b7e48a0ba5601219f2c273d0e0de8cbbf5075f93a16cc949a4e03f7')

prepare() {
  cd deepin-album-$pkgver
  sed -i '/#include <QPainter>/a #include <QPainterPath>' deepin-album/album/frame/toptoolbar.cpp deepin-album/album/thumbnail/thumbnaildelegate.cpp \
                                                          deepin-album/album/widgets/dlmenuarrow.h deepin-album/album/widgets/dialogs/dfmdarrowlineexpand.cpp \
                                                          deepin-album/album/widgets/popupmenustyle.cpp
  sed -i '/#include <QDebug>/a #include <QPainterPath>' deepin-album/album/module/view/contents/ttbcontent.cpp
}

build() {
  cd deepin-album-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-album-$pkgver
  DESTDIR="$pkgdir" ninja install
}
