# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# ThomasV PGP key: gpg --recv-key 6694D8DE7BE8EE5631BED9502BD5824B7F9470E6
# http://bitcoin-otc.com/viewgpg.php?nick=ThomasV

pkgname=electrum
pkgver=1.9.8
pkgrel=4
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('python2-ecdsa' 'python2-pbkdf2' 'python2-pyqt4' 'python2-sip' 'python2-slowaes')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'pygtk: GTK user interface'
            'python2-zbar: QR code reading support')
url="https://electrum.org"
license=('GPL3')
source=("https://download.electrum.org/${pkgname^}-${pkgver}.tar.gz"{,.asc}
        setup.py.patch
        gtk3.patch
        pyqt4-4.11.patch)
md5sums=('e3918fec0254267f08e41a1fb8691382'
         'SKIP'
         '562f61b12e8bc80de23fb56b8ce56fe7'
         'b74882fe1c8319faf8f7d1ecbb6e30f6'
         '0e865e4f0337f9a6acef6d15581627ed')
sha256sums=('8fc144a32013e4a747fea27fff981762a6b9e14cde9ffb405c4c721975d846ff'
            'SKIP'
            '5f8d846dedce20601f3131e687bd2dc54ca8df0137cbdaa1b06eab11eb6a52da'
            'ba97ed1716af25fb64e439415cbe55aadac3d6528ab29f7a0ea0f93b5a539299'
            '2542b0891b3e25674b4ab0755b4bf23523144b3e86dfb6281f852117e2e3d973')
validpgpkeys=('6694D8DE7BE8EE5631BED9502BD5824B7F9470E6')

provides=('electrum')
install=electrum.install

prepare() {
  cd ${pkgname^}-${pkgver}

  patch -p1 < "${srcdir}"/setup.py.patch
  patch -p1 < "${srcdir}"/gtk3.patch
  patch -p1 < "${srcdir}"/pyqt4-4.11.patch

  find . -type f -exec sed -i 's#/usr/bin/python#/usr/bin/python2#g' {} +
  find . -type f -exec sed -i 's#/usr/bin/env python#/usr/bin/env python2#g' {} +
}

build() {
  cd ${pkgname^}-${pkgver}

  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${pkgname^}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
