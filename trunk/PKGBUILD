# Maintainer: Baptiste Jonglez
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=2.1.5
pkgrel=2
pkgdesc='An efficient object serialization library'
arch=("x86_64")
url='http://msgpack.org/'
license=("Apache")
depends=("glibc")
checkdepends=("gtest")
makedepends=("cmake")
source=("https://github.com/msgpack/msgpack-c/releases/download/cpp-${pkgver}/msgpack-${pkgver}.tar.gz"
        "test-cflags.patch")
sha256sums=('6126375af9b204611b9d9f154929f4f747e4599e6ae8443b337915dcf2899d2b'
            '0e6bd401c3ff6f6c68e8beca682b1ecb1b125092a1b1739757294c3c040ba481')

prepare() {
  cd "msgpack-${pkgver}"
  # Remove -Werror when building tests (useless and makes build fail)
  patch -p0 < "$srcdir"/test-cflags.patch
}

build() {
  cd "msgpack-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DMSGPACK_CXX11=ON \
        -DMSGPACK_BUILD_EXAMPLES=OFF \
        .
  make
}

check() {
  cd "msgpack-${pkgver}"
  make test 
}

package() {
  cd "msgpack-${pkgver}"
  make DESTDIR="$pkgdir" install
}
