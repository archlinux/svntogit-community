# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=lens
pkgname=haskell-lens
pkgver=4.19.1
pkgrel=8
pkgdesc="Lenses, Folds and Traversals"
url="https://github.com/ekmett/lens/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-bifunctors' 'haskell-call-stack' 'haskell-comonad'
         'haskell-contravariant' 'haskell-distributive' 'haskell-exceptions' 'haskell-free'
         'haskell-hashable' 'haskell-kan-extensions' 'haskell-parallel' 'haskell-profunctors'
         'haskell-reflection' 'haskell-semigroupoids' 'haskell-tagged' 'haskell-th-abstraction'
         'haskell-transformers-compat' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-generic-deriving' 'haskell-hunit'
             'haskell-quickcheck' 'haskell-simple-reflect' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2'
             'haskell-test-framework-th')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('50a596ce919abc93b453189453abdbf0f455e0531e5b83397d1e61308842222433fd7d4c6c6f5ec74f98d8dd26eefe861a0d88d5faf56a7b896a0b083b920a10')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/nats/d' -e '/semigroups *>= 0.9/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-j -ftrustworthy -f-safe -ftest-templates -ftest-properties -ftest-hunit \
            -ftest-doctests -f-dump-splices -f-old-inline-pragmas -finlining \
            -f-benchmark-uniplate
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
