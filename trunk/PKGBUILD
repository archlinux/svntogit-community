# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-context
pkgname=(python-oslo-context python2-oslo-context)
pkgver=2.22.0
pkgrel=1
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-debtcollector'
             'python2-debtcollector')
checkdepends=('python-mock' 'python2-mock' 'python-oslotest' 'python2-oslotest')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.context/archive/$pkgver.tar.gz")
sha512sums=('f88cd618cd73a21787f1c49554f1d12863a1909724e6d29be2a5011c5103b54675c521049acb50b7ae8f69c7918b7947f3a84a75d806d267c30015e366d7c971')

prepare() {
  cp -a oslo.context-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.context-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.context-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.context-$pkgver
  stestr run

  cd "$srcdir"/oslo.context-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-oslo-context() {
  depends=('python-pbr' 'python-debtcollector')

  cd "$srcdir"/oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-context() {
  depends=('python2-pbr' 'python2-debtcollector')

  cd "$srcdir"/oslo.context-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
