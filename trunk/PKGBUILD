# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=singletons
pkgname=haskell-singletons
pkgver=2.5
pkgrel=2
pkgdesc="A framework for generating singleton types"
url="https://github.com/goldfirere/singletons"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-desugar' 'haskell-syb')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-golden')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('f3c895ca9bf268b6852f922bd5a93bc415af03c8f15c66a45a45987b8c99a708f7ba122dda119e05fdcf837eaf955f0eada51c91c0de4e78c498d6c4242e9006')

prepare() {
    cd $_hkgname-$pkgver
    # TODO: make it work :/
    # sed -i '/XEmptyCase/a \  , "-dynamic"' tests/SingletonsTestSuiteUtils.hs
    sed -i 's/< *4.12/<5/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
