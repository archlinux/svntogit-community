# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: TryA <tryagainprod {at} gmail.com>

_pkgbasename=bzip2
pkgname=lib32-bzip2
pkgver=1.0.8
pkgrel=3
pkgdesc="A high-quality data compression program (32-bit)"
arch=('x86_64')
license=('custom')
url="https://sourceware.org/bzip2/"
depends=('lib32-glibc' $_pkgbasename)
source=(https://sourceware.org/pub/bzip2/$_pkgbasename-$pkgver.tar.gz{,.sig})
sha512sums=('083f5e675d73f3233c7930ebe20425a533feedeaaa9d8cc86831312a6581cefbe6ed0d08d2fa89be81082f2a5abdabca8b3c080bf97218a1bd59dc118a30b9f3'
            'SKIP')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>

build() {
  cd ${_pkgbasename}-${pkgver}
 
  make -f Makefile-libbz2_so CC="gcc -m32 $CFLAGS $LDFLAGS"
  make libbz2.a CC="gcc -m32 $CFLAGS $LDFLAGS"
}

package(){
  cd ${_pkgbasename}-${pkgver}

  install -dm755 "$pkgdir"/usr/lib32
  cp -a libbz2.so* "$pkgdir"/usr/lib32
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib32/libbz2.so
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib32/libbz2.so.1 # For compatibility with some other distros

  install -Dm644 libbz2.a "$pkgdir"/usr/lib32/libbz2.a

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
