# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego Principe <cdprincipe@gmail.com>
# Contributor: paterbrown <paterbrown@silberhelme.de>
# Contributor: Josip <josipponjavic@gmail.com>

pkgname=elementary-icon-theme
pkgver=3.2.1
pkgrel=1
pkgdesc='An icon theme designed to be smooth, sexy, clear, and efficient'
arch=('any')
url='https://launchpad.net/elementaryicons'
license=('GPL3')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme')
options=('!emptydirs' '!strip')
install='elementary-icon-theme.install'
source=("https://launchpad.net/elementaryicons/${pkgver%%.*}.x/${pkgver}/+download/elementary-icon-theme-${pkgver}.tgz")
sha256sums=('3877a80c1428da9e6a3c97327ad17510271f729a2d7c162f13aafca225a1672f')

prepare() {
  cd elementary-icon-theme-${pkgver}

  rm AUTHORS CONTRIBUTORS COPYING
  sed '/Name/a Inherits=gnome,hicolor' -i index.theme
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/icons
  cp -dr --no-preserve='ownership' elementary-icon-theme-${pkgver} "${pkgdir}"/usr/share/icons/elementary
}

# vim: ts=2 sw=2 et:
