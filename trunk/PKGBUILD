# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan Fader <jan.fader@web.de>

pkgname=fish
pkgver=2.0b2
pkgrel=4
pkgdesc='Smart and user friendly shell intended mostly for interactive use'
arch=('i686' 'x86_64')
url='http://ridiculousfish.com/shell/'
license=('GPL2')
depends=('python2')
makedepends=('doxygen' 'python')
install=fish.install
source=($pkgname-$pkgver.tar.gz::http://ridiculousfish.com/shell/files/fishfish.tar.gz)
md5sums=('ebe5fe17f6925b9142aadc8ebae5fba1')

build() {
  cd "$srcdir"/fishfish
  autoconf
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --without-xsel
  make
}

package() {
  cd "$srcdir"/fishfish
  make DESTDIR="$pkgdir" install

  # use python2
  find "$pkgdir"/usr/share/fish/tools/ -type f -exec sed -e "1s|python|python2|" -i {} \;
}
