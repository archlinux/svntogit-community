# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-dkim
pkgver=1.1.1
pkgrel=1
pkgdesc='Python library that implements DKIM and ARC email signing and verification'
arch=('any')
url=https://launchpad.net/dkimpy
license=('ZLIB')
depends=('python-dnspython')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-authres' 'python-pynacl')
optdepends=('python-aiodns' 'python-authres' 'python-pynacl')
source=("https://files.pythonhosted.org/packages/source/d/dkimpy/dkimpy-$pkgver.tar.gz")
sha256sums=('7559744b5a9019691908f0a0c653e206b6cbf636c8c6d66e1a0827cfc8ec7f91')
b2sums=('6b9107129a0857a384d90b9c7d3970177aa32edaaeb41d62dd4edc6fdf7e8c16e3160162aaed074f865d476dc3f7f20ad299f7034858c0fc59ef88c651552ae4')

build() {
  cd dkimpy-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd dkimpy-$pkgver
  python -m unittest dkim.tests.test_suite
}

package() {
  cd dkimpy-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/dkimpy-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
