# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lilv
pkgver=0.24.4
pkgrel=4
pkgdesc="A C library interface to the LV2 plug-in standard"
arch=('x86_64')
url="https://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('sratom' 'jack')
makedepends=('swig' 'waf')
optdepends=('bash-completion: completion for bash'
            'libsndfile: for lv2apply')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        "autowaf.py::https://git.drobilla.net/cgit.cgi/autowaf/plain/extras/autowaf.py?id=d7a7ca42155924d29ce98a0b5f15dcf3d7a0cc3d")
sha512sums=('cb909c83dbac36b51a206fe508aec51e52ddf068f9a159d053888594402def8d140aae0d9e1e1866e722396f7ec8dea4b86d4c1b958f56eb3193b78307918a89'
            'SKIP'
            '7557aa23f994a527fbec88997e0e6a12fbead9bb3440d26c3350004a90c4bf1d36ed53f11590c8fb388c19f9d6b3516c8cc4c667b831866810a04d4de47a0e69')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -p tools
  touch __init__.py
  cp -v ../autowaf.py tools/
  # don't run local ldconfig
  # use local custom waf script
  sed -e '/ldconfig/d' \
      -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                        --bindings \
                        --no-bash-completion \
                        --dyn-manifest \
                        --test
  waf build
}

check() {
  cd "${pkgname}-${pkgver}"
  waf test
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # bash completion
  install -vDm 644 utils/lilv.bash_completion\
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}

# vim:set ts=2 sw=2 et:
