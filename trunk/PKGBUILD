# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=atomicwrites
pkgbase=python-atomicwrites
pkgname=('python-atomicwrites' 'python2-atomicwrites')
pkgver=1.3.0
pkgrel=1
pkgdesc="Atomic file writes on POSIX"
arch=('any')
url="https://github.com/untitaker/python-atomicwrites"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9b619b622f4509c2a41ad340f91bd6e74f238cf9a35f4662fa70f1f957a78069cb89f9bf16f8da70a597bc43a0c6c4fdea6afd0d157364a53a2fa5d8f4272a61')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
  py.test2
}

package_python-atomicwrites() {
  depends=('python')
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-atomicwrites() {
  depends=('python2')
  cd "${_name}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

