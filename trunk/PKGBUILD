# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=openfire
pkgver=3.9.3
pkgrel=3
pkgdesc="High performance XMPP (Jabber) server."
arch=('any')
url='http://www.igniterealtime.org/projects/openfire/'
license=('GPL')
depends=('java-runtime-headless')
makedepends=('apache-ant')
install='openfire.install'
backup=(
	'etc/openfire/openfire.xml' 'etc/openfire/openfire.xml' 'etc/openfire/openfire.xml' \
	'etc/conf.d/openfire' 'etc/openfire/security/keystore' 'etc/openfire/security/truststore' \
	'etc/openfire/security/client.truststore')
source=("http://www.igniterealtime.org/downloadServlet?filename=openfire/openfire_src_${pkgver//./_}.tar.gz" \
	'openfire.conf' 'openfire.service' 'user.conf')

build() {
	cd "$srcdir"/openfire_src/build
	rm -rf ../src/resources/nativeAuth/
	ant openfire
# 	ant plugins
}

package() {
	cd "$pkgdir"
	install -dm755 usr/lib usr/share/openfire/resources etc/openfire var/log/openfire

	cd "$srcdir"
	install -Dm644 openfire.conf "$pkgdir"/etc/conf.d/openfire
	install -Dm644 openfire.service "$pkgdir"/usr/lib/systemd/system/openfire.service
	install -Dm644 user.conf "$pkgdir"/usr/lib/sysusers.d/openfire.conf

	cd openfire_src/target/openfire
	rm -rf bin logs

	cp -R conf/* "$pkgdir"/etc/openfire/
	ln -s /etc/openfire "$pkgdir"/usr/share/openfire/conf

	cp -R resources/security "$pkgdir"/etc/openfire/
	ln -s /etc/openfire/security "$pkgdir"/usr/share/openfire/resources/security

	cp -R plugins "$pkgdir"/usr/share/openfire/
	cp -R resources/{database,spank} "$pkgdir"/usr/share/openfire/resources/
	cp -R lib "$pkgdir"/usr/lib/openfire
}

sha256sums=('37001835a3007f115214aa83f2a48f8c33bbf3576937f4d964d9eb3de3933ad2'
            'c8a612abee90bdc9a2869e562a4bbbf596159c60adcd23368833a0e2ca27c1d4'
            '772332b66a90935ddb8075eb10eab9b95a84caa45908bb3fa5861d7fa148399c'
            '743d07158d40f3690684a078d5f35c53c819e45d4ff0c2016b0f33662363614f')
