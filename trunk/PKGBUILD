# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-isort
pkgver=2.0.0
pkgrel=2
pkgdesc='pytest plugin to perform isort checks (import ordering)'
arch=('any')
license=('BSD')
url='https://github.com/moccu/pytest-isort'
depends=('python-pytest' 'python-isort')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/moccu/pytest-isort/archive/$pkgver.tar.gz")
sha512sums=('b338bf0de7e98be330c1ee391e0b1aaae363efbcfd89f14138704ad6d0c29a4d7a0b27e17251148dc26d04aae39184e0c5a465f3091769cf1b4043f6faf3c653')

build() {
  cd pytest-isort-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

# vim:set ts=2 sw=2 et:
