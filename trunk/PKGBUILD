# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=lib32-db
pkgver=5.3.28
pkgrel=1
pkgdesc='The Berkeley DB embedded database system'
arch=('x86_64')
url='http://www.oracle.com/technology/software/products/berkeley-db/index.html'
license=('custom')
depends=("${pkgname#*-}=${pkgver}" 'gcc-libs-multilib')
source=("http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz")
sha256sums=('e0a992d740709892e81f9d93f06daf305cf73fb81b545afe72478043172c3628')

build() {
  cd ${pkgname#*-}-${pkgver}/build_unix

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ../dist/configure --prefix='/usr' --libdir='/usr/lib32' --enable-{compat185,cxx,dbm,shared}
  make LIBSO_LIBS='-lpthread'
}

package() {
  cd ${pkgname#*-}-${pkgver}/build_unix

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,docs,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
