# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgname=python-w3lib
pkgver=1.21.0
pkgrel=4
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD')
url="https://w3lib.readthedocs.org"
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/scrapy/w3lib/archive/v$pkgver.tar.gz")
sha512sums=('7b55e31e3a942ef1ba3db0ac4d325d7a547ed45764b22d91503358d65dbef6e84abe9777899b3a966882013f1d02314b990078849c68c2e249a41e32025bb92e')

build() {
  cd "$srcdir"/w3lib-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/w3lib-$pkgver
  py.test
}

package() {
  cd w3lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
