# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.6.1
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('i686' 'x86_64')
license=('GPL')
url='https://github.com/definite/ibus-chewing'
depends=('ibus' 'libchewing' 'libxtst')
makedepends=('swig' 'gob2' 'cmake-fedora')
source=("$pkgname-$pkgver.tar.gz::https://github.com/definite/ibus-chewing/archive/$pkgver.tar.gz")
sha512sums=('b093db79c656433614b9f72719a2b0768a30c2fb6fa066eda1e79cc9932ce1836f7f1e5611612fb44f260fe80a9baa9e18fdc9754b285e8a5f12a87c376cbd39')

build() {
  cd $pkgname-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/usr/share \
    -DLIBEXEC_DIR=/usr/lib/ibus
  make
  make translations
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
