# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=sourcemap
pkgname=haskell-sourcemap
pkgver=0.1.7
pkgrel=96
pkgdesc="Implementation of source maps as proposed by Google and Mozilla."
url="https://github.com/juhp/sourcemap"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-attoparsec" "haskell-unordered-containers"
         "haskell-utf8-string")
makedepends=('ghc')
checkdepends=('nodejs' 'npm' 'nodejs-source-map')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8bb22dc3a66e5e63cb2b3ea44bdc15843dfd83d28d4d8cfacdbf99730ed9d0d832e47d9e4bb462f66753ffb177c0374b1f5f58c4ee9597193a6f4486150a1846')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    npm link source-map
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
