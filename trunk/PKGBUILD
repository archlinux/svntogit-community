# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: emersion <contact emersion fr>

pkgname=libvarlink
pkgver=22
pkgrel=1
license=('MIT')
pkgdesc='Varlink C library and command line tool'
makedepends=('meson')
arch=('x86_64')
url='https://github.com/varlink/libvarlink'
source=("https://github.com/varlink/libvarlink/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('932c0d7c3d2d8acdf5878d8fd8006887c5855189688d19eb09125682740b99bb')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr build/
  ninja -C build/
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build/ test
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  DESTDIR="$pkgdir/" ninja -C build/ install
}
