# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-browse
_pkgname=Browse
pkgver=200
pkgrel=1
pkgdesc="Sugar activity to browse the internet"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Browse"
license=('GPL')
groups=('sugar-fructose')
depends=('evince' 'gconf' 'sugar-toolkit-gtk3' 'webkit2gtk')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('60c4d53a855f069279a38938f8b63a4c25108373cf97faf2c7e7e69017c46c73')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
  sed -i "s/backup_url == '':/not backup_url:/" webactivity.py
  sed -i 's/"Feladó"/"Feladó: %s"/' po/hu.po
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
