# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=web-news
pkgver=1.6.4
pkgrel=8
pkgdesc="A PHP web-based NNTP client"
arch=('any')
url="https://sourceforge.net/projects/web-news"
license=('GPL')
depends=('php')
backup=('etc/webapps/web-news/.htaccess'
	'etc/webapps/web-news/webnews.cfg.php')
source=(https://downloads.sourceforge.net/sourceforge/web-news/WebNews-$pkgver.tar.gz
	utf8.patch
	php7.patch)
sha256sums=('1bdaa877dfee7e42b3d44784c049758cfffb6e959a3523923bae40cc64259041'
            'ec880d02c7d935e0f7dc2a5e2a5c5839d2a0830734f37386343d4bb7ff315518'
            '999d62293de394f1ebdc04890d32506e9a53d692e0cd6e039c8561a25728c88e')

prepare() {
  cd "$srcdir"
  patch -p1 <utf8.patch && rm -f utf8.patch
  patch -p1 <php7.patch && rm -f php7.patch
  find . -type f -name '*.orig' -exec rm -f {} \;
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/web-news
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/web-news
  cd ${_instdir}
  rm -f "$srcdir"/WebNews-$pkgver.tar.gz
  cp -ra "$srcdir"/* .

  echo -e "deny from all\nAddDefaultCharset utf-8" >"$pkgdir"/etc/webapps/web-news/.htaccess
  ln -s /etc/webapps/web-news/.htaccess .htaccess

  mv config/webnews.cfg.php "$pkgdir"/etc/webapps/web-news/
  ln -s /etc/webapps/web-news/webnews.cfg.php config/webnews.cfg.php
}
