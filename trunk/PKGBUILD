# $Id$
# Maintainer: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgname=apper
_pkgmainver=0.7
pkgver=0.7.2
pkgrel=6
pkgdesc="KDE tools for PackageKit"
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Apper?content=84745"
license=('GPL')
depends=('kdebase-workspace' 'packagekit-qt2>=0.6.17')
makedepends=('cmake' 'automoc4' 'chrpath')
install="$pkgname.install"
provides=('kpackagekit')
conflicts=('kpackagekit')
replaces=('kpackagekit')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.bz2")
sha256sums=('975fede728e7ab96d8e244ae721a2e15ae40b9fb1cd189a1f4afd46c400b219f')

build() {
	cd "$pkgname-$pkgver"
	[ -d "build" ] && rm -rf build
	mkdir build
	cd build

	cmake ../ \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$pkgname-$pkgver"
	cd build
	make DESTDIR="$pkgdir" install
	# avoid gnome-packagekit conflict (stolen from fedora as suggested)
	mv "$pkgdir/usr/share/dbus-1/services/org.freedesktop.PackageKit.service" \
		"$pkgdir/usr/share/dbus-1/services/kde-org.freedesktop.PackageKit.service"
	# Hack around cmake rpath bug (debian)
	chrpath --list "$pkgdir/usr/bin/apper"
	chrpath --replace "/usr/lib/apper" "$pkgdir/usr/bin/apper"
}
