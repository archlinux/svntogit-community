# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=warzone2100
pkgver=4.1.0
pkgrel=1
pkgdesc="3D realtime strategy game on a future Earth"
url="https://wz2100.net/"
arch=('x86_64')
license=('GPL')
depends=('sdl2' 'openal' 'libvorbis' 'libtheora' 'libsodium' 'physfs' 'sqlite' 'freetype2')
makedepends=('zip' 'unzip' 'asciidoc' 'mesa' 'vulkan-headers' 'shaderc' 'cmake' 'ninja')
source=(${pkgname}-${pkgver}_src.tar.xz::https://github.com/Warzone2100/warzone2100/releases/download/${pkgver}/${pkgname}_src.tar.xz)
sha256sums=('1d096986880ada38d35d5edc43c9453568c73019e94adcaf4f7c0a623943416d')

prepare() {
  rm -rf build && mkdir -p build
}

build() {
  cd build

  cmake -G Ninja ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWZ_DISTRIBUTOR="ArchLinux"
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install

  ## ??? WTF ???
  rm "${pkgdir}"/usr/bin/.portable
}
