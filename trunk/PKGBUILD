# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-pycryptodomex
pkgname=(python-pycryptodomex python2-pycryptodomex)
pkgver=3.7.3
pkgrel=1

pkgdesc='A self-contained Python package of low-level cryptographic primitives'
url='http://www.pycryptodome.org/'
arch=('x86_64')
license=('BSD')

makedepends=('python-setuptools'
             'python2-setuptools')

source=("https://pypi.org/packages/source/p/pycryptodomex/pycryptodomex-$pkgver.tar.gz")

md5sums=('c9ed9aeb5c46fc5fc7cc8cb1ce542040')

prepare() {
	cp -a pycryptodomex-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/pycryptodomex-$pkgver
	python setup.py build

	cd "$srcdir"/pycryptodomex-$pkgver-python2
	python2 setup.py build
}

package_python-pycryptodomex() {
	depends=('python')

	cd pycryptodomex-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-pycryptodomex() {
	depends=('python2')

	cd pycryptodomex-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
