# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=haskell-src-exts-util
pkgname=haskell-src-exts-util
pkgver=0.2.4
pkgrel=12
pkgdesc="Helper functions for working with haskell-src-exts trees"
url="https://github.com/pepeiborra/haskell-src-exts-util"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-src-exts' 'haskell-uniplate')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ba80f8d9e83e72ab602b5d204bcea753cccb277f0055d97cbe6f361cc1468d11196a048265d914136d239e4e5d73c800a55ed24e65f9fbb2ba255507927e71a0')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
