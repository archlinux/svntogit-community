# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-keystoneauth1
pkgver=5.1.1
pkgrel=1
pkgdesc="Authentication Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/developer/keystoneauth"
license=('Apache')
depends=('python-pbr' 'python-iso8601' 'python-stevedore' 'python-os-service-types' 'python-requests')
optdepends=('python-requests-kerberos: for kerberos support'
            'python-lxml: for saml2 support'
            'python-oauthlib: for oauth1 support'
            'python-betamax: for betamax support'
            'python-fixtures: for betamax support')
checkdepends=('python-requests-mock' 'python-testscenarios' 'python-lxml'
              'python-oslo-config' 'python-betamax' 'python-oslotest' 'python-oslo-utils'
              'python-requests-kerberos' 'python-oauthlib' 'python-hacking')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/keystoneauth/archive/$pkgver.tar.gz")
sha512sums=('cb02628cc34696f3e19df9c452622b4338e92c6eb0d8f41c7f38b2e04b19433eaa3a3df4e6a27a44b9511f9472c0dc83e1182953585c724f025e55b56c22a90c')

export PBR_VERSION=$pkgver

build() {
  cd keystoneauth-$pkgver
  python setup.py build
}

check() {
  cd keystoneauth-$pkgver
  stestr run
}

package() {
  cd keystoneauth-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
