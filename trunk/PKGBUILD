# $Id$
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxv
pkgname=lib32-$_pkgbasename
pkgver=1.0.7
pkgrel=1
pkgdesc='X11 Video extension library (32-bit)'
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext' $_pkgbasename)
makedepends=('pkgconfig' gcc-multilib)
options=('!libtool')
source=(${url}/releases/individual/lib/libXv-${pkgver}.tar.bz2)
sha1sums=('49c87e0ccb999966b7b1b4e72cb927f05e0835bd')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${startdir}/src/libXv-${pkgver}
  ./configure --prefix=/usr --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd ${startdir}/src/libXv-${pkgver}

  make DESTDIR=${startdir}/pkg install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
