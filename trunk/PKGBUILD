# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-timeout
pkgname=('python-pytest-timeout' 'python2-pytest-timeout')
pkgver=1.3.4
pkgrel=1
pkgdesc='py.test plugin to abort hanging tests'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-timeout'
makedepends=('python-pytest' 'python2-pytest')
checkdepends=('python-pexpect' 'python2-pexpect')
source=("https://github.com/pytest-dev/pytest-timeout/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5a05d34f76429291a3a9eae49c7e8e919a00a80c453de51b3a11d52c45822d3c1d5b3064366aa2a453cab9f7747e9ae8cc6f64cf569df72724a149e8dc584ccd')

prepare() {
  cp -a pytest-timeout-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-timeout-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-timeout-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-timeout-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-timeout-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-timeout() {
  depends=('python-pytest')

  cd pytest-timeout-$pkgver
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-timeout() {
  depends=('python2-pytest')

  cd pytest-timeout-$pkgver-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
