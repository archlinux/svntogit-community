# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=marked-man
pkgver=0.2.1
pkgrel=1
pkgdesc="Wrapper adding manpage output to 'marked', inspired by 'ronn'"
arch=('any')
url='https://github.com/kapouer/marked-man'
license=('MIT')
depends=('nodejs' 'marked')
makedepends=('npm')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('e2d55fd1b3a65f9e285d2d3b08d7cea39f6e82d2e08b3d45976828b217e6698e280c56acdc3ba941d627487d188865fdef5c5840bf875368a32164a6152611e2')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s ../../../lib/node_modules/marked-man/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in marked; do
    rm -r $dep;
    npm link $dep;
  done
}
