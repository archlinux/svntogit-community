# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pytest-xvfb
pkgname=python-${_pkg}
pkgver=2.0.0
pkgrel=6
pkgdesc="pytest plugin to run Xvfb for tests"
arch=(any)
url="https://github.com/The-Compiler/pytest-xvfb"
license=(MIT)
depends=(python-pytest python-pyvirtualdisplay xorg-server-xvfb)
makedepends=(python-setuptools)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('c4ba642de05499940db7f65ee111621939be513e3e75c3da9156b7235e2ed8cf')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  rm tests/test_xvfb_windows.py
  PYTHONPATH="${PWD}/build/lib/" PYTEST_PLUGINS=pytest_xvfb pytest
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
# Currently missing, fixed upstream
#  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
