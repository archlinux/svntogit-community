# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.4.11
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=(
  'cairo'
  'dbus'
  'imlib2'
  'libev'
  'libxdg-basedir'
  'lua'
  'pango'
  'startup-notification'
  'xcb-util-image'  
  'xcb-util-keysyms' 
  'xcb-util-wm'
)
makedepends=(
  'asciidoc'
  'cmake'
  'docbook-xsl'
  'doxygen'
  'gperf'
  'imagemagick'
  'luadoc'
  'xmlto'
  'setconf'
  'gendesk'
)
optdepends=('rlwrap: readline support for awesome-client')
provides=('notification-daemon')
source=("$url/download/$pkgname-$pkgver.tar.xz"
        '01-fix_glib2_build.patch')
md5sums=('d6aa71334b5cd4ef63ce69d6c612ecf2'
         'baa179074c36e26ef156ebb2060bfd5c')

build() {
  cd "$srcdir"
  gendesk
  setconf "$pkgname.desktop" Type XSession

  cd $pkgname-$pkgver
  # to remove in 3.4.12
  patch -N -p1 -i ../01-fix_glib2_build.patch
  make CMAKE_ARGS=' -DPREFIX=/usr -DSYSCONFDIR=/etc -DCMAKE_BUILD_TYPE=RELEASE'
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # install desktop file so you can start awesome from your login manager
  install -Dm644 ../awesome.desktop "$pkgdir/usr/share/xsessions/awesome.desktop"
  install -Dm644 ../awesome.png "$pkgdir/usr/share/pixmaps/awesome.png"
}

# vim:set ts=2 sw=2 ft=sh et:
