# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=go-bindata-assetfs
pkgver=1.0.0
pkgrel=3
pkgdesc="Serves embedded files from jteeuwen/go-bindata with net/http"
arch=('x86_64')
url="https://github.com/elazarl/go-bindata-assetfs"
license=('BSD')
depends=('glibc')
makedepends=('go-pie')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/elazarl/go-bindata-assetfs/archive/v${pkgver}.tar.gz"
  'https://github.com/elazarl/go-bindata-assetfs/commit/38087fe4dafb822e541b3f7955075cc1c30bd294.patch'
)
sha512sums=('009111511b10b14e83b166eaf37c36e6fc96097770cd6b1865d034f4e90cc3232930b6f5591ee1a960af46d76a7456a96dcc2ccc112234cb0d382a9f9cc575c2'
            '9364a53fe6ce05a81429ec96f8fc542484fa8a860c8c011daed61d06db608fd0bc22a3837321119ff2429dc1e1b008be90977fa4bab74a4a90c9b126dc899cea')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ../38087fe4dafb822e541b3f7955075cc1c30bd294.patch #https://github.com/elazarl/go-bindata-assetfs/issues/33
  mkdir -p "${srcdir}/src/github.com/elazarl/"
  mv "${srcdir}/${pkgname}-${pkgver}" "${srcdir}/src/github.com/elazarl/go-bindata-assetfs"
}

build() {
  cd "${srcdir}/src/github.com/elazarl/go-bindata-assetfs/"
  GOPATH="${srcdir}" go build -trimpath -ldflags "-extldflags ${LDFLAGS}" -o go-bindata-assetfs ./go-bindata-assetfs/
}

package() {
  cd "${srcdir}/src/github.com/elazarl/go-bindata-assetfs/"
  install -Dm755 "go-bindata-assetfs/go-bindata-assetfs" "${pkgdir}/usr/bin/go-bindata-assetfs"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
