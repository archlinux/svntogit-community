# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-rpyc
_name=rpyc
pkgver=5.2.1
pkgrel=1
pkgdesc='Remote Python Call - A transparent and symmetric RPC library for python'
url='https://github.com/tomerfiliba-org/rpyc'
arch=('any')
license=('MIT')
depends=('python' 'python-plumbum')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-hatchling')
source=(https://github.com/tomerfiliba-org/${_name}/archive/${pkgver}/${_name}-${pkgver}.tar.gz)
sha512sums=('a7a3c53bd06f0e54c63d264944df72b530f62d443296d8d4703a7b2743e7ed9120017bdac3ed00096d7b229abb4ee54de177f526b6a7548ecc734eae255b9518')
b2sums=('0da6272a801c7582d32d82c680d7a7886d46d9615c4bf66cc021e726b5917e784325729a5515c3025255ee62ec760889c2a0ae67758cd49f20117f1469eb5494')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
