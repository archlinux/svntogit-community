# Maintainer: David Runge <dvzrv@archlinux.org>

_name=jaraco.logging
pkgname=python-jaraco.logging
pkgver=3.1.1
pkgrel=1
pkgdesc="Quickly solicit log level info from command-line parameters"
arch=(any)
url="https://github.com/jaraco/jaraco.logging"
license=(MIT)
depends=(python-tempora)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm python-toml python-wheel)
conflicts=(python-jaraco)
# 3.1.1 not on pypi.org: https://github.com/jaraco/jaraco.logging/issues/2
# source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
source=($_name-$pkgver.tar.gz::https://github.com/jaraco/jaraco.logging/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('082ce44a7f04668eb5bae8ea5c7290c1720bd100907cce2c01fdb9c88b0dcf93')
b2sums=('32d978ae8e9105986926649dcccd710aea44e2b8ea2f4b41dfdb6cb6512cc8597951909c4e217d608245fb37b29ced544429b4f960d080f5a3436f454d042826')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  # NOTE: there are actually no tests to run
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 *.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
