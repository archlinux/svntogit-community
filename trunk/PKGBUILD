# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=3.0.3
pkgrel=2
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="http://www.sonicvisualiser.org/"
license=('GPL2')
depends=('capnproto' 'libfishsound' 'libid3tag' 'liblo'
'liblrdf' 'libmad' 'liboggz' 'libpulse' 'portaudio' 'qt5-svg'
'rubberband' 'sord' 'hicolor-icon-theme')
source=("https://code.soundsoftware.ac.uk/attachments/download/2246/${pkgname}-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/piper-audio/piper/02bafb5284eb20e4c8d96ac5fe5b697ed11f59a4/capnp/piper.capnp")
sha512sums=('845cc92231f6c8eec24e4f583d1a736042ecfa73b7a7c401efb3d6e26050657851b1672043482ebc78dd19ded0e6aee6f930165e6410330eafaa08e0d4a50aa1'
            '43d3f20d7849cf5fe69933e1c57ef56f3a1fadada5a0c73a01628ae62b14ef40c2ee7c907063eb08ad7a267c71420b4385fccf1bd7b0ed9cacd7907d1dcc9f4d')

prepare() {
  cd "${pkgname}-${pkgver}"
  # Fix capnproto use: https://sourceforge.net/p/sv1/bugs/239/
  cp "${srcdir}/piper.capnp" piper-cpp/vamp-capnp/piper.capnp
  capnp compile -oc++ piper-cpp/vamp-capnp/piper.capnp
  autoreconf -vi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  # install everything separately, because build system is broken:
  # https://sourceforge.net/p/sv1/bugs/247/

  # binaries
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 piper-vamp-simple-server \
    "${pkgdir}/usr/bin/piper-vamp-simple-server"
  install -Dm755 vamp-plugin-load-checker \
    "${pkgdir}/usr/bin/vamp-plugin-load-checker"

  # desktop files
  install -t "${pkgdir}/usr/share/applications" -Dm644 *.desktop

  # icons
  for _s in 16 22 24 32 48 64 128; do
    install -Dm644 icons/sv-${_s}x${_s}.png \
      "${pkgdir}/usr/share/icons/hicolor/${_s}x${_s}/apps/sv-icon.png"
  done
  install -Dm644 icons/sv-icon.svg \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/sv-icon.svg"

  # samples
  install -t "${pkgdir}/usr/share/${pkgname}/samples" -Dm644 samples/*

  # templates
  install -t "${pkgdir}/usr/share/${pkgname}/templates" -Dm644 templates/*

  # docs
  install -Dm644 CHANGELOG "${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG"
  install -Dm644 CITATION "${pkgdir}/usr/share/doc/${pkgname}/CITATION"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
