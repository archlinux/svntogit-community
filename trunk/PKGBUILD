# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-mysqlclient
_libname=${pkgname/python-/}
pkgver=2.0.3
pkgrel=2
pkgdesc="Fork of MySQL-python with support for Python 3"
arch=('x86_64')
url="https://github.com/PyMySQL/mysqlclient-python"
license=('GPL')
depends=('python' 'mariadb-libs')
makedepends=('mariadb' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz")

build() {
    cd "$srcdir"/$_libname-$pkgver
    python setup.py build
}

package() {
    cd "$srcdir"/$_libname-$pkgver
    python setup.py install -O1 --skip-build --root="$pkgdir"
}

sha256sums=('f6ebea7c008f155baeefe16c56cd3ee6239f7a5a9ae42396c2f1860f08a7c432')
