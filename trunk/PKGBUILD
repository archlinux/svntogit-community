# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Grey Christoforo

pkgname=python-pivy
pkgver=0.6.6
pkgrel=4
pkgdesc='Bindings to Coin3D'
arch=(x86_64)
license=(BSD)
url='https://github.com/coin3d/pivy'
depends=(python soqt)
makedepends=(cmake git glu python-setuptools swig)
source=("py310.patch::https://github.com/coin3d/pivy/pull/91/commits/377d97ce41519e879a6c0111f1193957653a06ae.diff"
        "git+$url#commit=55e659de7ea346d3caf176d7fe254224d36e4791") # tag: 0.6.6
b2sums=('9c160cb6f95e480e3c09cc61e4a8bda2b63e8073cbdd2572497f0772516beb26d7cbd5c46dddf0ccb23b6df9ec0df05040868e8efed0d749cafbcb9dbda1707a'
        'SKIP')

prepare() {
  cd pivy
  patch -p1 -i ../py310.patch
}

package() {
  cd pivy
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
