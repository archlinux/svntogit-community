# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-mockmodule
pkgver=0.13
pkgrel=1
pkgdesc="Override subroutines in a module for unit testing"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-super')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
url='http://search.cpan.org/dist/Test-MockModule'
source=("http://search.cpan.org/CPAN/authors/id/G/GF/GFRANKS/Test-MockModule-$pkgver.tar.gz")
sha512sums=('17c2aa7b67fcae2a333788db74fc94da9a680ad83786705e70417cb78df0cfa32e6fcca44962dcc9d7c2ee27c45d8f142ce03b62691da4940ccc405a0bb26472')

build() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build install destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
