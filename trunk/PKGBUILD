# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: huyz
# Contributor: Jingbei Li <i@jingbei.li>
pkgname=intel-oneapi-mkl
_pkgver=2023.0.0
_debpkgrel=25398
pkgver=${_pkgver}_${_debpkgrel}
pkgrel=1
pkgdesc="Intel oneAPI Math Kernel Library"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("custom")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}-${_pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-devel-${_pkgver}-${_pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-common-${_pkgver}-${_pkgver}-${_debpkgrel}_all.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-common-devel-${_pkgver}-${_pkgver}-${_debpkgrel}_all.deb"
        "${pkgname}.conf"
        "${pkgname}.sh")
depends=('intel-oneapi-compiler-dpcpp-cpp-runtime' 'intel-oneapi-tbb' 'intel-oneapi-common')
replaces=('intel-mkl' 'intel-mkl-static')
provides=('intel-mkl' 'intel-mkl-static')
conflicts=('intel-mkl' 'intel-mkl-static' 'intel-oneapi-basekit')
options=('staticlibs')
noextract=("${pkgname}-${_pkgver}-${_pkgver}-${_debpkgrel}_amd64.deb"
           "${pkgname}-devel-${_pkgver}-${_pkgver}-${_debpkgrel}_amd64.deb"
           "${pkgname}-common-${_pkgver}-${_pkgver}-${_debpkgrel}_all.deb"
           "${pkgname}-common-devel-${_pkgver}-${_pkgver}-${_debpkgrel}_all.deb")
sha256sums=('704cd8308d1824a76e53f0d62af2d23fa682abb2bc6b965fc7284de9473e1f98'
            'ee582b8e443b6104e3cc8813f990e8081a0e44d203aa0f65edca0f3406107793'
            '47fc873a5c0b736016ed76b7b99fd93b6bd9c27ee052a23e1944d8095b1b0a6c'
            '114861a697500e5ee5e0e5f1e920bc0c96dbacd80145877e4974c582acb26c52'
            'cd787d9843146ad5ccf28ec72311d76d95d6bb9e42974dd614d8b5aa76bb5bd9'
            '85a56750e03ad99a5b99e7296dcea411fb88c4a0a25e674c86ab48f5648d9ae9')

package() {
    ar x ${pkgname}-${_pkgver}-${_pkgver}-${_debpkgrel}_amd64.deb
    tar xf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz

    ar x ${pkgname}-devel-${_pkgver}-${_pkgver}-${_debpkgrel}_amd64.deb
    tar xf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz

    ar x ${pkgname}-common-${_pkgver}-${_pkgver}-${_debpkgrel}_all.deb
    tar xf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz

    ar x ${pkgname}-common-devel-${_pkgver}-${_pkgver}-${_debpkgrel}_all.deb
    tar xf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz

    # drop conda binaries
    rm -r "${pkgdir}"/opt/intel/oneapi/conda_channel

    # symlink mkl version
    ln -s "$_pkgver" ${pkgdir}/opt/intel/oneapi/mkl/latest

    install -Dm644 ${pkgname}.conf ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
    install -Dm644 ${pkgname}.sh ${pkgdir}/etc/profile.d/${pkgname}.sh

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi

    # pkgconfig
    cd ${pkgdir}/opt/intel/oneapi/mkl/latest/lib/pkgconfig
    install -d ${pkgdir}/usr/share/pkgconfig
    for _file in *.pc; do
        ln -s /opt/intel/oneapi/mkl/latest/lib/pkgconfig/${_file} ${pkgdir}/usr/share/pkgconfig/${_file}
        sed -e 's@prefix=.*@prefix=/opt/intel/oneapi/mkl/latest@g' -i ${_file}
    done

    # provide old intel-mkl
    install -d ${pkgdir}/usr/bin
    install -d ${pkgdir}/opt/intel/mkl
    install -d ${pkgdir}/opt/intel/mkl/lib
    ln -sf /opt/intel/oneapi/mkl/latest/bin/intel64 ${pkgdir}/opt/intel/mkl/bin
    ln -sf /opt/intel/oneapi/mkl/latest/lib/intel64 ${pkgdir}/opt/intel/mkl/lib/intel64
    ln -sf /opt/intel/oneapi/mkl/latest/lib/intel64 ${pkgdir}/opt/intel/mkl/lib/intel64_lin
    ln -sf /opt/intel/oneapi/mkl/latest/include ${pkgdir}/opt/intel/mkl/include
    ln -sf ${pkgdir}/opt/intel/mkl/bin/mkl_link_tool ${pkgdir}/usr/bin/mkl_link_tool
}
