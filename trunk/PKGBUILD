# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgname=python-routes
pkgver=2.5.1
pkgrel=6
pkgdesc="Routing recognition and generation tools"
arch=("any")
url="https://pypi.python.org/pypi/Routes"
license=("custom:BSD")
depends=("python-repoze.lru" 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-webob' 'python-webtest')
source=("https://pypi.io/packages/source/R/Routes/Routes-$pkgver.tar.gz")
sha512sums=('06ec9a8426ced86524096703de47e3da2fa08bd0ed8c962d451b4338966804245b5074077021fb58a5afcb63acd6dcffa05a89703b8b54841a69a4cdff12b755')

build() {
  cd Routes-$pkgver
  python setup.py build
}

check() {
  cd Routes-$pkgver
  nosetests3
}

package() {
  cd Routes-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
