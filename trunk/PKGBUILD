# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ocrfeeder
pkgver=0.7.11
pkgrel=4
pkgdesc="GTK+ document layout analysis and optical character recognition application"
arch=('any')
url="https://wiki.gnome.org/Apps/OCRFeeder"
license=('GPL')
depends=('python2-pyenchant' 'pygoocanvas' 'python2-gtkspell' 'python2-pillow' \
         'python2-lxml' 'python2-reportlab' 'ghostscript' 'unpaper' 'sane' 'iso-codes' \
         'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('tesseract: character recognition'
            'ocrad: character recognition'
            'gocr: character recognition'
            'cuneiform: character recognition')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        ocrfeeder-pillow.patch)
sha256sums=('4508f4a0e2fdab521ab4a6d0265058a21fda80622483b169fd041189938ea6d5'
            '54dbe61ae384d4f5ed48e9fae0d5d1fc65787916208c87f44862aa1a3eb31e80')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Pillow port
  patch -Np1 -i "$srcdir/ocrfeeder-pillow.patch"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/ocrfeeder*.in src/ocrfeeder/odf/*.py
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../ocrfeeder/icons/ocrfeeder.svg "$pkgdir/usr/share/pixmaps/ocrfeeder.svg"
}
