# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=0.10.4
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
depends=('python-pathtools' 'python-yaml' 'python-argh')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-timeout' 'python-pytest-runner')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('abaadebd74dd869af545b5a3f10edc9d58f4f0f31f43aaf2244aed2c0ef56828f0b4cf9eda919727c832b20c79e95f50df7193dc8ea9d05d28a52dc322a3a497')

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  python setup.py pytest
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
