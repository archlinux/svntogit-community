# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=1.8.2
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(i686 x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain-qt4 qtwebkit neon)
makedepends=(cmake python-sphinx)
optdepends=('python2-nautilus: integration with Nautilus')
install=$pkgname.install
backup=('etc/ownCloud/sync-exclude.lst')
#source=("https://download.owncloud.com/desktop/stable/$_name-$pkgver.tar.xz")
# see https://mailman.owncloud.org/pipermail/devel/2015-June/001323.html
source=("https://sources.archlinux.org/other/community/owncloud-client/$_name-$pkgver.tar.xz")
md5sums=('08c6abccdc6105146715bfa9b90f7be2')
validpgpkeys=('F05F7DD7953A07DF36579DAA498C45EBE94E7B37')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_name-$pkgver \
	-DBUILD_WITH_QT4=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname \
        
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
