# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsbeautifier
pkgver=1.10.2
pkgrel=2
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-six' 'python-editorconfig')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/beautify-web/js-beautify/archive/v$pkgver.tar.gz")
sha512sums=('6a65bcf0a0c2277c40d3eed6be47152aa4b27cd7613100b411522a18381fe15c2cf92ca64d5dcb1b65903e2ee47ac6be9a2b9c1b8d5d4caa7250e7af8454a49c')

build() {
  cd js-beautify-$pkgver/python
  python setup.py build
}

check() {
  cd js-beautify-$pkgver/python
  python setup.py test
}

package() {
  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
