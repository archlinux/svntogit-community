# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=3.0.90
pkgrel=1
pkgdesc="A KDE program that browses samba shares"
arch=(x86_64)
url="https://github.com/KDE/smb4k/"
license=(GPL)
depends=(kparts hicolor-icon-theme smbclient)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
source=("https://github.com/KDE/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('40f15c2eac079f97683b7389b1403bb0e18b3a6a594db2831e6c5c6c15a6c82b')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
  -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
