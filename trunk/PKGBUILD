# $Id$
# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor:Techlive Zheng <techlivezheng at gmail dot com>

pkgname=phpvirtualbox
pkgver=5.2_0rc1
_pkgver=${pkgver/_/-}
_pkgver=${_pkgver/rc/-rc}
pkgrel=4
pkgdesc="PHP/AJAX web interface for VirtualBox 4.*"
arch=(any)
url="https://github.com/phpvirtualbox/phpvirtualbox"
license=('GPL')
depends=('php')
backup=("etc/webapps/phpvirtualbox/config.php")
source=("https://github.com/phpvirtualbox/phpvirtualbox/archive/${_pkgver}.tar.gz"
	"https://github.com/phpvirtualbox/phpvirtualbox/commit/c84bf6b462cc43854fa7e04ce0fa71cadd4eadc7.patch"
	"https://github.com/phpvirtualbox/phpvirtualbox/commit/4c639d47106d0e5a272ef4a75eeaf7cb3a4a9d05.patch")
sha256sums=('519979ef23ac371a93e5ee52662a099691d2131423319d5376b01f6132c22f8f'
            '9fc0819708348a14ca4ac57dcab0652f9cc921dc5b9c053283aee6f464e6c1b6'
            '7dadb83f959bfd7d92c332b182674ea7237d257207f874bc8d1e98a9f830417f')

prepare() {
  cd "$srcdir"/${pkgname}-${_pkgver}
  patch -p1 <../c84bf6b462cc43854fa7e04ce0fa71cadd4eadc7.patch
  patch -p1 <../4c639d47106d0e5a272ef4a75eeaf7cb3a4a9d05.patch
}

package() {
  cd "$srcdir"/${pkgname}-${_pkgver}

  mkdir -p "$pkgdir"/etc/webapps/phpvirtualbox
  install -D -m644 config.php-example "$pkgdir"/etc/webapps/phpvirtualbox/config.php

  # Apache configuration
  cat > "$pkgdir"/etc/webapps/phpvirtualbox/apache.example.conf <<EOF
  Alias /phpvirtualbox "/usr/share/webapps/phpvirtualbox"
  <Directory "/usr/share/webapps/phpvirtualbox">
    Options FollowSymlinks
    AllowOverride All
    Require all granted
  </Directory>
EOF

  find . -type f -exec install -D -m644 {,"$pkgdir"/usr/share/webapps/${pkgname}/}{} \;
  ln -s /etc/webapps/phpvirtualbox/config.php "$pkgdir"/usr/share/webapps/phpvirtualbox/config.php
}
