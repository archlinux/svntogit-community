# Maintainer: Spike29 <leguen.yannick@gmail.com>
# Contributor: Samir Faci <csgeek@archlinux.us>
# Contributor: TimothÃ©e Ravier <tim@siosm.fr>

pkgname=qxmpp
pkgver=0.9.3
pkgrel=2
pkgdesc='Cross-platform C++ XMPP client and server library'
arch=(i686 x86_64)
url='https://github.com/qxmpp-project/qxmpp'
license=(LGPL2.1)
depends=(qt5-base opus speex libvpx libtheora)
source=("https://github.com/qxmpp-project/qxmpp/archive/v$pkgver.tar.gz")
sha1sums=('f9391bedd5153ae19f68f25276bf2624c5020d09')

prepare() {
  mkdir -p build
}

build() {
  cd build
	
  qmake-qt5 PREFIX=/usr ../$pkgname-$pkgver \
    QXMPP_USE_OPUS=1    \
    QXMPP_USE_SPEEX=1   \
    QXMPP_USE_THEORA=1  \
    QXMPP_USE_VPX=1
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install
}
