# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.8.22
pkgrel=1
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm'
arch=('x86_64')
url='https://www.nongnu.org/duplicity/'
license=('GPL')
depends=('gnupg' 'librsync' 'python-paramiko' 'python-fasteners' 'python-future')
makedepends=('python-setuptools-scm')
optdepends=('lftp: backend for ftp, ftps, fish'
            'ncftp: alternative backend for ftp'
            'python-boto: Amazon S3 and Google Cloud Storage backend'
            'python-dropbox: Dropbox backend'
            'python-gobject: GIO backend'
            'gvfs: GIO backend'
            'python-pydrive: Google Drive backend'
            'python-requests-oauthlib: OneDrive backend'
            'rsync: rsync backend')
source=("https://launchpad.net/$pkgname/0.8-series/$pkgver/+download/$pkgname-$pkgver.tar.gz"{,.sig})
b2sums=('3a8b6eb79d5f67e8f274bdaaaf0bb5b6c9f0ebf91a3b6a849b876cdf2c9af0f5435946150b5db6ab5e524cedc5b98b40c456b1de0fe8c6e33c024fe3c7d881fa'
        'SKIP')
validpgpkeys=('9D95920CED4A8D5F8B086A9F8B6F8FF4E654E600'
              '9946FB5BE18E34E016DAE992330B3C2A96A9EA9C')

package() {
	cd "${pkgname}-${pkgver}"
	python setup.py install --root="$pkgdir" --optimize=1

  # move documentation to the correct directory
  cd "$pkgdir/usr/share/doc"
  mv "$pkgname-$pkgver" "$pkgname"
}
