# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: totoloco <totoloco at gmail _dot_com>
pkgname=mysql-workbench
pkgver=5.2.28
pkgrel=1
pkgdesc="MySQL Workbench is a cross-platform, visual database design tool developed by MySQL."
arch=('i686' 'x86_64')
url="http://dev.mysql.com/workbench/"
license=('GPL2')
depends=('libgnome' 'libzip' 'libmysqlclient' 'lua' 'gtkmm' 'libglade' 'libgl' 'libsigc++' 'python-paramiko' 'python-pexpect')
makedepends=('glib' 'boost' 'curl' 'mesa')
optdepends=('python: For python modules')
options=('!libtool')
source=(ftp://mirror.anl.gov/pub/mysql/Downloads/MySQLGUITools/$pkgname-gpl-$pkgver-src.tar.gz
        ArchLinux.xml arch.patch)
md5sums=('bfde0a534bd8384b14c8a66c9da74250'
         '668eed06edb1ba1b58c5219a4a45ed35'
         '23430d24400fd65c9fd872bd8e2411e5')

build() {
  cd "$srcdir/$pkgname-gpl-$pkgver-src"

  cp "$srcdir/ArchLinux.xml" res/mysql.profiles
  patch -Np1 -i $srcdir/arch.patch

  #we use System provided libraries
  rm -rf ext/curl
  rm -rf ext/yassl
  rm -rf ext/boost
  rm -rf ext/libsigc++

  export CPPFLAGS=$(pkg-config --cflags sigc++-2.0)
  unset LDFLAGS

  ./autogen.sh --prefix=/usr --disable-debug --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-gpl-$pkgver-src"

  make DESTDIR="$pkgdir" install
}
