# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=libdom
pkgver=0.3.0
pkgrel=1
pkgdesc='Implementation of the W3C DOM'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libdom/'
license=('MIT')
depends=('libhubbub>=0.3.3' 'libwapcaplet>=0.3.0' 'libparserutils>=0.2.3' 'expat')
makedepends=('netsurf-buildsystem' 'git' 'libparserutils>=0.2.3')
source=("git://git.netsurf-browser.org/libdom.git#tag=release/$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  sed 's:_BSD_SOURCE:_DEFAULT_SOURCE:' -i Makefile
  export CFLAGS="-Wno-error=unused-but-set-variable"
  make INCLUDEDIR=include LIBDIR=lib PREFIX=/usr COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname"

  make install INCLUDEDIR=include LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libdom.git
# vim:set ts=2 sw=2 et:
