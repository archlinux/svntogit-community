# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: kaylyn
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cryfs
pkgver=0.9.9
pkgrel=4
pkgdesc="Cryptographic filesystem for the cloud"
arch=(x86_64)
depends=(crypto++ fuse2 curl)
makedepends=(cmake python boost)
url="https://www.cryfs.org"
license=(LGPL3)
source=("https://github.com/cryfs/cryfs/releases/download/$pkgver/cryfs-$pkgver.tar.xz"{,.asc})
sha256sums=('aa8d90bb4c821cf8347f0f4cbc5f68a1e0f4eb461ffd8f1ee093c4d37eac2908'
            'SKIP')
validpgpkeys=(C3111FE656732704EF6DF059017DC932549E65B2) # CryFS Team <messmer@cryfs.org>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCRYFS_UPDATE_CHECKS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 "$srcdir"/README.md -t "$pkgdir"/usr/share/doc/$pkgname
}
