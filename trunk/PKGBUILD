# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT CoM>
# Contributor: Xyne <ca dot archlinux at xyne, backwards>

pkgname=qd
pkgver=2.3.18
pkgrel=1
pkgdesc='C++/Fortran-90 double-double and quad-double library'
arch=(x86_64)
url="http://crd-legacy.lbl.gov/~dhbailey/mpdist"
license=(BSD)
makedepends=(gcc-fortran)
source=("http://crd-legacy.lbl.gov/~dhbailey/mpdist/$pkgname-$pkgver.tar.gz")
sha256sums=('81096b5b33519cbeed5fc8ef58e1d47ee8f546382514849967627b972483716e')

build() {
  cd $pkgname-$pkgver

  export FC=gfortran
  ./configure --prefix=/usr --enable-shared --with-pic
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
