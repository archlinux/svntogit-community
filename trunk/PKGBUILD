# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Spencer <sonrisesoftware@gmail.com>

pkgname=vibe
pkgver=0.9.0
pkgrel=1
pkgdesc="A collection of core classes used throughout Liri"
arch=("x86_64")
url="https://github.com/lirios/vibe"
license=("LGPL")
depends=("fluid" "kwallet" "solid" "networkmanager-qt" "modemmanager-qt" "pulseaudio" "libqtxdg"
         "mobile-broadband-provider-info")
makedepends=("git" "python" "extra-cmake-modules")
replaces=('libpapyros')
source=("git+https://github.com/lirios/vibe.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake "$srcdir/${pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
