# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-libepoxy
pkgver=1.5.10
pkgrel=1
pkgdesc="Library handling OpenGL function pointer management"
url="https://github.com/anholt/libepoxy"
arch=(x86_64)
license=(MIT)
depends=(lib32-glibc)
makedepends=(git meson lib32-libgl lib32-systemd)
checkdepends=(xorg-server-xvfb)
_commit=c84bc9459357a40e46e2fec0408d04fbdde2c973  # tags/1.5.10^0
source=("git+https://github.com/anholt/libepoxy#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('53EF3DC3B63E2899271BD26322E8091EEA11BBB7') # Emmanuele Bassi <ebassi@gnome.org>

pkgver() {
  cd libepoxy
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libepoxy
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson libepoxy build --libdir=/usr/lib32
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  provides=(libepoxy.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir/usr/include"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libepoxy/COPYING
}

# vim:set sw=2 et:
