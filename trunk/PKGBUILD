# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=prettyprinter-convert-ansi-wl-pprint
pkgname=haskell-prettyprinter-convert-ansi-wl-pprint
pkgver=1.1.1
pkgrel=22
pkgdesc="Converter from ansi-wl-pprint documents to prettyprinter-based ones."
url="https://github.com/quchen/prettyprinter"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-ansi-wl-pprint' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal')
makedepends=('ghc' 'haskell-doctest')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('12582ba31dc7401047a8e98c8059aee687c9b420825f6887567dbab1181602e97ad973f883592e3f035840e7abec61daa3718dba90a3cf8cac4f543674a31142')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
