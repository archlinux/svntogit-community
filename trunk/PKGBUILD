# Maintainer:

_pkgname=multivolumefile
pkgname=python-$_pkgname
pkgver=0.2.3
pkgrel=1
pkgdesc='Multi volume file wrapper library'
arch=(any)
url='https://github.com/miurahr/multivolume'
license=(LGPL)
depends=(python)
makedepends=(python-setuptools-scm)
checkdepends=(python-pytest python-hypothesis)
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('a0648d0aafbc96e59198d5c17e9acad7eb531abea51035d08ce8060dcad709d6')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest -rsx
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
