# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=snes9x
pkgname=('snes9x' 'snes9x-gtk')
pkgver=1.54.1
pkgrel=1
arch=('x86_64')
url="https://code.google.com/p/snes9x-gtk/"
license=('custom')
makedepends=('intltool' 'nasm' 'mesa' 'libpulse'  'libpng' 'sdl' 'gtk2' 'libxv'
             'adwaita-icon-theme')
source=(https://github.com/snes9xgit/snes9x/archive/$pkgver.tar.gz)
sha1sums=('985825beb519c39df7b85fee3ae7e41669011c6b')

build() {
  cd ${pkgbase}-${pkgver}/unix
  ./configure --prefix=/usr --enable-netplay
  make

  cd ../gtk
  ./autogen.sh
  ./configure --prefix=/usr --with-netplay --with-opengl
  make
}

package_snes9x() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System"
  depends=('libpng' 'libxext' 'libsm' 'libxv')

  cd ${pkgbase}-${pkgver}/unix
  install -D -m755 snes9x "${pkgdir}/usr/bin/snes9x"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_snes9x-gtk() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System - GTK version"
  license=('custom' 'LGPL')
  depends=('sdl' 'libpulse' 'gtk2' 'libxv' 'adwaita-icon-theme')

  cd ${pkgbase}-${pkgver}/gtk
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
