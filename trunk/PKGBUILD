# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgbasename=libcups
pkgname=lib32-$_pkgbasename
pkgver=1.6.1
pkgrel=1
pkgdesc="The CUPS Printing System - client libraries (32-bit)"
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=(lib32-krb5 lib32-libtiff lib32-libpng $_pkgbasename)
makedepends=(gcc-multilib)
source=(ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2 fix-libusb-configure.patch)
md5sums=('87ade07e3d1efd03c9c3add949cf9c00'
         '9cf39a66f21d1208a585367b25cf117b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/cups-${pkgver}

  patch -p1 -i "$srcdir/fix-libusb-configure.patch"
  
  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --disable-ldap --enable-raw-printing --disable-gssapi --disable-dbus \
     --enable-ssl=no --disable-gnutls --enable-threads --enable-libusb=no \
	 --disable-avahi \
     --with-optim="$CFLAGS" --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/cups-${pkgver}
  make BUILDROOT=${pkgdir} install-libs
}

