# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=1.8.0
pkgrel=2
pkgdesc="A personal finances manager for KDE"
arch=('i686' 'x86_64')
url="http://skrooge.org/"
license=('GPL')
depends=('kdebase-runtime' 'libofx' 'qca-ossl')
makedepends=('cmake' 'automoc4' 'docbook-xsl' 'boost' 'kdepim-libkdepim')
optdepends=('kdepim-libkdepim: for KDE PIM integration')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('98c0ed6cfefd4c6c92a010a1ab9afc1ef230eacc391a45aa200901ba6fb3bf08')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
}
