# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-build-tools
pkgver=0.12.0
pkgrel=2
pkgdesc="Various packaging tools and scripts for LXQt applications"
arch=("any")
url="https://github.com/lxqt/$pkgname"
license=("BSD")
depends=("cmake" "qt5-tools" "git" "perl")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	"$pkgname-pr83.patch"::"https://github.com/lxqt/lxqt-build-tools/commit/8e53618a1c96b42b0b9a36d0d7d3f2c06a62db81.patch"
)
sha256sums=('10075cf6ba6d3bcd8bf444ef78e7e754f40e1f40cd90e8338c1e4c784ca89530'
            'SKIP'
            '65ef8ec7d2e0930d19d0867364d0c3f16b5b0e200cfac7381897e2a9d5734935')
validpgpkeys=(
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # https://github.com/tsujan.gpg
)

prepare() {
	cd $pkgname-$pkgver
	# Backport https://github.com/lxqt/lxqt-build-tools/pull/83 to fix building lxqt-config (merged)
	patch -Np1 -i ../$pkgname-pr83.patch
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	install -Dm644 ../$pkgname-$pkgver/BSD-3-Clause -t "$pkgdir/usr/share/licenses/$pkgname"
}
