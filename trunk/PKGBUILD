# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.273
pkgrel=4
pkgdesc="A free cross-platform lobby client for the Spring RTS project"
arch=('x86_64')
url="https://springlobby.springrts.com/"
license=('GPL2')
depends=('hicolor-icon-theme' 'wxwidgets-gtk3' 'curl' 'jsoncpp' 'alure'
         'minizip' 'libsm')
makedepends=('boost' 'cmake' 'ninja')
source=("https://springlobby.springrts.com/dl/stable/springlobby-${pkgver}.tar.bz2")
sha512sums=('33f84ef901ecf6580cff0800187abd5eb02fb1893ae369c396411a224a7087f7b3575e5e04a72bd98766f94a382300b1a9d2ce0b2fc759e7353d0727fc723790')

prepare() {
  cd $pkgname-$pkgver
  # remove bundled libraries
  rm -r src/downloader/lib/src/lib/jsoncpp
  rm -r src/downloader/lib/src/lib/minizip
}

build() {
  cd $pkgname-$pkgver

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPRD_JSONCPP_INTERNAL=OFF
  ninja -C build
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
}

# vim: sw=2:ts=2 et:
