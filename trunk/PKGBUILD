# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Christoph 'delmonico' Neuroth <delmonico@gmx.net>

pkgname=librsync
pkgver=2.0.0
pkgrel=1
pkgdesc="A free software library that implements the rsync remote-delta algorithm (rdiff)"
arch=('i686' 'x86_64')
url="http://librsync.sourceforge.net/"
license=('LGPL')
depends=('popt' 'zlib' 'bzip2')
makedepends=('cmake')
provides=('librsync.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('b5c4dd114289832039397789e42d4ff0d1108ada89ce74f1999398593fae2169')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ..
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make -k test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir" install

  install -Dm644 doc/rdiff.1 "$pkgdir/usr/share/man/man1/rdiff.1"
  install -Dm644 doc/librsync.3 "$pkgdir/usr/share/man/man3/librsync.3"
}

# vim:set ts=2 sw=2 et:
