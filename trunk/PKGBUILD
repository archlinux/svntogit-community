# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=piping-server
pkgver=0.14.0
pkgrel=1
pkgdesc='Infinitely transfer between every device over pure HTTP with pipes or browsers'
arch=('x86_64')
url="https://github.com/nwtgck/piping-server-rust"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname.service")
sha256sums=('9d76e29bfe07b55b155eca3e5642e1b1acb09cc2074b32a5d82ebd90ba84e8e4'
            '9182467638a583b29bed0ae69ed00013b1721ce8753e5ba90a56cbaac07d768c')

build() {
  cd "piping-server-rust-$pkgver"
  cargo build --release --locked --target-dir=target
}

check() {
  cd "piping-server-rust-$pkgver"
  cargo test --release --locked
}

package() {
  install -Dm644 piping-server.service "$pkgdir/usr/lib/systemd/system/piping-server.service"

  cd "piping-server-rust-$pkgver"

  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"

  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
