# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openxr
pkgver=1.0.26
pkgrel=1
pkgdesc='An open standard for virtual reality and augmented reality platforms and devices'
arch=('x86_64')
url='https://www.khronos.org/openxr/'
depends=('jsoncpp' 'libx11' 'vulkan-icd-loader' 'libglvnd' 'libxrandr' 'libxxf86vm')
makedepends=('cmake' 'vulkan-headers' 'python' 'ninja' 'wayland' 'mesa')
license=('APACHE')
source=("https://github.com/KhronosGroup/OpenXR-SDK-Source/releases/download/release-${pkgver}/OpenXR-SDK-Source-release-${pkgver}.tar.gz"{,.asc}
        fix-gcc11.patch)
validpgpkeys=(F029455EAB70F520FF4A03BD7F534CD56F00321E)
sha512sums=('cab949f3b75596257fe57f03778f96fe5de258d93b40abbb0c2b3c2fec6523743c9a852a04bfb8cf5ca7b5c673bbd54dea4bcff79f39392668a4d1775f1840aa'
            'SKIP'
            '783726fb48dc4551dfec0da2f28049d73afe66d237db54f0914b5156b8a45c9d5edf2f9a4b97a8adbbbfa1157c07cd418dbd4d53252f2feb49e9420fde0b0840')

prepare() {
  cd OpenXR-SDK-Source-release-${pkgver}

  patch -Np1 -i "${srcdir}"/fix-gcc11.patch
}

build() {
  cd OpenXR-SDK-Source-release-${pkgver}

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=ON \
    -DPRESENTATION_BACKEND=xlib \
    -DDYNAMIC_LOADER=ON
  ninja -C build
}

package() {
  cd OpenXR-SDK-Source-release-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
}
