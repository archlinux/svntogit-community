# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=hatch_jupyter_builder
pkgname=python-hatch-jupyter-builder
pkgver=0.8.1
pkgrel=1
pkgdesc='A hatch plugin to help build Jupyter packages'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(python-hatchling)
makedepends=(python-build python-installer)
checkdepends=(python-pytest python-pytest-mock)
source=(https://github.com/jupyterlab/hatch-jupyter-builder/releases/download/v$pkgver/${_pipname//-/_}-$pkgver.tar.gz)
sha256sums=('bae5eda6bacb700fd5120e95a5702f32615a7ce06ed8893b8700568d48baf46f')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  pytest -v
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
