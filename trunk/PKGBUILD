# Maintainer: David Runge <dvzrv@archlinux.org>

# needs rebuild on minor soname bumps

pkgname=libiio
pkgver=0.23
pkgrel=3
pkgdesc="Interface to the Linux Industrial Input/Output (IIO) Subsystem"
arch=('x86_64')
url="https://github.com/analogdevicesinc/libiio"
license=('LGPL2.1')
depends=('gcc-libs' 'glibc' 'libaio' 'libserialport' 'libxml2')
makedepends=('avahi' 'cmake' 'libusb' 'python' 'python-setuptools')
optdepends=('python: Python bindings')
provides=('libiio.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/analogdevicesinc/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('d3b1e1da46764025d757d558ba623730ac08c590261e546aa3c8e628959a74e40c90eedfc1eb2198f4a67a14e1900647fd150d7545e2c7cb44e72bbbab5f116a')
b2sums=('9dfbeee48a0685496f9f257c947b791b91c1e675f33437aa8f7ce9afd0763df641fd90dbaf3a0d41c1b63dc7ad54c437a12992ede7f10582ebf2a2bc0aa92695')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_SBINDIR='bin' \
        -DCMAKE_INSTALL_LIBDIR='lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -DPYTHON_BINDINGS=ON \
        -DSYSTEMD_UNIT_INSTALL_DIR='/usr/lib/systemd/system' \
        -DUDEV_RULES_INSTALL_DIR='/usr/lib/udev/rules.d' \
        -DWITH_MAN=ON \
        -DWITH_SYSTEMD=ON \
        -Wno-dev \
        -B build \
        -S .
  make -C build VERBOSE=1
}

package() {
  depends+=('libavahi-client.so' 'libavahi-common.so' 'libusb-1.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -C build install
  install -vDm 644 {CONTRIBUTING,Contributors,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
