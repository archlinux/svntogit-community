# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=bluez-tools
pkgver=0.2.0
pkgrel=4
pkgdesc="A set of tools to manage Bluetooth devices for Linux"
arch=('x86_64')
url="https://github.com/khvzak/bluez-tools"
license=('GPL')
depends=('bluez')
makedepends=(git)
_commit=7cb788c9c43facfd2d14ff50e16d6a19f033a6a7  # master
source=("git+https://github.com/khvzak/bluez-tools#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd $pkgname
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
