# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-supply-chain
pkgver=0.3.1
pkgrel=1
pkgdesc="Gather author, contributor and publisher data on crates in your dependency graph"
arch=('x86_64')
url="https://github.com/rust-secure-code/cargo-supply-chain"
license=('Apache' 'MIT' 'ZLIB')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate")
sha512sums=('dcf96555e71e4d6f66e1dc08919fb185ce70928d7e3e0a9d3df6ff64b78af55a94a8a4ecf7896991e70c992acbb11ea087c1233edba19438140a5df3e36fa008')
b2sums=('794137a07caf68ab443febb728f0477deafd353323507eb8ab46e589247f284bb49eae93758ae4d63ebe58bc1f730e3040e0b681f0baab3f30c2aa3425ffddd0')

prepare() {
  cd "$pkgname-$pkgver"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --release --frozen --all-features
}

check() {
  cd "$pkgname-$pkgver"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
