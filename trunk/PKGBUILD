# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=passenger
pkgver=5.0.30
pkgrel=1
pkgdesc="mod_rails passenger"
arch=('i686' 'x86_64')
url="http://www.modrails.com"
_watch="https://github.com/phusion/passenger/releases"
license=('GPL')
depends=('ruby' 'ruby-rack' 'curl')
makedepends=('apache')
install=passenger.install
options=('!emptydirs' 'staticlibs')
validpgpkeys=('D5F0851426939232F437AB722AC745A50A212A8C')
source=(https://s3.amazonaws.com/phusion-passenger/releases/passenger-$pkgver.tar.gz{,.asc})
sha256sums=('f367e0c1d808d7356c3749222194a72ea03efe61a3bf1b682bd05d47f087b4e3'
            'SKIP')

build(){
  cd "$srcdir"/passenger-$pkgver
  rake nginx CACHING=false
  ./bin/passenger-install-apache2-module -a
}

package() {
  cd "$srcdir"/passenger-$pkgver

  mkdir -p "$pkgdir"/usr/lib/passenger/
  cp -R * "$pkgdir"/usr/lib/passenger/

  mkdir -p "$pkgdir"/usr/lib/httpd/modules
  ln -sf /usr/lib/passenger/buildout/apache2/mod_passenger.so "$pkgdir"/usr/lib/httpd/modules/mod_passenger.so
}
