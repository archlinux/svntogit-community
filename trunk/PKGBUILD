# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

_pkgbasename=fakeroot
pkgname=lib32-${_pkgbasename}
pkgver=1.20.2
pkgrel=1
pkgdesc='Tool for simulating superuser privileges (32-bit)'
arch=('x86_64')
license=('GPL')
url="http://packages.debian.org/fakeroot"
groups=('multilib-devel')
install=fakeroot.install
depends=(lib32-glibc $_pkgbasename)
source=("http://ftp.debian.org/debian/pool/main/f/${_pkgbasename}/${_pkgbasename}_${pkgver}.orig.tar.bz2")
md5sums=('a4b4564a75024aa96c86e4d1017ac786')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32/libfakeroot \
              --disable-static --with-ipc=sysv
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR=${pkgdir} install
  rm -rf "$pkgdir"/usr/{bin,share}

  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib32/libfakeroot' > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf"
}
