# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>
# Contributor: Nick Ward <ward.nickjames@gmail.com>
# Contributor: Rich Lindsley <rich at dranek dot com>

pkgname='python-patsy'
pkgver=0.5.2
pkgrel=1
pkgdesc="A Python package for describing statistical models using symbolic formulas"
arch=('any')
url="https://github.com/pydata/patsy"
license=('BSD')
depends=('python-numpy')
makedepends=('python-setuptools')
optdepends=('python-nose: needed for integrated test suite'
            'python-scipy: needed for spline-related functions')
source=("https://github.com/pydata/patsy/archive/v${pkgver}.tar.gz")
sha256sums=('440b4e3915911f2e0d13d73cfbcface6d90002611dc76923a86ba02fd3095cad')

build() {
  cd "$srcdir"/patsy-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/patsy-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
