# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.6.3
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop gobject-introspection-runtime libcanberra libinput
         libsm libxkbcommon-x11 startup-notification zenity graphene pipewire)
makedepends=(meson intltool gobject-introspection)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f0e19708ae91ea83d282968802428d4b5cd16e327abfd1672f1ae73653f90a77')
b2sums=('199f70ccac80af54555d0268013faa2ec79b31366c7e810528b79f84d73c54dfd5d32ac0f54576c9c900b3999ced65c214bc0f7376d0fb7674d27b8786a96bce')

build() {
  arch-meson build ${pkgname}-${pkgver}
  meson compile -C build
}

package() {
  meson install --destdir="${pkgdir}" -C build
}
