# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>

pkgname=catfish
pkgver=1.4.7
pkgrel=1
pkgdesc='Versatile file searching tool'
arch=(any)
url='https://git.xfce.org/apps/catfish/about/'
license=(GPL)
install=$pkgname.install
depends=(gtk3 python-cairo python-gobject python-pexpect
         python-ptyprocess python-xdg xdg-utils)
makedepends=(git intltool python-distutils-extra)
optdepends=('mlocate: filename search'
            'zeitgeist: integration with zeitgeist')
source=("git+https://git.xfce.org/apps/catfish/#tag=catfish-$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  python setup.py build
}

package () {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1

  install -d "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/share/icons/hicolor/scalable/apps/$pkgname.svg" \
    "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}

# getver: git.xfce.org/apps/catfish/plain/catfish.1
# vim: ts=2 sw=2 et:
