# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=104.0
pkgrel=2
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'jdk8-openjdk' 'mesa')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=(https://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma
        gold-linker.patch
        0001-fix-build-with-curl-7.50.0.patch)
sha512sums=('be8922e798d7c959c248501494ab61cff625154c109debd324875a4e2a7149748224b6fcb428948f2b4d66cc9e484bc002a63810f55c5f25a499c34a01dfc225'
            'd400f9b5055e4a3bddab4a1fc839dc1f35b34e8cd309e3bece53028184250013186480f5ffb60c0b1a7d35844c75c80d2619c9cc8e51be94deca3f44d4d1ec9f'
            'c87954af0192784e6318ceb997a7fb21b3dcc740e5b8a8ed14671ac19923bd0ab6655512fa3d7b17ce0997505b0ad6f51bf0866dbbfe1c0b22cc2b9ba14e8ab1')

prepare() {
  cd spring_$pkgver

  # https://springrts.com/mantis/view.php?id=5413
  # patch -Np1 -i ../gold-linker.patch
  #
  # patch -Np1 -d tools/pr-downloader <../0001-fix-build-with-curl-7.50.0.patch

}

build() {
  cd spring_${pkgver}

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DJAVA_HOME=/usr/lib/jvm/java-8-openjdk \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd spring_${pkgver}

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.spring' > "$pkgdir/etc/spring/datadir"
}

# vim sw=2:ts=2 et:
