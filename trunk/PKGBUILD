# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Based on extra/make

pkgname=remake
_remakever="dbg-1.4"
pkgver=4.2.1+dbg1.4
pkgrel=2
pkgdesc="Enhanced GNU Make - tracing, error reporting, debugging, profiling and more"
arch=('x86_64')
url="http://bashdb.sourceforge.net/remake/"
license=('GPL3')
depends=('glibc' 'guile')
source=("https://sourceforge.net/projects/bashdb/files/remake/4.2%2B${_remakever}/remake-4.2.1%2B${_remakever}.tar.bz2"
        remake-glibc-2.27.patch)
sha256sums=('55df3b2586ab90ac0983a049f1911c4a1d9b68f7715c69768fbb0405e96a0e7b'
            '46ff42675d64d648a91c0b20cee2c2b43286539268aa806b22cdaf2eb7873694')

prepare() {
  cd "${pkgname}-4.2.1+${_remakever}"
  patch -p1 -i ../remake-glibc-2.27.patch # Fix build with glibc 2.27 (Debian)
  autoreconf -fi
}

build() {
  cd "${pkgname}-4.2.1+${_remakever}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-4.2.1+${_remakever}"
  make DESTDIR="${pkgdir}" install
  rm "$pkgdir"/usr/share/info/make.info{,-1,-2}
}
