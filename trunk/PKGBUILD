# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-daemon
pkgver=3.2.2
pkgrel=1
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=(x86_64)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(dbus vhba-module libao 'libmirage>=3.1.0')
makedepends=(cmake intltool ninja)
optdepends=('alsa-lib: to enable the ALSA audio driver'
            'pulseaudio: to enable the PA audio driver')
backup=(etc/conf.d/cdemu-daemon)
install=cdemu-daemon.install
source=("https://downloads.sourceforge.net/cdemu/$pkgname/$pkgname-$pkgver.tar.bz2"
        0001-Patch-system-daemon-for-Arch-friendliness.patch)
sha256sums=('2b8934ad09d5a1c844202d5e2020193dd5bb00112732559c23594b0761f63542'
            '233dc79359e53da3a0df880984b5ea6033756070cfe84183f39c602274f4b2d6')

prepare() {
  cd $pkgname-$pkgver

  # It's disabled by default. Should we even ship it?
  patch -Np2 -i ../0001-Patch-system-daemon-for-Arch-friendliness.patch
}

build() {
  cmake -H$pkgname-$pkgver -Bbuild -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/$pkgname
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  echo vhba | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/cdemu.conf"
}

# vim:set ts=2 sw=2 et:
