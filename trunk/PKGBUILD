# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.99.11
pkgrel=1
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('openssl')
optdepends=('lzo: for libddr_lzo')
arch=('x86_64')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.bz2"{,.asc})
sha512sums=('e1d32711421ebbafd80fd210718667ff8c2d22b5349945105b3e4c29d54d381385b1fd188b2d8aa1b7a0aaf2cc9d8f7374373cff3992726a3d17549ca50f3904'
            'SKIP')
validpgpkeys=('6669F7340D31E95EC5565490DE4F1B3A2BFFC5BF') # Kurt Garloff <kurt@garloff.de>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi

  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile
}

build() {
  cd $pkgname-$pkgver
  ./configure
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}
