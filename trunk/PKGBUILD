# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=11.0
pkgrel=1
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=(any)
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=(MPL2)
depends=(python-packaging python-pytest)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest-xdist)
optdepends=(
  'python-pytest-xdist: for recovering from crashes'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('8e0f43f63a76161447de6cfbe9382b7aece05d25cf9d1f03b6f12dcb8d98edd85d6e8f5d74d360143cd99188f082c3ab412324543307b08c4bef0e5a2dc06b3a')
b2sums=('41c3695250e809123c468325d6bb78275c2e09911eb98de1b0951add44fffc24ee2157bdad346fb9bc923ee3865fe205eaeadd1a1e7b908ee728ac2a8e1375c7')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv -k 'not test_rerun_passes_after_temporary_test_crash'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES.rst,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
