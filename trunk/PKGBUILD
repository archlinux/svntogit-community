# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=meshbird
pkgver=0.2
pkgrel=2
pkgdesc="Distributed private networking"
arch=('i686' 'x86_64')
url="https://github.com/meshbird/meshbird"
license=('Apache')
depends=('glibc')
makedepends=('git' 'go')
backup=(etc/meshbird.conf)
source=("$pkgname-$pkgver.tar.gz::https://github.com/meshbird/meshbird/archive/v$pkgver.tar.gz"
        meshbird.service meshbird.conf)
sha512sums=('7e1d5453b7953c9c522207afcb97b9a88824f03493e77bed1626f578a6a34e152da6c7ab81bec1b1d1b2a32b3a74e1bf4f20877749c3b6b75e64321550a054d5'
            'b9b552f09d9f98c4ff90ce3d31b15f7a5c2453c1535fa17454043826bf5f108510a7a7512ef79dfd7b4d53615d06c690fe8fdd878a6894f8dc75ee934c0328e7'
            '08a493f299549b9eea830e1201d2131314b2c0c931c9fdd06a30e849c90368c7809c6a42bfded33aec44403cd8f61caad168b54bad08f5b21867f2d562f0c872')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath

  go get github.com/codegangsta/cli github.com/meshbird/meshbird
}

build() {
  cd meshbird-$pkgver
  go build # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd meshbird-$pkgver
  install -Dm755 meshbird-$pkgver "$pkgdir"/usr/bin/meshbird
  install -Dm644 "$srcdir"/meshbird.service "$pkgdir"/usr/lib/systemd/system/meshbird.service
  install -Dm644 "$srcdir"/meshbird.conf "$pkgdir"/etc/meshbird.conf
}
