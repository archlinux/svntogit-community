# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=couchdb
pkgver=1.2.0
pkgrel=4
pkgdesc="A document-oriented database that can be queried and indexed in a MapReduce fashion using JSON"
arch=('i686' 'x86_64')
url="http://couchdb.apache.org"
license=('APACHE')
depends=('icu' 'erlang' 'js' 'openssl' 'curl')
install=couchdb.install
options=('!libtool')
backup=('etc/couchdb/local.ini'
	'etc/conf.d/couchdb'
	'etc/logrotate.d/couchdb')
source=("http://www.apache.org/dist/couchdb/releases/${pkgver}/apache-${pkgname}-${pkgver}.tar.gz"{,.asc}
        "rc-script.patch")
md5sums=('a5cbbcaac288831b3d8a08b725657f10'
         '890a85b22219ea113a4901a289c442f8'
         '8a3b1a1ff98a6411827ad991db7a355b')

build() {
  cd "$srcdir/apache-$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
  
  patch -R etc/init/couchdb <$srcdir/rc-script.patch
}

package() {
  cd "$srcdir/apache-$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 etc/default/couchdb $pkgdir/etc/conf.d/couchdb
  sed -i 's|\(CONFIGURATION_FILE=/etc/\)default\(/couchdb\)|\1conf.d\2|' $pkgdir/etc/rc.d/couchdb
  sed -i 's|\(COUCHDB_OPTIONS=\)|\1"-p /var/run/couchdb/couchdb.pid"|' $pkgdir/etc/conf.d/couchdb

  rm -rf $pkgdir/etc/default/ $pkgdir/var/run
}
