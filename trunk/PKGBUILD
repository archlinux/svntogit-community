# Maintainer: Anatol Pomozov

pkgname=aws-c-sdkutils
pkgver=0.1.2
pkgrel=1
pkgdesc='AWS SDK utility library'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-sdkutils'
license=(Apache)
depends=(aws-c-common)
makedepends=(cmake)
source=(aws-c-sdkutils-$pkgver.zip::https://github.com/awslabs/aws-c-sdkutils/archive/v$pkgver.zip)
sha256sums=('72b03e8e3d15479fe1a2807a3dc65ad87ea3c33495ea3aafb57fc83927b4061c')

build() {
  cd aws-c-sdkutils-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-sdkutils-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-sdkutils-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
