# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.6.2
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop gobject-introspection-runtime libcanberra libinput
         libsm libxkbcommon-x11 startup-notification zenity graphene pipewire)
makedepends=(meson intltool gobject-introspection)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('61fb2e05ef500148bdaa6180a2d1c94b13b5cd6b4234dd1bd1ccd54d8dc012d0')
b2sums=('95d4599f59838e1b281683ddcfb06eb152274482991ae2eea4e5c47056072df5714fc187967bdd58342ce660e3874c606de29f0e9f089d5b8511f116e703aca4')

build() {
  arch-meson build ${pkgname}-${pkgver}
  meson compile -C build
}

package() {
  meson install --destdir="${pkgdir}" -C build
}
