# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=glabels
pkgver=3.4.0
pkgrel=1
pkgdesc="Creating labels and business cards the very easy way"
arch=('i686' 'x86_64')
url="http://glabels.org/"
license=('GPL' 'LGPL')
depends=('librsvg' 'evolution-data-server' 'qrencode' 'iec16022' 'dconf' 'desktop-file-utils' 'xdg-utils')
makedepends=('intltool' 'itstool')
install=$pkgname.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('d40e079395d30adbcd8204f41d08f7a8da9ec130bffa4cb3c130fbe2322b6410')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
