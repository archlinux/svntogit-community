# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: catwell <catwell@archlinux.us>

pkgname=lout
pkgver=3.39
pkgrel=2
pkgdesc="A lightweight document formatting system."
arch=('i686' 'x86_64')
url="http://lout.wiki.sourceforge.net/"
license=('GPL')
depends=('glibc')
install=lout.install
source=(http://mirrors.ctan.org/support/$pkgname/$pkgname-$pkgver.tar.gz
        makefile.arch
        lout.install
        http://pkgs.fedoraproject.org/repo/pkgs/lout/slides.pdf/6822c33e49a1dca0b090f297d404d7fa/slides.pdf)
md5sums=('151e1fba676c7052936ccc1bcd9663a9'
         '088a29ca16477a9a30da9fafc9391de8'
         '597cd52eb87ef6253cf769bed4db3952'
         '6822c33e49a1dca0b090f297d404d7fa')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp ../makefile.arch .
  make -f makefile.arch
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -f makefile.arch PKGDIR="$pkgdir" install
  chmod 0775  "$pkgdir/usr/lib/lout/data/"
  chgrp users "$pkgdir/usr/lib/lout/data/"
  install -Dm644 "$srcdir/slides.pdf" "$pkgdir/usr/share/doc/$pkgname/introduction.pdf"
}

