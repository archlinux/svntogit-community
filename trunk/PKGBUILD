# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ini2toml
pkgver=0.11.3
pkgrel=1
pkgdesc="Automatically conversion of .ini/.cfg files to TOML equivalents"
url="https://github.com/abravalheri/ini2toml"
license=('MPL')
arch=('any')
# The default installation is broken. Adding [full] flavor dependencies here.
depends=('python-configupdater' 'python-packaging' 'python-tomlkit')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-cov' 'python-tomli-w' 'python-validate-pyproject')
source=("https://github.com/abravalheri/ini2toml/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f1331bc47a2d4593788de9d8b1fdbc2231ac22524aaf3a83695c810e0dcbdce4bb46c48363ea150a583e4b24264d7a25f85ece737b31fa0ec0b8a6a2e098a9d5')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd ini2toml-$pkgver
  python setup.py build
}

check() {
  cd ini2toml-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  # TODO
  PYTHONPATH="$PWD/tmp_install$site_packages:$PYTHONPATH" pytest --deselect tests/test_cli.py::test_auto_formatting
}

package() {
  cd ini2toml-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
