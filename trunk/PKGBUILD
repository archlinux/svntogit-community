# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Joost Bremmer <toost dot b at gmail dot com>

pkgname=timew
pkgver=1.4.2
pkgrel=1
pkgdesc="A command line time tracking application"
arch=('x86_64')
url="https://timewarrior.net/"
license=('MIT')
makedepends=('cmake' 'gcc-libs')
optdepends=('task: Taskwarrior integration'
            'python: Holiday refresh script')
install=$pkgname.install
source=("https://github.com/GothenburgBitFactory/timewarrior/releases/download/v${pkgver}/timew-${pkgver}.tar.gz")
sha256sums=('c3d3992aa8d2cc3cd86e59d00060fb4a3e16c15babce78451cc9d39a7f5bb2e1')

build() {
    cd "${pkgname}-${pkgver}"
    cmake -DCMAKE_BUILD_TYPE=release -DCMAKE_INSTALL_PREFIX=/usr .
    make
}


package() {
    cd "${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    # Install the refresh holiday scripts in /usr/bin/ for user convenience.
    chmod 755 "${pkgdir}"/usr/share/doc/timew/doc/holidays/refresh
    ln -s /usr/share/doc/timew/doc/holidays/refresh "${pkgdir}"/usr/bin/timew-refresh-holidays
}
