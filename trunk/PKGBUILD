# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>
# Contributor: David Scholl <djscholl@gmail.com> 
# Contributor: David Pretty <david.pretty@gmail.com> 

pkgname=python-rpy2
pkgver=3.5.5
pkgrel=1
pkgdesc='A very simple, yet robust, Python interface to the R Programming Language'
arch=(x86_64)
url='https://rpy2.github.io/'
license=(MPL GPL LGPL)
depends=(python-cffi python-numpy python-pytz python-tzlocal python-jinja r)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
optdepends=('python-pandas: conversion between rpy and pandas')
source=(https://pypi.io/packages/source/r/rpy2/rpy2-$pkgver.tar.gz)
sha256sums=('a252c40e21cf4f23ac6e13bffdcb82b5900b49c3043ed8fd31da5c61fb58d037')

build() {
  cd rpy2-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd rpy2-$pkgver
  PYTHONPATH="$PWD"/`echo build/lib.*` \
  pytest -v -k 'not test_rendertofile and not test_rendertobytes_plot' # Skip test that segfaults in build server
}

package() {
  cd rpy2-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
}
