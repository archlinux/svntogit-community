# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=0.9.8
pkgrel=1
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=(x86_64)
url="https://github.com/rncbc/liblscp"
license=(LGPL2.1)
depends=(glibc)
makedepends=(cmake)
provides=(liblscp.so)
source=($pkgname-$pkgver.tar.gz::https://github.com/rncbc/liblscp/archive/refs/tags/${pkgname}_${pkgver//./_}.tar.gz)
sha512sums=('153aee5690cd9a4020e7e5f3b6b726522c94bdb2def2712650c408c72f8e1112a121f31e27da4b33ad28ae139fe8ca1d03f9473c77fa5ecde5df7311faa00f8f')
b2sums=('c1c53dc558890555e1295b170e748076b0c1da9da1d972502d5c1a29d7a4c1e1288c432fb2e65f580418736e01752e7660c3d30e9a9549d445e6fa55bf983181')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-${pkgname}_${pkgver//./_}
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-${pkgname}_${pkgver//./_}/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

