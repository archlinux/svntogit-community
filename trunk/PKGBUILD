# $Id$
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Kyle <kyle@gmx.ca>

pkgbase=java-atk-wrapper
pkgname=('java-atk-wrapper-common' 'java-atk-wrapper-openjdk7' 'java-atk-wrapper-openjdk8')
pkgver=0.33.3
pkgrel=1
arch=('x86_64')
url="https://git.gnome.org/browse/java-atk-wrapper/"
license=('LGPL')
makedepends=('java-environment' 'gobject-introspection'
             'gnome-common' 'gtk2' 'gtk3'
             'xorg-xprop')
_pkgbase=$(echo $pkgbase | awk '{print toupper($0)}' | tr '-' '_')
_pkgver=$(echo $pkgver | tr '.' '_')
_altnamever="${_pkgbase}_${_pkgver}"
source=(https://git.gnome.org/browse/$pkgbase/snapshot/$_altnamever.tar.xz
        no-werror.patch)
sha256sums=('11d4a1c5491b2ba5a24b710d695c3c5fea5047d06eee07171f23ac5707fef4c5'
            'ee22d01cc6282e005cef89456208193d0c9b329a65fc5e1c38304ebb22b95842')

prepare() {
  cd $_altnamever
  # https://anonscm.debian.org/cgit/pkg-a11y/java-atk-wrapper.git/tree/debian/patches/no-werror
  patch -p1 -i ../no-werror.patch
}

build() {
  cd $_altnamever
  ./autogen.sh --prefix=/usr
  make
  # make install is broken
  mkdir "build"
  make DESTDIR="$(pwd)/build" install
}

package_java-atk-wrapper-common() {
  pkgdesc='ATK wrapper for screen reader access to Java Swing applications - common files'
  depends=('xorg-xprop' 'atk')
  conflicts=('java-atk-wrapper')

  cd $_altnamever/build

  mkdir -p $pkgdir/usr/share/java
  cp build/usr/lib/jvm/java-7-openjdk/jre/lib/ext/java-atk-wrapper.jar \
     $pkgdir/usr/share/java
  cp -r usr $pkgdir
}

package_java-atk-wrapper-openjdk7() {
  pkgdesc='ATK wrapper for screen reader access to Java Swing applications - OpenJDK 7'
  depends=('java-atk-wrapper-common' 'java-runtime-headless-openjdk=7')
  replaces=('java-atk-wrapper')
  provides=('java-atk-wrapper-openjdk=7')

  cd $_altnamever/build

  mkdir -p $pkgdir/usr/lib/jvm/java-7-openjdk/jre/lib/ext
  ln -s /usr/share/java/java-atk-wrapper.jar \
    $pkgdir/usr/lib/jvm/java-7-openjdk/jre/lib/ext
  cp build/usr/lib/jvm/java-7-openjdk/jre/lib/accessibility.properties \
    $pkgdir/usr/lib/jvm/java-7-openjdk/jre/lib
}

package_java-atk-wrapper-openjdk8() {
  pkgdesc='ATK wrapper for screen reader access to Java Swing applications - OpenJDK 8'
  depends=('java-atk-wrapper-common' 'java-runtime-headless-openjdk=8')
  provides=('java-atk-wrapper-openjdk=8')

  cd $_altnamever/build

  mkdir -p $pkgdir/usr/lib/jvm/java-8-openjdk/jre/lib/ext
  ln -s /usr/share/java/java-atk-wrapper.jar \
    $pkgdir/usr/lib/jvm/java-8-openjdk/jre/lib/ext
  cp build/usr/lib/jvm/java-7-openjdk/jre/lib/accessibility.properties \
    $pkgdir/usr/lib/jvm/java-8-openjdk/jre/lib
}
