# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=2.4.5
pkgrel=2
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gobject-introspection-runtime' 'cinnamon-desktop' 'libcanberra' 'libsm'
         'startup-notification' 'zenity' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common' 'gobject-introspection') 
options=('!emptydirs')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/muffin/archive/$pkgver.tar.gz"
        "gtk316.patch")
sha256sums=('b38d62cb0d4ec9e213cc41501bda0d4712eebab7cd4fbc5ca0bdf8b5e74f83f7'
            '95af3a9267d4e82c412c6c57740c14dfd8d322b53abe9a38047e295e1b6f3db4')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../gtk316.patch
}

build() {
  cd ${pkgname}-${pkgver}

  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin \
  				 			 --localstatedir=/var --disable-static --disable-schemas-compile \
                 --enable-compile-warnings=minimum
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir/" install

  # Remove unused stuff
  make -C src DESTDIR="$pkgdir" uninstall-binPROGRAMS uninstall-desktopfilesDATA
  make -C src/tools DESTDIR="$pkgdir" uninstall
  make -C src/compositor/plugins DESTDIR="$pkgdir" uninstall
  make -C doc/man DESTDIR="$pkgdir" uninstall
}

# vim:set ts=2 sw=2 et:
