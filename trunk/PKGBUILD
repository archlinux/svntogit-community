# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace
pkgver=6.1
pkgrel=2
pkgdesc="Graphics API Tracing"
arch=('i686' 'x86_64')
url="https://github.com/apitrace/apitrace"
license=('custom')
makedepends=('cmake' 'mesa' 'libgl' 'python2' 'qtwebkit')
source=("$pkgname-$pkgver.zip::https://github.com/apitrace/apitrace/archive/${pkgver}.tar.gz")
md5sums=('d8fa88e0e594420afb5d17fdafdab0e4')

build() {
  cd ${srcdir}/apitrace-*

  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DENABLE_GUI=TRUE
  make -C build
}

package() {
  depends=('python2' 'libgl')
  optdepends=('qtwebkit: GUI support')

  cd ${srcdir}/apitrace-*

  make -C build DESTDIR="${pkgdir}/" install

  install -m755 -d "${pkgdir}/usr/share/licenses/apitrace"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/apitrace/"
}
