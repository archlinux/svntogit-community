# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Jason Chu <jason@archlinux.org>

pkgname=procstatd
pkgver=1.4.2
pkgrel=9
pkgdesc="A daemon that can be run either from userspace or inetd that can be queried via a simple API to parse various files in /proc"
arch=('x86_64')
license=('GPL')
url="http://www.phy.duke.edu/brahma/"
source=(https://downloads.sourceforge.net/herdtools/$pkgname.tar.gz)
sha256sums=('c6157ae30de179d71c6c9c4d755b3a4bfd5637d08b27e8b2ab77ccddac4d9fa5')

build() {
  cd "$srcdir"/$pkgname
  # Get rid of the funky make install forever bug
  sed -i 's/make install/echo/;s/cd watchman/echo/;s@$(INSTDIR)/sbin@$(INSTDIR)/bin@' Makefile
  make
}

package() {
  cd "$srcdir"/$pkgname
  make INSTDIR="$pkgdir"/usr install
  mkdir "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man
}
