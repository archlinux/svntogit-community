# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sound
pkgver=2.3.2
pkgrel=1
pkgdesc='Switchboard Sound Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sound
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcanberra
  libgee
  libgranite.so
  libpulse
  libswitchboard-2.0.so
 )
makedepends=(
  git
  meson
  vala
)
_tag=35837b527c9f217a5b58b7305fe9b739417a2295
source=(git+https://github.com/elementary/switchboard-plug-sound.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-sound

  git describe --tags
}

build() {
  arch-meson switchboard-plug-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
