# $Id: PKGBUILD 264278 2017-10-25 15:45:36Z spupykin $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=psi-l10n
pkgver=1.3
pkgrel=1
pkgdesc="Language packs for Psi+ (Qt 5.x build)"
arch=('any')
url="http://psi-plus.com"
license=('GPL2')
depends=('psi')
conflicts=('psi-l10n')
replaces=('psi-l10n')
makedepends=('qt5-tools' 'git')
options=('!strip' '!zipman')
source=("git://github.com/psi-plus/psi-plus-l10n.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
	cd psi-plus-l10n/translations
	for langfile in *.ts; do
		lrelease-qt5 "$langfile"
	done
}

package() {
	cd psi-plus-l10n/translations
	install -v -dm755 "$pkgdir/usr/share/psi-plus/translations"
	install -v -m644 *.qm "$pkgdir/usr/share/psi-plus/translations"
}
