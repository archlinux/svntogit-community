# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haskell-hslogger
pkgver=1.3.0.0
pkgrel=8
pkgdesc="Versatile logging framework"
url="https://hackage.haskell.org/package/hslogger"
license=('LGPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-network' 'haskell-old-locale')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/hslogger/${pkgver}/hslogger-${pkgver}.tar.gz")
sha512sums=('4b4b32a08e6cd58f9df72d170f3711e02ce748f6dd8ea098fbfa19c9b8e86565050b678475ad798a9588cb6cd0e07973b9c448bfb529afdc2e5d8cfa613b9ced')

build() {
    cd "${srcdir}/hslogger-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/hslogger-${pkgver}"
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
