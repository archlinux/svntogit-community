# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Benjamin Chr√©tien <chretien + aur [at] lirmm [dot] fr>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Rasi <rasi@xssn.at>
# Contributor: Sean Pringle <sean.pringle@gmail.com>
# Contributor: SanskritFritz (gmail)

pkgname=rofi
pkgver=0.15.11
pkgrel=1
pkgdesc='Popup window switcher roughly based on superswitcher, requiring only xlib and xft'
arch=(i686 x86_64)
url='https://davedavenport.github.io/rofi/'
license=(MIT)
depends=(libx11 libxft freetype2 libxdg-basedir libxinerama pango startup-notification)
makedepends=(i3-wm)
optdepends=('i3-wm: use as a window switcher')
source=(rofi-$pkgver.zip::https://github.com/DaveDavenport/rofi/archive/$pkgver.zip)
sha256sums=('dd1f86cffd07f24bd74fca80a31f3f67e0d19bd2fb7427bcca17c19b74e3e0a7')

build() {
  cd rofi-$pkgver
  autoreconf -i
  ./configure --prefix=/usr
  make
}

check() {
  cd rofi-$pkgver
  make test
}

package() {
  cd rofi-$pkgver
  make install install-man DESTDIR="$pkgdir"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/rofi/COPYING"
  install -dm755 "$pkgdir/usr/share/doc/rofi/examples"
  install -Dm755 Examples/*.sh "$pkgdir/usr/share/doc/rofi/examples"
}
