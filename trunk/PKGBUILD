# Maintainer:	Ondřej Surý <ondrej@sury.org>
# Maintainer:	Julian Brost <julian@0x4a42.net>
# Contributor:	Oleander Reis <oleander@oleander.cc>
# Contributor:	Otto Sabart <seberm[at]gmail[dot]com>

pkgname=knot
pkgver=2.6.1
pkgrel=1
pkgdesc="High-performance authoritative-only DNS server"
arch=('i686' 'x86_64')
url="https://www.knot-dns.cz/"
license=('GPL3')
depends=('lmdb' 'liburcu' 'protobuf-c' 'fstrm' 'libedit' 'systemd' 'libcap-ng' 'gnutls')
optdepends=('python: for pykeymgr')
backup=('etc/knot/knot.conf')

source=("https://secure.nic.cz/files/knot-dns/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('3013d45b4c7484268f3cad078f66f730a5bc9606e6b1061488dd821c1dce41e3'
            'SKIP'
            '353e109d07483d029a85106021c108767aab74427abec6a66c8e3c6b39a0b41f'
            '76c47120f91dbe467454a2f5330067d3dfd6ac5dacda5f3a095138b9a8877855'
            '793fee121d03acce87e3b04cfa869d4eb65e6ac0ee3e1257804fa526fa60d7f5')
validpgpkeys=('742FA4E95829B6C5EAC6B85710BB7AF6FEBBD6AB') # Daniel Salzman <daniel.salzman@nic.cz>

build() {
    cd ${pkgname}-${pkgver}

    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --localstatedir=/var/lib \
        --libexecdir=/usr/lib/knot \
        --with-rundir=/run/knot \
        --with-storage=/var/lib/knot \
        --enable-recvmmsg \
        --enable-dnstap \
        --enable-systemd \
        --enable-reuseport \
        --disable-silent-rules

    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    # Don’t keep those empty dir, manage them with systemd-tmpfiles instead
    rmdir "${pkgdir}"/{var/{lib/{knot/,},},run/{knot/,}}

    rm "${pkgdir}"/etc/knot/example.com.zone
    mv "${pkgdir}"/etc/knot/{knot.sample.conf,knot.conf}

    cd ..
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}
