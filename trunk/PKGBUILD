# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=CGNS
pkgname=cgns
pkgver=4.1.2
pkgrel=1
pkgdesc="Standard for recording and recovering computer data associated with the numerical solution of fluid dynamics equations"
arch=(x86_64)
url="https://cgns.github.io/"
license=(custom)
depends=(tk hdf5 libxmu glu)
makedepends=(gcc-fortran cmake)
options=(!makeflags)
source=(https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        cgns-fix-gfortran10.patch::https://github.com/CGNS/CGNS/commit/d463a92cd05545b0c76207589665e3827b3a10ed.patch)
sha256sums=('951653956f509b8a64040f1440c77f5ee0e6e2bf0a9eef1248d370f60a400050'
            '44a27ae954ac38106236148ff9058aa90646127ef105eb5a89c951372e5efa06')

prepare() {
    cd ${_pkgname}-${pkgver}
    patch -Np1 -i ../cgns-fix-gfortran10.patch
}

build() {
    cmake -B build -S ${_pkgname}-${pkgver} \
        -DCGNS_BUILD_CGNSTOOLS=ON \
        -DCGNS_BUILD_SHARED=ON \
        -DCGNS_ENABLE_64BIT=ON \
        -DCGNS_ENABLE_FORTRAN=ON \
        -DCGNS_ENABLE_HDF5=ON \
        -DCGNS_ENABLE_LEGACY=ON \
        -DCGNS_ENABLE_SCOPING=OFF \
        -DCGNS_ENABLE_TESTS=ON \
        -DHDF5_FOUND=OFF \
        -DCMAKE_BUILD_TYPE="Release" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON
# HDF5_FOUND=OFF is a (silly) workaround for https://cgnsorg.atlassian.net/browse/CGNS-143
    make -C build
}


check() {
    make -C build test
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 ${_pkgname}-${pkgver}/license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
