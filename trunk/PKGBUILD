# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-cwd-guard
pkgver=0.04
pkgrel=1
pkgdesc="Temporary changing working directory (chdir)"
arch=('any')
url="http://search.cpan.org/dist/Cwd-Guard/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/K/KA/KAZEBURO/Cwd-Guard-$pkgver.tar.gz")
md5sums=('26d12d3e4313943c7754afeec0f6462c')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/Cwd-Guard-$pkgver"
  /usr/bin/perl Build.PL
  ./Build
}

check() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  cd "$srcdir/Cwd-Guard-$pkgver"
  ./Build test
}

package() {
  export PERL_AUTOINSTALL=--skipdeps                       \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'"

  cd "$srcdir/Cwd-Guard-$pkgver"
  ./Build install
}
