# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgname=python-ldap
pkgver=3.3.1
pkgrel=4
pkgdesc="LDAP client API for Python"
url="https://python-ldap.readthedocs.io/"
arch=('x86_64')
license=('MIT')
depends=('python-pyasn1' 'python-pyasn1-modules' 'libldap')
makedepends=('python-setuptools' 'python-coverage')
checkdepends=('openldap' 'python-pyasn1' 'python-pyasn1-modules')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/python-ldap/python-ldap/archive/${pkgname}-${pkgver}.tar.gz"
        openldap-2.5.patch)
sha256sums=('014681ee155a61fde50cf3f19a2f85c5c094439da695968f7fcdf75e95db84f9'
            'a309bacc490ad11ec48693c72330d4530fd687d7808d8bed90470fbb5e68eac1')

prepare() {
  cd "$pkgbase-$pkgbase-$pkgver"
  # https://github.com/python-ldap/python-ldap/issues/432
  patch -Np1 -i ../openldap-2.5.patch

  # slapd fails to start because -F points to a non-empty directory.
  # Simply don't pass both -f and -F to slapd, it is only done when
  # converting slapd.conf to LDIF and pointless here.
  sed -i "/'-F', self.testrundir,/d" Lib/slapdtest/_slapdtest.py
}

build(){
  cd "$pkgbase-$pkgbase-$pkgver"
  python setup.py build
}

check(){
  cd "$pkgbase-$pkgbase-$pkgver"
  python -m coverage run --parallel setup.py test
}

package() {
  cd "$pkgbase-$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
