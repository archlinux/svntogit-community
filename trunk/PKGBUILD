# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-crossref
pkgver=0.2.5.0
pkgrel=62
pkgdesc="Pandoc filter for cross-references"
url="https://hackage.haskell.org/package/${pkgname}"
license=("GPL2")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-data-default" "haskell-data-accessor" "haskell-data-accessor-template"
         "haskell-data-accessor-transformers" "haskell-mtl" "pandoc" "haskell-pandoc-types"
         "haskell-roman-numerals" "haskell-syb" "haskell-utility-ht")
conflicts=('haskell-pandoc-crossref')
replaces=('haskell-pandoc-crossref')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d4d93bbe448e2cf187a0b7bcc605d0445e28021e4e31bfef890b93bee2b28491')

prepare() {
    sed -i 's/pandoc >= 1.17.1 && <1.19/pandoc >= 1.17.1 \&\& <1.20/' ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
