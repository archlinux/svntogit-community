# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sensors-applet
pkgver=3.0.0
pkgrel=2
pkgdesc='Applet for GNOME Panel to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings'
arch=('i686' 'x86_64')
url="http://sensors-applet.sourceforge.net/"
license=('GPL')
depends=('gnome-panel' 'libatasmart' 'lm_sensors' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('hddtemp: get HDD temperatures'
            'udisks: get HDD temperatures')
options=('!libtool')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sensors-applet/$pkgname-$pkgver.tar.gz)
md5sums=('425078fc1ff5a98af1547e039002a73f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-scrollkeeper --with-nvidia=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
