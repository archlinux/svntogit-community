# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libwapcaplet
pkgver=0.2.0
pkgrel=1
pkgdesc='String internment library with rapid string comparison'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libwapcaplet/'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('5fb49841ba335b0c7a1420d92c3a60480df63696c50353c02724aa8c6b45cb24')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make PREFIX=/usr COMPONENT_TYPE="lib-shared" 
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared" 
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
