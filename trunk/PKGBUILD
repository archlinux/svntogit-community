# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Markus Martin <markus@archwyrm.net>

pkgname=alure
pkgver=1.2
pkgrel=1
pkgdesc='Utility library to help manage common tasks with OpenAL applications.'
arch=('i686' 'x86_64')
url='http://kcat.strangesoft.net/alure.html'
license=('MIT')
depends=('openal')
makedepends=('cmake' 'libsndfile' 'libvorbis' 'flac' 'mpg123' 'dumb' 'fluidsynth')
optdepends=('libsndfile: for uncompressed audio support'
            'libvorbis: for OGG Vorbis support'
            'flac: for FLAC support'
            'mpg123: for MPEG support'
            'dumb: for IT, XM, S3M and MOD support'
            'fluidsynth: for SoundFont 2 support')
source=("http://kcat.strangesoft.net/alure-releases/${pkgname}-${pkgver}.tar.bz2")
md5sums=('3088aba074ad02d95ea51e705053b9f5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
