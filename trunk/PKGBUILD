# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgbase=python-parse
pkgname=(python-parse python2-parse)
pkgver=1.11.1
pkgrel=1

pkgdesc='Parse strings using a specification based on the Python format() syntax.'
url='http://pypi.python.org/pypi/parse'
arch=('any')
license=('BSD')

makedepends=('python-setuptools' 'python2-setuptools')

source=("https://pypi.org/packages/source/p/parse/parse-$pkgver.tar.gz"
        https://raw.githubusercontent.com/r1chardj0n3s/parse/b48b0494d27f64a9088cd92c1f8d58928af07317/LICENSE) # license from the bottom of parse.py

md5sums=('9148ef37ebcbd959e39619b4af6d1a7e'
         '68c10fa20482cc4328a1052695fb7e53')

prepare() {
  cp -r parse-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/parse-$pkgver
  python setup.py build

  cd "$srcdir"/parse-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parse-$pkgver
  python test_parse.py

  cd "$srcdir"/parse-$pkgver-py2
  python2 test_parse.py
}


package_python-parse() {
  depends=('python')

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$srcdir"/parse-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}


package_python2-parse() {
  depends=('python2')

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$srcdir"/parse-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
