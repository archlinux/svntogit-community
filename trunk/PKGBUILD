# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>

pkgname=symengine
pkgver=0.10.0
pkgrel=3
pkgdesc='Fast symbolic manipulation library, written in C++'
url='http://sympy.org/'
arch=(x86_64)
license=(MIT)
depends=(llvm-libs arb libmpc gmp-ecm primesieve gperftools)
makedepends=(cmake boost llvm cereal)
source=(https://github.com/symengine/symengine/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        primesieve.patch
        llvm-shared.patch)
sha256sums=('27eae7982f010e4901a5922d44e0de4b81c3b8dd52c57b147a1994f0541da50e'
            'f19af7885923a6fb80783796a24e045201014415e8b9f195b87443600864e7b1'
            'ef05a806139b35e0336f00cb6960ef6cf483f11b0041e436a99ab1f46bdcb11c')

prepare() {
  patch -d $pkgname-$pkgver -p1 < primesieve.patch # Actually use primesieve
  patch -d $pkgname-$pkgver -p1 < llvm-shared.patch # Use shared LLVM (Gentoo)
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_TCMALLOC=ON \
    -DWITH_PTHREAD=ON \
    -DWITH_SYMENGINE_THREAD_SAFE=ON \
    -DWITH_ARB=ON \
    -DWITH_ECM=ON \
    -DWITH_LLVM=ON \
    -DWITH_MPFR=ON \
    -DWITH_MPC=ON \
    -DWITH_PRIMESIEVE=ON \
    -DWITH_BOOST=ON \
    -DWITH_COTIRE=OFF \
    -DWITH_SYSTEM_CEREAL=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
