# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>
# Contributor: John Lane <archlinux at jelmail dot com>
# Contributor: Fisher Duan <steamedfish@njuopen.com>
# Contributor: Ryan Corder <ryanc@greengrey.org>

pkgbase=cyrus-imapd
pkgname=(cyrus-imapd cyrus-imapd-docs)
pkgver=3.6.0
pkgrel=3
pkgdesc="An email, contacts and calendar server"
arch=(x86_64)
url="https://www.cyrusimap.org/"
license=(BSD)
makedepends=(libsasl jansson libbsd icu util-linux openssl sqlite
             krb5 mariadb-libs postgresql-libs brotli libchardet libical
             libxml2 libnghttp2 shapelib libwslay zlib zstd libcap
             clamav libldap pcre2 perl xapian-core rsync)
checkdepends=(cunit)
source=(https://github.com/cyrusimap/cyrus-imapd/releases/download/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.gz{,.sig}
        pcre2.patch
        imapd.conf.patch
        ${pkgname}.service
        ${pkgname}.sysusers
        ${pkgname}.tmpfiles)
sha512sums=('d2b21117e7d0dd93212a43ca42098dc8b59613cb45ed2287d381c8c363026ac29386dfd90f07fa2ce8a77f56dddd19f2cc5b80d9c60c400d65cbb1d52a8db93c'
            'SKIP'
            'ee40b72a52125ee9f59a26a99ab1c75116929644b05ade339aa921d6c4173d80d9d48ec5d9ad71a95f5adc025337d1d13ad8e23184b60f3c11e397c6e9746cd5'
            '0862ffc8c05208efd4d2fb50a6e3719ebc65fc2d72f8e6404235aa32cc44d8227056a17b78f2726e15ff8e38d473795f837c34bfbe89b694b2298c9baab9d5db'
            '738242e80cec2c25ae6a85a889cc8d35d7c2f43b2b4d64d74f99a230b21024f168a885f1e319aec1aab0e0599e41211478b99dc608a4ba036be90f8d7e23fd96'
            '148f20fc018196bc54078dbd63469542617aebde80937866282b0811acd90065529cfc6b5f4cf34d0095f5052b31e4bbaf877424f575753a09912a8b40a66f4b'
            '6cb3b4d0c0c8e301d0fa9c4bf38dd971aa8309eb538c86fb95480768223e8b29f7f7f4da8a9afd8693169830418f92a6f2ddb60661b455b5ca678248ad9a8a41')
validpgpkeys=(5B55619A9D7040A9DEE2A2CB554F04FEB36378E0)

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 < ../pcre2.patch
  rm sieve/sieve.c
  autoreconf -vfi
}

build() {
  cd ${pkgbase}-${pkgver}

  export PERL_MM_OPT="NO_PACKLIST=true NO_PERLLOCAL=true"
  # Work around Cyrus bug #3562, #3588
  export CFLAGS="${CFLAGS} -fno-toplevel-reorder"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/cyrus \
    --sysconfdir=/etc/cyrus \
    --sbindir=/usr/bin \
    --enable-xapian \
    --enable-autocreate \
    --enable-idled \
    --enable-nntp \
    --enable-murder \
    --enable-http \
    --enable-calalarmd \
    --enable-jmap \
    --enable-replication \
    --enable-backup \
    --enable-unit-tests \
    --with-mysql=yes \
    --with-pgsql=yes \
    --with-sqlite=yes \
    --with-ldap \
    --with-libcap \
    --with-pidfile=/run/cyrus-master.pid \
    --with-syslogfacility=MAIL

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd ${pkgbase}-${pkgver}
  make check
}

package_cyrus-imapd() {
  depends=(libsasl jansson icu util-linux-libs openssl sqlite
           krb5 mariadb-libs postgresql-libs brotli libchardet libical
           libxml2 libnghttp2 shapelib libwslay zlib zstd libcap
           libldap pcre2 perl xapian-core e2fsprogs)
  optdepends=('cyrus-imapd-docs: documentation'
              'clamav: for cyr_virusscan'
              'rsync: for compacting Xapian databases')
  provides=(imap-server pop3-server)
  backup=(etc/cyrus/cyrus.conf etc/cyrus/imapd.conf)

  # Install system configuration
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf

  cd ${pkgbase}-${pkgver}

  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"

  # Rename httpd.8 and master.8 so they donâ€™t conflict with the identically
  # named manpages from postfix and apache
  mv "${pkgdir}"/usr/share/man/man8/httpd.8{,cyrus}
  mv "${pkgdir}"/usr/share/man/man8/master.8{,cyrus}

  # Install additional utilities
  for i in arbitronsort.pl masssievec mkimap mknewsgroups rehash translatesieve; do
    install -Dm755 tools/${i} -t "${pkgdir}"/usr/bin/
  done

  # Install configuration files
  install -Dm644 doc/examples/cyrus_conf/normal.conf "${pkgdir}"/etc/cyrus/cyrus.conf
  install -Dm644 doc/examples/imapd_conf/normal.conf "${pkgdir}"/etc/cyrus/imapd.conf
  patch "${pkgdir}"/etc/cyrus/imapd.conf "${srcdir}"/imapd.conf.patch

  # Install basic documentation
  install -Dm644 -t "${pkgdir}"/usr/share/doc/cyrus-imapd/ README.md doc/README.*
  cp -r doc/examples "${pkgdir}"/usr/share/doc/cyrus-imapd/

  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_cyrus-imapd-docs() {
  pkgdesc="Documentation for the Cyrus IMAP server"
#  arch=(any)
  cd ${pkgbase}-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/doc/cyrus-imapd
  cp -r doc/html doc/internal doc/legacy doc/text "${pkgdir}"/usr/share/doc/cyrus-imapd
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
