# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Aleksandar Micovic <metaleks@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>

_hkgname=pandoc
pkgname=haskell-pandoc
pkgver=1.11.1
pkgrel=8
pkgdesc="Conversion between markup formats"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-bytestring' 'haskell-citeproc-hs' 'haskell-containers' 'haskell-directory' 'haskell-extensible-exceptions' 'haskell-filepath' 'haskell-highlighting-kate' 'haskell-http' 'haskell-json' 'haskell-mtl' 'haskell-network' 'haskell-old-locale' 'haskell-pandoc-types' 'haskell-parsec' 'haskell-process' 'haskell-random' 'haskell-syb' 'haskell-tagsoup' 'haskell-temporary' 'haskell-texmath' 'haskell-time' 'haskell-utf8-string' 'haskell-xml' 'haskell-zip-archive' 'haskell-zlib' haskell-data-default)
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=$pkgname.install
md5sums=('e895b22745affba37c0ba5e6e6d0f17c')
provides=(pandoc)
conflicts=(pandoc)
replaces=(pandoc)

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
}
