# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.5.2
pkgrel=4
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
depends=('ncurses')
makedepends=('rust')
url=https://github.com/lotabout/skim
license=('MIT')
source=("$url/archive/v$pkgver/skim-v$pkgver.tar.gz")
sha512sums=('018962a728b9729b51afdc8975cddeceec84731f43b776a740ad2b4d2a479ceadd7a4d45c6eb5389ef73cdf18955a74ac85e9bbb4ad823e4d4a5d9972a4aa19d')

build() {
  cd skim-$pkgver
  cargo build --release
}

check() {
  cd skim-$pkgver
  cargo test --release
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}
