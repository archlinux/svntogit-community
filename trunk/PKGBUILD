# Maintainer:  Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgbase='python-cherrypy'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cherrypy' 'python2-cherrypy')
pkgver=16.0.2
pkgrel=2
pkgdesc="A pythonic, object-oriented web development framework"
arch=('any')
url="https://cherrypy.org"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python-six' 'python2-six' 'python-cheroot'
  'python2-cheroot' 'python-portend' 'python2-portend')
checkdepends=('python-tox' 'python2-tox')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/${_pkgbase}/${_pkgbase}/archive/v$pkgver.tar.gz")
sha512sums=('00c89a99250e44f2578a1ff5d77f402062fc829c9de86aac029963d6b0570a862e48a84781c3fa6dd74e60e09a02e4e7bfc8f3e485a37b31f9b2870bc3ff2066')

prepare() {
  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"

  # disable broken tests
  rm "${srcdir}/${_pkgbase}-${pkgver}/${_pkgbase}/test"/test_{objectmapping.py,request_obj.py}
  rm "${srcdir}/${_pkgbase}-${pkgver}-py2/${_pkgbase}/test"/test_{encoding.py,objectmapping.py,request_obj.py}
}

build() {

  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py build

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  # fix incorrect shebang
  sed -i 's|#!/usr/bin/python$|#!/usr/bin/python2|' cherrypy/test/sessiondemo.py
  python2 ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  tox

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  tox2
}

package_python-cherrypy() {
  depends=('python' 'python-six' 'python-cheroot' 'python-portend' 'python-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}"

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_python2-cherrypy() {
  depends=('python2' 'python2-six' 'python2-cheroot' 'python2-portend' 'python2-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"

  python2 ./setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}/usr/bin/cherryd" "${pkgdir}/usr/bin/cherryd2"

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
