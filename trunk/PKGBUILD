# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filter-rspamd
pkgname=opensmtpd-${_pkg}
pkgver=0.1.5
pkgrel=1
pkgdesc="OpenSMTPD filter integration for Rspamd"
arch=(x86_64)
url=https://github.com/poolpOrg/filter-rspamd
license=(ISC)
depends=(glibc rspamd)
makedepends=(go-pie)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('adb781be19952718bb240bea3d6368a71f0ecf22cb14f647ba99330035c20621')

build() {
  cd ${_pkg}-${pkgver}
  go build -v \
    -trimpath \
    -ldflags "-extldflags $LDFLAGS" \
    -o ${_pkg}
}

package() {
  cd ${_pkg}-$pkgver
  install -D filter-rspamd -t "${pkgdir}"/usr/lib/smtpd/opensmtpd
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}/
}
