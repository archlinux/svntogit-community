# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=perl-canary-stability
_realname=Canary-Stability
pkgver=2013
pkgrel=5
pkgdesc="CPAN/Canary-Stability - canary to check perl compatability for schmorp's modules"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url="https://metacpan.org/release/${_realname}"
source=("https://cpan.metacpan.org/authors/id/M/ML/MLEHMANN/${_realname}-${pkgver}.tar.gz")
sha256sums=('a5c91c62cf95fcb868f60eab5c832908f6905221013fea2bce3ff57046d7b6ea')

build() {
  cd "$srcdir/$_realname-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_realname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
