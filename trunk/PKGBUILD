# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=0.9.8
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=(x86_64)
url="https://qmidinet.sourceforge.io"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(alsa-lib cmake jack2 qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('8b10ef625cd5f1992f88b6eb486654e57253d84ec1dad5a3136f60a63c0fc40d89fc1901269e359a72733471019d13a844be4f0858b775fc256ffe7a2da44e3e')
b2sums=('35f50d97a05db2de543796905b40a45b75eee82655ebc1a8df25cb5535027c1b920b7630d0fb6e9832bfdca50ff5162ffa1cb975a6922c5174c29023ab5162f0')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libjack.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
