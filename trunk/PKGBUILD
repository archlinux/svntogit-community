# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applet-streamer
pkgver=0.4.0
pkgrel=1
pkgdesc="MATE panel applet to let you play your favourite online radio station with a single click"
url="http://www.zavedil.com/online-radio-applet/"
arch=('x86_64')
license=('GPL')
depends=('gst-plugins-base' 'libnotify' 'mate-panel')
optdepends=('gst-plugins-bad: for playing MP3 streams')
source=("http://www.zavedil.com/wp-content/uploads/2017/06/${pkgname}-${pkgver}.tar.gz")
sha1sums=('59e61ccfdb8585976b6eaac11fe4a923215b3d5f')

build() {
    cd ${pkgname}-${pkgver}
    CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/glib-2.0/schemas/gschemas.compiled"
}
