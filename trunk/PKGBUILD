# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=cage
pkgver=0.1.4
pkgrel=11
pkgdesc="A kiosk compositor for Wayland"
arch=(x86_64)
url="https://www.hjdskes.nl/projects/cage/"
license=(MIT)
depends=(glibc wayland wlroots0.15)
makedepends=(git libxkbcommon meson pixman scdoc wayland-protocols
xorg-server-xwayland)
optdepends=(
  'polkit: System privilege control. Required if not using seatd service'
  'xorg-server-xwayland: X11 support'
)
source=("${pkgname}::git+https://github.com/Hjdskes/${pkgname}.git#tag=v${pkgver}?signed")
install=cage.install
sha512sums=('SKIP')
validpgpkeys=('13B6A35E9FD677D02CD68DBE37C445296EBC43B1') # Jente Hidskes <dev@hjdskes.nl>

prepare() {
  cd "${pkgname}"
  # add support for wlroots 0.15.0
  git cherry-pick -n 1a3ab3eb3ad0f4a1addb8f9a9427d8b369b19511
  git cherry-pick -n 8385b62a9b31028fa356cc4130baebc8b6f4adec
  git cherry-pick -n 388d60d6b88733330fb83dd440051ee805cc7ec7
  git cherry-pick -n 395189fb051ed722c7b10b6cb11caa8f6904079c
}

build() {
  export PKG_CONFIG_PATH='/usr/lib/wlroots0.15/pkgconfig'
  arch-meson -Dxwayland=true build "$pkgname"
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(libwlroots.so=10 libxkbcommon.so libpixman-1.so)

  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 "$pkgname/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "$pkgname/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
