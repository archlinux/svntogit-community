# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-responses
pkgname=('python-responses' 'python2-responses')
pkgver=0.6.2
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-cookies' 'python2-cookies' 'python-six' 'python2-six' 'python2-mock')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-cov'
              'python2-pytest-cov' 'flake8' 'python2-flake8')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz")
sha512sums=('438252864ed99c823f4ea0691acfabd8fe38f5e2dfa8da1aa31d93adc8483e2e1421a4bc79a3cc81c3258c84eb0569e0c9cc320302fb2dffe2c6874c93d51f49')

prepare() {
  cp -a responses-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py pytest
}

package_python-responses() {
  depends=('python-requests' 'python-cookies' 'python-six')

  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-responses() {
  depends=('python2-requests' 'python2-cookies' 'python2-six' 'python2-mock')

  cd responses-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
