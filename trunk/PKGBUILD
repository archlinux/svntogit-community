# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus
pkgver=1.2.0.20100111
pkgrel=4
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://ibus.googlecode.com"
depends=('dbus-python>=0.83.0' 'gconf' 'python>=2.5.0' 'pygtk' 'pyxdg' 'iso-codes' 'librsvg' 'python-notify')
optdepends=('notification-daemon')
makedepends=('intltool')
provides=('ibus=1.2.0')
options=('!libtool')
install=ibus.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('73eabe0f768cf97b7205cb4e74fba706')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr --libexecdir=/usr/lib/ibus --sysconfdir=/etc \
	    --with-gconf-schema-file-dir=/usr/share/gconf/schemas
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install
	echo "Categories=System;Utility;" >> $pkgdir/usr/share/applications/ibus.desktop
}
