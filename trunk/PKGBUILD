# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>

pkgname=gbrainy
pkgver=2.00
pkgrel=1
pkgdesc="A brain teaser game and trainer to have fun and to keep your brain trained"
arch=('any')
url="http://live.gnome.org/gbrainy"
license=('GPL2')
depends=('gtk-sharp-2' 'mono-addins' 'librsvg' 'hicolor-icon-theme')
makedepends=('intltool' 'gettext' 'gnome-doc-utils')
install=gbrainy.install
source=(http://gent.softcatala.org/jmas/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('d7d29348868ee764408fd1a42d01d961')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export MONO_SHARED_DIR="$srcdir"
  ./configure --prefix=/usr --disable-gnome
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
