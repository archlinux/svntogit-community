# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-widgetsnbextension
pkgver=4.0.5
pkgrel=1
epoch=1
pkgdesc="Interactive HTML widgets for Jupyter notebooks"
arch=('any')
url="https://pypi.python.org/pypi/widgetsnbextension"
license=('BSD')
depends=('jupyter-notebook' 'python-ipywidgets')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://pypi.io/packages/source/w/widgetsnbextension/widgetsnbextension-$pkgver.tar.gz")
sha256sums=('003f716d930d385be3fd9de42dd9bf008e30053f73bddde235d14fbeaeff19af')

build() {
  cd widgetsnbextension-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd widgetsnbextension-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir/usr/etc" "$pkgdir/etc"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
