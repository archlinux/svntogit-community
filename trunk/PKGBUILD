# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.38.0
_image_pkgver=5.12.0
_podman_pkgver=3.1.2
_skopeo_pkgver=1.2.3
_storage_pkgver=1.30.2
pkgrel=2
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('41000f5bb30396025fb0bd061f72096d8d18999dfdebba804d5dd379483632811d7555e482c0f457a75e38904a59b6d436b0f537a443b273b540e7c6e8ab09d9'
            'c690b1c4fd67754c41cc590279382a8f4228e3160db5ba482455b4c0ef4b6bd40ea92d0ca7662b2cc1504d82afdb33bdce8d60552ba3e216102ed34e1418d8c0'
            'fdc26368f3cce6a11fe9adeae34a2f04396a554bb5f245e07fc98d1ce21dff66faba5549e9e0b94793d67768ff03fea2990e7a2759dd41ed2b72778431f5dd9d'
            '06e3ddc17f45a7b145c44c58d67894d3fff809cd1521753027c703c44425241d4580096fd44d9156eae70336544110e2af95ab6f9d22deff88903c6e77bbe4d8'
            '88fbe0f868887039793fabb7f859ee578cd300571279eb06adbc1e3f194076e7085e8968d1a630eb3ab2f88e2b9f52526da41e18226bfa133f7c335263de6eab'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('5253933e1899263004251371a173a45bd2c0c2a0f16324b997a90af3bf634e6484e0279cbc21b647b91a3a74966c53cffb0bb4d3f44ff2c6089bcafac6d13880'
        'c90e13a400fd7de3d4d57e6e5490d36f47949aa2170f832cd22d34e663ede528fa85cc84c836d48301568657b78a8fafcea2c7ed475da179dc6f0507b8501523'
        '8536f6856e436467541f7714e75d5055545e1514604d6541671894117e752609a9e791355c162ef8fcdbc0cbd992823ecab8a3ab760bab3a469d9768b6b008e4'
        '3db447726d6a545f5225aa37a215da8a7893ea8beae08547bf93148d632a80863173b014949cc69cb033a2e68653738fe86d1c8758b12fc3c2c16ebb5582b203'
        '71e922626dd749b659c873e9f2b79228b777b96e84f259a998abdb048232a3e1033e7ee97a8144eaa1c35dee37f0813496f33964b5104347d7a5ad22fbecf845'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
