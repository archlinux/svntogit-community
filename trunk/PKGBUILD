# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=flickcurl
pkgver=1.26
pkgrel=2
pkgdesc="C library for the Flickr API"
arch=(x86_64)
url="http://librdf.org/flickcurl/"
license=('GPL')
depends=('raptor' 'curl')
validpgpkeys=('F879F0DEDA780198DD08DC6443EC92504F71955A')
source=(http://download.dajobe.org/flickcurl/flickcurl-$pkgver.tar.gz{,.asc}
	https://github.com/dajobe/flickcurl/commit/a5cc2a5d2fc7074f50fbaa772232b6e0fea7ce89.patch)
sha256sums=('ff42a36c7c1c7d368246f6bc9b7d792ed298348e5f0f5d432e49f6803562f5a3'
            'SKIP'
            '59cffda1e432483e362f2786102e2bcb41e52e64df9a9e13d953f84103192f10')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <"$srcdir"/a5cc2a5d2fc7074f50fbaa772232b6e0fea7ce89.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|#include <curl/types.h>||' src/flickcurl_internal.h
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
