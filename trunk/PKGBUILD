# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-pw-check
pkgver=5.1.16
pkgrel=1
pkgdesc='Tool to verify the validity of the password'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-pw-check"
license=('GPL3')
depends=('cracklib' 'gtk3' 'iniparser')
makedepends=('deepin-gettext-tools' 'git' 'go' 'golang-deepin-lib' 'golang-github-fsnotify-fsnotify'
             'golang-github-linuxdeepin-go-dbus-factory' 'golang-github-stretchr-testify'
             'golang-gopkg-yaml.v3')
groups=('deepin')
source=("https://github.com/linuxdeepin/deepin-pw-check/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('db59b3ab8d6f8971342bd906a6f67f421db5dc2c5846b314698012831e22daaecfbb0fe595b47fd856bd523375d93a2001dcaa096d518e65c55fdcb982f960a7')

export GO111MODULE=off

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/godbus/dbus

  cd deepin-pw-check-$pkgver
  sed -i 's|iniparser/||' */*.c
  sed -i 's|gcc |gcc ${CFLAGS} ${LDFLAGS} |' Makefile
  sed -i 's|sprintf(outbuf, err_to_string|sprintf(outbuf, "%s", err_to_string|' pam/pam.c
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd deepin-pw-check-$pkgver
  # broken Makefile
  make -j1
}

package() {
  cd deepin-pw-check-$pkgver
  make DESTDIR="$pkgdir" PKG_FILE_DIR=/usr/lib/pkgconfig PAM_MODULE_DIR=/usr/lib/security install
}
