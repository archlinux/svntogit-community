# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Andre Klitzing <andre AT incubo DOT de>
# Contributor: max-k <max-k AT post DOT com>

pkgname=reprepro
pkgver=5.4.1
pkgrel=2
pkgdesc='Debian package repository producer'
url="https://salsa.debian.org/debian/reprepro"
arch=('x86_64')
license=('GPL2')
depends=('db' 'gpgme' 'zlib' 'bzip2' 'xz' 'libarchive' 'libgpg-error')
source=("https://salsa.debian.org/debian/reprepro/-/archive/reprepro-${pkgver}/reprepro-reprepro-${pkgver}.tar.bz2")
sha512sums=('2219b99f1d5154512ea13430a94df6f43628062c963dca6a07bc71373b0517a111d4a358016f1970db02d5a0414e50a710119f20870e83c152413c76a4fbe573')
b2sums=('517d20a531aa7ff8a2d8de1c9c026edd82135915d27ca3138a62ea568718f6c495052c3a441eab1e27b23f64d447c1574abfbbad93cbd2419f77fb2ea7ae4d12')

build() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  set -x

  libtoolize --force
  aclocal
  autoheader
  automake --add-missing
  autoconf

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
