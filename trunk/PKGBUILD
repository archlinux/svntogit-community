# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=xh
pkgver=0.10.0
pkgrel=1
pkgdesc="Friendly and fast tool for sending HTTP requests"
arch=('x86_64')
url="https://github.com/ducaale/xh"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
replaces=('ht-rs' 'ht')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('07d63045f13a0c77990dfd1132f60d20bfddfd0625df49ee22cf4c7e3b3b46fa')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "doc/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 "completions/$pkgname.bash" "${pkgdir}/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "${pkgdir}/usr/share/zsh/site-functions"
  # `xh` will default to HTTPS scheme if the binary name is one of `xhs`, `https`, or `xhttps`
  ln -s "/usr/bin/$pkgname" "$pkgdir/usr/bin/${pkgname}s"
}

# vim: ts=2 sw=2 et:
