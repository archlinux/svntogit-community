# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

pkgname=ruby-test-unit
pkgver=3.3.5
pkgrel=1
pkgdesc='An xUnit family unit testing framework for Ruby'
arch=('any')
url='https://github.com/test-unit/test-unit'
license=('GPL')
depends=('ruby-power_assert')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://rubygems.org/downloads/test-unit-$pkgver.gem")
noextract=("test-unit-$pkgver.gem")
sha512sums=('eab194e2361b7aa82dbe18d87dd309e81ddb1cb063e289b63a99c4803c0b88d0323a2028afd6585c14e11e458d4cd5ceb67e674bb7d5065d31b91649be2cbd68')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" test-unit-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/test-unit-$pkgver.gem"
}
