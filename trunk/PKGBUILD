# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.4.31
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="http://pylib.readthedocs.org/en/latest/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/py/py-$pkgver.tar.gz")
sha512sums=('fd0f4f509d81beb8e5cdb5a8d5d7d40d8a74737c017c915b0630ce740868c32fbf1af4a01dec2ae998e5a9516578e8404ad2b8d966606919767ae25f27eb0b0d')

prepare() {
  cp -a py-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/py-${pkgver}"
  python setup.py build

  cd "$srcdir/py-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/py-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" LANG=en_US.utf8 py.test

  cd "$srcdir/py-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" LANG=en_US.utf8 py.test2
}

package_python-py() {
  depends=('python')

  cd py-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-py() {
  depends=('python2')

  cd py-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

