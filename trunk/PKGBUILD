# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=xosd
pkgver=2.2.14
pkgrel=11
pkgdesc="Displays text on your screen. On-Screen-Display-libs for some tools"
arch=('x86_64')
license=('GPL2')
url="https://sourceforge.net/projects/libxosd/"
depends=('libxt' 'libxinerama' 'sh')
optdepends=('xorg-fonts-misc: contains default font')
source=("https://downloads.sourceforge.net/libxosd/${pkgname}-${pkgver}.tar.gz"
        "https://deb.debian.org/debian/pool/main/x/xosd/xosd_$pkgver-2.1.debian.tar.xz")
sha256sums=('4f1fa96f4895baee6a3cba40f188a0da23bbac9d68e8c326d749e084143cb508'
            'ccde8324c909d75e60b124146d1e20c6a586385934dc15df02b7209f7324322d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <"$srcdir"/debian/patches/25_fix_mangapges.diff
  patch -p1 <"$srcdir"/debian/patches/328676.diff
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  sed -i 's/AC_DEFUN(AM_PATH_LIBXOSD,/AC_DEFUN([AM_PATH_LIBXOSD],/' \
    "$pkgdir/usr/share/aclocal/libxosd.m4"
}
