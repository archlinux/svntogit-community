# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.43
pkgrel=1
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('glibc')
optdepends=('lzo2: for libddr_lzo')
arch=('x86_64' 'i686')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.gz")

prepare() {
  cd $pkgname
  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}

sha512sums=('d0c090d5d94916cda4bd046b22324ea5c44cfd2f856af56b698ea0e9ab4b622564a652869e50fa4e2e3d59b8d10bbffd00b826453bed06d778e7326ebf72e87d')
