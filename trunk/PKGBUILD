# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.7.6.164
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'libxtst' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-mpv-qthelper.patch::https://github.com/linuxdeepin/deepin-movie-reborn/commit/cc9484fdb63589c3596d2914d180e351e0e0330a.patch
        $pkgname-libavformat-version-check.patch::https://github.com/linuxdeepin/deepin-movie-reborn/commit/7bff486b84a6c9e8602ef2fb201cfa602d6ca1d8.patch)
sha512sums=('baa8e4439b25c1daacfd36d98dc1ccab9be7ab358d694d1c50c05ba3ac76c2390f03fc3c378c691b4b9bb756f1209de03da6db025cad75fe62b90b6fd4f542b1'
            'ed9414601226feb6e93828098efae4ff007dbd40c5ac1c102cdeabb3e138898f22b11ffe6c0052902591667904454795b95dcd9b75a6d1a10c84b70232c78f8d'
            '157296a3e48ba4b6606a859397f0c24311dead227b301448c94c9eb2dc4408d5d6466ce2a0873636f8ed48d71137e9556fc8103a6764e65ce2392ba042f94fed')

prepare() {
  cd deepin-movie-reborn-$pkgver
  patch -p1 -i ../$pkgname-mpv-qthelper.patch
  patch -p1 -i ../$pkgname-libavformat-version-check.patch
  sed -i '/#include <DPalette>/a #include <QPainterPath>' src/widgets/{tip,toolbutton}.h
}

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
