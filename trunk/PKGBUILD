# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=5.7.0
pkgrel=2
pkgdesc="KDE Photo Album, picture collection manager"
url="https://www.kphotoalbum.org/"
arch=('x86_64')
license=('GPL')
depends=('libkipi' 'libkdcraw' 'marble-common' 'exiv2' 'purpose')
makedepends=('extra-cmake-modules' 'plasma-framework' 'phonon-qt5' 'kdoctools' 'kparts' 'libkexiv2')
optdepends=()
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('b97135ebe4606a4e68ab0f38399a79db3e22eaba109e50c0841fe097a4c9a031'
            'SKIP')
validpgpkeys=('8A53F0CB9F752E6F4F72FEC6F8DECD0DDF0F74A8') # Tobias Leupold <tobias.leupold@gmx.de>
validpgpkeys+=('A53563BC888E6CE5215535992523842AF9681DDF')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
