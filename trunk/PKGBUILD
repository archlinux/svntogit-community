# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sigil
pkgver=1.4.2
pkgrel=4
pkgdesc="multi-platform EPUB2/EPUB3 ebook editor"
arch=('x86_64')
url="https://sigil-ebook.com/"
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'mathjax2' 'minizip' 'python-css-parser'
         'python-dulwich' 'python-lxml' 'qt5-svg' 'qt5-webengine')
makedepends=('qt5-tools' 'cmake')
optdepends=('hunspell-en_US: for English dictionary support'
            'hyphen-en: for English hyphenation support in plugins'
            'pageedit: external editor to replace BookView'
            'python-html5lib: recommended for plugins'
            'python-chardet: recommended for plugins'
            'python-cssselect: recommended for plugins'
            'python-pillow: recommended for plugins'
            'python-regex: recommended for plugins'
            'python-pyqt5: recommended for plugins'
            'tk: recommended for plugins')
source=("Sigil-${pkgver}.tar.gz::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "https://github.com/Sigil-Ebook/Sigil/releases/download/${pkgver}/Sigil-${pkgver}.tar.gz.sig"
        "https://github.com/Sigil-Ebook/Sigil/commit/16babc881ac0f996641cc693a7564c4e08e27266.patch"
        "https://github.com/Sigil-Ebook/Sigil/commit/966fe5e8fa16d791d995d4655894f1d328e3c76e.patch")
sha256sums=('e31b7c84507ffe9ca896d8a169867923d3274c02d957bd59e5c6b1fd12b5e4c8'
            'SKIP'
            '3ba750eeea63f7a18af70773398de4904c4703d7ee29dc5d970800a99607b914'
            '44e8a692f92cb8a359732c2365160af58bbff17c499c2ae9fe80f0327849a2ef')
b2sums=('e93b2fa5876d35baf258845dc1bc3ff12c79aeb3dd702d04d2c2b54580c5d96a1a07438b28953553a91defe97625af3cf538b810ce748936cf6ba4efa92c9385'
        'SKIP'
        '7126b3136733e22db610e4233efa718f17b8a5b04f5d3c554864adeb0e2057d022bb9f86cbb8a0635f25b1ccaf22168bdbf495d3fb470f6cc2bfeaa53d476ef3'
        'ea22b827192c42c93e24b831182e9ba8a37071a8a698be8cf27f9b9a1460624a65946722c5d34e94c65c21d515c6b3429192517633767c5e13a150b9163facba')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

prepare() {
    cd "${srcdir}"/Sigil-${pkgver}

    # backport fix for crash if no dictionaries are available
    # see: https://github.com/Sigil-Ebook/Sigil/issues/574
    patch -p1 -i ../16babc881ac0f996641cc693a7564c4e08e27266.patch
    patch -p1 -i ../966fe5e8fa16d791d995d4655894f1d328e3c76e.patch
}

build() {
    mkdir -p "${srcdir}"/Sigil-${pkgver}/build
    cd "${srcdir}"/Sigil-${pkgver}/build

    cmake -G "Unix Makefiles" \
        -DUSE_SYSTEM_LIBS=1 \
        -DSYSTEM_LIBS_REQUIRED=1 \
        -DINSTALL_BUNDLED_DICTS=0 \
        -DINSTALL_HICOLOR_ICONS=1 \
        -DMATHJAX_DIR=/usr/share/mathjax2 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
        -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
        -DCMAKE_SKIP_RPATH=ON ..

    make
}

package() {
    cd "${srcdir}"/Sigil-${pkgver}/build

    make install DESTDIR="${pkgdir}"

    # Compile python bytecode
    python -m compileall "${pkgdir}"/usr/share/sigil/{plugin_launchers/python/,python3lib}
    python -O -m compileall "${pkgdir}"/usr/share/sigil/{plugin_launchers/python/,python3lib}
}
