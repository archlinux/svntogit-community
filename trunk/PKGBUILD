# $Id: PKGBUILD 116950 2011-03-26 15:07:29Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
_pkgbasename=keyutils
pkgname=lib32-$_pkgbasename
pkgver=1.5.1
pkgrel=1
pkgdesc="Linux Key Management Utilities (32-bit)"
arch=(x86_64)
url="http://www.kernel.org"
license=('GPL2' 'LGPL2.1')
depends=(lib32-glibc $_pkgbasename)
makedepends=(gcc-multilib)
source=(http://people.redhat.com/~dhowells/$_pkgbasename/$_pkgbasename-$pkgver.tar.bz2)

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  sed -i -e '/CFLAGS/s|:= -g -O2|+=|' Makefile
  sed -i -e 's/^\(USR\)\?LIBDIR\s*:=.*$/\1LIBDIR=\/usr\/lib32/' Makefile
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/{usr/{include,share,bin,sbin},etc,{s,}bin}
}
md5sums=('3ab62455a37a20f73b2d6cc794aaba13')
