# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

_name=flake8-black
pkgname=python-flake8-black
pkgver=0.2.2
pkgrel=1
pkgdesc='Flake8 plugin to call black as a code style validator'
arch=('any')
url=https://github.com/peterjc/flake8-black
license=('MIT')
depends=('flake8' 'python-black' 'python-toml')
makedepends=('python-setuptools')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('5c38e6ce506c61638526622f716736fbc33a95bdcf80fcd182048a714d107601cadf164456b23e754aeb24181a4ad10ad141fbc5309362c101a7ae08dda56458')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  cd tests
  PATH="${PWD%/tests}/temp/usr/bin:$PATH" PYTHONPATH="${PWD%/tests}/temp/$sitepackages" ./run_tests.sh
}

package() {
  cd $_name-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.rst
}

# vim:set ts=2 sw=2 et:
