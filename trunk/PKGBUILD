# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk2hs-buildtools
pkgver=0.13.3.0
pkgrel=1
pkgdesc="Tools to build the Gtk2Hs suite of User Interface libraries."
url="https://hackage.haskell.org/package/gtk2hs-buildtools"
license=('GPL2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-random' 'haskell-hashtables')
makedepends=('alex' 'happy' 'ghc')
provides=('gtk2hs-buildtools')
conflicts=('gtk2hs-buildtools')
replaces=('gtk2hs-buildtools')
source=("https://hackage.haskell.org/packages/archive/gtk2hs-buildtools/${pkgver}/gtk2hs-buildtools-${pkgver}.tar.gz")
sha512sums=('c2184bc8699b74beb92cdc32e3ee86d29f0f4ce79d03475d5ec6a12e5de440029a16b8a621bffc1fff111a8967d7d3f38be8a2e8e51448b3f338ba5df34be9ca')

build() {
    cd "${srcdir}/gtk2hs-buildtools-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/gtk2hs-buildtools-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
}
