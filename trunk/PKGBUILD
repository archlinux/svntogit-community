# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-big-o
pkgver=0.10.2
_commit=82b8bb318f93a8b7664d989ec069973291929823
pkgrel=2
pkgdesc="Empirical estimation of time complexity from execution time"
url="https://github.com/pberkes/big_O"
license=('BSD')
arch=('any')
depends=('python-numpy')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/pberkes/big_O.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cd big_O
  # stablize tests on python 3.10+
  # TODO: doesn't apply
  # git cherry-pick -n 81a3a5483fd7804d95ece8cc8f18a1f840ac7a7b
}

build() {
  cd big_O
  python -m build -nw
}

check() {
  cd big_O
  pytest
}

package() {
  cd big_O
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
