# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=1.1
pkgrel=13
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('x86_64')
url="http://performous.org/"
license=('GPL')
depends=('boost-libs' 'libepoxy' 'libxml++2.6' 'portaudio' 'portmidi' 'librsvg' 'ffmpeg' 'opencv' 'sdl2')
makedepends=('cmake' 'pkgconfig' 'help2man' 'boost' 'mesa')
optdepends=('performous-freesongs: free songs for performous')
source=($pkgname-$pkgver.tar.gz::https://github.com/performous/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('6659aa03960e68d7af65b072e179e42b122d2a39d25229c6daf765146379a115a52e51c1dc2bc69081ea17fe7a22f1d7007b7cf9e09bc32d134c3ad8e3444dd1')

build() {
  cd performous-${pkgver}

  mkdir -p build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd performous-${pkgver}/build

  make DESTDIR="$pkgdir" install
}
