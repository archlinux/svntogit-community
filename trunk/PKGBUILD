# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxcb
pkgname=lib32-$_pkgbasename
pkgver=1.12
pkgrel=2
pkgdesc="X11 client-side library (32-bit)"
arch=(x86_64)
url="http://xcb.freedesktop.org/"
depends=('lib32-libxdmcp' 'lib32-libxau' $_pkgbasename)
makedepends=('pkgconfig' 'libxslt' 'python' 'xorg-util-macros' 'gcc-multilib'
             'autoconf')
license=('custom')
source=(${url}/dist/${_pkgbasename}-${pkgver}.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch
        python-35x-fixes.patch)
sha256sums=('4adfb1b7c67e99bc9c2ccb110b2f175686576d2f792c8a71b9c8b19014057b5b'
            '37debaf892235a516a50a4975e6229538d3b4696b4571a8dea05eed774004aff'
            'e66140ac2270b47e940db82975e2d28d3536ca1bd0eae63bce3b900ed81fa517')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  patch -Np1 -i ../libxcb-1.1-no-pthread-stubs.patch
  patch -Np1 -i ../python-35x-fixes.patch
  autoreconf -vfi
}

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # workaround for FS#49560
  export CFLAGS="${CFLAGS} -O1"

  ./autogen.sh \
	  --prefix=/usr \
	  --enable-xinput \
          --enable-xkb \
	  --libdir=/usr/lib32 \
	  --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
