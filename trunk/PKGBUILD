# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Alan Young <harleypig@gmail.com>

pkgname=perl-sub-exporter-progressive
_pkgname=Sub-Exporter-Progressive
pkgver=0.001011
pkgrel=1
pkgdesc="Only use Sub::Exporter if you need it"
arch=('any')
url="http://search.cpan.org/dist/Sub-Exporter-Progressive"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/F/FR/FREW/$_pkgname-$pkgver.tar.gz)
md5sums=('bb50b3ba1538902b197c04818a84230a')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""  \
      PERL_AUTOINSTALL="--skipdeps"           \
      PERL_MM_OPT="INSTALLDIRS=vendor"        \
      PERL_MB_OPT="--installdirs vendor"      \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/$_pkgname-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir"/$_pkgname-$pkgver

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver

  make DESTDIR="$pkgdir"/ install
}
