# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=cddlib
pkgver=094h
pkgrel=1
pkgdesc="C library implementing Doudble Description Method for convex polyhedra"
arch=('i686' 'x86_64')
url="http://www.ifor.math.ethz.ch/~fukuda/cdd_home/cdd.html"
license=('GPL')
depends=('gmp')
source=("ftp://ftp.ifor.math.ethz.ch/pub/fukuda/cdd/$pkgname-$pkgver.tar.gz" 'cdd_both_reps.c' 'cdd_both_reps.patch')
md5sums=('1467d270860bbcb26d3ebae424690e7c'
         'c80ebe354f615144de14c826cadc3bf6'
         '84c7d505ffd46524ffc8ab2de1c5713d')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/cdd_both_reps.patch
  ln -s "$srcdir"/cdd_both_reps.c src
  ln -s "$srcdir"/cdd_both_reps.c src-gmp
}

build() {
  cd $pkgname-$pkgver

  autoreconf -ifs
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
