# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=sshguard
pkgver=1.6.1
pkgrel=1
pkgdesc="Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with iptables"
arch=('i686' 'x86_64')
url="http://www.sshguard.net/"
license=('GPL')
depends=('iptables')
options=(zipman)
install=sshguard.install
source=("http://downloads.sourceforge.net/sourceforge/sshguard/sshguard-$pkgver.tar.xz"
	sshguard.service
	sshguard-journalctl)
sha256sums=('f431899c20fa2f41fa293605af96ff97d44823b84db41c914ee60da44f1ff6c8'
            '5be8fbec010c719b4bd8885e0d436af157800b74a6c344d6778b305fee9b4101'
            'a8f315d163128d4e8458c1d50c90082581092476937921cba2afb167f51cbe3f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|^DAYNO.*|DAYNO       [0-9]?[1-9]|' src/parser/attack_scanner.l
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin --with-firewall=iptables
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  strings src/sshguard | grep -E "^`which iptables`[[:space:]]" >/dev/null
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 "$srcdir/sshguard.service" "$pkgdir/usr/lib/systemd/system/sshguard.service"
  install -Dm0755 "$srcdir/sshguard-journalctl" "$pkgdir/usr/lib/systemd/scripts/sshguard-journalctl"
}
