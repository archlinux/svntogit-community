# Maintainer: David Runge <dave@sleepmap.de>
_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=7.0
pkgrel=1
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('df26952f4c88c43a7df59cc9b41d794987e4d7c43cf4b656fb2e10734e16b0218ea0dfcc184943f96c39add800fe519ee6b8843b3a0f4d36e9feb59109199df3')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build/lib:$PYTHONPATH"
  py.test test_pytest_rerunfailures.py
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}
