# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Andrew Sun <adsun701 at gmail dot com>
# Contributor: Dmitrij D. Czarkoff <czarkoff at gmail dot com>

pkgname=baresip
pkgver=2.10.0
pkgrel=2
pkgdesc="portable and modular SIP User-Agent with audio and video support"
arch=('x86_64')
url="https://github.com/baresip/baresip"
license=('BSD')
depends=(libre.so librem.so openssl)
optdepends=('libfdk-aac: AAC audio codec'
            'alsa-lib: ALSA audio driver'
            'opencore-amr: Adaptive Multi-Rate (AMR) audio codec'
            'aom: (av1 module}'
            'codec2: CODEC2 low-bitrate speech audio codec'
            'ffmpeg: (avcodec, avformat, h265 modules)'
            'gsm: (gsm module)'
            'gst-plugins-bad: (gst1, gst_video1 modules)'
            'gst-plugins-base: (gst1, gst_video1 modules)'
            'gst-plugins-good: (gst1, gst_video1 modules)'
            'gst-plugins-ugly: (gst1, gst_video1 modules)'
            'gtk3: gtk+ user interface'
            'jack2: JACK Audio Connection Kit audio driver'
            'lame: MPA audio codec'
            'libvpx: (vpx module)'
            'libsndfile: (sndfile module)'
            'libpng: Snapshot video module'
            'libpulse: Pulseaudio audio driver'
            'libx11: (x11, x11grab modules)'
            'mpg123: (rst module)'
            'opus: (opus module)'
            'portaudio: Portaudio audio driver'
            'sdl2: (sdl2 module)'
            'spandsp: (g722, g726,plc modules)'
            'speexdsp: (speex_aec, speex_pp modules)'
            'sndio: sndio audio driver'
            'v4l-utils: (v4l2 module)')
makedepends=(${optdepends[@]/%:*/} python)
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/baresip/baresip/archive/v${pkgver}.tar.gz")
sha256sums=('1bbeda0cb6c0f0480fb67f4fcc389df42384da7da909d9d950538fbbbfb92495')

build() {
  cd ${pkgname}-${pkgver}
  export LFLAGS=$LDFLAGS
  make RELEASE=1 V=1 USE_GST= USE_GST_VIDEO= USE_AVFILTER=1 DESTDIR="/usr" PREFIX="/usr"
}

package() {
  cd ${pkgname}-${pkgver}
  export LFLAGS=$LDFLAGS
  make RELEASE=1 USE_GST= USE_GST_VIDEO= USE_AVFILTER=1 PREFIX="/usr" \
    DESTDIR="$pkgdir/" install
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
