# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnsbmp
pkgver=0.1.4
pkgrel=2
pkgdesc='Decoding library for BMP and ICO image file formats'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsbmp/'
license=('MIT')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('2bcbb3e880e46b94259a294c22b8c7543ddd2ae8910f14d227981a4aec038a78')

build() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname-$pkgver"

  make install \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
