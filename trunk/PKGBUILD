# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-swiftclient
pkgname=(python-swiftclient python2-swiftclient)
pkgver=3.4.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="http://docs.openstack.org/python-swiftclient"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python2-futures'
             'python-requests' 'python2-requests')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-mock' 'python2-mock'
              'python-keystoneauth1' 'python2-keystoneauth1')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-swiftclient/archive/$pkgver.tar.gz")
sha512sums=('a370ee47d96fd40c819575c99b6c911176135b8ee0f3a30fa98abd969df911b41bf0fca83f6d064937c5b145b576b8b4bc336c29278b81761d1d2eabade309e9')

prepare() {
  cp -a python-swiftclient-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-swiftclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py testr

  cd "$srcdir"/python-swiftclient-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-swiftclient() {
  depends=('python-six' 'python-requests')

  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-swiftclient() {
  depends=('python2-six' 'python2-futures' 'python2-requests')

  cd "$srcdir"/python-swiftclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/swift{,2}
  mv "$pkgdir"/usr/share/man/man1/swift{,2}.1
}

# vim:set ts=2 sw=2 et:
