# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=boinctui
pkgver=2.5.2
pkgrel=1
pkgdesc="Curses based fullscreen BOINC manager"
arch=('x86_64')
url="https://sourceforge.net/projects/boinctui/"
license=('GPL')
depends=('expat' 'gcc-libs' 'ncurses' 'openssl')
makedepends=("cmake")
source=("https://sourceforge.net/projects/boinctui/files/${pkgname}_${pkgver}.tar.gz"{,.asc})
sha512sums=('0796a6f1d8f2d68aa35da0472566dfbc7f77b10d2465570a8834e8a3264255d93d002f4ee1aef47cb8efc811578f7e687b40c9942ce021284dcdf7b0e544e5ce'
            'SKIP')
validpgpkeys=('4D2DBBB4F06EC2C977B26BFD80878E59832BEC15')  # Sergey Suslov

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/\\ $/\\/' configure.in
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-gnutls --mandir=/usr/share/man
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
