# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: RaphaÃ«l Doursenaud <rdoursenaud at free dot fr>

pkgname=python-mysql-connector
_pkgname=mysql-connector-python
pkgver=8.0.27
pkgrel=1
pkgdesc="A standardized database driver for Python platforms and development"
arch=('any')
url="https://dev.mysql.com/downloads/connector/python/"
license=('GPL')
depends=('python' 'python-dnspython')
makedepends=('python-setuptools')
source=("https://github.com/mysql/mysql-connector-python/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3ba5beff16e743f7098e211cbde35bea7bc7451cf77c544b60beeebcb2c9724f3afd5ab6ff6f9d9809075f4cf29e42f40cd326be944408942d06d357f6a95a23')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1
}
