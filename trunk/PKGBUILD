# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calculator
pkgver=1.7.0
pkgrel=1
pkgdesc='The Pantheon Calculator'
arch=(x86_64)
url=https://github.com/elementary/calculator
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=ca1cdfdea9ad5908ee254ab394c3a85054279999
source=(pantheon-calculator::git+https://github.com/elementary/calculator.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-calculator

  git describe --tags
}

build() {
  arch-meson pantheon-calculator build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
