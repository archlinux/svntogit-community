# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexander Monakov <amonakov@gmail.com>

pkgname=lib32-primus
pkgver=20151110
pkgrel=4
pkgdesc="Faster OpenGL offloading for Bumblebee (32-bit)"
arch=('x86_64')
url="https://github.com/amonakov/primus"
license=('custom:ISC')
depends=('bumblebee' 'lib32-mesa-libgl')
makedepends=('git' 'gcc-multilib')
source=("git://github.com/amonakov/primus.git#commit=d1afbf6"
        "register_cleanup.patch")
sha512sums=('SKIP'
            'f0b3312e7d20fea2b1dfacc896c951ab30f532561608654dedb1d6c863653b01cc233b386e5a7832c8ed3f9ca3246c6374fd1ebf2da07721edd802e15dc6b0b0')

prepare() {
  cd primus

  patch -Np1 < "$srcdir/register_cleanup.patch"
}

build() {
  cd primus

  export CC="g++ -m32"
  export CXX="g++ -m32"

  LIBDIR=lib32 make
}

package() {
  cd primus

  install -D "lib32/libGL.so.1" "$pkgdir/usr/lib32/primus/libGL.so.1"

  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
