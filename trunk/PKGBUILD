# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=7.3.0
pkgrel=2
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-iniconfig' 'python-packaging' 'python-pluggy')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-argcomplete' 'python-attrs' 'python-hypothesis'
              'python-nose' 'python-requests' 'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'lsof' 'python-decorator' 'python-docutils' 'python-jinja'
              'python-numpy' 'python-pexpect' 'python-pygments'
              'python-pytest-xdist' 'python-twisted')
source=("https://files.pythonhosted.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")
sha256sums=('58ecc27ebf0ea643ebfdf7fb1249335da761a00c9f955bcd922349bcb68ee57d')
b2sums=('49057296154775276cc700c309837d85733c3da362ec8e50e5063fccc0615c54bd502574fed86d9b27d3086da630c13f0f87164150a900ab4ae08ab49ff9e0e5')

# Remove dep on setuptools_scm
prepare() {
  cd pytest-$pkgver
  sed -i "/^\[metadata\]/a version = $pkgver
  /setuptools-scm/d" setup.cfg
  sed -i '/setuptools-scm\|wheel/d
  /\[tool.setuptools_scm\]/,/^$/d' pyproject.toml
}

build() {
  cd pytest-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd pytest-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # https://github.com/pytest-dev/pytest/issues/10042
  test-env/bin/python -m pytest -o xfail_strict=False
}

package() {
  cd pytest-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pytest-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
