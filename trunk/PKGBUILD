# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgname=python-routes
pkgver=2.4.1
pkgrel=4
pkgdesc="Routing recognition and generation tools"
arch=("any")
url="https://pypi.python.org/pypi/Routes"
license=("custom:BSD")
depends=("python-repoze.lru" 'python-six')
makedepends=('python-setuptools' 'python-repoze.lru' 'python-six')
checkdepends=('python-nose' 'python-webob' 'python-coverage')
source=("https://pypi.io/packages/source/R/Routes/Routes-$pkgver.tar.gz")
md5sums=('c058dff6832941dec47e0d0052548ad8')

build() {
  cd Routes-$pkgver
  python setup.py build
}

check() {
  cd Routes-$pkgver
  nosetests3
}

package() {
  cd Routes-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
