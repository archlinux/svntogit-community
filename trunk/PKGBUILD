# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=0.4.0
pkgrel=3
pkgdesc='Fast and neat VNC server library'
arch=(x86_64)
url=https://github.com/any1/neatvnc
license=(custom:ISC)
depends=(
  glibc
  gnutls
  libaml.so
  libpixman-1.so
  libturbojpeg.so
  zlib
)
makedepends=(
  git
  libdrm
  meson
  ninja
)
provides=(libneatvnc.so)
_tag=b1d32694d0a310e36da1cf84420c827bbf665755
source=(git+https://github.com/any1/neatvnc.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd neatvnc
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
