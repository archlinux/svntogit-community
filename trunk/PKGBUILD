# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.25
pkgrel=3
pkgdesc='Tool for monitoring webpages for updates'
arch=('any')
url=https://thp.io/2008/urlwatch/
license=('BSD')
depends=('python-appdirs' 'python-cssselect' 'python-keyring' 'python-lxml'
         'python-minidb' 'python-requests' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-docutils' 'python-pycodestyle' 'python-pytesseract'
              'python-pytest')
optdepends=('lynx: for one of the html2text filter methods'
            'python-beautifulsoup4: for the beautify filter and one of the html2text filter methods'
            'python-cssbeautifier: for the beautify filter'
            'python-html2text: for one of the html2text filter methods'
            'python-jq: for the jq filter'
            'python-jsbeautifier: for the beautify filter'
            'python-pdftotext: for the pdf2text filter'
            'python-pyppeteer: for the browser job'
            'python-pytesseract: for the ocr filter')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('6802297d3318286e7f3d36b9a4567a2fb09b0ae779d4b76811dd29a7281c1f8a')
b2sums=('85f76e849495f5457f43ccd37035aae84c6ae4c8649005e617a6a585bf3b73d30914f8c7a89c0fb9bb04cc07a8797d77be07807e8d7c64976355749417b39e40')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/COPYING \
    "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
