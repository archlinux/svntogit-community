# Maintainer: Anatol Pomozov
# Contributor: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>

pkgname=bpftrace
pkgver=0.12.0
pkgrel=1
pkgdesc='High-level tracing language for Linux eBPF'
arch=('x86_64')
url='https://github.com/iovisor/bpftrace'
license=('Apache')
depends=('libelf' 'zlib' 'llvm-libs' 'clang' 'bcc' 'libbpf')
makedepends=('cmake' 'llvm' 'git' 'linux-headers' 'ninja')
options=(!strip)
source=("https://github.com/iovisor/bpftrace/archive/v$pkgver/bpftrace-$pkgver.tar.gz")
sha512sums=('ebbdd909763ba142551d79e06c0fa0ec84245ffc4386b33144dce7e67d14e4fe0e81d0003adc7c9194d82c6b41fc49f16f9960db0dd78a620a69f83d17e8811f')

build() {
  cd bpftrace-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -G Ninja -B build
  ninja -C build
}

package() {
  cd bpftrace-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
}
