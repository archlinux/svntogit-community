# $Id$
# Maintainer: Kyle Keen < keenerd at gmail >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=('bpython' 'bpython2')
pkgver=0.13.1
pkgrel=1
pkgdesc='Fancy interface to the Python interpreter'
arch=('any')
url='http://bpython-interpreter.org/'
license=('MIT')
makedepends=('python-distribute' 'python2-distribute')
source=(http://bpython-interpreter.org/releases/bpython-${pkgver}.tar.gz)
md5sums=('47b2ae18ee39d519752218b938024a85')

build() {
  cp -r bpython-$pkgver bpython2-$pkgver

  cd bpython-$pkgver
  python setup.py build

  cd "$srcdir/bpython2-$pkgver"
  python2 setup.py build
}

package_bpython() {
  depends=('python-pygments')
  optdepends=('python-urwid: for bpython-urwid')

  cd bpython-$pkgver

  python setup.py install --root="$pkgdir" --optimize=0

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bpython/LICENSE"

  # pygtk isn't available for python3 (fs#33705)
  rm "$pkgdir/usr/bin/bpython-gtk"
}

package_bpython2() {
  depends=('python2-pygments')
  optdepends=('pygtk: for bpython2-gtk'
              'python2-urwid: for bpython2-urwid')

  cd bpython2-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=0

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bpython2/LICENSE"

  # conflicting files with bpython
  pushd "$pkgdir/usr/bin/"
  mv bpython bpython2
  mv bpython-gtk bpython2-gtk
  mv bpython-urwid bpython2-urwid
  mv bpython-curtsies bpython2-curtsies
  popd
  pushd "$pkgdir/usr/share/applications/"
  mv bpython.desktop bpython2.desktop

  # fix the .desktop file for the rename
  sed -i 's/bpython/&2/' bpython2.desktop
  popd
}
