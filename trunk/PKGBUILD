# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us

pkgname=acpid
pkgver=2.0.16
pkgrel=2
pkgdesc='A daemon for delivering ACPI power management events with netlink support'
arch=('i686' 'x86_64')
url='http://tedfelix.com/linux/acpid-netlink.html'
license=('GPL')
depends=('bash')
optdepends=('perl: use perl based examples')
replaces=('acpid2')
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything' 'etc/conf.d/acpid')
source=("http://www.tedfelix.com/linux/$pkgname-$pkgver.tar.xz"
        'acpid.rc'
        'acpid.conf'
        'acpid.socket'
        'acpid.service'
        'anything'
        'handler.sh')
md5sums=('d59fc02c9c34f0d5c137495302e2c074'
         '077475c75fbdd9de75f9a397130ccd91'
         '929c6d2e91295c22ed9ec6212d7eabef'
         'ee6cb99e50e580c50331a73045412ae9'
         '953e26477c5d4f5f013d6861c04b4af6'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '74ee3c33714ff5cb44f30c9a0dceeef0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  popd

  # default config
  install -Dm644 anything "$pkgdir/etc/acpi/events/anything"
  install -Dm755 handler.sh "$pkgdir/etc/acpi/handler.sh"

  # initscripts
  install -Dm755 acpid.rc "$pkgdir/etc/rc.d/acpid"
  install -Dm644 acpid.conf "$pkgdir/etc/conf.d/acpid"
  install -Dm644 acpid.socket "$pkgdir/usr/lib/systemd/system/acpid.socket"
  install -Dm644 acpid.service "$pkgdir/usr/lib/systemd/system/acpid.service"

  # fix acpid rights
  chmod 755 "$pkgdir/usr/sbin/acpid"
}

# vim:set ts=2 sw=2 ft=sh et:
