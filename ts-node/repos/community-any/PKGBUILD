# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

pkgname=ts-node
pkgver=8.10.1
pkgrel=1
pkgdesc="TypeScript execution and REPL for node.js, with source map support"
arch=('any')
url="https://github.com/TypeStrong/ts-node"
license=('MIT')
depends=('nodejs' 'typescript')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('6dd373d4be1e907889ba5e921ed656b35ba310a1112671ece07c4deeb8d3cb254e15fdc7689eac2ea7ba68f1bad974f3001ffade92918798d34962f40fb6ecbf')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}

# vim:set ts=2 sw=2 et:
