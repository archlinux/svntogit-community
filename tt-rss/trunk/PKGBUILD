# Maintainer:  Anatol Pomozov <anatol@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>

pkgname=tt-rss
pkgver=r10198.6d8f2221b
pkgrel=1
epoch=2
pkgdesc='Web-based news feed (RSS/Atom) aggregator'
arch=(any)
url='https://tt-rss.org'
license=(GPL)
backup=(etc/webapps/tt-rss/config.php)
depends=(php7 php7-intl)
install=tt-rss.install
optdepends=('mysql'
            'postgresql'
            'php7-gd: for coloured feed badges')
makedepends=('git')
_commit=6d8f2221b82b6a31becbeed8baf5e05ad9e053fe
# periodic releases are over https://tt-rss.org/forum/viewtopic.php?f=10&t=3262
source=(git+https://git.tt-rss.org/fox/tt-rss.git#commit=$_commit
        service
	php7.patch)
sha1sums=('SKIP'
          '6975508c8cbe86152489fc6f1887c1d0d7df19e0'
          '6e6a6350bf645f2d84785aa1c8e613be0cca337e')

pkgver() {
  cd tt-rss
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd tt-rss
  patch -p1 < ../php7.patch # https://bugs.archlinux.org/task/69462#comment196184
}

package() {
  cd tt-rss
  _instdir="$pkgdir"/usr/share/webapps/$pkgname

  install -d "$_instdir"
  cp -ra * "$_instdir/"

  echo $pkgver > "$_instdir/version_static.txt"

  install -Dm640 -g 33 config.php-dist "$pkgdir"/etc/webapps/tt-rss/config.php
  ln -s /etc/webapps/tt-rss/config.php "$_instdir"/config.php

  install -d "$pkgdir"/var/lib/tt-rss
  mv "$_instdir"/{lock,feed-icons,cache} "$pkgdir"/var/lib/tt-rss
  ln -s /var/lib/tt-rss/lock "$_instdir"/lock
  ln -s /var/lib/tt-rss/feed-icons "$_instdir"/feed-icons
  ln -s /var/lib/tt-rss/cache "$_instdir"/cache
  chown -R 33:33 "$pkgdir"/var/lib/tt-rss
  chmod -R 777 "$pkgdir"/var/lib/tt-rss/*

  install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/tt-rss.service
}
