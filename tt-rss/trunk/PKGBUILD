# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>

pkgname=tt-rss
pkgver=1.15.3
pkgrel=1
pkgdesc='Web-based news feed (RSS/Atom) aggregator'
arch=('any')
url='http://tt-rss.org/redmine/'
license=('GPL')
depends=('php')
optdepends=('mysql' 'postgresql')
install=tt-rss.install
source=($pkgname-$pkgver.tar.gz::https://github.com/gothfox/Tiny-Tiny-RSS/archive/$pkgver.tar.gz
        service)
md5sums=('08b56d8f83fcd2a9abb9a331b9864b11'
         'cb9900413dd53d44a5ed1116c962dbcb')

package() {
  cd Tiny-Tiny-RSS-$pkgver
  _instdir="$pkgdir"/usr/share/webapps/$pkgname

  install -d "$_instdir"
  cp -ra * "$_instdir/"
  rm -rf "$_instdir"/debian

  install -d "$pkgdir"/etc/webapps/tt-rss
  install -gm640 -g http config.php-dist "$pkgdir"/etc/webapps/tt-rss/config.php-dist
  ln -s /etc/webapps/tt-rss/config.php "$_instdir"/config.php

  install -d "$pkgdir"/var/lib/tt-rss
  mv "$_instdir"/{lock,feed-icons,cache} "$pkgdir"/var/lib/tt-rss
  ln -s /var/lib/tt-rss/lock "$_instdir"/lock
  ln -s /var/lib/tt-rss/feed-icons "$_instdir"/feed-icons
  ln -s /var/lib/tt-rss/cache "$_instdir"/cache
  chown -R 33:33 "$pkgdir"/var/lib/tt-rss
  chmod -R 777 "$pkgdir"/var/lib/tt-rss/*

  install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/tt-rss.service
}
