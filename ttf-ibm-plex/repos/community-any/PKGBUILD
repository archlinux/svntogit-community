# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>
# Contributor: Matt Spaulding <matt at mattops dot io>
# Contributor: pyjano <pyjano at protonmail dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Adrià Arrufat <swiftscythe@gmail.com>
# Contributor: Utkarsh Singh <utkarsh190601@gmail.com>


pkgname=ttf-ibm-plex
pkgver=6.0.0
pkgrel=1
pkgdesc="IBM Plex Mono, Sans, and Serif"
arch=('any')
url='https://github.com/IBM/plex'
license=('custom:OFL')
source=("${url}/releases/download/v${pkgver}/TrueType.zip")
provides=('ttf-font')
sha256sums=('16253500de140321175bb94e65fd227c6b12efc050eb298c20322dbf3713a8e0')

conflicts=('ibm-plex-fonts')

package() {
    cd TrueType
    find -name *.ttf ! -path "*/unhinted/*" \
         -exec install -Dm644 {} -t "${pkgdir}/usr/share/fonts/TTF" \;

    # All the other licenses are the same as the one here...
    install -Dm644 IBM-Plex-Mono/license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
