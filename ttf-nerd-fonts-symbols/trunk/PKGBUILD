# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname=('ttf-nerd-fonts-symbols-1000-em' 'ttf-nerd-fonts-symbols-1000-em-mono'
         'ttf-nerd-fonts-symbols-2048-em' 'ttf-nerd-fonts-symbols-2048-em-mono'
         'ttf-nerd-fonts-symbols-common')
pkgbase=ttf-nerd-fonts-symbols
pkgver=2.2.2
pkgrel=3
pkgdesc="High number of extra glyphs from popular 'iconic fonts'"
arch=('any')
url=https://github.com/ryanoasis/nerd-fonts
license=('MIT')
provides=('nerd-fonts' 'ttf-font-nerd')
install=$pkgbase.install
source=("Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-1000-em%20Nerd%20Font%20Complete.ttf"
        "Symbols-1000-em_Nerd_Font_Complete_Mono-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-1000-em%20Nerd%20Font%20Complete%20Mono.ttf"
        "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete.ttf"
        "Symbols-2048-em_Nerd_Font_Complete_Mono-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete%20Mono.ttf"
        "10-nerd-font-symbols-$pkgver.conf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/10-nerd-font-symbols.conf"
        "Nerd_Fonts-$pkgver-LICENSE::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/LICENSE")
b2sums=('f784ec1f770dd3fd796d253cb7332914ed649cc2832320c61d0c8a7ca6ecd07a205ac69c4162416632634d179bdfac0886f90b972273f7b6ecb03e21044a63da'
        'bbc63fa7760753832511d1e6ca3e6f8d840ba075944a5a81c72932be3c010742f7aa3ccffdce53fab0cbde52a39a7864e4e9d32f616beaf8e79eb1a8c4e843c8'
        '4b747286be4e3939d10cd4fabed6bd031cc62bd59b10127bced85f1bd7f2ce0db7749322054b220fd2d1950be1ab1d0fb5a8ec1b4cc7c96b949d904cc10d039b'
        '77a12675e8357d8f323e6d116d2f50a735c2254dc266ef1f3c77f8310b5f00790e7713634f64e5b955d8078c64f0138ce33fd40c490bdd6e50d9280e4f186cdb'
        'd73e826f433d42428bc467a734d3fd6280876b2fe23a1384730b2bc0fd4521a6d372654f835350d28b346e3cad8e7d6fa47dd11be8d3fae6e61d4707e7fd397b'
        '386dfcb5ead367bfc44fc55f7e94548c03bdb24ca088ab56f9609e7cdaa3eab9c56b548966feaf7312da606b645e9af35556fc212132733efcdbcc17397c808a')

_package() {
  depends=('ttf-nerd-fonts-symbols-common')

  install -d "$pkgdir"/usr/share/licenses/"$1"
  ln -s /usr/share/licenses/ttf-nerd-fonts-symbols-common/LICENSE \
    "$pkgdir"/usr/share/licenses/"$1"/LICENSE

  case "$1" in
    ttf-nerd-fonts-symbols-1000-em)
      pkgdesc+=' (1000-em)'
      conflicts=('ttf-nerd-fonts-symbols-mono' 'ttf-nerd-fonts-symbols-2048-em')
      replaces=('ttf-nerd-fonts-symbols-mono')

      install -Dm644 "Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-1000-em Nerd Font Complete.ttf"
    ;;
    ttf-nerd-fonts-symbols-1000-em-mono)
      pkgdesc+=' (1000-em monospace)'
      conflicts=('ttf-nerd-fonts-symbols-2048-em-mono')

      install -Dm644 "Symbols-1000-em_Nerd_Font_Complete_Mono-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-1000-em Nerd Font Complete Mono.ttf"
    ;;
    ttf-nerd-fonts-symbols-2048-em)
      pkgdesc+=' (2048-em)'
      conflicts=('ttf-nerd-fonts-symbols' 'ttf-nerd-fonts-symbols-1000-em')
      replaces=('ttf-nerd-fonts-symbols')

      install -Dm644 "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-2048-em Nerd Font Complete.ttf"
    ;;
    ttf-nerd-fonts-symbols-2048-em-mono)
      pkgdesc+=' (2048-em monospace)'
      conflicts=('ttf-nerd-fonts-symbols-1000-em-mono')

      install -Dm644 "Symbols-2048-em_Nerd_Font_Complete_Mono-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-2048-em Nerd Font Complete Mono.ttf"
    ;;
  esac
}

package_ttf-nerd-fonts-symbols-common() {
  install -Dm644 10-nerd-font-symbols-$pkgver.conf \
    "$pkgdir"/usr/share/fontconfig/conf.avail/10-nerd-font-symbols.conf
  install -Dm644 Nerd_Fonts-$pkgver-LICENSE \
    "$pkgdir"/usr/share/licenses/ttf-nerd-fonts-symbols-common/LICENSE
}

for _pkgname in "${pkgname[@]/*-common}"; do
  eval "package_$_pkgname() { _package $_pkgname; }"
done
