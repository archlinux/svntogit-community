# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname=('ttf-nerd-fonts-symbols-1000-em' 'ttf-nerd-fonts-symbols-1000-em-mono'
         'ttf-nerd-fonts-symbols-2048-em' 'ttf-nerd-fonts-symbols-2048-em-mono'
         'ttf-nerd-fonts-symbols-common')
pkgbase=ttf-nerd-fonts-symbols
pkgver=2.3.1
pkgrel=1
pkgdesc="High number of extra glyphs from popular 'iconic fonts'"
arch=('any')
url=https://github.com/ryanoasis/nerd-fonts
license=('MIT')
install=$pkgbase.install
source=("Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-1000-em%20Nerd%20Font%20Complete.ttf"
        "Symbols-1000-em_Nerd_Font_Complete_Mono-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-1000-em%20Nerd%20Font%20Complete%20Mono.ttf"
        "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete.ttf"
        "Symbols-2048-em_Nerd_Font_Complete_Mono-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete%20Mono.ttf"
        "10-nerd-font-symbols-$pkgver.conf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/10-nerd-font-symbols.conf"
        "Nerd_Fonts-$pkgver-LICENSE::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/LICENSE")
b2sums=('e94f4b2321cf73c3198471d3323fc2c1e7a69109941723204e8e18bc5cfd58401ee08b38466bcba84747bfd202f2707f1d4ad2fdb3fc3042f5217a4c3d946482'
        '934de323b6dfc44b186d11e4ebb788d21befabb2be280209c1446da7a2516ed8386615ef6d02f62c29e9285e1d43a651b2f71af51d45ae91a08888bd648c1fed'
        '2f43fea91b3127584335ce452548ebf1a8efac978741392fa7fed4fe4fea45c46749ef4393d42e18f103481ae04ea26c7234ee283ab8b3cca3a00637d66f247d'
        'cca3de79b0f96bf6648139f4215d8681e4b24914fe1288203459b38b2b9559ed23e4e83634be1857f4a972ea7d25cfdcb2da23d59dd4ed7c206449bc8c87ca0f'
        '9d3733af4fad8faf8ac0f0274f2116ca128909eea14df5e7141c5eebc8746da824b9dd3e6f9db852cb7069e9586171d882a49241f9a5a39347134d268c2d8431'
        'cf89e6d30dfd223c0b67941c1641d3290f0ff9bdb49b618a5938079c885ff74e61c555119fc5e0d9111bd37dbb21401f9cd6dc8a29bf3d6f97fc3a4827b6c1e3')

_package() {
  depends=('ttf-nerd-fonts-symbols-common')
  provides=('ttf-font-nerd')

  install -d "$pkgdir"/usr/share/licenses/"$1"
  ln -s /usr/share/licenses/ttf-nerd-fonts-symbols-common/LICENSE \
    "$pkgdir"/usr/share/licenses/"$1"/LICENSE

  case "$1" in
    ttf-nerd-fonts-symbols-1000-em)
      pkgdesc+=' (1000-em)'
      conflicts=('ttf-nerd-fonts-symbols-mono' 'ttf-nerd-fonts-symbols-2048-em')
      replaces=('ttf-nerd-fonts-symbols-mono')

      install -Dm644 "Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-1000-em Nerd Font Complete.ttf"
    ;;
    ttf-nerd-fonts-symbols-1000-em-mono)
      pkgdesc+=' (1000-em monospace)'
      conflicts=('ttf-nerd-fonts-symbols-2048-em-mono')

      install -Dm644 "Symbols-1000-em_Nerd_Font_Complete_Mono-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-1000-em Nerd Font Complete Mono.ttf"
    ;;
    ttf-nerd-fonts-symbols-2048-em)
      pkgdesc+=' (2048-em)'
      conflicts=('ttf-nerd-fonts-symbols' 'ttf-nerd-fonts-symbols-1000-em')
      replaces=('ttf-nerd-fonts-symbols')

      install -Dm644 "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-2048-em Nerd Font Complete.ttf"
    ;;
    ttf-nerd-fonts-symbols-2048-em-mono)
      pkgdesc+=' (2048-em monospace)'
      conflicts=('ttf-nerd-fonts-symbols-1000-em-mono')

      install -Dm644 "Symbols-2048-em_Nerd_Font_Complete_Mono-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-2048-em Nerd Font Complete Mono.ttf"
    ;;
  esac
}

package_ttf-nerd-fonts-symbols-common() {
  pkgdesc+=' (common files)'
  install -Dm644 10-nerd-font-symbols-$pkgver.conf \
    "$pkgdir"/usr/share/fontconfig/conf.avail/10-nerd-font-symbols.conf
  install -Dm644 Nerd_Fonts-$pkgver-LICENSE \
    "$pkgdir"/usr/share/licenses/ttf-nerd-fonts-symbols-common/LICENSE
}

for _pkgname in "${pkgname[@]/*-common}"; do
  eval "package_$_pkgname() { _package $_pkgname; }"
done
