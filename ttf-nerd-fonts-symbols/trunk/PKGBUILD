# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname=('ttf-nerd-fonts-symbols' 'ttf-nerd-fonts-symbols-mono')
pkgbase=ttf-nerd-fonts-symbols
pkgver=2.1.0+36+gd0bf73a1
pkgrel=5
pkgdesc="High number of extra glyphs from popular 'iconic fonts'"
arch=('any')
url=https://github.com/ryanoasis/nerd-fonts
license=('MIT')
provides=('ttf-font-nerd')
install=$pkgbase.install
source=("Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/d0bf73a19c3459aab39734a05159e2694911d7d6/src/glyphs/Symbols-1000-em%20Nerd%20Font%20Complete.ttf"
        "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/d0bf73a19c3459aab39734a05159e2694911d7d6/src/glyphs/Symbols-2048-em%20Nerd%20Font%20Complete.ttf"
        "10-nerd-font-symbols-2.1.0.conf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/2.1.0/10-nerd-font-symbols.conf"
        "Nerd_Fonts-LICENSE-2.1.0::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/2.1.0/LICENSE")
b2sums=('9aabb03695dc93a229a88460ad748595674d6f8f583758e29cef1599d59c7e7429882af0689e09df45d997d5162183bb9ba211f8447bd958211f787b026efe76'
        '53abd4d97372628850a6b361c41a209cd55dc2b434dd61189f1abcdbb9e44bbcd891ddc3a538b11d03637fe1236bda511ce12b0495010dc0bb09e1b4fd841a11'
        'a3472125cfbb2dd4de47c8899b38ba9566ea5eb9bbb30d136866c4016d4e575d7588e0d1cf126cef05ece0525a6ae0caf8ef4a265f46e4b571954541c06ebb67'
        'ece38b21e41c31a10260b2a99ea23661a945c11b5edbcb625b0e317be59050cc3bc04d72d4b36f94d89c263fd742d31b55f449057ad54a7e912cd1b472a38a84')

package_ttf-nerd-fonts-symbols() {
  pkgdesc+=' (2048-em)'
  conflicts=('ttf-nerd-fonts-symbols-mono')
  install -Dm644 "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf" \
    "$pkgdir"/usr/share/fonts/TTF/'Symbols-2048-em Nerd Font Complete.ttf'
  install -Dm644 10-nerd-font-symbols-2.1.0.conf \
    "$pkgdir"/usr/share/fontconfig/conf.avail/10-nerd-font-symbols.conf
  install -Dm644 Nerd_Fonts-LICENSE-2.1.0 \
    "$pkgdir"/usr/share/licenses/${pkgname[0]}/LICENSE
}

package_ttf-nerd-fonts-symbols-mono() {
  pkgdesc+=' (1000-em)'
  conflicts=('ttf-nerd-fonts-symbols')
  install -Dm644 "Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf" \
    "$pkgdir"/usr/share/fonts/TTF/'Symbols-1000-em Nerd Font Complete.ttf'
  install -Dm644 10-nerd-font-symbols-2.1.0.conf \
    "$pkgdir"/usr/share/fontconfig/conf.avail/10-nerd-font-symbols.conf
  install -Dm644 Nerd_Fonts-LICENSE-2.1.0 \
    "$pkgdir"/usr/share/licenses/${pkgname[1]}/LICENSE
}

# vim:set ts=2 sw=2 et:
