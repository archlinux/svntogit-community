# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=ttf-ubuntu-font-family
pkgver=0.83
pkgrel=3
pkgdesc="Ubuntu font family"
arch=('any')
url="http://font.ubuntu.com/"
license=('custom:Ubuntu Font Licence 1.0')
depends=('fontconfig' 'xorg-font-utils')
provides=('ttf-font')
backup=(etc/fonts/conf.avail/81-ubuntu.conf)
source=(http://font.ubuntu.com/download/ubuntu-font-family-$pkgver.zip
        81-ubuntu.conf)
sha256sums=('456d7d42797febd0d7d4cf1b782a2e03680bb4a5ee43cc9d06bda172bac05b42'
            '16e29b1b8d13edddb588e4470f6da4c15870a9a27dd25eb4553c2c575d6dc14e')

package() {
	cd "$srcdir"/ubuntu-font-family-$pkgver
	install -dm755 "$pkgdir"/usr/share/fonts/TTF "$pkgdir"/etc/fonts/conf.d
	install -m644 Ubuntu{,Mono}-*.ttf "$pkgdir"/usr/share/fonts/TTF/
	install -Dm644 LICENCE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
	install -Dm644 "$srcdir"/81-ubuntu.conf "$pkgdir"/etc/fonts/conf.avail/81-ubuntu.conf
	ln -s ../conf.avail/81-ubuntu.conf "$pkgdir"/etc/fonts/conf.d/81-ubuntu.conf
}
