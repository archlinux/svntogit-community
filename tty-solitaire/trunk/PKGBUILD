# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tty-solitaire
pkgver=1.3.1
pkgrel=1
pkgdesc='ncurses-based klondike solitaire game'
arch=('x86_64')
url=https://github.com/mpereira/tty-solitaire
license=('MIT')
depends=('ncurses')
source=("$url/archive/v$pkgver/tty-solitaire-$pkgver.tar.gz")
b2sums=('03cddc9420a3072ee2ea7e03a5fdf413255eb7687c76e7bf2f824d9a0d4bcf43d683cf0c4b3798c7116d99d53f8e5b0239f4a84b38bcd00f637507c9327304d0')

build() {
  cd tty-solitaire-$pkgver
  make
}

check() {
  cd tty-solitaire-$pkgver
  make test
}

package() {
  cd tty-solitaire-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
