# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=ttyd
pkgver=1.7.2
pkgrel=3
pkgdesc='Share your terminal over the web'
arch=('x86_64')
url='https://tsl0922.github.io/ttyd/'
license=('MIT')
depends=(
  'zlib'
  'libwebsockets'
  'json-c'
  'libuv'
  'openssl'
)
makedepends=('git' 'cmake')
_commit='020e2f3ded508d5fca95196a647fde09f05758b3'
source=("$pkgname::git+https://github.com/tsl0922/ttyd#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -W no-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
