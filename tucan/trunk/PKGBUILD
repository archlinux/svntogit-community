# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Raul Pons Lopez <raulponslopez+aur {at} gmail {dot} com>

pkgname=tucan
pkgver=0.3.10
pkgrel=3
pkgdesc='Download and upload manager for hosting sites'
arch=('any')
url='http://tucaneando.com/'
license=('GPL3')
depends=('python2-pillow' 'pygtk' 'tesseract' 'librsvg')
source=("http://build-tucan-doc.googlecode.com/files/tucan-$pkgver.tar.gz")
sha256sums=('41c4e68de970eb4388b41fa75b7c38142b989483318490abf8af377303c99e5c')

prepare() {
    cd "$pkgname-$pkgver"

    # python2 fix
	  sed -i '0,/on/s//on2/' tucan.py
    # PIL/Pillow fix
	  sed -i 's/import Image/from PIL import Image/g' core/tesseract.py
}

package() {
    cd "$pkgname-$pkgver"

    make DESTDIR="$pkgdir/usr" install
    ln -sf /usr/share/tucan/tucan.py "$pkgdir/usr/bin/tucan"
}

# vim:set ts=2 sw=2 et:
