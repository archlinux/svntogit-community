# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=tuna
pkgver=0.13.1
pkgrel=7
epoch=1
pkgdesc="Thread and IRQ affinity setting GUI and cmd line tool"
url="https://rt.wiki.kernel.org/index.php/Tuna"
arch=('any')
license=('GPL')
groups=('realtime')
depends=('python2-ethtool' 'python2-linux-procfs' 'python2-schedutils'
'python2-numpy' 'python2-matplotlib' 'pygtk' 'libglade')
optdepends=('python2-inet_diag: Show network sockets in use by threads')
source=("https://git.kernel.org/pub/scm/utils/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('cb182f45ac0f55bc392d00df69178e7c19721156a7a88e5ed5a3061192aad7520ffe3f200d3bb95ad2d283615e004af30c380b7aee826079491b3744a73fb314')

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i '1s/python/python2/' {tuna-cmd,oscilloscope-cmd}.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"

  install -vDm 755 "${pkgname}-cmd.py" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 755 oscilloscope-cmd.py "${pkgdir}/usr/bin/oscilloscope"
  install -vDm 644 "etc/${pkgname}/example.conf" \
    "${pkgdir}/etc/${pkgname}/example.conf"
  install -vDm 644 "etc/${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"
  install -vDm 644 "docs/oscilloscope+${pkgname}.pdf" \
    "${pkgdir}/usr/share/${pkgname}/docs/oscilloscope+${pkgname}.pdf"
  install -t "${pkgdir}/usr/share/${pkgname}/help/kthreads" \
    -vDm 644 help/kthreads/*
  install -vDm 644 "${pkgname}/${pkgname}_gui.glade" \
    "${pkgdir}/usr/share/${pkgname}/${pkgname}_gui.glade"
  install -vDm 644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -vDm 644 "org.${pkgname}.policy" \
    "${pkgdir}/usr/share/polkit-1/actions/org.${pkgname}.policy"
  install -vDm 644 "docs/${pkgname}.8" \
    "${pkgdir}/usr/share/man/man8/${pkgname}.8"
  install -vDm 644 ChangeLog \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:

