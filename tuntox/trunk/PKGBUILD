# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tuntox
pkgver=0.0.10
pkgrel=1
pkgdesc='Tunnel TCP connections over the Tox protocol'
arch=('x86_64')
url='https://github.com/gjedeer/tuntox'
license=('GPL3')
depends=('toxcore' 'libevent')
makedepends=('cscope' 'git')
source=("https://github.com/gjedeer/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        #"https://github.com/gjedeer/${pkgname}/releases/download/${pkgver}/${pkgver}.tar.gz.asc"
       )
sha512sums=('25536fc8fbccc6e25aab42131a9973422a0e3313669d5fe5aa7317162250aa96d05d42b8eea239f4e621856ff2ba3a2d886c35ff77b0f5cdb82fd4263794bf82')
validpgpkeys=('11C1B15A5D5DD662E469928AEBDA6B974ED3D2B7')  # GDR!_ <gdr@gdr.name>

prepare() {
  cd $pkgname-$pkgver
  #sed -i "s|DEPS=libsodium toxcore|DEPS=libsodium libtoxcore|" Makefile
  sed -i "/^CFLAGS=/d" Makefile # use CFLAGS from makepkg
  sed -i "/^LDFLAGS=/d" Makefile # use LDFLAGS from makepkg
  sed -i "s/^DSO_LDFLAGS=/DSO_LDFLAGS=$LDFLAGS/" Makefile # use DSO_LDFLAGS from makepkg
  cat Makefile
}

build() {
  cd $pkgname-$pkgver

  make tuntox_nostatic  # use dynamic linking
  mv tuntox_nostatic tuntox
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 tuntox "$pkgdir/usr/bin/tuntox"
  install -Dm755 scripts/tokssh "$pkgdir/usr/bin/tokssh"
  install -Dm644 scripts/tuntox.service "$pkgdir/usr/lib/systemd/system/tuntox.service"
}
