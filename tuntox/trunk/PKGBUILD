# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tuntox
pkgver=0.0.10.1
pkgrel=1
pkgdesc='Tunnel TCP connections over the Tox protocol'
arch=('x86_64')
url='https://github.com/gjedeer/tuntox'
license=('GPL3')
depends=('toxcore' 'libevent')
makedepends=('cscope' 'git')
source=("https://github.com/gjedeer/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        #"https://github.com/gjedeer/${pkgname}/releases/download/${pkgver}/${pkgver}.tar.gz.asc"
       )
sha512sums=('6f5a01df207df62c3c3371418702ab96ba5b9a9ea8cb4499aaf96bf68f88861f6b5b73769366949de21e9c4a330ce1f02bd4fd222ef24b608a49a71a6e542861')
validpgpkeys=('11C1B15A5D5DD662E469928AEBDA6B974ED3D2B7')  # GDR!_ <gdr@gdr.name>

prepare() {
  cd $pkgname-$pkgver
  #sed -i "s|DEPS=libsodium toxcore|DEPS=libsodium libtoxcore|" Makefile
  sed -i "/^CFLAGS=/d" Makefile # use CFLAGS from makepkg
  sed -i "/^LDFLAGS=/d" Makefile # use LDFLAGS from makepkg
  sed -i "s/^DSO_LDFLAGS=/DSO_LDFLAGS=$LDFLAGS/" Makefile # use DSO_LDFLAGS from makepkg
  cat Makefile
}

build() {
  cd $pkgname-$pkgver

  make tuntox_nostatic  # use dynamic linking
  mv tuntox_nostatic tuntox
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 tuntox "$pkgdir/usr/bin/tuntox"
  install -Dm755 scripts/tokssh "$pkgdir/usr/bin/tokssh"
  install -Dm644 scripts/tuntox.service "$pkgdir/usr/lib/systemd/system/tuntox.service"
}
