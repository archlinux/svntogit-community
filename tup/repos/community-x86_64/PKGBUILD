# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Arvid Norlander <anmaster A_T tele2 d_o_t (ccTLD for Sweden, which is se)>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=tup
pkgver=0.7.1
pkgrel=1
pkgdesc='A fast, file-based build system'
arch=(i686 x86_64)
url='http://gittup.org/tup/index.html'
license=(GPL2)
depends=(fuse)
install=tup.install
source=(https://github.com/gittup/tup/archive/v$pkgver.zip)
sha256sums=('9c9107bc1ea2ee18fdd706fd870deec155c45655b1199492d1a14ccbc498a918')

build() {
  cd tup-$pkgver
  # We can't run tup itself here as it requires fuse, but fuse is not available
  # in clean chroot environment. Use bootstrapped version of tup.
  TUP_LABEL=$pkgver ./build.sh
}

check() {
  cd tup-$pkgver/test
  #./test.sh
}

package() {
  cd tup-$pkgver

  install -d "$pkgdir"/usr/bin "$pkgdir"/usr/share/man/man1 "$pkgdir"/usr/share/vim/vimfiles/{syntax,ftdetect}
  install -m755 -t "$pkgdir"/usr/bin build/tup
  install -m644 -t "$pkgdir"/usr/share/man/man1 tup.1
  install -m644 -t "$pkgdir"/usr/share/vim/vimfiles/syntax contrib/syntax/tup.vim
  echo 'au BufNewFile,BufRead Tupfile,*.tup setf tup' > "$pkgdir"/usr/share/vim/vimfiles/ftdetect/tup.vim
}
