# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=tuxcmd-modules
pkgver=0.6.70
pkgrel=4
pkgdesc='Modules for Tux Commander'
arch=('x86_64')
url='http://tuxcmd.sourceforge.net/'
license=('GPL')
depends=('tuxcmd')
makedepends=('sharutils')
optdepends=('gvfs: for gvfs module')
options=('!docs' '!emptydirs')
source=("http://downloads.sourceforge.net/project/tuxcmd/tuxcmd/tuxcmd-dev-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        tuxcmd-module-gvfs_include-glib.patch
		tuxcmd-module-libarchive_libarchive-3.0.0a.patch
		tuxcmd-module-ziparchive-gcc-4.7-2.patch
		tuxcmd-module-ziparchive-gcc-4.7.patch)
md5sums=('e3e2471f6f245d3191e9a845c73518ff'
         '8c82d2c004d81d9a849a13a3e949dae2'
         'b57bfb177a0a519f015627d42ca6dd86'
         'fdfabb07e54b7de580d2a56b10c1d38b'
         '12aacf065f993bcb2129dd55b20a9ef0')

prepare() {
	cd ${pkgname}-${pkgver}
	patch -p1 -i ../tuxcmd-module-gvfs_include-glib.patch
	patch -p1 -i ../tuxcmd-module-libarchive_libarchive-3.0.0a.patch
	patch -p1 -i ../tuxcmd-module-ziparchive-gcc-4.7.patch
	patch -p1 -i ../tuxcmd-module-ziparchive-gcc-4.7-2.patch


	sed -i 's|$(INSTALL) \(.*\) $(DESTDIR)/lib$(LIB_SUFFIX)/tuxcmd/|$(INSTALL) -D \1 $(DESTDIR)/lib/tuxcmd/\1|' */Makefile
}

build() {
	cd ${pkgname}-${pkgver}

	make
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR=${pkgdir}/usr install
}
