# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=tuxcmd-modules
pkgver=0.6.70
pkgrel=2
pkgdesc='Modules for Tux Commander'
arch=('i686' 'x86_64')
url='http://tuxcmd.sourceforge.net/'
license=('GPL')
depends=('tuxcmd')
makedepends=('sharutils')
optdepends=('gvfs: for gvfs module')
options=('!docs' '!emptydirs')
source=("http://downloads.sourceforge.net/project/tuxcmd/tuxcmd/tuxcmd-dev-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e3e2471f6f245d3191e9a845c73518ff')

build() {
	cd ${pkgname}-${pkgver}

	# fix Makefiles
	sed -i 's|$(INSTALL) \(.*\) $(DESTDIR)/lib$(LIB_SUFFIX)/tuxcmd/|$(INSTALL) -D \1 $(DESTDIR)/lib/tuxcmd/\1|' */Makefile

	make -j1
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR=${pkgdir}/usr install
}
