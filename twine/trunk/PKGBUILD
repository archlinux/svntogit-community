# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Warrick <aur@chriswarrick.com>

pkgbase=twine
pkgname=('twine' 'python2-twine')
pkgver=1.5.0
pkgrel=3
pkgdesc='Collection of utilities for interacting with PyPI'
arch=('any')
url='https://pypi.python.org/pypi/twine'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests-toolbelt' 'python2-requests-toolbelt'
             'python-pkginfo' 'python2-pkginfo')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pretend' 'python2-pretend')
options=('!emptydirs')
source=("https://pypi.python.org/packages/source/${pkgbase:0:1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
md5sums=('12948245aeb59acf32f663e1d81fed34')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  cp -a "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/${pkgbase}-${pkgver}-py2"
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {(
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py ptr

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py ptr
  ) || warning "Tests failed"
}

package_twine() {
  depends=('python-setuptools' 'python-requests-toolbelt' 'python-pkginfo')
  provides=('python-twine')
  conflicts=('python-twine')
  replaces=('python-twine')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  ln -s ${pkgbase} "${pkgdir}/usr/bin/${pkgbase}3"
}

package_python2-twine() {
  depends=('python2-setuptools' 'python2-requests-toolbelt' 'python2-pkginfo')

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  mv "${pkgdir}/usr/bin/${pkgbase}" "${pkgdir}/usr/bin/${pkgbase}2"
}

# vim:set ts=2 sw=2 et:
