# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Ila√Ø Deutel

pkgname=typst
pkgver=0.2.0
pkgrel=1
epoch=1
pkgdesc='A markup-based typesetting system for the sciences'
arch=(x86_64)
url="https://$pkgname.app"
_url="https://github.com/$pkgname/$pkgname"
license=(Apache)
depends=(gcc-libs
         glibc)
makedepends=(cargo
             git)
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('f52e1cab191034354b3adaa97c27cbf16ed99486dc302b014bf211a8b6a7e964')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	local hash=$(gunzip < "$_archive.tar.gz" | git get-tar-commit-id)
	cd "$_archive"
	export TYPST_VERSION="$pkgver (${hash::8})"
	export GEN_ARTIFACTS=./
	cargo build -p typst-cli --frozen --release --all-features
}

check() {
	cd "$_archive"
	cargo test --workspace --frozen --all-features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" "cli/$pkgname"*.1
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "cli/_$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "cli/$pkgname.fish"
	install -Dm0644 "cli/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
