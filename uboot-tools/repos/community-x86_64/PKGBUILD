# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2022.10
pkgrel=2
pkgdesc='U-Boot bootloader utility tools'
arch=(x86_64)
url='https://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
makedepends=(python swig)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig})
validpgpkeys=('1A3C7F70E08FAB1707809BBF147C39FF9634B72C')
options=(!lto)
sha1sums=('7b4cab68c38c3f449f80eabb63183a271405f6b4'
          'SKIP')

build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all KBUILD_HOSTLDFLAGS=${LDFLAGS}
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -D u-boot-$pkgver/doc/mkimage.1 "$pkgdir"/usr/share/man/man1/mkimage.1
}
