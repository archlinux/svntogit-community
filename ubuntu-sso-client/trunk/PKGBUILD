# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntu-sso-client
pkgver=13.10
pkgrel=1
pkgdesc="Desktop service to allow applications to sign into Ubuntu services via SSO"
arch=('any')
url="https://launchpad.net/ubuntu-sso-client"
license=('GPL')
depends=('python2-dbus' 'python2-pyqt' 'python2-dirspec' 'python2-httplib2' 'python2-oauthlib' 'python2-pyopenssl' 'python2-pillow' 'twisted' 'gnome-keyring' 'gsettings-desktop-schemas')
makedepends=('python2-distutils-extra')
source=(https://launchpad.net/$pkgname/stable-13-10/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('2937163edb8ba06038fa0036c5c34c99')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root=$pkgdir/ --optimize=1

  # Remove unneeded files
  rm "$pkgdir"/usr/share/ubuntu-sso-client/*.ttf
}
