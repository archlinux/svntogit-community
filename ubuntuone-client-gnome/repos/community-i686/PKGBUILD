# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-client-gnome
pkgver=3.0.2
pkgrel=1
pkgdesc="Some plug-ins, extensions, and data for integrating Ubuntu One features in some core parts of GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntuone-client-gnome"
license=('GPL')
depends=('nautilus' 'evolution-data-server' 'ubuntuone-client')
makedepends=('intltool' 'gnome-settings-daemon')
optdepends=('gnome-settings-daemon: shows a warning when Ubuntu One runs out of space')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/ubuntuone-client-gnome/stable-3-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120513.tar.gz
        fix-build.patch)
md5sums=('59a871d767e832b37e95e1aca2ee4374'
         'cabbf043f9be0ea8df4c9a4b020cd2c3'
         '8d34fed775be23d1743dda079a293b78')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/fix-build.patch"

  # Install language files
  echo 'am ar ast az be bg bn bs ca ca@valencia cs da de el en_AU en_GB eo es et eu fi fo fr gd gl gu he hi hr hu id it ja kk km ko ku lt lv ms my nb nl nn oc pa pl pt pt_BR ro ru shn si sk sl sq sr sv ta te th tr ug uk vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
