# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-client-gnome
pkgver=4.2.0
pkgrel=2
pkgdesc="Some plug-ins, extensions, and data for integrating Ubuntu One features in some core parts of GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntuone-client-gnome"
license=('GPL')
depends=('nautilus' 'gconf' 'ubuntuone-client')
makedepends=('intltool' 'gnome-settings-daemon')
options=('!libtool')
install=$pkgname.install
source=(https://launchpad.net/ubuntuone-client-gnome/stable-4-2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        disable-gsd-plugin.patch)
md5sums=('e2f5e4ef6f4eca6ffcf838b3aa46c3ef'
         'e2b1b677844c3b61530f3653808b6644')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Disable gsd plugin, as it's not compatible with gnome-settings-daemon 3.8
  patch -Np1 -i "$srcdir/disable-gsd-plugin.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
