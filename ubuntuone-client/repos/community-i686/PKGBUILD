# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-client
pkgver=4.2.0
pkgrel=1
pkgdesc="Ubuntu One helps you store, sync and share files between your computers"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntuone-client"
license=('GPL' 'CCPL:cc-by-sa')
depends=('dbus-glib' 'python2-configglue' 'python2-distribute' 'python2-gobject' 'libnotify' 'python2-pyinotify' 'ubuntu-sso-client' 'python2-ubuntuone-storageprotocol' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'imake' 'python2-distutils-extra')
options=('!libtool')
install=$pkgname.install
source=(https://launchpad.net/ubuntuone-client/stable-4-2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        https://launchpad.net/ubuntuone-client-data/stable-4-2/$pkgver/+download/$pkgname-data-$pkgver.tar.gz
        fix-notify-hint.patch)
md5sums=('37d2de5c6e795e75f447682ee1c3bacc'
         'd3c445a9957a80471a84f323f24e8f84'
         'c38cb043e3241f12610ed6eb9ae0f087')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*

  # Workaround for crash in notification-daemon
  # https://bugzilla.gnome.org/show_bug.cgi?id=665166
  patch -Np1 -i "$srcdir/fix-notify-hint.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install data
  cd "$srcdir/$pkgname-data-$pkgver"
  python2 setup.py install --root=$pkgdir/ --optimize=1

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/{etc/apport,usr/share/apport}
}
