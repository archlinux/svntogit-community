# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-client
pkgver=13.10
pkgrel=1
pkgdesc="Ubuntu One helps you store, sync and share files between your computers"
arch=('any')
url="https://launchpad.net/ubuntuone-client"
license=('GPL' 'CCPL:cc-by-sa')
depends=('python2-configglue' 'python2-setuptools' 'python2-gobject' 'libnotify' 'python2-pyinotify' 'ubuntu-sso-client' 'python2-ubuntuone-storageprotocol' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'imake' 'python2-distutils-extra')
install=$pkgname.install
source=(https://launchpad.net/ubuntuone-client/stable-13-10/$pkgver/+download/$pkgname-$pkgver.tar.gz
        https://launchpad.net/ubuntuone-client-data/stable-13-10/$pkgver/+download/$pkgname-data-$pkgver.tar.gz
        fix-notify-hint.patch)
md5sums=('eff7ba76da463182e7622c68fd4f8fb6'
         'c7da0cc8ec856f015c502577fa2dab0f'
         'c38cb043e3241f12610ed6eb9ae0f087')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Workaround for crash in notification-daemon
  # https://bugzilla.gnome.org/show_bug.cgi?id=665166
  patch -Np1 -i "$srcdir/fix-notify-hint.patch"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/* contrib/*.py
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build

  cd "$srcdir/$pkgname-data-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root=$pkgdir/ --optimize=1

  cd "$srcdir/$pkgname-data-$pkgver"
  python2 setup.py install --root=$pkgdir/ --optimize=1

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/{etc/apport,usr/share/apport}
}
