# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-client
pkgver=3.0.2
pkgrel=1
pkgdesc="Ubuntu One helps you store, sync and share files between your computers"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntuone-client"
license=('GPL')
depends=('dbus-glib' 'python2-configglue' 'python2-distribute' 'libnotify' 'python2-gobject2' 'python2-pyinotify' 'python-simplejson' 'ubuntu-sso-client' 'python2-ubuntuone-storageprotocol' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'imake' 'gobject-introspection' 'vala')
optdepends=('ubuntu-sso-client-qt: required for first-time log in or'
            'ubuntu-sso-client-gtk: required for first-time log in'
            'python2-pyqt: proxy support')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/ubuntuone-client/stable-3-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120513.tar.gz)
md5sums=('5251fad8d316e8730188078239a4dd73'
         '6f4603f11ea21d030bfaef34d3567f68')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*

  # Install language files
  echo 'ace af am an ar ast az be bg bn bo br bs ca ca@valencia ckb crh cv cy cs csb da de el en_AU en_CA en_GB eo es et eu fa fi fo fr fy gd gl gu he hi hr hu hy id is it ja jv kk km kn ko ku ky lt lv ml mr ms my nb nds nl nn oc pl pt pt_BR ro ru sd shn si sk sl sq sr sv ta te th tr ug uk ur uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  # Fix desktop file
  sed -i 's/ubuntuone-installer/ubuntuone-control-panel-qt/' \
    ubuntuone/platform/linux/{messaging,launcher}.py

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/{etc/apport,usr/share/apport}
}
