# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-control-panel
pkgver=3.0.2
pkgrel=1
pkgdesc="Graphical user interface that allows the users to manage their Ubuntu One subscription and preferences"
arch=('any')
url="https://launchpad.net/ubuntuone-control-panel"
license=('GPL')
depends=('python2-dirspec' 'ubuntuone-client' 'ubuntu-sso-client-qt')
makedepends=('python2-distutils-extra')
source=(http://launchpad.net/ubuntuone-control-panel/stable-3-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120513.tar.gz
        ubuntuone-control-panel-qt.desktop)
md5sums=('1d99edb17aa397a4aa1ffdd20b339e95'
         'c2b21611ab448baf3664ecf49ce584f7'
         '503ce0ce7e75e856b11b99425e6c614a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*
  sed -i 's|ubuntuone-installer.desktop|ubuntuone-control-panel-qt.desktop|' ubuntuone-control-panel.in

  # Install language files
  echo 'am ar ast be bg bn bs ca ca@valencia cs csb da de el en_AU en_GB eo es et eu fi fo fr fy gd gl gu he hi hr hu hy id it ja kk km ko ku ky lt lv mr ms my nb ne nl nn oc pa pl pt pt_BR ro ru shn si sk sl sq sr sv ta te th tr ug uk vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # Remove unneeded files
  rm "$pkgdir"/usr/share/ubuntuone-control-panel/*.ttf

  # Install desktop file and icon
  install -dm755 "$pkgdir/usr/share/pixmaps"
  ln -s ../ubuntuone-control-panel/icon.png "$pkgdir/usr/share/pixmaps/ubuntuone.png"
  install -Dm644 "$srcdir/ubuntuone-control-panel-qt.desktop" "$pkgdir/usr/share/applications/ubuntuone-control-panel-qt.desktop"

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/usr/share/{apport,doc}
}
