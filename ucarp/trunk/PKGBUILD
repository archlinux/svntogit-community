# $Id$ 
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=ucarp
pkgver=1.5.2
pkgrel=3
pkgdesc='Portable implementation of the CARP protocol'
arch=('i686' 'x86_64')
url='http://www.ucarp.org/project/ucarp'
license=('GPL2')
depends=('libpcap')
optdepends=('iproute2')
backup=('etc/conf.d/ucarp')
source=("http://download.pureftpd.org/pub/ucarp/ucarp-$pkgver.tar.gz"
        'ucarp.rc'
        'ucarp.conf'
        'ucarp.service'
        'ucarp.8'
        '01-fix-getopt.patch'
        '02-fix-downscript-on-error.c')
md5sums=('e3caa733316a32c09e5d3817617e9145'
         '33b87863932e99450e6a37d2b0a899f1'
         '79c01b9ba580d2431997efb9df59cf5d'
         '582d59cd969b6447a710f58bbaf28846'
         '05b64a5daeb7aab45a2a59e0f4d83c8f'
         '3f20699aaf2ef8139dcd337f1e7f06ab'
         '09d26233c37956cf08e629554a91b8cd')

build() {
  cd $pkgname-$pkgver
  patch -N -p1 -i ../01-fix-getopt.patch
  patch -N -p1 -i ../02-fix-downscript-on-error.c

  ./configure --prefix=/usr
  make

  # fix iproute2 path
  sed -i 's|/sbin|/usr/sbin|g' examples/linux/vip-down.sh
  sed -i 's|/sbin|/usr/sbin|g' examples/linux/vip-up.sh
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # install examples files
  install -D -m 644 examples/linux/vip-down.sh \
    "$pkgdir/usr/share/doc/$pkgname/examples/vip-down.sh"
  install -D -m 644 examples/linux/vip-up.sh \
    "$pkgdir/usr/share/doc/$pkgname/examples/vip-up.sh"

  # install README
  install -D -m 644 README "$pkgdir/usr/share/doc/$pkgname/README"

  # add initscripts
  cd ..
  install -D -m 755 $pkgname.rc "${pkgdir}/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "${pkgdir}/etc/conf.d/$pkgname"
  install -D -m 644 $pkgname.service \
    "${pkgdir}/usr/lib/systemd/system/$pkgname.service"

  # add manpage
  install -D -m 644 $pkgname.8 "${pkgdir}/usr/share/man/man8/$pkgname.8"

}

# vim:set ts=2 sw=2 ft=sh et:
