# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: eric <eric@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=ude
pkgver=0.2.11
pkgrel=2
pkgdesc="The ultimate window manager for UDE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libjpeg' 'libxmu' 'libxpm' 'bash')
makedepends=('gendesk')
url="http://udeproject.sourceforge.net/index.shtml"
source=(http://downloads.sourceforge.net/udeproject/uwm-$pkgver.tar.gz)
md5sums=('d4952a7edffd30ac07f32fce244c86d7')

build() {
  cd "$srcdir"
  gendesk -n --pkgname=uwm --name=UWM --pkgdesc="$pkgdesc"

  cd "uwm-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/uwm-$pkgver"

  make prefix="$pkgdir/usr" install
  install -Dm644 "$srcdir/uwm.desktop" "$pkgdir/usr/share/xsessions/uwm.desktop"
  mv "$pkgdir/usr/doc" "$pkgdir/usr/share/"
}
