# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: lucck <lucck@ep.com.pl>
# Contributor: aksr <aksr at t-com dot me>

pkgname=udftools
pkgver=2.0
pkgrel=2
pkgdesc='Linux tools for UDF filesystems and DVD/CD-R(W) drives'
url='https://github.com/pali/udftools'
arch=('x86_64')
license=('GPL')
depends=('ncurses' 'readline')
validpgpkeys=('B856B21074A8AE9B692B80858BF0C93D03E44352') # Pali Roh√°r <pali.rohar@gmail.com>
source=("https://github.com/pali/$pkgname/releases/download/$pkgver/$pkgname-${pkgver}.tar.gz"{,.asc})
sha256sums=('67fe428d452901215cfad8049d250540c97114b1a20dd63277b91c2c4fae8292'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|/lib/|$(libdir)/|' pktsetup/Makefile.am
  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

