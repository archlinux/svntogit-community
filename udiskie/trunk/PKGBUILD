# $Id: PKGBUILD 73684 2012-07-14 05:00:28Z dwallace $
# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Byron Clark <byron@theclarkfamily.name>

pkgname=udiskie
pkgver=1.4.10
pkgrel=1
pkgdesc="Removable disk automounter using udisks"
arch=("any")
url="https://pypi.python.org/pypi/udiskie"
license=("MIT")
depends=("udisks2" "python-gobject" "python-setuptools" "python-yaml" "python-docopt" "gettext" "libnotify" "hicolor-icon-theme")
makedepends=("asciidoc")
optdepends=("udisks: optional for use with old backend")
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://codeload.github.com/coldfix/$pkgname/tar.gz/$pkgver")
md5sums=('b032523110e01469cf2920f1483e76a5')

package() {
	cd "$srcdir/$pkgname-$pkgver"
	export LC_ALL=en_US.utf8

	python setup.py install --root="$pkgdir" --optimize=1

	make -C doc
	install -m 0644 -D "doc/$pkgname.8" "$pkgdir/usr/share/man/man8/$pkgname.8"
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
