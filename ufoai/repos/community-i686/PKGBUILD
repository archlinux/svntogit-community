# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ufoai
pkgver=2.5.0
pkgrel=1
pkgdesc="In UFO: Alien Invasion you fight aliens trying to capture Earth"
arch=('i686' 'x86_64')
url="http://ufoai.ninex.info/"
license=('GPL')
depends=('libjpeg' 'libgl' 'libpng' 'curl' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'xvidcore' 'ufoai-data' 'glu')
makedepends=('mesa' 'git')
source=("ufoai-${pkgver}::git://git.code.sf.net/p/ufoai/code#tag=ufoai_${pkgver}")
md5sums=('SKIP')

build() {
  cd ufoai-${pkgver}

  ./configure --prefix=/usr --datadir=/usr/share/ufoai --enable-release --localedir=/usr/share/ufoai/base/i18n/
  make #LDFLAGS="-lm"
  make lang
}

package() {
  cd ufoai-${pkgver}

  install -Dm755 ufo "$pkgdir"/usr/bin/ufo
  install -Dm755 ufoded "$pkgdir"/usr/bin/ufoded
  install -Dm755 ufo2map "$pkgdir"/usr/bin/ufo2map
  install -Dm755 ufomodel "$pkgdir"/usr/bin/ufomodel

  mkdir -p "$pkgdir"/usr/share/ufoai
  cp -r base "$pkgdir"/usr/share/ufoai/

  install -Dm644 debian/ufoai.desktop "$pkgdir"/usr/share/applications/ufoai.desktop
  install -Dm644 debian/ufoai-safe.desktop "$pkgdir"/usr/share/applications/ufoai-safe.desktop
  install -Dm644 debian/ufoded.desktop "$pkgdir"/usr/share/applications/ufoded.desktop

  sed -i 's|/usr/games/||' "$pkgdir"/usr/share/applications/*

  install -Dm644 debian/ufoai.xpm "$pkgdir"/usr/share/pixmaps/ufoai.xpm
  install -Dm644 debian/ufoded.xpm "$pkgdir"/usr/share/pixmaps/ufoded.xpm
}

# vim: sw=2:ts=2 et:
