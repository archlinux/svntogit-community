# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: Sorin Ionescu <sorin.ionescu@gmail.com>

pkgname=ufw
pkgver=0.31.1
pkgrel=2
pkgdesc="Uncomplicated and easy to use CLI tool for managing a netfilter firewall"
arch=('any')
url="https://launchpad.net/ufw"
license=('GPL3')
depends=('iptables' 'python2')
backup=('etc/ufw/before.rules'
        'etc/ufw/before6.rules'
        'etc/ufw/after.rules'
        'etc/ufw/after6.rules'
        'etc/ufw/ufw.conf'
        'etc/ufw/user.rules'
        'etc/ufw/sysctl.conf'
        'etc/default/ufw'
        'usr/lib/ufw/user.rules'
        'usr/lib/ufw/user6.rules')
source=(http://launchpad.net/$pkgname/0.31/$pkgver/+download/${pkgname}-${pkgver}.tar.gz
        rc.d
        service)
md5sums=('74b49d4d06e26359a55bf4ff576833a7'
         '3d32964a23e0d6b3b7002272154bfad3'
         'd125e918ce9a832c528e6982cf6f6d98')

package() {
  cd $srcdir/$pkgname-$pkgver
  
  # FS#28769 - move from /lib to /usr/lib
  sed -e 's|/lib|/usr/lib|' -i setup.py

  python2 setup.py install --root=$pkgdir

  install -Dm755 $srcdir/rc.d $pkgdir/etc/rc.d/$pkgname
  install -Dm644 $srcdir/service $pkgdir/usr/lib/systemd/system/ufw.service

  install -D -m755 $pkgdir/usr/sbin/$pkgname $pkgdir/usr/bin/$pkgname
  rm -rf $pkgdir/usr/sbin

  chmod 644 $pkgdir/etc/ufw/*.rules $pkgdir/usr/lib/ufw/*.rules
}
