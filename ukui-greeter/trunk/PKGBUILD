# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-greeter
pkgver=3.0.2
pkgrel=3
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-greeter"
groups=('ukui')
depends=('imlib2' 'liblightdm-qt5' 'libxrandr' 'libxtst' 'opencv' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools' 'openmpi' 'pugixml' 'jsoncpp' 'fmt' 'vtk' 'glew' 'hdf5')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-greeter/archive/v$pkgver.tar.gz")
sha512sums=('68cfc828181f12209bddb4c41942ee380e8ca0818d2db47810119b7e41adaba090acacd3bb1f85521f13f31830c2e77ee6c5d1ae7d3b26801f4116b16bbee3f8')

prepare() {
  cd ukui-greeter-$pkgver
  sed -i 's|target.path = /usr/sbin/|target.path = /usr/bin/|' ukui-greeter/ukui-greeter.pro
}

build() {
  cd ukui-greeter-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_LFLAGS="${LDFLAGS}"
  make
}

package() {
  cd ukui-greeter-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
