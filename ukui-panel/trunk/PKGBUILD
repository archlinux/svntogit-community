# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-panel
pkgver=2.0.7
pkgrel=1
pkgdesc="Launcher and docking facility for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-panel"
groups=('ukui')
provides=('ukui-indicators')
replaces=('ukui-indicators')
depends=('dconf' 'gsettings-qt' 'kwindowsystem' 'libqtxdg' 'peony' 'qt5-webkit' 'ukui-biometric-auth'
         'ukui-menu' 'ukui-menus')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-panel/archive/v$pkgver.tar.gz")
sha512sums=('b0e6e829fb0cdb721c42b548c64cf92dad3a1c025f60864192c1a53f0f92fddcb71ced849f8ff96c23cf4d543af559768ce200a182060f8b8bdbc02e8de12594')

prepare() {
  mkdir -p build
  sed -i '1i #include <QPainterPath>' $pkgname-$pkgver/ukui-flash-disk/ejectInterface.cpp
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
