# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-screensaver
pkgver=2.1.1
pkgrel=3
pkgdesc="Screensaver for UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-screensaver"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'libxtst' 'qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-screensaver/archive/v$pkgver.tar.gz"
        ukui-screensaver-autostart.patch::https://github.com/ukui/ukui-screensaver/pull/9.patch)
sha512sums=('52e0b184c775fefceb44cf9766879b5cdf8ab1289fddde42d61f0b704d9dc87360a06eb28a5a53c63cce32a230abd83d56a85c0dd338138342eaaab6a7d076cb'
            '13459b31ae919dd1ac4e46ea1ba37043003bdeadb6149b312194d5904de0f4c7a17e593b966d9f942b490aae07a967e41bd8a96db134234f81b32708c0ec4e51')

prepare() {
  mkdir -p build
  patch -d $pkgname-$pkgver -p1 -i ../ukui-screensaver-autostart.patch

  # https://github.com/ukui/ukui-screensaver/issues/65
  cp /etc/pam.d/login $pkgname-$pkgver/data/ukui-screensaver-qt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
