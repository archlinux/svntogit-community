# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gleidson Echeli <gleidson dot echeli at gmail dot com>

pkgname=umlet
pkgver=13.3
pkgrel=1
pkgdesc="Free UML Tool for Fast UML Diagrams (stand-alone version)"
arch=('any')
url="http://www.umlet.com"
license=('GPL')
depends=('java-runtime')
makedepends=('unzip' 'patch')
source=("http://www.umlet.com/umlet_${pkgver//./_}/umlet_${pkgver}.zip"
	'umlet.desktop'
	'UMLet.sh.diff'
	'slowrendering.patch')
md5sums=('5bb2dbb382bdb0501ff587db80877a52'
         '4efcfeea2f54b849bc86e3f1905449be'
         'ef9620bd90f0e3bba7133629cf5885c8'
         '0063953819236bd54ddb216e5dcda75c')

prepare() {
  cd ${srcdir}/Umlet
  patch -Np1 -i ../UMLet.sh.diff
  patch -Np1 -i ../slowrendering.patch
  rm -f umlet.sh.orig
  rm -f Umlet.exe
}

package() {
  cd ${srcdir}/Umlet
  mkdir -p ${pkgdir}/usr/bin
  ln -s /usr/share/umlet/umlet.sh ${pkgdir}/usr/bin/umlet
  install -d -m755 ${pkgdir}/usr/share
  mv ${srcdir}/Umlet ${pkgdir}/usr/share/umlet
  install -D -m644 ${srcdir}/umlet.desktop $pkgdir/usr/share/applications/umlet.desktop
}
