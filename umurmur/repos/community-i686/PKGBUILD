# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.15
pkgrel=1
pkgdesc='Minimalistic Mumble server'
arch=('i686' 'x86_64')
url='http://code.google.com/p/umurmur/'
license=('custom')
depends=('polarssl' 'libconfig' 'protobuf-c')
install=$pkgname.install
backup=('etc/umurmur/umurmur.conf')
source=(https://github.com/fatbob313/umurmur/archive/$pkgver.tar.gz
        umurmur.service)
md5sums=('e29d831a8a4cbc465668ae7b8b4cd020'
         'd9d556e4ffa77e193fb40ce508804720')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-ssl=polarssl
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 umurmur.conf.example "$pkgdir"/etc/umurmur/umurmur.conf
  install -Dm644 ../umurmur.service "$pkgdir"/usr/lib/systemd/system/umurmur.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
