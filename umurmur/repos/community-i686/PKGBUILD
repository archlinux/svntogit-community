# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.16_a
pkgrel=3
pkgdesc='Minimalistic Mumble server'
arch=('i686' 'x86_64')
url='https://github.com/umurmur/umurmur'
license=('custom')
depends=('mbedtls' 'libconfig' 'protobuf-c')
makedepends=('cmake')
install=$pkgname.install
backup=('etc/umurmur/umurmur.conf')
source=($pkgname-$pkgver.tar.gz::$url/archive/${pkgver/_/}.tar.gz
	mbedtls2.patch
        umurmur.service)
md5sums=('061aa71eb059eb00d2b123ec9200b405'
         '9d1dbd7b9827609dfdc02389336b9f83'
         'd9d556e4ffa77e193fb40ce508804720')

prepare() {
  cd $pkgname-${pkgver/_/}
  patch -p1 <$srcdir/mbedtls2.patch
}

build() {
  cd $pkgname-${pkgver/_/}
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DSSL=mbedtls
  make
}

package() {
  cd $pkgname-${pkgver/_/}
  make DESTDIR="$pkgdir" install
  install -dm0755 $pkgdir/etc/umurmur/
  mv $pkgdir/usr/etc/umurmur.conf $pkgdir/etc/umurmur/
  rmdir $pkgdir/usr/etc
  install -Dm644 ../umurmur.service "$pkgdir"/usr/lib/systemd/system/umurmur.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
