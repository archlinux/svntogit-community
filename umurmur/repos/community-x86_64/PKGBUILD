# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.17
pkgrel=11
pkgdesc='Minimalistic Mumble server'
url='https://github.com/umurmur/umurmur'
arch=('x86_64')
license=('custom')
depends=('mbedtls' 'libconfig' 'protobuf-c')
makedepends=('cmake')
backup=('etc/umurmur/umurmur.conf')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/umurmur/umurmur/archive/${pkgver}.tar.gz
        umurmur.sysusers
        umurmur.service)
sha256sums=('e77b7b6616768f4a1c07442afe49a772692f667b00c23cc85909d4dd0ce206d2'
            '0fc68df464ee51a431d934d068aed0be5f8c5e64d0bd29848f97532d39f8c310'
            '3575e69da5ad1fc8541ef37af90cdd245e3202980589ffd26d124fd9e785b738')
sha512sums=('a496a51fd7815ad117f5aee17bb78cbd319c584ad60ab8aebbfd8ddf7b1760f443f2337bc74be1e0d5af17d3c3df2ae6c9060eca576cf1e6ed4c6cb0825e9c15'
            'd84950a32ab8a2e84f5fe333cd2894e52aba624531644d106c982aa4ff04271d318543398fa7f48c719f26338679fa971bb5332472e9040ac9aa8a9b4a1f2832'
            'ceb42e7ed4385d16c9d9f30312cc5a57b0ee0ba0ec423e7e2f3e9abc1956506e8817e9a3cb2257112434d568594f4e495f1661af0f89451f802f924790df8ffa')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSSL=mbedtls
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 "${pkgdir}/usr/etc/umurmur.conf" -t "${pkgdir}/etc/umurmur"
  rm -r "${pkgdir}/usr/etc"
  install -Dm 644 "${srcdir}/umurmur.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm 644 "${srcdir}/umurmur.sysusers" "${pkgdir}/usr/lib/sysusers.d/umurmur.conf"
}

# vim: ts=2 sw=2 et:
