# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: N30N <archlinux@alunamation.com>

pkgname=unarchiver
pkgver=1.10.7
_UniversalDetector_pkgver=1.1
pkgrel=7
pkgdesc="unar and lsar: Objective-C tools for uncompressing archive files"
arch=(x86_64)
url="https://github.com/MacPaw/XADMaster"
license=('LGPL2.1')
depends=(gnustep-base openssl bzip2 icu gcc-libs zlib wavpack)
makedepends=(gcc-objc gnustep-make)
source=("XADMaster-${pkgver}.tar.gz::https://github.com/MacPaw/XADMaster/archive/v${pkgver}.tar.gz"
        "UniversalDetector-${_UniversalDetector_pkgver}.tar.gz::https://github.com/MacPaw/universal-detector/archive/${_UniversalDetector_pkgver}.tar.gz"
        "native_obj_exceptions.patch")
sha1sums=('edeb1078e36339869a4c48bfc4988016d0f55029'
          '609aee66fe866f086d8c9eeb66f552d243927a85'
          '9672c57d4a1705fb4f324ac909c3faa6ccfe33df')

prepare() {
  # The project requires UniversalDetector next to the source dir. See the project's README for more info about the build requirements.
  ln -s universal-detector-${_UniversalDetector_pkgver} UniversalDetector

  cd "$srcdir/XADMaster-${pkgver}"
  patch < ../native_obj_exceptions.patch
}

build() {
  cd "$srcdir/XADMaster-${pkgver}"

  . /usr/share/GNUstep/Makefiles/GNUstep.sh
  make -f Makefile.linux
}

package() {
  cd "$srcdir/XADMaster-${pkgver}"
  install -d "$pkgdir/usr/bin/"
  install -m755 unar lsar "$pkgdir/usr/bin/"

  install -d "$pkgdir/usr/share/man/man1"
  install -m644 Extra/{lsar.1,unar.1} "$pkgdir/usr/share/man/man1/"
  install -d "$pkgdir/usr/share/bash-completion/completions/"
  install -m644 Extra/unar.bash_completion "$pkgdir/usr/share/bash-completion/completions/unar"
  install -m644 Extra/lsar.bash_completion "$pkgdir/usr/share/bash-completion/completions/lsar"
}
