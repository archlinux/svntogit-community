# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=uncrustify
pkgver=0.74.0
pkgrel=1
pkgdesc="A source code beautifier"
arch=(x86_64)
url="http://uncrustify.sourceforge.net/"
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake python)
source=("https://github.com/uncrustify/uncrustify/archive/uncrustify-$pkgver.tar.gz")
sha256sums=('b7d24e256e7f919aa96289ac8167ac98340df7faa2d34b60d2242dc54700caaa')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" PREFIX=/usr install

  cd ../$pkgname-$pkgname-$pkgver
  install -d -m755 "$pkgdir"/usr/share/uncrustify
  install -m644 etc/*.cfg "$pkgdir"/usr/share/uncrustify
}
