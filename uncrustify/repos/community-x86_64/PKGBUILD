# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=uncrustify
pkgver=0.73.0
pkgrel=1
pkgdesc="A source code beautifier"
arch=(x86_64)
url="http://uncrustify.sourceforge.net/"
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake python)
source=("https://github.com/uncrustify/uncrustify/archive/uncrustify-$pkgver.tar.gz")
sha256sums=('2df0326ba8c413d675b796e051d89a318b7c9cccebc993d66466e2e7fd970672')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" PREFIX=/usr install

  cd ../$pkgname-$pkgname-$pkgver
  install -d -m755 "$pkgdir"/usr/share/uncrustify
  install -m644 etc/*.cfg "$pkgdir"/usr/share/uncrustify
}
