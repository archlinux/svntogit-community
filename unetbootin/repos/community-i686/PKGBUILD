# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>
# Contributor: Michai Coman <mihai@m1x.ro>

pkgname=unetbootin
pkgver=585
pkgrel=2
arch=('x86_64' 'i686')
license=('GPL')
pkgdesc='Create bootable Live USB drives'
url='http://unetbootin.sourceforge.net/'
depends=('syslinux' 'p7zip' 'qt4' 'mtools')
optdepends=('polkit: to run unetbootin directly from menu')
makedepends=('setconf')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/$pkgname/UNetbootin/$pkgver/$pkgname-source-$pkgver.tar.gz"
        'org.archlinux.pkexec.unetbootin.policy'
        'unetbootin_polkit')
sha256sums=('63ee0459045c955b1ac7a6ae35be80363556e0f169565b29235e67b708ac9ccb'
            'd7cc55f8d7340be08c33d40443d413f8c15d8d012c29cab98ac5ab5a0fc57fb3'
            'b662e7082873602e9c224a291752e3c2274fb751018bfa23a7560f79c6ecb88f')

prepare() {
  setconf unetbootin.desktop Exec /usr/bin/unetbootin_polkit
}

build() {
  ./build-nostatic 1> output.log 2> error.log
}

package() {
  install -d "$pkgdir/usr/share/$pkgname"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -m755 "${pkgname}_polkit" "$pkgdir/usr/bin/${pkgname}_polkit"
  install -m644 "$pkgname"_*.qm "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  for i in 16 22 24 32 48 256; do
    install -D "${pkgname}_${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
  install -Dm644 'org.archlinux.pkexec.unetbootin.policy' \
    "$pkgdir/usr/share/polkit-1/actions/org.archlinux.pkexec.unetbootin.policy"
}

# vim:set ts=2 sw=2 et:
