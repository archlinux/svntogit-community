# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>
# Contributor: Michai Coman <mihai@m1x.ro>

pkgname=unetbootin
pkgver=613
pkgrel=2
arch=('x86_64' 'i686')
license=('GPL')
pkgdesc='Create bootable Live USB drives'
url='https://unetbootin.github.io'
depends=('syslinux' 'p7zip' 'qt4' 'mtools')
makedepends=('setconf' 'git')
optdepends=('polkit: run unetbootin directly from menu'
            'zenity: display an error if no authentication agent is found')
install="$pkgname.install"
source=("git://github.com/$pkgname/$pkgname.git#tag=$pkgver"
        'org.archlinux.pkexec.unetbootin.policy'
        'unetbootin_polkit.sh'
        'unetbootin.sh')
sha256sums=('SKIP'
            'fa9bb53d90cb10a0ab8dd317ed6a3506b228b0e26ed2ed8b108b5990f641641a'
            '6399c6a44b270a4ec67a36e3914c7c2f47a9008efb0133a33f92d9ad4284cc57'
            'c59c486c0e74fbb23b842c28328937d8c2f8bbf00428711e33dec900a3a7140c')

prepare() {
  cd unetbootin/src/unetbootin

  setconf unetbootin.desktop Exec /usr/bin/unetbootin_polkit
}

build() {
  cd unetbootin/src/unetbootin

  ./build-nostatic 1> output.log 2> error.log
}

package() {
  cd unetbootin/src/unetbootin

  install -d "$pkgdir/usr/share/$pkgname"
  install -m644 "$pkgname"_*.qm "$pkgdir/usr/share/$pkgname/"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname.elf"

  # Application shortcut
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  for i in 16 22 24 32 48 256; do
    install -Dm644 "${pkgname}_${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done

  cd "$srcdir"

  install -m755 "${pkgname}_polkit.sh" "$pkgdir/usr/bin/${pkgname}_polkit"
  install -m755 "${pkgname}.sh" "$pkgdir/usr/bin/${pkgname}"

  # Polkit policy
  install -Dm644 'org.archlinux.pkexec.unetbootin.policy' \
    "$pkgdir/usr/share/polkit-1/actions/org.archlinux.pkexec.unetbootin.policy"
}

# getver: -u 12 github.com/unetbootin/unetbootin
# vim:set ts=2 sw=2 et:
