# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=unhide
pkgver=20220611
pkgrel=1
pkgdesc='A forensic tool to find processes hidden by rootkits, LKMs or by other techniques.'
arch=('x86_64')
url='https://github.com/YJesus/Unhide/'
license=('GPL3')
depends=('iproute2' 'lsof' 'psmisc' 'net-tools' 'procps-ng')
source=("https://github.com/YJesus/Unhide/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "Makefile")
md5sums=('cb0fc465ef26e907ba2166551dc27369'
         '68ec8dcd2f25d4d5dad089a8535d462f')

prepare() {
  cp -f Makefile Unhide-$pkgver/
}

build() {
  cd Unhide-$pkgver
  make
}

package() {
  cd Unhide-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  ln -s unhide-linux "$pkgdir/usr/bin/unhide"
}
