# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=unhide
pkgver=20210124
pkgrel=1
pkgdesc='A forensic tool to find processes hidden by rootkits, LKMs or by other techniques.'
arch=('x86_64')
url='https://sourceforge.net/projects/unhide/'
license=('GPL3')
depends=('iproute2' 'lsof' 'psmisc' 'net-tools' 'procps-ng')
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname/Unhide-$pkgver.tar.gz"
        "Makefile")
md5sums=('6a6a6d64c99a58bb99f04aed5b2f7283'
         '68ec8dcd2f25d4d5dad089a8535d462f')

prepare() {
  cp -f Makefile Unhide-$pkgver/
}

build() {
  cd Unhide-$pkgver
  make
}

package() {
  cd Unhide-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  ln -s unhide-linux "$pkgdir/usr/bin/unhide"
}
