# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Contributor: Steve Sansom <snsansom@gmail.com>

pkgname=units
pkgver=2.16
pkgrel=1
pkgdesc="converts between different units"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/units/units.html"
depends=('readline' 'python' 'python-unidecode')
license=("GPL")
options=('!makeflags')
validpgpkeys=('9AD8FC4162D7937CF64F972E1889D5F0E0636F49')
source=(https://ftp.gnu.org/gnu/units/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('dcf3f78482a13b150fc6bf0f8d611510816a3424bc2b26229b85b1cadeb81686'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --datadir=/usr/share
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
