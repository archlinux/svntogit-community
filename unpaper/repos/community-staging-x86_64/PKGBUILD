# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

pkgname=unpaper
pkgver=6.1
pkgrel=6
pkgdesc="post-processing tool for scanned sheets of paper"
arch=('x86_64')
url="https://github.com/Flameeyes/unpaper"
license=("GPL")
depends=('ffmpeg')
makedepends=('libxslt')
source=("$pkgname-${pkgver}.tar.gz::https://github.com/Flameeyes/unpaper/archive/unpaper-$pkgver.tar.gz")
sha256sums=('213f8143b3361dde3286537eb66aaf7cdd7e4f5e7bde42ac6e91020997a81f1d')

prepare() {
  cd "$srcdir"/unpaper-unpaper-$pkgver
  aclocal
  automake --add-missing
  autoconf
}

build() {
  cd "$srcdir"/unpaper-unpaper-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/unpaper-unpaper-$pkgver
  make install DESTDIR="$pkgdir"
}
