# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=4.0.16
pkgrel=1
pkgdesc="Open Source IRC Server"
arch=('i686' 'x86_64')
url="http://unrealircd.com"
license=('GPL2')
depends=('openssl' 'curl' 'c-ares' 'tre' 'pcre2')
conflicts=('ircd')
provides=('ircd')
backup=('etc/unrealircd/unrealircd.conf')
install=unrealircd.install
source=(https://www.unrealircd.org/unrealircd4/unrealircd-$pkgver.tar.gz
	unrealircd.service
	unrealircd.tmpfiles.d
	install.pl)
sha256sums=('40b43cbf1229550c663d73efa0e58e7353b78160087e3f80f0a5f180cb0da144'
            '0a6d3c269ba5379f967b2ec035ad4c6226d42ce22ffaaf8c3e5b37ba8ee02ddd'
            '2b2efb4b87b5b270e87fc0187ac1c559d41a41e6c81615182dc2bdd18bfcaccd'
            '43f07093ada8eb5c954f9e2e189610575906c690c0953fec041d4f2d31210d85')

prepare() {
  cd "$srcdir"/unrealircd-$pkgver
  sed -i \
    -e 's|$(INSTALL) -m 0700|$(INSTALL) -m 0755|g' \
    -e 's|$(INSTALL) -m 0600|$(INSTALL) -m 0644|g' \
    Makefile.in
}

build() {
  cd "$srcdir"/unrealircd-$pkgver
  ./configure \
    --with-pidfile=/run/unrealircd/ircd.pid \
    --with-showlistmodes \
    --enable-ssl=/usr \
    --with-bindir=/usr/bin \
    --with-datadir=/var/lib/unrealircd \
    --with-confdir=/etc/unrealircd \
    --with-modulesdir=/usr/lib/unrealircd \
    --with-logdir=/var/log/unrealircd \
    --with-cachedir=/var/cache/unrealircd \
    --with-docdir=/usr/share/doc/unrealircd \
    --with-tmpdir=/tmp \
    --with-scriptdir=/usr \
    --with-nick-history=2000 \
    --with-sendq=3000000 \
    --with-permissions=0644 \
    --with-fd-setsize=1024 \
    --enable-dynamic-linking
  make
}

package() {
  cd "$srcdir"/unrealircd-$pkgver

  export pkgdir
  make INSTALL="$srcdir"/install.pl install
  mv "$pkgdir"/usr/unrealircd "$pkgdir"/etc/unrealircd/unrealircd
  cp "$pkgdir"/etc/unrealircd/examples/example.conf "$pkgdir"/etc/unrealircd/unrealircd.conf
  rm -rf "$pkgdir"/tmp

  install -Dm0644 "$srcdir"/unrealircd.service "$pkgdir"/usr/lib/systemd/system/unrealircd.service
  install -Dm0644 "$srcdir"/unrealircd.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/unrealircd.conf
}
