# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=3.2.10
pkgrel=1
pkgdesc="Open Source IRC Server"
arch=('i686' 'x86_64')
url="http://unrealircd.com"
license=('GPL2')
depends=('openssl' 'curl' 'c-ares')
conflicts=('ircd')
provides=('ircd')
backup=('etc/unrealircd/unrealircd.conf'
	'etc/conf.d/unrealircd')
source=(http://www.unrealircd.com/downloads/Unreal$pkgver.tar.gz
	unrealircd.rc
	unrealircd.service
	arch-fixes.patch)
md5sums=('05b0bbdbfab9ffc2304f7595951d7cec'
         '07ffa1e82a01f319a8b997e155d2af1d'
         '2103ce8a4a4e79f6955920bfebea0202'
         '77807313c4578f2c30286b9f9e3fb21c')

build() {
  cd $srcdir/Unreal$pkgver

  patch -p1 <$srcdir/arch-fixes.patch

  ./configure --with-showlistmodes \
      --enable-hub --enable-prefixaq --with-listen=5 \
      --with-dpath=/etc/unrealircd \
      --with-spath=/usr/bin \
      --with-nick-history=2000 --with-sendq=3000000 \
      --with-bufferpool=18 --with-hostname=archlinux \
      --with-permissions=0600 --with-fd-setsize=1024 \
      --enable-dynamic-linking \
      --enable-ziplinks \
      --enable-ssl \
      --enable-libcurl=/usr \
      --with-system-cares

  make
}

package() {
  cd $srcdir/Unreal$pkgver

  mkdir -p $pkgdir/etc/unrealircd
  mkdir -p $pkgdir/usr/bin

  make IRCDDIR=$pkgdir/etc/unrealircd BINDIR=$pkgdir/usr/bin install

  mv $srcdir/Unreal$pkgver/src/ircd $pkgdir/usr/bin/unrealircd
  mv $srcdir/Unreal$pkgver/doc/example.conf $pkgdir/etc/unrealircd/unrealircd.conf
  mkdir -p $pkgdir/usr/lib/unrealircd
  mv $pkgdir/etc/unrealircd/modules $pkgdir/usr/lib/unrealircd/
  ln -s /usr/lib/unrealircd/modules $pkgdir/etc/unrealircd/modules
  install -D -m0755 $srcdir/unrealircd.rc $pkgdir/etc/rc.d/unrealircd

  # log
  mkdir -p $pkgdir/var/log/unrealircd/
  touch $pkgdir/var/log/unrealircd/ircd.log
  ln -s /var/log/unrealircd/ircd.log $pkgdir/etc/unrealircd/ircd.log

  mkdir -p $pkgdir/etc/conf.d/
  echo 'UNREALIRCD_USER=root' >$pkgdir/etc/conf.d/unrealircd

  find $pkgdir/usr -type f -exec chmod ugo+r {} \;
  find $pkgdir/usr -type d -exec chmod ugo+rx {} \;

  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
