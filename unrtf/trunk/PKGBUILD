# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: froggie <sullivanva@gmail.com>

pkgname=unrtf
pkgver=0.21.9
pkgrel=2
pkgdesc="Command-line program which converts RTF documents to other formats"
arch=('x86_64')
url="http://www.gnu.org/software/unrtf/unrtf.html"
license=('GPL3')
depends=('glibc')
changelog=$pkgname.changelog
source=(http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz
        CVE-2016-10091.patch)
sha256sums=('22a37826f96d754e335fb69f8036c068c00dd01ee9edd9461a36df0085fb8ddd'
            '7a535e96764c7d5291060b4e0548b155e1f44357bf78463ad2ed3678c14f749d')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np1 -i "${srcdir}"/CVE-2016-10091.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  autoreconf -if
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
