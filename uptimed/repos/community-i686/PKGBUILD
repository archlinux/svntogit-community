# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=uptimed
pkgver=0.3.16
pkgrel=3
pkgdesc="A system uptime record daemon"
arch=(i686 x86_64)
url="http://podgorny.cz/uptimed/"
license=('GPL')
depends=('glibc' 'bash')
backup=(etc/uptimed.conf)
options=('!libtool')
install=$pkgname.install
source=(http://podgorny.cz/$pkgname/releases/$pkgname-$pkgver.tar.bz2
        uptimed)
md5sums=('f87ec2cc39039239004afa542cf99c59'
         '4ae90bee9fc78014a889a5072effb735')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  # Change log path to Arch standard
  for M in `ls M*`; do
    sed -i "s|/var/spool/uptimed|$pkgdir/var/log/uptimed|" $M
  done
  sed -i 's|spool|log|' libuptimed/urec.h

  autoreconf
  libtoolize --force

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=$pkgdir/usr sysconfdir=$pkgdir/etc install

  mv $pkgdir/etc/uptimed.conf-dist $pkgdir/etc/uptimed.conf
  install -D $srcdir/uptimed $pkgdir/etc/rc.d/uptimed
}
