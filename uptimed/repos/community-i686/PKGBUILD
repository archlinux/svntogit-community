# $Id$
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=uptimed
pkgver=0.3.18
pkgrel=2
pkgdesc="A system uptime record daemon"
arch=(i686 x86_64)
#url="http://podgorny.cz/uptimed/"
url="https://github.com/rpodgorny/uptimed"
license=('GPL')
depends=()
makedepends=('systemd')
backup=(etc/uptimed.conf)
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/rpodgorny/uptimed/archive/v$pkgver.tar.gz")
md5sums=('f76a5f549833590e84e23b955cc1f894')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  # Change log path to Arch standard
  for M in `ls M*`; do
    sed -i "s|/var/spool/uptimed|/var/log/uptimed|" $M
  done
  sed -i 's|spool|log|' libuptimed/urec.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make install DESTDIR=$pkgdir
  mv $pkgdir/etc/uptimed.conf-dist $pkgdir/etc/uptimed.conf
}
