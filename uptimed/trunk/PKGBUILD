# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=uptimed
pkgver=0.4.2
pkgrel=2
pkgdesc="A system uptime record daemon"
arch=(x86_64)
#url="http://podgorny.cz/uptimed/"
url="https://github.com/rpodgorny/uptimed"
license=('GPL')
depends=()
makedepends=('systemd')
backup=(etc/uptimed.conf)
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/rpodgorny/uptimed/archive/v$pkgver.tar.gz")
sha512sums=('a18cc8580a4dc7db7f4d97d70e25d76e7b98b9d328a0fa4ece4176d40fc26589149d63ffc9d2638cc35003cf485b43ae6e115aa1821c662d77f234eb3c4c0a4f')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make install DESTDIR="$pkgdir"
  mv "$pkgdir"/etc/uptimed.conf-dist "$pkgdir"/etc/uptimed.conf
}
