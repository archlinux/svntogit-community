# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: bekks <eduard.warkentin@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=upx
pkgver=4.0.0
pkgrel=1
pkgdesc='Extendable, high-performance executable packer for several executable formats'
arch=(x86_64)
url='https://github.com/upx/upx'
license=(custom GPL2)
makedepends=(cmake git perl setconf)
source=("git+$url#commit=69ca635c4c0ba474e9ca583dbb42e3d028c4533a") # v4.0.0
b2sums=(SKIP)

prepare() {
  cd $pkgname
  git submodule update --init --recursive
  # upx believes this is not a release version if the git revision is set
  setconf src/main.cpp 'bool warn' 'false;'
}

build() {
  make -C $pkgname \
    CHECK_WHITESPACE=/bin/true \
    UPX_LZMA_VERSION=0x465 \
    UPX_LZMADIR="$srcdir" \
    CXXFLAGS_WERROR=""
}

package() {
  cd $pkgname
  install -Dm755 build/release/upx "$pkgdir/usr/bin/upx"
  install -Dm644 doc/upx.1 "$pkgdir/usr/share/man/man1/upx.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
