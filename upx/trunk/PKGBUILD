# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: bekks <eduard.warkentin@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=upx
pkgver=3.93
pkgrel=1
pkgdesc='Ultimate executable compressor.'
arch=('i686' 'x86_64')
url='http://upx.sourceforge.net/'
license=('GPL')
depends=('zlib' 'ucl')
source=("https://github.com/$pkgname/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver-src.tar.xz")
md5sums=('d8da85311492a5f9e3a1fecc979f879b')

build() {
  cd "${pkgname}-${pkgver}-src"
  make UPX_LZMA_VERSION=0x465 UPX_LZMADIR="${srcdir}" CHECK_WHITESPACE=/bin/true all
}

package() {
  cd "${pkgname}-${pkgver}-src"

  install -Dm0755 src/upx.out "${pkgdir}/usr/bin/upx"
  install -Dm0644 doc/upx.1 "${pkgdir}/usr/share/man/man1/upx.1"
}
