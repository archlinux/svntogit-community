# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

pkgname=uranium
pkgver=4.7.1
pkgrel=5
pkgdesc="A Python framework for building Desktop applications."
url="https://github.com/Ultimaker/Uranium"
arch=('any')
license=('LGPL')
depends=('python' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'python-pyqt5' 'python-numpy'
         'arcus' 'python-shapely' 'python-scipy' 'python-cryptography' 'python-certifi')
makedepends=('cmake')
checkdepends=('mypy' 'python-pytest' 'python-twisted')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('16a72ef9e73df86016ceff37a068e3d70a111223669bfa4f86608c29fdaacc0c')

prepare() {
  cd Uranium-${pkgver}
  sed -i 's,/dist-packages,.${PYTHON_VERSION_MINOR}/site-packages,g' CMakeLists.txt
}

build() {
  cd Uranium-${pkgver}
  mkdir -p build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=ON

  make
}

check() {
  cd Uranium-${pkgver}/build
  # few tests fail atm
  #make CTEST_OUTPUT_ON_FAILURE=TRUE test
}

package() {
  cd Uranium-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
