# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.4
pkgrel=1
pkgdesc="A tool for monitoring webpages for updates"
arch=('any')
url="http://thp.io/2008/urlwatch/"
license=('BSD')
depends=('python-keyring' 'python-minidb' 'python-requests' 'python-yaml')
source=("http://thp.io/2008/urlwatch/$pkgname-$pkgver.tar.gz")
sha256sums=('678145ca0677091c783a8a627aa793eac3783a83f00915ac038da9057306c578')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
