# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.22
pkgrel=1
pkgdesc='Tool for monitoring webpages for updates'
arch=('any')
url=https://thp.io/2008/urlwatch/
license=('BSD')
depends=('python-appdirs' 'python-cssselect' 'python-keyring' 'python-lxml'
         'python-minidb' 'python-requests' 'python-setuptools' 'python-yaml')
optdepends=('lynx: for one of the html2text filter methods'
            'python-beautifulsoup4: for the beautify filter and one of the html2text filter methods'
            'python-cssbeautifier: for the beautify filter'
            'python-html2text: for one of the html2text filter methods'
            'python-jsbeautifier: for the beautify filter'
            'python-pdftotext: for the pdf2text filter'
            'python-pytesseract: for the ocr filter')
checkdepends=('python-docutils' 'python-pycodestyle' 'python-pytesseract'
              'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('6aa63ef6aa9476d571d177936dcdb40df82fd50fca58d0fb24cc851bccc54423')
b2sums=('e7f10872e5d7f42ec0ac1073783431c4b0e4fcdcf40884a9f7f52aee36705fbdbaaf5f9e671223715040c6f68cc580f531ba753c9326a2b1380d2fb707f66f1c')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname COPYING
}

# vim:set ts=2 sw=2 et:
