# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=usb_modeswitch
pkgver=1.2.2
pkgrel=1
_pkgdata=20120120
pkgdesc="Activating switchable USB devices on Linux."
arch=('i686' 'x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL')
depends=('libusb-compat' 'tcl')
makedepends=('gcc' 'make')
backup=("etc/$pkgname.conf")
source=("http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-${pkgver}.tar.bz2"
	"http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-data-${_pkgdata}.tar.bz2"
	"usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/usb_modeswitch/device_reference.txt")
md5sums=('4f3d3b9342b59b488089a8a81abda3ae'
         'c899cc9c71bb35f3ec600b7cd1455c65'
         'c12e19e3992b5beac9b6c48555cff660')

build() {
  cd $srcdir/usb-modeswitch-$pkgver/
  make clean
  make
}

package() {
  cd $srcdir/usb-modeswitch-$pkgver/

  mkdir -p $pkgdir/lib/udev/usb_modeswitch
  mkdir -p $pkgdir/etc
  mkdir -p $pkgdir/usr/share/man/man1

  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/lib/udev/usb_modeswitch/usb_modeswitch.sh ${pkgdir}/lib/udev/usb_modeswitch.sh
  rmdir ${pkgdir}/lib/udev/usb_modeswitch
  mv ${pkgdir}/lib/udev/usb_modeswitch.sh ${pkgdir}/lib/udev/usb_modeswitch
  cp $srcdir/usb-modeswitch-${pkgver}-device_reference.txt $pkgdir/etc/usb_modeswitch.setup

  cd $srcdir/usb-modeswitch-data-${_pkgdata}
  make DESTDIR=$pkgdir install
}
