# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
pkgname=usd
pkgver=20.11
pkgrel=2
pkgdesc="3D VFX pipeline interchange file format"
arch=('x86_64')
url="https://graphics.pixar.com/usd/docs/index.html"
license=('Apache')
depends=('boost-libs' 'glew' 'openexr' 'opensubdiv' 'python')
makedepends=('boost' 'cmake' 'intel-tbb' 'ninja')
provides=("usd=${pkgver}")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PixarAnimationStudios/USD/archive/v${pkgver}.tar.gz")
sha256sums=('adbdf451a10a7af201b0fafa44ebc27cc2c3b412d3dbbd90f1a9319abe2e203d')

build() {
  cd USD-${pkgver}
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPXR_USE_PYTHON_3=ON \
    -DPXR_BUILD_IMAGING=OFF \
    -DPXR_BUILD_TESTS=OFF \
    -DPXR_BUILD_MONOLITHIC=ON
  ninja -C build
}

package() {
  cd USD-${pkgver}
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
