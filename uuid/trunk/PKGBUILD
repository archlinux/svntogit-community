# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred.com`
# Contributor: Jeff Cook <jeff@deserettechnology.com>
# Contributor: Michael Asher < michael at we solve every thing dot com >
# Contributor: William DÃ­az <wdiaz@archlinux.us>
# Contributor: vwyodajl <donjuansjiz   GmaIL   com>

pkgname=uuid
pkgver=1.6.2
pkgrel=11
pkgdesc="OSSP Universally Unique Identifier"
arch=('i686' 'x86_64')
url="http://www.ossp.org/pkg/lib/uuid"
license=('MIT')
depends=('sh')
options=('!libtool')
source=("http://www.mirrorservice.org/sites/ftp.ossp.org/pkg/lib/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        ossp.patch)
md5sums=('5db0d43a9022a6ebbbc25337ae28942f'
         '6f0591cfa3e6d5f70c290963d2c5a0a0')

build() {
  cd ${pkgname}-${pkgver}

  # Rename because conflicts with util-linux
  patch -p1 -i "${srcdir}"/ossp.patch

  ./configure --prefix=/usr --with-perl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 README \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
