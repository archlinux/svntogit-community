# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=uusi
pkgver=0.3.1.0
pkgrel=7
pkgdesc="Tweak dependencies in .cabal files"
url="https://github.com/berberman/uusi"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-microlens' 'haskell-microlens-th')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1156531f8a294cd19f6d46068c3892bb450ef965504ae6711c993c74748e549b41fefe689df9a9dea2e626af9812eb45934acfefea777b8a16e8edbc9a5e817c')

prepare() {
  cd $pkgname-$pkgver
  echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
  sed -i 's/<3.1/<4/' $pkgname.cabal
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $pkgname-$pkgver

  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
