# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=uvicorn
pkgver=0.17.0.post1
pkgrel=1
pkgdesc='The lightning-fast ASGI server'
arch=('any')
url='https://github.com/encode/uvicorn'
license=('BSD')
depends=('python' 'python-asgiref' 'python-click' 'python-h11' 'python-websockets' 'python-httptools' 'python-uvloop')
optdepends=('python-watchgod: watchgod reload')
makedepends=('git' 'python-setuptools')
_tag=e659cf5d87c2aaf9ca8863625be436d7694471a4
source=("git+$url#tag=${_tag}")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
