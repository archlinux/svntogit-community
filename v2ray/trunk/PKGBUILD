# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=4.14.0
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2ray/v2ray-core"
license=('MIT')
depends=('glibc')
makedepends=('go-pie' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.zip::https://github.com/v2ray/v2ray-core/releases/download/v$pkgver/src_all.zip"
        'v2ray.service')
sha512sums=('ef3096571c9cdd5fca0fc57298ed8f4d5b087b360718bd166f50e2d3ecb3759725391945c00c25c8cc71ce9cb1ada3f9d3220859131bbc98a99a3188f376a14e'
            '37efc20ef71147f4400eba2bf57766462b5803b5952768bd32d5224f35e37c9c5639aaa19e0f3b63b52063dafd0381f40e2bfbb60dace08b055f0b3245cfa2b7')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p "$GOPATH"/src

  mv *.com *.io *.org "$GOPATH"/src/
}

build() {
  go build -o v2ray v2ray.com/core/main
  go build -o v2ctl v2ray.com/ext/tools/control/main
}

check() {
  cd "$GOPATH"/src/v2ray.com/core
  go test -p 1 -tags json -v v2ray.com/core/...
}

package() {
  cd "$GOPATH"/src/v2ray.com/core
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  sed -i -e '/ExecStart/i Environment=V2RAY_LOCATION_ASSET=/etc/v2ray' \
         -e 's|/usr/bin/v2ray/v2ray|/usr/bin/v2ray|' \
         "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/geoip.dat release/config/geosite.dat release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 "$srcdir"/v2ray -t "$pkgdir"/usr/bin/
  install -Dm755 "$srcdir"/v2ctl -t "$pkgdir"/usr/bin/

  install -Dm644 "$srcdir"/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
}
