# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=5.1.0
pkgrel=2
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("https://github.com/v2fly/v2ray-core/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-prefix-checking-fix.patch::https://github.com/v2fly/v2ray-core/commit/1b0e046c9934bf872f2e886a215c5d23da054d86.patch)
sha512sums=('a25d990e7f938319720e067a1de795ffc4a8a5c688cbba13be89f9c9f606181673101eb11b80ceb3fab15de67656f5d59f0d6ddc6f7c0084f496d4ad55517d6a'
            'f5c14f2c704d2a32380d6300c4c8808ba59f5c94ebcb42305cef85fbd3fac416893dce0658bdbdefb7f6ce44bcd37a22d5d5e9a59393f795ad7a61377f573872')

prepare() {
  cd v2ray-core-$pkgver
  patch -p1 < ../$pkgname-prefix-checking-fix.patch
  sed -i 's|/usr/local/bin|/usr/bin|;s|/usr/local/etc|/etc|' release/config/systemd/system/*.service
}

build() {
  cd v2ray-core-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o v2ray ./main
}

check() {
  cd v2ray-core-$pkgver
  go test -p 1 -tags json -v -timeout 30m ./...
}

package() {
  cd v2ray-core-$pkgver
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/system/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/systemd/system/v2ray@.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 v2ray -t "$pkgdir"/usr/bin/
}
