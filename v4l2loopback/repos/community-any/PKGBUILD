# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=v4l2loopback
pkgname=(v4l2loopback-dkms v4l2loopback-utils)
pkgver=0.12.6
pkgrel=1
pkgdesc="v4l2-loopback device"
arch=(any)
url="https://github.com/umlaeute/v4l2loopback"
license=(GPL2)
makedepends=(help2man)
source=(${url}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('c5a5231b0c364c195c4c50ccb4effac0bdc43710885f275d4c6b2f683d932adf')

package_v4l2loopback-dkms() {
  pkgdesc+=" – module sources"
  depends=(dkms)
  provides=(V4L2LOOPBACK-MODULE)
  cd ${pkgbase}-${pkgver}
  install -Dm644 v4l2loopback.c v4l2loopback_formats.h dkms.conf Makefile -t "${pkgdir}"/usr/src/${pkgbase}-${pkgver}/
}
package_v4l2loopback-utils() {
  pkgdesc+=" – utilities only"
  depends=(V4L2LOOPBACK-MODULE)
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install-utils install-man
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgbase}
}
