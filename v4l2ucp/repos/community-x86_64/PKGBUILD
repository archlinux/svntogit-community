# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>

pkgname=v4l2ucp
pkgver=2.0.2
pkgrel=3
pkgdesc="A universal control panel for Video for Linux Two (V4L2) devices"
arch=('i686' 'x86_64')
url="http://v4l2ucp.sourceforge.net/"
license=('GPL')
depends=('qt4' 'v4l-utils')
optdepends=('mplayer: video preview')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('e5bc6e71e2cd3ab123c277b2f7154b4f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # use videodev2.h header
  sed -i 's/videodev/&2/' src/v4l2ctrl.c
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
