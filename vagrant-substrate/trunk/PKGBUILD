# Maintainer: Jonathan Steel <jsteel at archlinux.org>

pkgname=vagrant-substrate
_pkgname=vagrant-installers
pkgver=726.d082972
pkgrel=2
pkgdesc="Substrate layer for Vagrant"
arch=('x86_64')
url="http://vagrantup.com"
license=('MIT')
options=('!emptydirs' '!makeflags')
makedepends=('git' 'puppet' 'chrpath' 'wget' 'go-pie')
depends=('lzo')
source=(git://github.com/mitchellh/$_pkgname.git#commit=d082972
        ruby23_gcc7.patch
        puppet_module_ruby.patch)
md5sums=('SKIP'
         '6e3db41f8918479dd408b8557c3a1ea1'
         '63b8c25d33344dfd2be299143e85c4a2')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git describe --always)
}

prepare() {
  cd $_pkgname/substrate

  # Use $srcdir for the working directory
  sed -i "s:/vagrant-substrate:$srcdir/$_pkgname/vagrant-substrate:" \
    hiera/common.yaml
  sed -i "s:\"/opt/vagrant\":\"$srcdir/$_pkgname/opt/vagrant\":" \
    hiera/common.yaml

  # Use latest openssl
  sed -i 's/openssl-1.0.2e.tar.gz/openssl-1.0.2k.tar.gz/' \
    modules/openssl/manifests/init.pp
  # Disable SSLv3
  sed -i '16 s/shared/shared no-ssl3-method/' \
    modules/openssl/manifests/install/linux.pp

  # Don't create a zip of the substrate
  sed -i '48,50d' modules/vagrant_substrate/manifests/init.pp

  # Do not set file ownership
  find . -name *.pp -exec sed -i "/owner.*.=.*.root/d" {} +
  find . -name *.pp -exec sed -i "/group.*.=.*.root/d" {} +

  # Fix compile issues
  # https://github.com/mitchellh/vagrant-installers/issues/60
  sed -i '20 s/-I/-O2 -I/' modules/vagrant_substrate/manifests/staging/posix.pp
  # chmod u+w the file before chrpath
  sed -i 's/chrpath --replace/chmod u+w $name; chrpath --replace/' \
    modules/vagrant_substrate/manifests/staging/linux_chrpath.pp
  # ruby23 gcc7 patch
  patch -Np1 -i "$srcdir"/puppet_module_ruby.patch
}

build() {
  cd $_pkgname/substrate

  FACTER_param_output_dir="$srcdir" puppet apply \
    --detailed-exitcodes \
    --hiera_config=config/hiera.yaml --confdir=config \
    --modulepath=modules manifests/init.pp || test $? -eq 2
}

package() {
  cd $_pkgname

  install -d "$pkgdir"/opt

  cp -r vagrant-substrate/staging "$pkgdir"/opt/vagrant

  install -Dm644 LICENSE.md \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # FS#54246 Fix pkg-config paths
  sed -i 's@/build/vagrant-substrate/src/vagrant-installers/@/@g' \
    "$pkgdir"/opt/vagrant/embedded/lib/pkgconfig/*.pc
}
