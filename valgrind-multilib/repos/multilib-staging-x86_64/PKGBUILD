# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=valgrind-multilib
pkgver=3.10.1
pkgrel=3
pkgdesc='A tool to help find memory-management problems in programs for multilib'
arch=(x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('lib32-glibc>=2.22' 'lib32-glibc<2.23' 'perl')
makedepends=('gdb' 'openmpi' 'gcc-multilib')
optdepends=('openmpi: MPI support')
options=('!emptydirs')
source=(http://valgrind.org/downloads/valgrind-$pkgver.tar.bz2
        valgrind-glibc.patch
	valgrind-kernel-version.patch)
sha1sums=('15518427de842547ffa84eff8b2b67f3b16581a8'
          '1fa127ef24630ad5f4901ea0e37ea2acb12d3d17'
          '08b71e1ebc3e3906d0ff273db0aba2177a857c47')

provides=("valgrind=$pkgver-$pkgrel")
conflicts=('valgrind')

prepare() {
  cd valgrind-$pkgver
  patch -p1 -i "$srcdir"/valgrind-glibc.patch
  patch -p1 -i "$srcdir"/valgrind-kernel-version.patch
}

build() {
  cd valgrind-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd valgrind-$pkgver
  make DESTDIR="$pkgdir" install
}
