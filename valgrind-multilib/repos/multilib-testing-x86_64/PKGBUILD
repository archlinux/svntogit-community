# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=valgrind-multilib
pkgver=3.10.1
pkgrel=2.1
pkgdesc="A tool to help find memory-management problems in programs for multilib"
arch=(x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('lib32-glibc>=2.21' 'lib32-glibc<2.22' 'perl')
makedepends=('gdb' 'openmpi' 'gcc-multilib')
optdepends=('openmpi: MPI support')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/valgrind-$pkgver.tar.bz2
        valgrind-3.9.0-glibc-2.21.patch)
sha1sums=('15518427de842547ffa84eff8b2b67f3b16581a8'
          'f1740a5b03c69baee4aa0cefb27315d6b5ec6699')

provides=("valgrind=$pkgver-$pkgrel")
conflicts=('valgrind')

prepare() {
  cd valgrind-$pkgver
  patch -p1 -i "$srcdir"/valgrind-3.9.0-glibc-2.21.patch
}

build() {
  cd valgrind-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd valgrind-$pkgver
  make DESTDIR="$pkgdir" install
}
