# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind-multilib
pkgver=3.9.0
pkgrel=2
pkgdesc="A tool to help find memory-management problems in programs for multilib"
arch=('x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('lib32-glibc>=2.19' 'lib32-glibc<2.20' 'perl')
makedepends=('gdb' 'gcc-multilib')
options=('staticlibs' '!emptydirs')
provides=('valgrind')
conflicts=('valgrind')
source=(http://valgrind.org/downloads/valgrind-${pkgver}.tar.bz2
        valgrind-3.9.0-glibc-2.19.patch)
md5sums=('0947de8112f946b9ce64764af7be6df2'
         '21020338ad5a93e6f9694946183e8835')

prepare() {
  cd ${srcdir}/valgrind-${pkgver}

  patch -p1 -i $srcdir/valgrind-3.9.0-glibc-2.19.patch
}

build() {
  cd ${srcdir}/valgrind-${pkgver}

  ./configure --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd ${srcdir}/valgrind-${pkgver}

  make DESTDIR=${pkgdir} install
}

