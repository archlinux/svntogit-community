# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Orivej Desh <smpuj@bk.ru>

_dir=2588
pkgname=vamp-plugin-sdk
pkgver=2.9.0
pkgrel=1
pkgdesc="The Vamp audio analysis plug-in system"
arch=('x86_64')
url="https://www.vamp-plugins.org/"
license=('BSD')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libsndfile')
provides=('libvamp-hostsdk.so' 'libvamp-sdk.so')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_dir}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7ef7f837d19a08048b059e0da408373a7964ced452b290fae40b85d6d70ca9000bcfb3302cd0b4dc76cf2a848528456f78c1ce1ee0c402228d812bd347b6983b')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  # license
  install -vDm 644 COPYING \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CHANGELOG,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
