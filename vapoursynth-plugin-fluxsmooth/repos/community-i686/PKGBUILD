# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-fluxsmooth
pkgver=1.0
pkgrel=1
pkgdesc='FluxSmooth plugin for VapourSynth'
arch=('i686' 'x86_64')
url='https://github.com/dubhater/vapoursynth-fluxsmooth'
license=('None')
depends=('vapoursynth')
[[ $CARCH == 'x86_64' ]] && makedepends=('yasm')
source=("vapoursynth-plugin-fluxsmooth-${pkgver}.tar.gz::https://github.com/dubhater/vapoursynth-fluxsmooth/archive/v${pkgver}.tar.gz")
sha256sums=('f178f81a8b3f4a33896903024084d8f877cebf0f9e4d4b4729f787311a25198b')

build() {
  cd vapoursynth-fluxsmooth-${pkgver}

  export LDFLAGS="$LDFLAGS,-z,noexecstack"

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib/vapoursynth'
  make
}

package(){
  cd vapoursynth-fluxsmooth-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/plugins/FluxSmooth
  install -m 644 readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/plugins/FluxSmooth/README
}

#vim: ts=2 sw=2 et:
