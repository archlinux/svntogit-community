# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=variety
pkgver=0.8.4
pkgrel=2
pkgdesc='Changes the wallpaper on a regular interval using user-specified or automatically downloaded images.'
arch=('any')
url="https://peterlevi.com/variety/"
license=('GPL3')
depends=('gtk3' 'imagemagick' 'libgexiv2' 'libnotify' 'python-beautifulsoup4' 'python-cairo'
         'python-configobj' 'python-dbus' 'python-gobject' 'python-httplib2' 'python-pillow'
         'python-lxml' 'python-pycurl' 'python-requests')
makedepends=('python-distutils-extra')
optdepends=('xdg-user-dirs: set wallpaper for KDE Plasma')
source=($pkgname-$pkgver.tar.gz::https://github.com/varietywalls/variety/archive/$pkgver.tar.gz)
sha256sums=('477021bcb8d649420dbc25e5a1229a8fb4648ff7c69e37abcfe22943ae3d207d')

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python setup.py install --root="${pkgdir}" --optimize=1
}
