# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2022.10.1
pkgver=2022.10.1
_upstreamver=${_pkgver%[[:lower:]]}
#_patchver=$_upstreamver
_patchver=2022.10.0 # almost always out of sync now
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=('any')
url='https://github.com/dani-garcia/bw_web_builds'
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs-lts-gallium' 'npm')
provides=('bitwarden_rs-vault' 'bitwarden_rs-web')
replaces=('bitwarden_rs-vault' 'bitwarden_rs-web')
install=$pkgname.install
source=("bitwarden-clients::git+https://github.com/bitwarden/clients.git#tag=web-v$_upstreamver"
        "$pkgname-v$_pkgver-$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        '3a4f2de3d62b4d1462d76bd58de048214bb5ff83ffe65c53710e6605d1c6c38c1138dc7fb9c09a92c050e24dc0dbe98acb7d76fd0aa7dd04acae39a06444faae')

pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-clients
  git submodule update --init
  patch --forward --strip=1 --input=../$pkgname-v$_pkgver-$_patchver.patch

  npm ci
}

build() {
  cd bitwarden-clients/apps/web
  npm run dist:oss:selfhost
}

package() {
  cd bitwarden-clients/apps/web
  install -d "$pkgdir"/usr/share/webapps/$pkgname
  cp -R build/* "$pkgdir"/usr/share/webapps/$pkgname
}

# vim:set ts=2 sw=2 et:
