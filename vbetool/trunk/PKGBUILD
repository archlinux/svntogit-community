# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Christian Storm <Christian.Storm@gmx.DE>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=vbetool
pkgver=1.1
pkgrel=2
pkgdesc="Uses lrmi in order to run code from the video BIOS"
url="http://www.srcf.ucam.org/~mjg59/vbetool/"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('zlib' 'libx86')
makedepends=('pciutils')
source=("http://www.codon.org.uk/~mjg59/${pkgname}/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('ffb03b118867a02296d7449019ad8846')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --without-x86emu
  else
    ./configure --prefix=/usr
  fi
  make LIBS=-lpci
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
