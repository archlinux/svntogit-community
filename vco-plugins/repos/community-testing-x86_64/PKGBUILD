# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vco-plugins
pkgver=0.3.0
pkgrel=4
pkgdesc="LADSPA anti-aliased oscillator plugins"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/ladspa/"
license=('GPL2')
groups=('ladspa-plugins')
depends=('ladspa')
source=("${url}../downloads/VCO-plugins-${pkgver}.tar.bz2")
sha512sums=('44e066acdda38137dcc6a6b504a8453f8d0d25c90ba091e71a48554b47e60365a1e6b0bad694ec7340d399951b8b0592507be153370d1b4924d399310cf106c5')

build() {
  cd "VCO-plugins-${pkgver}"
  make
}

package() {
  cd "VCO-plugins-${pkgver}"

  # plugin
  install -Dm755 blvco.so "${pkgdir}/usr/lib/ladspa/blvco.so"
  ln -s blvco.so "${pkgdir}/usr/lib/ladspa/vco_sawpulse.so"
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
