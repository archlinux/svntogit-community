# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Lone_Wolf lonewolf@xs4all.nl

pkgname=vdrift
pkgver=2011.10.22
pkgrel=7
pkgdesc="Open source driving simulation made with drift racing in mind"
arch=('i686' 'x86_64')
url="http://vdrift.net/"
license=('GPL')
depends=('bullet' 'curl' 'sdl_gfx' 'sdl_image' 'glew' 'libvorbis' 'vdrift-data' 
         'gtk-update-icon-cache' 'hicolor-icon-theme' 'libarchive')
makedepends=('scons' 'boost' 'asio')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-${pkgver//./-}.tar.bz2)
md5sums=('6f8806ab1be303e9e1e47522c9eee890')
install=vdrift.install

build() {
  cd "$srcdir"

  cd $pkgname-${pkgver//./-}

  # select arch
  if [ `uname -m` = "x86_64" ]; then
    _sconsarch="a64"
  else
    _sconsarch="686"
  fi

  #sed -i 's/glGenerateMipmap/glGenerateMipmapEXT/g' src/texture.cpp
  #sed -i '/types.h/d' src/http.h

  # build and install
  scons $MAKEFLAGS \
    "destdir"="$pkgdir" \
    "arch"=$_sconsarch \
    "release"=1 \
    "force_feedback"=1 \
    "prefix"=/usr \
    "datadir"=share/$pkgname/ \
    "extbullet"=1
}

package() {
  cd "$srcdir"/$pkgname-${pkgver//./-}

  scons install

  # install .desktop file
  install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  sed -i '1 s/^\xef\xbb\xbf//' "$pkgdir"/usr/share/applications/$pkgname.desktop # remove BOM (WHY IS THERE?)

  # install icons
  install -Dm644 data/textures/icons/vdrift-16x16.png \
    "$pkgdir"/usr/share/icons/hicolor/16x16/apps/vdrift.png
  install -Dm644 data/textures/icons/vdrift-32x32.png \
    "$pkgdir"/usr/share/icons/hicolor/32x32/apps/vdrift.png
  install -Dm644 data/textures/icons/vdrift-64x64.png \
    "$pkgdir"/usr/share/icons/hicolor/64x64/apps/vdrift.png
  install -Dm644 data/textures/icons/vdrift-64x64.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  rm -r "$pkgdir"/usr/share/vdrift
}
# vim: sw=2:ts=2 et:
