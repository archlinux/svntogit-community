# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Lone_Wolf lonewolf@xs4all.nl

pkgname=vdrift
pkgver=2010.06.30
pkgrel=2
pkgdesc="An open source driving simulation made with drift racing in mind"
arch=('i686' 'x86_64')
url="http://vdrift.net/"
license=('GPL')
depends=('sdl_gfx' 'sdl_image' 'glew' 'libvorbis' 'vdrift-data')
makedepends=('scons' 'boost' 'asio')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-${pkgver//./-}/$pkgname-${pkgver//./-}.tar.bz2
        $pkgname.desktop)
md5sums=('ddcaf90bb25f9ce9cc084d7f467d7b56'
         '8bb429f37bcb4aed5e7ab1d80c5a4f93')

build() {
  cd "$srcdir"/$pkgname-${pkgver//./-}

  # select arch
  if [ `uname -m` = "x86_64" ]; then
    _sconsarch="a64"
  else
    _sconsarch="686"
  fi

  # patch for crashing with bad drivers
  sed -i 's/glGenerateMipmap/glGenerateMipmapEXT/g' src/texture.cpp

  # build and install
  scons \
    "destdir"="$pkgdir" \
    "arch"=$_sconsarch \
    "release"=1 \
    "force_feedback"=1 \
    "prefix"=/usr \
    "datadir"=share/$pkgname/data || return 1
}

package() {
  cd "$srcdir"/$pkgname-${pkgver//./-}

  scons install || return 1

  # remove some unneeded files
  find "$pkgdir"/usr/share/$pkgname -name SConscript -delete

  # install icons
  install -Dm644 data/textures/icons/$pkgname-16x16.png \
    "$pkgdir"/usr/share/icons/hicolor/16x16/apps/$pkgname.png
  install -Dm644 data/textures/icons/$pkgname-32x32.png \
    "$pkgdir"/usr/share/icons/hicolor/32x32/apps/$pkgname.png
  install -Dm644 data/textures/icons/$pkgname-64x64.png \
    "$pkgdir"/usr/share/icons/hicolor/64x64/apps/$pkgname.png

  # install .desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop

  # delete installed data (will be provided by -data package)
  rm -r "$pkgdir"/usr/share/$pkgname
}
# vim: sw=2:ts=2 et:
