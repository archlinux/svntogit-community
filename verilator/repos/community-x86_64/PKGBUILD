# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=4.224
pkgrel=1
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/projects/verilator/wiki/Intro'
arch=('x86_64')
license=('LGPL')
depends=('perl')
optdepends=('systemc')
# lsb-release is used by configure script to enable usage of c++17
makedepends=('python' 'systemc' 'lsb-release')
source=("https://github.com/verilator/verilator/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-use-c++17-on-arch.patch::https://github.com/verilator/verilator/pull/3479.patch)
sha512sums=('a2a37948878ad897668ce89b0f12978dcbda3726cebb134db6ff6f172573d4e488748699a84008359d808d5b6fad19c013edaebc7f8a24a5452b61b1d57dc283'
            'd608fba917db62974a3a083829fcaf3bb11d4cf25b3bea5a078d720132e104cdbba1500d1d69ccbc090c3731338f00c84fa46fb6772434367835736e821259dc')

prepare() {
  cd verilator-$pkgver
  patch -p1 -i ../$pkgname-use-c++17-on-arch.patch
  autoconf
}

build() {
  cd verilator-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator-$pkgver
  make test
}

package() {
  cd verilator-$pkgver
  make install DESTDIR="$pkgdir"
}
