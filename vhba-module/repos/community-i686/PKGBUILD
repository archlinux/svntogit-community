# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=vhba-module
pkgver=20130607
_extramodules=extramodules-3.15-ARCH
pkgrel=29
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux>=3.15' 'linux<3.16')
makedepends=('linux-headers>=3.15' 'linux-headers<3.16')
options=(!makeflags)
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        '60-vhba.rules'
        '0001-VHBA-module-define-scmd_dbg-and-scmd_warn-macros-onl.patch')
md5sums=('660df41d08b1fd86baad1f16a0264438'
         '4dc37dc348b5a2c83585829bde790dcc'
         '8c929ef662de89f348d277983cbb3952')

prepare() {
  cd $pkgname-$pkgver

  # Fix build with Linux 3.15
  # http://sourceforge.net/p/cdemu/bugs/76/
  patch -Np2 -i ../0001-VHBA-module-define-scmd_dbg-and-scmd_warn-macros-onl.patch
}

build() {
  cd $pkgname-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make KDIR=/usr/lib/modules/$_kernver/build
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"
  install -Dm644 ../60-vhba.rules "$pkgdir/usr/lib/udev/rules.d/60-vhba.rules"

  cd $startdir
  cp -f $install ${install}.pkg
  true && install=${install}.pkg
  sed -i "s/EXTRAMODULES=.*/EXTRAMODULES=$_extramodules/" $install
}

# vim:set ts=2 sw=2 et:
