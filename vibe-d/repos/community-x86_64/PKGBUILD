# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=vibe-d
_pkgname=vibe.d
pkgver=0.8.5
pkgrel=4
pkgdesc='High-performance asynchronous I/O, concurrency and web application toolkit'
arch=('x86_64')
url='https://vibed.org'
license=('MIT')
depends=('liblphobos' 'openssl' 'libevent' 'zlib' 'diet-ng' 'd-stdx-allocator')
makedepends=('meson' 'ldc' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/vibe-d/vibe.d/archive/v$pkgver.tar.gz"
        "fix-meson.patch::https://github.com/vibe-d/vibe.d/pull/2302/commits/b2e5f979b52ed8ae1d044c37d556fae70882917c.patch")
sha512sums=('de4b14087e0df0de8ccad272481987d956487e8d18ab7142c3f25e54c41820e3f24aa6307416b67ff805240316c6dedc69f89fddf377307f0d5f35b9481cecdf'
            '33b987cd59bda57f032b2b29535c83552329051271d3b0c23bcae23da1a1b8f352055d94a9663371c01d53bbf463958497780d3c8563f6c7ab030ccb7f06081f')

prepare() {
  cd $_pkgname-$pkgver

  patch -p1 < "$srcdir"/fix-meson.patch
}

build() {
  mkdir -p $_pkgname-$pkgver/build
  cd $_pkgname-$pkgver/build

  export DC=ldc

  arch-meson ..

  ninja
}

check() {
  cd $_pkgname-$pkgver/build

  ninja test
}

package() {
  cd $_pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install

  install -Dm 644 ../LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

