# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=vibe-d
_pkgname=vibe.d
pkgver=0.8.4
pkgrel=4
pkgdesc='High-performance asynchronous I/O, concurrency and web application toolkit'
arch=('x86_64')
url='https://vibed.org'
license=('MIT')
depends=('liblphobos' 'openssl' 'libevent' 'zlib' 'diet-ng' 'd-stdx-allocator')
makedepends=('meson' 'ldc' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/vibe-d/vibe.d/archive/v$pkgver.tar.gz"
"fix-aa-tests.patch::https://github.com/vibe-d/vibe.d/commit/926691489fcc6da40e3c8e187e557398acbef532.patch")
sha512sums=('b0536f6f33592c8e0af5e212d9f4a7d0ad1bf711c315fbd321dd1f5a8ea648fe03793820b26dedf6dc03ec2dc35e809641724bc71c800e2562cc930e686ffcc3'
            'a4aa070a9b1cf4c5c6920e8537bdcd0a4b8bae94a3a0a16db1c2d667118f13a98936ccd5f4f8055a2c25ceced1e98a950b60620be1e825eb8ebbbe88821c5c5b')

prepare() {
  cd $_pkgname-$pkgver

  # Disable failing tests
  sed -i "/test('vibe-test_utils', vibe_test_utils_exe)/d" utils/meson.build

	patch -p1 < "$srcdir"/fix-aa-tests.patch
}

build() {
  mkdir -p $_pkgname-$pkgver/build
  cd $_pkgname-$pkgver/build

  export DC=ldc

  arch-meson ..

  ninja
}

check() {
  cd $_pkgname-$pkgver/build

  ninja test
}

package() {
  cd $_pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install

  install -Dm 644 ../LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

