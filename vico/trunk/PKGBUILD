# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vico
pkgver=1.2.1
pkgrel=2
pkgdesc="Minimalistic Midi sequencer intended to be used in parallel with other software"
arch=('x86_64')
url="https://www.laborejo.org/vico.git/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('glib2' 'jack' 'nuitka' 'libsndfile' 'util-linux-libs')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('539684eada6457d19a0ef043819b124b520a5edddadd5d6e078339262c316e7d475e924d8a9420799713a36e70a58178e20b3cbe18f108dcd98c48131fd3ccfc'
            'SKIP')
b2sums=('5760a8e4b385a6dfb258281fc72fb74d255497033d150d9ff061dd5db8a9dc3153a3b0118afeb4ae1ebec413be0235fb4e56e22a666a92ffad8163c8656ca8c0'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so' 'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
