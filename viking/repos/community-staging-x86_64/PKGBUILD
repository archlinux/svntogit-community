# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Nick Ã˜stergaard <oe.nick@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=viking
pkgver=1.8
pkgrel=4
pkgdesc="GTK+2 application to manage GPS data"
arch=('x86_64')
url="https://sourceforge.net/projects/viking/"
license=('GPL2')
depends=('curl' 'file' 'gpsd' 'gtk2' 'libgexiv2' 'mapnik' 'geoclue' 'liboauth' 'libzip')
makedepends=('boost' 'gtk-doc' 'intltool' 'yelp-tools')
optdepends=('gpsbabel: import and export GPS tracks from various formats')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        viking-geoclue_layer.xml::https://raw.githubusercontent.com/viking-gps/viking/6f7b180ebefe2e2d45b2e4d6343d04581216adeb/help/C/geoclue_layer.xml
        yelp-tools.patch
        viking-gcc10.patch::"https://github.com/viking-gps/viking/commit/7883ec0d.patch")
sha256sums=('855cbccd5f17ef0dc6d7e2d080a4b20383a079dfb882edd04dba602297030fa8'
            'fdcc6caca271cd1b63369a6874d23c5902e1ba6d829c1c3e65c06872189ddc07'
            'bbacf06a9e3cf1a16ab5a9da343e18dcb4fadf1f338295da68fe428dc75e429c'
            '57a3b4bb2958d1790865ec13792b4a86dacc85872b16670ee51e7388aecf53d1')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  
  # Remove Mapbox-Outdoors map, it doesn't work without an access token
  sed -i '/maps_layer_register_map_source (mapbox_type);/d' src/osm.c
  
  # Don't use deprecated gnome-doc-utils
  # https://github.com/viking-gps/viking/pull/83
  mv ../viking-geoclue_layer.xml help/C/geoclue_layer.xml
  sed -i 's/ghelp:/help:/g' help/*/*.xml src/vikwindow.c
  mv help/C/{viking.xml,index.docbook}
  patch -Np1 -i ../yelp-tools.patch
  autoreconf -fi
  intltoolize --force

  patch -p1 -i ../viking-gcc10.patch # Fix build with GCC 10
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  export CXXFLAGS="$CXXFLAGS -I/usr/include/mapnik"
  ./configure --prefix=/usr --enable-geocaches
  make
}

package(){
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
