# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Nick Ã˜stergaard <oe.nick@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=viking
pkgver=1.9
pkgrel=1
pkgdesc="GTK application to manage GPS data"
arch=('x86_64')
url="https://sourceforge.net/projects/viking/"
license=('GPL2')
depends=('curl' 'file' 'gpsd' 'gtk3' 'libgexiv2' 'mapnik' 'geoclue' 'liboauth' 'libzip')
makedepends=('boost' 'docbook-xsl' 'intltool' 'itstool')
optdepends=('gpsbabel: import and export GPS tracks from various formats')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        viking.appdata.xml)
sha256sums=('fe4acf3d24121d6f3097d17ea704b42c0336a8f693151886b8b26e308dbfcb3b'
            'f4df7b51d078c3c992ecdddb41a88766733e0ec50a67954310aa3970a4689e99')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  export CXXFLAGS="$CXXFLAGS -I/usr/include/mapnik"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
  install -Dm644 ../viking.appdata.xml "${pkgdir}"/usr/share/metainfo/viking.appdata.xml
}
