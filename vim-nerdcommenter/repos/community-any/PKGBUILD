# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Laszlo Papp <djszapi @ gmail at com>

pkgname=vim-nerdcommenter
pkgver=2.6.0
pkgrel=1
pkgdesc='Plugin that allows for easy commenting of code for many filetypes'
url='https://github.com/preservim/nerdcommenter'
arch=('any')
license=('CCPL')
depends=('vim')
groups=('vim-plugins')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('e5b576812351955641011ceb1c8af0e85a2f4be3db8b8bd3e8da05aff6d3beba8e0dfcbd3c78cf6833a0f4aaf07fd87cb05151fdd5f341037b4393590134023b')

package() {
  cd ${pkgname#vim-}-${pkgver}
  _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/* -t "${_installpath}/doc"
  install -Dm 644 autoload/* -t "${_installpath}/autoload"
  install -Dm 644 plugin/* -t "${_installpath}/plugin"
  install -Dm 644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
