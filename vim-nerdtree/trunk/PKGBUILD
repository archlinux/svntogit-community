# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kwpolska <kwpolska@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laszlo Papp <djszapi @ gmail at com>

pkgname=vim-nerdtree
pkgver=6.4.4
pkgrel=1
pkgdesc='Tree explorer plugin for navigating the filesystem'
url='https://github.com/scrooloose/nerdtree'
arch=('any')
license=('custom:WTFPL')
depends=('vim')
groups=('vim-plugins')
source=(https://github.com/scrooloose/nerdtree/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e288ade0159eb5d3042f644e18e4c886fd000b1d669bfd59c0673b2d21e58b0ccf0ea6951a5528a9d1cc92a8cecb87240dcf09dfa6c648598e0f2315646827ce')

prepare() {
  cd nerdtree-${pkgver}
  sed 's|\[s:rootNERDTreePath,|["/usr/share/doc/vim-nerdtree",|' -i autoload/nerdtree.vim
}

package() {
  cd nerdtree-${pkgver}
  local _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin syntax nerdtree_plugin lib
  install -Dm 644 CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.markdown CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
