# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Robert Timm <mail@rtti.de>
# Contributor: Jason Ryan <jasonwryan@gmail.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

pkgname=vimb
pkgver=3.6.0
pkgrel=2
pkgdesc='The vim like browser'
arch=(x86_64)
url=https://fanglingsu.github.io/vimb/
license=(GPL3)
depends=(webkit2gtk)
makedepends=(git)
source=(
  git+https://github.com/fanglingsu/vimb.git#tag=6b73a6fc16f1a01d246f4bf5732aea01fc7727bb
  vimb-ldflags.patch
)
sha256sums=('SKIP'
            'daf758d2f0cf20c24c432ec4297cc45fb52dbee839726ffb57936c5a20ff5aa9')

pkgver() {
  cd vimb

  git describe --tags
}

prepare() {
  cd vimb

  patch -Np1 -i ../vimb-ldflags.patch
}

build() {
  make PREFIX=/usr -C vimb
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C vimb install
}

# vim: ts=2 sw=2 et:
