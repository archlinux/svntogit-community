# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=virt-manager
pkgname=(virt-install virt-manager)
pkgver=1.4.3
pkgrel=2
pkgdesc="Console user interface for managing virtual machines"
arch=('any')
url="http://virt-manager.org/"
license=('GPL')
depends=('python2-gobject' 'libvirt-glib' 'libvirt-python2'
	 'python2-requests' 'python2-ipaddr' 'libosinfo')
makedepends=('intltool' 'python2-cairo')
source=("https://virt-manager.org/download/sources/virt-manager/virt-manager-$pkgver.tar.gz")
sha256sums=('528226b9a0fdd6cd66451a8217fc7c87f3deb6bebd08599c25fe5c7291fe6a24')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py build
}

package_virt-install() {
  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py --no-update-icon-cache --no-compile-schemas install --root "$pkgdir"
  python2 -m compileall "${pkgdir}/usr/share/virt-manager"
  python2 -O -m compileall "${pkgdir}/usr/share/virt-manager"
  rm "${pkgdir}/usr/bin/virt-manager"
}

package_virt-manager() {
  pkgdesc="Desktop user interface for managing virtual machines"
  depends=("virt-install=$pkgver" 'vte3' 'gtk-vnc' 'spice-gtk3' 'python2-cairo')
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm 755 build/virt-manager -t "${pkgdir}/usr/bin"
}
