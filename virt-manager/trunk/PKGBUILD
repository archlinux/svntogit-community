# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=virt-manager
pkgname=(virt-install virt-manager)
pkgver=2.1.0
pkgrel=2
pkgdesc="Console user interface for managing virtual machines"
arch=('any')
url="https://virt-manager.org/"
license=('GPL')
depends=('libosinfo' 'libvirt-python' 'python-gobject' 'python-requests')
makedepends=('intltool')
source=("https://virt-manager.org/download/sources/virt-manager/virt-manager-$pkgver.tar.gz")
sha512sums=('48590ff58246e13b73c7ab741ca854bfebae27fb90a891168db5d3fac1d066cce603f83b8d993e70e387624f911fc8503abe488e7992bc50f49d7e980aed5afb')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build
}

package_virt-install() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py --no-update-icon-cache --no-compile-schemas install --root "$pkgdir"
  python -m compileall "${pkgdir}/usr/share/virt-manager"
  python -O -m compileall "${pkgdir}/usr/share/virt-manager"

  # Split virt-manager
  [[ -d $srcdir/virt-manager ]] && rm -r "$srcdir/virt-manager/"
  mkdir "$srcdir"/virt-manager
  mv "$pkgdir"/usr/bin/virt-manager \
     "$pkgdir"/usr/share/{appdata,applications,glib-2.0,icons}/ \
     "$pkgdir"/usr/share/man/man1/virt-manager.1 \
     "$srcdir/virt-manager/"
}

package_virt-manager() {
  pkgdesc="Desktop user interface for managing virtual machines"
  depends=("virt-install=$pkgver" 'gtk-vnc' 'libvirt-glib' 'spice-gtk' 'vte3' 'python-cairo')

  cd "$srcdir"/virt-manager
  mkdir -p "$pkgdir"/usr/{bin,share/man/man1}
  mv virt-manager "$pkgdir/usr/bin"
  mv appdata applications glib-2.0 icons "$pkgdir"/usr/share
  mv virt-manager.1 "$pkgdir"/usr/share/man/man1
}
