# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=virt-viewer
pkgver=10.0
pkgrel=1
pkgdesc='A lightweight interface for interacting with the graphical display of virtualized guest OS.'
arch=('x86_64')
url='https://gitlab.com/virt-viewer/virt-viewer'
license=('GPL')
depends=('gtk-vnc' 'libvirt' 'spice-gtk' 'libvirt-glib' 'libgovirt' 'vte3')
makedepends=('meson' 'intltool' 'spice-protocol' 'gobject-introspection' 'bash-completion')
replaces=('virtviewer')
source=("https://virt-manager.org/download/sources/virt-viewer/virt-viewer-${pkgver}.tar.xz"{,.asc})
b2sums=('3368164c2f770f8aa963e0fb93e713249b05d31600747b68197864a40bf6fdb221ad44abb8ea5465706543241bd5898353cadf2a3e0917455383283722fdfd4f'
        'SKIP')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF') # Daniel P. Berrange

build() {
  cd virt-viewer-${pkgver}
  arch-meson build \
    -Dlibvirt=enabled \
    -Dvnc=enabled \
    -Dspice=enabled \
    -Dovirt=enabled \
    -Dvte=enabled \
    -Dbash_completion=enabled

  ninja -C build
}

check() {
  cd virt-viewer-${pkgver}

  ninja -C build test
}

package() {
  cd virt-viewer-${pkgver}

  DESTDIR="${pkgdir}" ninja -C build install
}
