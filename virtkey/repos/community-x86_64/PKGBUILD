# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: codl <codl@aquageek.net>
# Contributor: Megamixman <Megamixman@gmail.com>

pkgbase=virtkey
pkgname=('python2-virtkey' 'python-virtkey')
pkgver=0.61.0
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/virtkey"
license=('LGPL')
makedepends=('python2' 'python' 'libxtst' 'gtk2')
source=(http://launchpad.net/$pkgbase/${pkgver%.*}/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('536d30acb2c8ddadc6b4a26fc01e8390')

build() {
  cd "$srcdir"

  cp -a {,python2-}$pkgbase-$pkgver

  # Build python 2 module
  cd "$srcdir/python2-$pkgbase-$pkgver"
  python2 setup.py build

  # Build python 3 module
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build
}

package_python2-virtkey() {
  pkgdesc="Python 2 extension for emulating keypresses and getting layout information from the X server"
  depends=('python2' 'libxtst' 'gtk2')

  cd "$srcdir/python2-$pkgbase-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}

package_python-virtkey() {
  pkgdesc="Python extension for emulating keypresses and getting layout information from the X server"
  depends=('python' 'libxtst' 'gtk2')

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py install --root=$pkgdir/ --optimize=1
}
