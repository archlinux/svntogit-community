# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=virtualbox-modules-arch
pkgname=('virtualbox-host-modules-arch' 'virtualbox-guest-modules-arch')
pkgver=5.0.20
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux>=4.5' 'linux<4.6'
             'linux-headers>=4.5' 'linux-headers<4.6'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver")

# remember to also adjust the .install files and the package deps below
_extramodules=extramodules-4.5-ARCH

package_virtualbox-host-modules-arch(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Virtualbox host kernel modules for Arch Kernel'
  depends=('linux>=4.5' 'linux<4.6')
  replaces=('virtualbox-modules' 'virtualbox-host-modules')
  conflicts=('virtualbox-modules' 'virtualbox-host-modules'
			 'virtualbox-host-dkms')
  provides=('VIRTUALBOX-HOST-MODULES')
  install=virtualbox-modules-arch.install

  cd "/var/lib/dkms/vboxhost/${pkgver}_OSE/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"

  # compress earch modules individually
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +

  # systemd module loading
  install -Dm644 /dev/null "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
  printf "vboxdrv\nvboxpci\nvboxnetadp\nvboxnetflt\n" >  \
    "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}

package_virtualbox-guest-modules-arch(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Virtualbox guest kernel modules for Arch Kernel'
  license=('GPL')
  depends=('linux>=4.5' 'linux<4.6')
  replaces=('virtualbox-archlinux-modules' 'virtualbox-guest-modules')
  conflicts=('virtualbox-archlinux-modules' 'virtualbox-guest-modules'
             'virtualbox-guest-dkms')
  provides=('VIRTUALBOX-GUEST-MODULES')
  install=virtualbox-modules-arch.install

  cd "/var/lib/dkms/vboxguest/${pkgver}_OSE/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"

  # compress earch modules individually
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +

  # systemd module loading
  install -Dm644 /dev/null "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
  printf "vboxguest\nvboxsf\nvboxvideo\n" >  \
    "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:

