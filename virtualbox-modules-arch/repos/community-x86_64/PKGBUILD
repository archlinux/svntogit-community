# Maintainer: SÃ©bastien Luttringer
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=virtualbox-modules-arch
pkgname=('virtualbox-host-modules-arch' 'virtualbox-guest-modules-arch')
pkgver=6.1.4
pkgrel=3
arch=('x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-headers'
             "virtualbox-host-dkms=$pkgver"
             "virtualbox-guest-dkms=$pkgver")

build() {
    _kernver=$(</usr/src/linux/version)

    dkms build --dkmstree "$srcdir" -m vboxhost/${pkgver}_OSE -k $_kernver
    dkms build --dkmstree "$srcdir" -m vboxsf/${pkgver}_OSE -k $_kernver
}

package_virtualbox-host-modules-arch(){
  _kernver="$(</usr/src/linux/version)"
  pkgdesc='Virtualbox host kernel modules for Arch Kernel'
  depends=('linux')
  replaces=('virtualbox-modules' 'virtualbox-host-modules')
  conflicts=('virtualbox-modules' 'virtualbox-host-modules'
             'virtualbox-host-dkms')
  provides=('VIRTUALBOX-HOST-MODULES')

  install -Dt "$pkgdir/usr/lib/modules/$_kernver/extramodules" -m0644 \
    vboxhost/${pkgver}_OSE/$_kernver/$CARCH/module/*

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

  # systemd module loading
  printf '%s\n' vboxdrv vboxnetadp vboxnetflt |
    install -D -m0644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}

package_virtualbox-guest-modules-arch(){
  _kernver="$(</usr/src/linux/version)"
  pkgdesc='Virtualbox guest kernel modules for Arch Kernel'
  depends=('linux')
  replaces=('virtualbox-archlinux-modules' 'virtualbox-guest-modules')
  conflicts=('virtualbox-archlinux-modules' 'virtualbox-guest-modules'
             'virtualbox-guest-dkms')
  provides=('VIRTUALBOX-GUEST-MODULES')

  install -Dt "$pkgdir/usr/lib/modules/$_kernver/extramodules" -m0644 \
    vboxsf/${pkgver}_OSE/$_kernver/$CARCH/module/*

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +
}

# vim:set sw=2 et:
