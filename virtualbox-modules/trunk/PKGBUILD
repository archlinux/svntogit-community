# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: SÃ©bastien Luttringer

pkgbase=virtualbox-modules
pkgname=('virtualbox-host-modules' 'virtualbox-guest-modules')
pkgver=4.2.12
pkgrel=6
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux'
             'linux-headers'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver"
             'expac'
             'sed'
             'bc')

build() {
  _kernmaj=$(expac -Q '%v' linux|sed -r 's/([[:digit:]]+\.[[:digit:]]+).*/\1/')
  _kernver=$(< /usr/lib/modules/extramodules-$_kernmaj-ARCH/version)
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-host-modules(){
  _kernmaj=$(expac -Q '%v' linux|sed -r 's/([[:digit:]]+\.[[:digit:]]+).*/\1/')
  _kernver=$(< /usr/lib/modules/extramodules-$_kernmaj-ARCH/version)
  pkgdesc='Host kernel modules for VirtualBox'
  depends=("linux>=$_kernmaj" "linux<$(echo "$_kernmaj+0.1"|bc)")
  replaces=('virtualbox-modules')
  conflicts=('virtualbox-modules')
  install=virtualbox-host-modules.install

  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-ARCH/"
  install -m644 * "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-ARCH/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/VERSION='.*'/VERSION='$_kernver'/" "$startdir/virtualbox-host-modules.install"
}

package_virtualbox-guest-modules(){
  _kernmaj=$(expac -Q '%v' linux|sed -r 's/([[:digit:]]+\.[[:digit:]]+).*/\1/')
  _kernver=$(< /usr/lib/modules/extramodules-$_kernmaj-ARCH/version)
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=("linux>=$_kernmaj" "linux<$(echo "$_kernmaj+0.1"|bc)")
  replaces=('virtualbox-archlinux-modules')
  conflicts=('virtualbox-archlinux-modules')
  install=virtualbox-guest-modules.install

  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-ARCH/"
  install -m644 * "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-ARCH/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/VERSION='.*'/VERSION='$_kernver'/" "$startdir/virtualbox-guest-modules.install"
}

# vim:set ts=2 sw=2 et:
