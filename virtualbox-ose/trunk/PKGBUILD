# $Id$
#Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgbase=('virtualbox-ose')
pkgname=('virtualbox-ose' 'virtualbox-ose-additions' 'virtualbox-ose-additions-modules')
pkgver=3.1.8
pkgrel=1
arch=('i686')
url='http://virtualbox.org'
license=('GPL' 'custom')
makedepends=('libstdc++5' 'bin86' 'dev86' 'iasl' 'libxslt' 'libxml2'
             'libxcursor' 'qt' 'libidl2' 'sdl_ttf' 'alsa-lib' 'pulseaudio'
             'hal' 'libxtst' 'xalan-c' 'sdl' 'libxmu' 'curl' 'python' 'kernel26-headers>=2.6.33' 'mesa' 'libxrandr')
source=(http://download.virtualbox.org/virtualbox/$pkgver/VirtualBox-$pkgver-OSE.tar.bz2
        '60-virtualbox.rules' '60-vboxguest.rules' 'LocalConfig.kmk' 'vboxdrv.sh' 'gcc45.patch')

build() {
    cd "$srcdir/VirtualBox-${pkgver}_OSE"
    patch -Np0 -i ${srcdir}/gcc45.patch || return 1

    cp "$srcdir/LocalConfig.kmk" .
    sed -i 's/python2.4/python2.6/g' configure

    ./configure || return 1
    source ./env.sh
    kmk all || return 1

    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"
    make || return 1
    cd vboxvideo
    make || return 1
}

package_virtualbox-ose() {
    pkgdesc="Powerful x86 virtualization for enterprise as well as home use (Open Source Edition)"
    depends=('libxcursor' 'libidl2' 'libxslt' 'sdl' 'libxmu' 'curl' 'kernel26-headers')
    optdepends=('qt: For VirtualBox GUI'
                'libgl: For Shared OpenGL'
                'libxt: For Shared Clipboard'
	        'pulseaudio: For PulseAudio Support'
	        'python: For Python Support')
    provides=("virtualbox=${pkgver}")
    replaces=('virtualbox-modules')
    conflicts=('virtualbox-modules<=3.1.6-2')
    install=virtualbox.install

    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin"
    mkdir -p "$pkgdir"/usr/{bin,lib/virtualbox/components,share/virtualbox/nls}

    #Binaries and Wrapper with Launchers
    install -m 0755 VBox.sh "$pkgdir/usr/bin/VBox"
    ln -sf VBox "$pkgdir/usr/bin/VBoxHeadless"
    ln -sf VBox "$pkgdir/usr/bin/VBoxManage"
    ln -sf VBox "$pkgdir/usr/bin/VBoxSDL"
    ln -sf VBox "$pkgdir/usr/bin/VirtualBox"

    install -m 0755 VBoxTunctl "$pkgdir/usr/bin" || return 1

    #components
    install -m 0755 components/* -t "$pkgdir/usr/lib/virtualbox/components" || return 1

    #lib
    install -m 0755 *.so "$pkgdir/usr/lib/virtualbox" || return 1
    install -m 0644 *.gc *.r0  VBoxEFI*.fd "$pkgdir/usr/lib/virtualbox" || return 1

    #setuid root binaries
    install -m 4755 VBoxHeadless VBoxSDL VBoxNetDHCP VBoxNetAdpCtl VirtualBox VBoxBFE \
	    -t "$pkgdir/usr/lib/virtualbox" || return 1
    #other binaries
    install -m 0755 VBoxManage VBoxSVC VBoxXPCOMIPCD VBoxSysInfo.sh xpidl \
                    VBoxTestOGL EfiThunk \
	    -t "$pkgdir/usr/lib/virtualbox" || return 1

    #language
    install -m 0755 nls/* -t "$pkgdir/usr/share/virtualbox/nls" || return 1

    #icon
    install -D -m 0644 VBox.png "$pkgdir/usr/share/pixmaps/VBox.png" || return 1

    #desktop
    install -D -m 0644 virtualbox.desktop "$pkgdir/usr/share/applications/virtualbox.desktop" || return 1

    #install configuration
    mkdir -p "$pkgdir/etc/vbox"
    echo 'INSTALL_DIR=/usr/lib/virtualbox' > "$pkgdir/etc/vbox/vbox.cfg"

    #udev and licence
    install -D -m 0644 "$srcdir/VirtualBox-${pkgver}_OSE/COPYING" \
            "$pkgdir/usr/share/licenses/virtualbox-ose/LICENSE" || return 1
    install -D -m 0644 "$srcdir/60-virtualbox.rules" \
            "$pkgdir/lib/udev/rules.d/60-virtualbox.rules" || return 1

    #python sdk
    mkdir -p ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/
    pushd sdk/installer
    VBOX_INSTALL_PATH="/usr/lib/virtualBox" python vboxapisetup.py install --root "${pkgdir}" || return 1
    popd
    pushd sdk/bindings/xpcom/python
    cp -r xpcom ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/
    chmod 644 ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/xpcom/*.py
    chmod 644 ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/xpcom/server/*.py
    chmod 644 ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/xpcom/client/*.py
    popd

    #sources for modules
    cp -r src ${pkgdir}/usr/lib/virtualbox
    #install compilation script
    install -D -m 755 "${srcdir}/vboxdrv.sh" "${pkgdir}/etc/rc.d/vboxdrv" || return 1
}

package_virtualbox-ose-additions(){
    pkgdesc="Guest Additions for VirtualBox (Open Source Edition)"
    depends=("virtualbox-ose-additions-modules=$pkgver" 'gcc-libs' 'libxmu')
    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions"

    install -d "$pkgdir/usr/bin"
    install -d "$pkgdir/sbin"
    install -m755 VBoxClient VBoxControl VBoxService "$pkgdir/usr/bin" || return 1
    install -m755 mount.vboxsf "$pkgdir/sbin" || return 1

#    install -m644 "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/50-vboxmouse.conf \
#	   "$pkgdir"/etc/xorg.conf.d/50-vboxmouse.conf || return 1
    install -m755 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/98vboxadd-xclient \
	   "$pkgdir"/usr/bin/VBoxClient-all || return 1
    install -m755 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/vboxclient.desktop \
	   "$pkgdir"/etc/xdg/autostart/vboxclient.desktop || return 1
    install -D vboxmouse_drv_17.so \
	   "$pkgdir/usr/lib/xorg/modules/input/vboxmouse.so" || return 1
    install -D vboxvideo_drv_17.so \
	  "$pkgdir/usr/lib/xorg/modules/drivers/vboxvideo.so" || return 1
    install -m755 -D $srcdir/VirtualBox-${pkgver}_OSE/src/VBox/Additions/linux/installer/90-vboxguest.fdi \
	  "$pkgdir/usr/share/hal/fdi/policy/20thirdparty/90-vboxguest.fdi"  || return 1
    install -d "$pkgdir/usr/lib/"
    install -m755 VBoxOGL*.so "$pkgdir/usr/lib" || return 1
}
package_virtualbox-ose-additions-modules(){
    _kernver=2.6.33-ARCH
    pkgdesc="Kernel modules for VirtualBox Guest Additions"
    license=('GPL')
    install=virtualbox-ose-additions-modules.install
    depends=('kernel26>=2.6.33' 'kernel26<2.6.34')

    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"

    cd vboxguest
    install -D -m644 vboxguest.ko \
	"$pkgdir/lib/modules/$_kernver/misc/vboxguest.ko" || return 1

    cd ../vboxsf
    install -D -m644 vboxsf.ko \
    	"$pkgdir/lib/modules/$_kernver/misc/vboxsf.ko" || return  1

    cd ../vboxvideo
    install -D -m644 vboxvideo.ko \
	"$pkgdir/lib/modules/$_kernver/misc/vboxvideo.ko" || return 1

    install -D -m 0644 "$srcdir/60-vboxguest.rules" \
        "$pkgdir/lib/udev/rules.d/60-vboxguest.rules" || return 1

    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/virtualbox-ose-additions-modules.install" || return 1
}
md5sums=('93b5caaac8571591c21b679987cbe518'
         '641bd2d65f0183ef5897d8dd0322f7bc'
         'ed1341881437455d9735875ddf455fbe'
         '2d65145285050d9e0e47fed01f4001bf'
         'e17eea803dcf9d8eed271fc923ba0033'
         'd6f441a8e0e6aec995a240d2c664b754')
