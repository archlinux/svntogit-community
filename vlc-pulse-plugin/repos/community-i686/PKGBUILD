# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>

pkgname=vlc-pulse-plugin
pkgver=1.1.4.1
pkgrel=1
pkgdesc="The PulseAudio plugin for the VLC media player"
arch=(i686 x86_64)
url="http://www.videolan.org/vlc/"
license=(GPL2)
depends=("vlc=$pkgver" pulseaudio)
provides=("vlc-pulse=$pkgver-$pkgrel")
conflicts=(vlc-pulse)
replaces=(vlc-pulse)
makedepends=(pkg-config "live-media>=2010.01.11")
options=(!libtool)
source=(http://downloads.videolan.org/pub/videolan/vlc/$pkgver/vlc-$pkgver.tar.bz2)
md5sums=('ce17c335b38b322949694313173fcd49')

build() {
  cd "$srcdir/vlc-$pkgver"

  export CFLAGS="-fPIC"
  sed -i -e 's:truetype/freefont:TTF:g' modules/misc/freetype.c

  ./configure --prefix=/usr --disable-rpath --disable-vlc --disable-alsa --disable-oss \
    --disable-sdl --disable-sout --enable-shared --enable-nls --enable-release --disable-jack \
    --enable-pulse --with-live555-tree=/usr/lib/live

  make -C compat libcompat.la
  make -C src libvlccore.la
  make -C modules/audio_output libpulse_plugin.la
}
package() {
  cd "$srcdir/vlc-$pkgver/modules/audio_output"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/lib/vlc/plugins/audio_output/libaout_file_plugin.so"
}
