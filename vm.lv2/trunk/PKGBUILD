# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vm.lv2
pkgver=0.14.0
pkgrel=2
pkgdesc="A virtual machine LV2 plugin bundle"
arch=(x86_64)
url="https://open-music-kontrollers.ch/lv2/vm/"
license=(Artistic2.0)
groups=(lv2-plugins pro-audio)
depends=(glibc libglvnd libx11 lv2-host)
makedepends=(glew glu lv2 meson)
checkdepends=(lv2lint)
source=(https://git.open-music-kontrollers.ch/lv2//$pkgname/snapshot/$pkgname-$pkgver.tar.xz{,.asc})
sha512sums=('d5494c1b38652b3d3324864507047c2da83922f8d6f57e8b3606b869b30d9c1af1b082d0b4b4cac374fc6b6d4f0e0968058149709cb92842628fa0175ed2f023'
            'SKIP')
b2sums=('1f10180521f9ccde1f6f0a415115a712528b3dc089b42647f2ce37bf27ffdf36481a154797c72e197dc497e9fbfab2d1ef7477ac5b44a41f073822ef488506ef'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  arch-meson build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(libGLEW.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
