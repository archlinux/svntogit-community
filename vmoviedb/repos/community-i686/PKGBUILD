# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=vmoviedb
pkgver=0.1.5
pkgrel=8
pkgdesc="A movie collection manager for the Gnome desktop"
arch=('i686' 'x86_64')
url="http://vmoviedb.sourceforge.net"
license=('GPL')
depends=('libglade' 'gconf')
makedepends=('rarian' 'intltool')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        drop-libgnomeui.patch
        fix-gconf-schema.patch)
md5sums=('7bfc10573bb890f3924755f2446dd3b6'
         'a6e219afabcb690a4e1764269e853093'
         '6e8aa1619cfb3571e61c1a6035b69dec')

prepare() {
  cd "$srcdir"/vmoviedb-$pkgver

  # Remove libgnomeui dependency
  patch -Np1 -i ../drop-libgnomeui.patch

  # Fix gconf schema
  patch -Np1 -i ../fix-gconf-schema.patch

  # Fix install paths
  sed -i 's|vmoviedbdocdir = ${prefix}/doc/vmoviedb|vmoviedbdocdir = $(datadir)/doc/vmoviedb|' Makefile.am
  sed -i 's|schemasdir = $(sysconfdir)/gconf/schemas|schemasdir = $(GCONF_SCHEMA_FILE_DIR)|' data/Makefile.am

  autoreconf -fi
  intltoolize --force
}

build() {
  cd "$srcdir"/vmoviedb-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir"/vmoviedb-$pkgver
  make DESTDIR="$pkgdir" install
}
