# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=vmoviedb
pkgver=0.1.5
pkgrel=7
pkgdesc="A movie collection manager for the Gnome desktop"
arch=('i686' 'x86_64')
url="http://vmoviedb.sourceforge.net"
license=('GPL')
depends=('libgnomeui' 'gconf>=2.18.0.1-4' 'desktop-file-utils')
makedepends=('rarian' 'intltool')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('7bfc10573bb890f3924755f2446dd3b6')

build() {
  cd $srcdir/vmoviedb-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $srcdir/vmoviedb-$pkgver
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install

  mkdir -p $pkgdir/usr/share/gconf/schemas
  gconf-merge-schema $pkgdir/usr/share/gconf/schemas/${pkgname}.schemas \
    $pkgdir/etc/gconf/schemas/*.schemas
  rm -f $pkgdir/etc/gconf/schemas/*.schemas
  mv $pkgdir/usr/doc $pkgdir/usr/share/
}
