# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.6.2a
pkgrel=4
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
depends=('drumstick' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'qt5-tools' 'libxkbcommon-x11')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('89ba161c3ca7ae260d02c175d1116bea7cdfdfddb0f303d8b9177eeb3d77ded8f6126bed43efa6c279daec0a987d0be30c280d59f42a7c145167fb54a7b8db16')

prepare() {
  cd "${pkgname}-${pkgver}"
  [ -d b ] || mkdir b
}

build() {
  cd "${pkgname}-${pkgver}/b"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=release
  make
}

package() {
  cd "${pkgname}-${pkgver}/b"
  make DESTDIR="$pkgdir/" install
  cd ..
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -Dm644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"
}
