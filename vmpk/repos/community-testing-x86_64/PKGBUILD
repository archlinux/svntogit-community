# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.7.2
pkgrel=2
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('drumstick' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'qt5-tools' 'libxkbcommon-x11')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('69e31d6fd91a6604b7482b18840ed0b274723f380d7fe59827391e9031c3c670cb4e331c8bff01aacf6271973689f0bdb8c1dad4b749524095b398063634a477')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,ChangeLog,NEWS,README,TODO}
}
