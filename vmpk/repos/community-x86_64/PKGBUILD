# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.5.1
pkgrel=2
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('i686' 'x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
depends=('qt4' 'jack')
makedepends=('cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('07e376936c38c7c244374ff9ddca6a72')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=release
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}
