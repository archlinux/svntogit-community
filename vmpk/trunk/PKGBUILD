# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.8.7
pkgrel=2
pkgdesc="Virtual MIDI Piano Keyboard"
arch=(x86_64)
url="http://vmpk.sourceforge.net/"
license=(GPL3)
groups=(pro-audio)
depends=(gcc-libs glibc libxcb qt5-base qt5-x11extras)
makedepends=(cmake docbook-xsl drumstick qt5-tools)
options=(debug)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/${pkgver/a/}/$pkgname-$pkgver.tar.bz2)
sha512sums=('83867aed8be8e620e18070d5eba9f5e4026b1f39ff9d7fe55e342d4a040c96e2353844296191c0efb46483254b1d2d30824894077562a165f7474d02a882cfc6')
b2sums=('788c70034fcfb53edfb4c6b0faf80c06e2ae096c06819bc727359d4597cb217ca60efc9ebbbb19b223dc0d7106ddd9083abbce881f175b98203f3b6acc9b44e2')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libdrumstick-rt.so libdrumstick-widgets.so)

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname"
}
