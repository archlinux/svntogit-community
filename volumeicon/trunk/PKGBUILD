# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Maato <maato softwarebakery com>

# TODO: Consider adding an install-time message about
#       removing ~/.config/volumeicon/volumeicon if there are
#       problems running volumeicon. Ref FS#34239

pkgname=volumeicon
pkgver=0.5.0
pkgrel=2
pkgdesc='Volume control for your system tray'
arch=('x86_64' 'i686')
url='http://softwarebakery.com/maato/volumeicon.html'
license=('GPL3')
depends=('gtk3' 'alsa-lib' 'libnotify')
makedepends=('intltool')
source=("http://softwarebakery.com/maato/files/volumeicon/$pkgname-$pkgver.tar.gz")
sha256sums=('e5f1179f9ec5ab25e3740e4f6bbe0baa75368f0ae87d370197b3fbefb61bd782')

prepare() {
  cd "$pkgname-$pkgver"

  aclocal
  autoconf
  automake --add-missing
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-notify
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
