# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=3.0.2
pkgrel=2
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
url="https://security.appspot.com/vsftpd.html"
license=('GPL2')
depends=('openssl')
backup=('etc/vsftpd.conf' 'etc/xinetd.d/vsftpd')
install=vsftpd.install
source=(https://security.appspot.com/downloads/$pkgname-$pkgver.tar.gz{,.asc}
        vsftpd.xinetd vsftpd-ssl.socket  vsftpd.socket
        vsftpd.service  vsftpd@.service vsftpd-ssl.service vsftpd-ssl@.service)
sha256sums=('be46f0e2c5528fe021fafc8dab1ecfea0c1f183063a06977f8537fcd0b195e56'
            'SKIP'
            '5909f9b95479429c236170a06879d27d07ddda52d9c4c5543c961500c4cac2e0'
            'd5185e48fffc6253499a55e0fe0f90a3424fc639640af11a9d38df33fb145afe'
            '9fdbfd2ec0207170371ca3cf2b0ddca2dc2fe3d062e5792e0d3e51474c3198c9'
            '50c392f373f8ce37aa226a9af7a6b038a8683ee3e041ebbad1bb483e47e8a1f8'
            'd7b8e4827d4f6bafcbf52f9d2d7380958c7b08bb3f757806aa89d4bc06c9671c'
            'b88a50fc68b3bf746d13c9a777df77791cd3eac6eb7c2df655418071c2adf422'
            '4a55c2468b08d858f71bacf1f4885847bec8e548b0e92088068d9bdd3884af84')

build() {
  cd $pkgname-$pkgver

  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h

  CFLAGS+=' -fPIE' LINK='' LDFLAGS="-fPIE -pie ${LDFLAGS} -Wl,-z,now" make
}

package() {
  cd $pkgname-$pkgver

  install -D -m755 vsftpd $pkgdir/usr/bin/vsftpd
  install -D -m644 vsftpd.conf $pkgdir/etc/vsftpd.conf
  install -D -m644 vsftpd.8 $pkgdir/usr/share/man/man8/vsftpd.8
  install -D -m644 vsftpd.conf.5 $pkgdir/usr/share/man/man5/vsftpd.conf.5
  install -D -m644 $srcdir/vsftpd.xinetd $pkgdir/etc/xinetd.d/vsftpd

  install -D -m644 $srcdir/vsftpd.service $pkgdir/usr/lib/systemd/system/vsftpd.service
  install -D -m644 $srcdir/vsftpd@.service $pkgdir/usr/lib/systemd/system/vsftpd@.service 
  install -D -m644 $srcdir/vsftpd-ssl.service $pkgdir/usr/lib/systemd/system/vsftpd-ssl.service
  install -D -m644 $srcdir/vsftpd-ssl@.service $pkgdir/usr/lib/systemd/system/vsftpd-ssl@.service
  install -D -m644 $srcdir/vsftpd.socket $pkgdir/usr/lib/systemd/system/vsftpd.socket
  install -D -m644 $srcdir/vsftpd-ssl.socket $pkgdir/usr/lib/systemd/system/vsftpd-ssl.socket

  install -d -m755 $pkgdir/usr/share/empty
}
