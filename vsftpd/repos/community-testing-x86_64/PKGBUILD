# $Id$
# Maintainer:  Jonathan Steel <jsteel at archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=3.0.3
pkgrel=3
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
url="https://security.appspot.com/vsftpd.html"
license=('GPL2')
depends=('libcap' 'pam')
optdepends=('logrotate')
backup=('etc/vsftpd.conf' 'etc/xinetd.d/vsftpd' "etc/logrotate.d/$pkgname")
source=(https://security.appspot.com/downloads/$pkgname-$pkgver.tar.gz{,.asc}
        $pkgname-ssl.socket $pkgname.socket $pkgname.service
        $pkgname@.service $pkgname-ssl.service $pkgname-ssl@.service)
md5sums=('da119d084bd3f98664636ea05b5bb398' 'SKIP'
         'fcef2cd7631f73a883a3c3d479b25cb8' 'd05045a1244a1be9f3946578bfd0252d'
         '605e82df215a2f5dab1e1ee22b80460d' '32c9266536204fd8c4917c682b6359f1'
         'c899151c1dbec8ea9051fae4d701f0f3' '987349e5c5570c762fed9e87c0b4e715')
validpgpkeys=('8660FD3291B184CDBC2F6418AA62EC463C0E751C') # Chris Evans

build() {
  cd $pkgname-$pkgver

  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h

  # fix linking to openssl 1.1
  sed -e 's|SSL_library_init|SSL_CTX_new|' -i vsf_findlibs.sh

  CFLAGS+=' -fPIE' LINK='' LDFLAGS="-fPIE -pie ${LDFLAGS} -Wl,-z,now" make
}

package() {
  cd $pkgname-$pkgver

  install -D -m755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -D -m644 $pkgname.conf "$pkgdir"/etc/$pkgname.conf
  install -D -m644 $pkgname.8 "$pkgdir"/usr/share/man/man8/$pkgname.8
  install -D -m644 $pkgname.conf.5 "$pkgdir"/usr/share/man/man5/$pkgname.conf.5
  install -D -m644 EXAMPLE/INTERNET_SITE/$pkgname.xinetd "$pkgdir"/etc/xinetd.d/$pkgname
  install -D -m644 RedHat/$pkgname.log "$pkgdir"/etc/logrotate.d/$pkgname

  install -D -m644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -D -m644 "$srcdir"/$pkgname@.service "$pkgdir"/usr/lib/systemd/system/$pkgname@.service 
  install -D -m644 "$srcdir"/$pkgname-ssl.service "$pkgdir"/usr/lib/systemd/system/$pkgname-ssl.service
  install -D -m644 "$srcdir"/$pkgname-ssl@.service "$pkgdir"/usr/lib/systemd/system/$pkgname-ssl@.service
  install -D -m644 "$srcdir"/$pkgname.socket "$pkgdir"/usr/lib/systemd/system/$pkgname.socket
  install -D -m644 "$srcdir"/$pkgname-ssl.socket "$pkgdir"/usr/lib/systemd/system/$pkgname-ssl.socket

  install -d -m755 "$pkgdir"/usr/share/empty

  for i in BENCHMARKS BUGS Changelog FAQ INSTALL README README.ssl REFS \
           REWARD SPEED TODO TUNING; do
    install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
  done
}
