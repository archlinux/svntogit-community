# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=3.0.0
pkgrel=4
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
url="https://security.appspot.com/vsftpd.html"
license=('GPL2')
depends=('openssl')
backup=('etc/vsftpd.conf' 'etc/xinetd.d/vsftpd')
install=vsftpd.install
source=(https://security.appspot.com/downloads/${pkgname}-${pkgver}.tar.gz{,.asc}
        vsftpd.xinetd vsftpd.d  vsftpd-ssl.socket  vsftpd.socket
        vsftpd.service  vsftpd@.service vsftpd-ssl.service vsftpd-ssl@.service)
sha1sums=('436da30cefa33e22a0266077ab95397e9432e297'
          '2142ac8a84f18ecc1ebac21b9ac07858c35ae6af'
          'c87b4ce56dac15223694a6e86c01ea813b877596'
          '24f268956c24e78be0c234c6d31f41487922eafe'
          'f81dab50243f7e82dc9722ca79b2b24de0882eb2'
          '5b7a7a1e0c04acfcc2dba1346557f1193b9905ba'
          'cde3528825b073d3941c263e9e63b4c298a97c47'
          '3a040a18893a5103a1d4a22e631ee247bde8c8d3'
          '2b0a778d7b2329a6d4c01a64ef6c7d90144cd3ab'
          'e5bd183664008bb0cc0353d4efb2e8e92a365798')

build() {
  cd $srcdir/$pkgname-$pkgver

  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h

  CFLAGS+=' -fPIE' LINK='' LDFLAGS="-fPIE -pie ${LDFLAGS} -Wl,-z,now" make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -D -m755 vsftpd $pkgdir/usr/sbin/vsftpd
  install -D -m644 vsftpd.conf $pkgdir/etc/vsftpd.conf
  install -D -m644 vsftpd.8 $pkgdir/usr/share/man/man8/vsftpd.8
  install -D -m644 vsftpd.conf.5 $pkgdir/usr/share/man/man5/vsftpd.conf.5
  install -D -m644 $srcdir/vsftpd.xinetd $pkgdir/etc/xinetd.d/vsftpd
  install -D -m755 $srcdir/vsftpd.d $pkgdir/etc/rc.d/vsftpd

  install -D -m644 $srcdir/vsftpd.service $pkgdir/usr/lib/systemd/system/vsftpd.service
  install -D -m644 $srcdir/vsftpd@.service $pkgdir/usr/lib/systemd/system/vsftpd@.service 
  install -D -m644 $srcdir/vsftpd-ssl.service $pkgdir/usr/lib/systemd/system/vsftpd-ssl.service
  install -D -m644 $srcdir/vsftpd-ssl@.service $pkgdir/usr/lib/systemd/system/vsftpd-ssl@.service
  install -D -m644 $srcdir/vsftpd.socket $pkgdir/usr/lib/systemd/system/vsftpd.socket
  install -D -m644 $srcdir/vsftpd-ssl.socket $pkgdir/usr/lib/systemd/system/vsftpd-ssl.socket

  install -d -m755 $pkgdir/usr/share/empty
}
