# $Id$
# Maintainer:  Jonathan Steel <jsteel at aur.archlinux.org
# Contributor: BartÅ‚omiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=3.0.2
pkgrel=3
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
url="https://security.appspot.com/vsftpd.html"
license=('GPL2')
depends=('libcap' 'pam')
optdepends=('logrotate')
backup=('etc/vsftpd.conf' 'etc/xinetd.d/vsftpd')
install=$pkgname.install
source=(https://security.appspot.com/downloads/$pkgname-$pkgver.tar.gz{,.asc}
        $pkgname.xinetd $pkgname-ssl.socket $pkgname.socket $pkgname.service
        $pkgname@.service $pkgname-ssl.service $pkgname-ssl@.service
        $pkgname.logrotate)
md5sums=('8b00c749719089401315bd3c44dddbb2' 'SKIP'
         'a8b05df3cf0087ab32245aa1d22e724a' 'fcef2cd7631f73a883a3c3d479b25cb8'
         'd05045a1244a1be9f3946578bfd0252d' 'e36f6a1fdd68e88092677c99f1cbe09b'
         '32c9266536204fd8c4917c682b6359f1' 'c899151c1dbec8ea9051fae4d701f0f3'
         '987349e5c5570c762fed9e87c0b4e715' 'f6276f8b80c13308c1da5310adcf6442')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h

  CFLAGS+=' -fPIE' LINK='' LDFLAGS="-fPIE -pie ${LDFLAGS} -Wl,-z,now" make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -D -m755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -D -m644 $pkgname.conf "$pkgdir"/etc/$pkgname.conf
  install -D -m644 $pkgname.8 "$pkgdir"/usr/share/man/man8/$pkgname.8
  install -D -m644 $pkgname.conf.5 "$pkgdir"/usr/share/man/man5/$pkgname.conf.5
  install -D -m644 "$srcdir"/$pkgname.xinetd "$pkgdir"/etc/xinetd.d/$pkgname
  install -D -m644 "$srcdir"/$pkgname.logrotate "$pkgdir"/etc/logrotate.d/$pkgname

  install -D -m644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -D -m644 "$srcdir"/$pkgname@.service "$pkgdir"/usr/lib/systemd/system/$pkgname@.service 
  install -D -m644 "$srcdir"/$pkgname-ssl.service "$pkgdir"/usr/lib/systemd/system/$pkgname-ssl.service
  install -D -m644 "$srcdir"/$pkgname-ssl@.service "$pkgdir"/usr/lib/systemd/system/$pkgname-ssl@.service
  install -D -m644 "$srcdir"/$pkgname.socket "$pkgdir"/usr/lib/systemd/system/$pkgname.socket
  install -D -m644 "$srcdir"/$pkgname-ssl.socket "$pkgdir"/usr/lib/systemd/system/$pkgname-ssl.socket

  install -d -m755 "$pkgdir"/usr/share/empty
}
