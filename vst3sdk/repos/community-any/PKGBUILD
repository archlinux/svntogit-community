# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vst3sdk
pkgver=3.7.3_build_20
_commit=2cc517019a2681fccf211e6a95776a0af1e16d04 # 3.7.3_build_20
pkgrel=1
pkgdesc="VST 3 Plug-In SDK"
arch=('any')
url="https://github.com/steinbergmedia/vst3sdk"
license=('GPL3' 'BSD')
makedepends=('git')
# upstream does not provide proper tarballs:
# https://github.com/steinbergmedia/vst3sdk/issues/65
source=("git+https://github.com/steinbergmedia/${pkgname}#commit=${_commit}"
        "git+https://github.com/steinbergmedia/vst3_base"
        "git+https://github.com/steinbergmedia/vst3_cmake"
        "git+https://github.com/steinbergmedia/vst3_doc"
        "git+https://github.com/steinbergmedia/vst3_pluginterfaces"
        "git+https://github.com/steinbergmedia/vst3_public_sdk"
        "git+https://github.com/steinbergmedia/vstgui"
        "${pkgname}.pc"
)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '87b9f6ac84af35dfe7e7c33c51243a69afd8c49362b2d7c720556307b71c0eb1cc6e647778c22abf0bdbdea13de4c5796ffa9620723687f3c3fc4c557f36fde1')
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '663f3f78032b0ab19fc8758862c108599d4506b797953a7d8e5cb0808badd5fd4e7273c2e195f4a1d84324352e3c569beeec30145bb36590c959eb1e6dbf8503')

prepare() {
  # upstream does not provide a pkg-config integration:
  # https://github.com/steinbergmedia/vst3sdk/issues/68
  sed -e "s/VERSION/${pkgver}/" -i "${pkgname}.pc"
  cd "$pkgname"
  git submodule init
  git config submodule.base.url ../vst3_base
  git config submodule.cmake.url ../vst3_cmake
  git config submodule.doc.url ../vst3_doc
  git config submodule.pluginterfaces.url ../vst3_pluginterfaces
  git config submodule.public.sdk.url ../vst3_public_sdk
  git config submodule.vstgui4.url ../vstgui
  git submodule update
}

package() {
  cd "$pkgbase"
  install -vDm 644 "../${pkgname}.pc" -t "${pkgdir}/usr/lib/pkgconfig/"
  find base -type f \( -iname "*.cpp" -or -iname "*.h" \) -exec install -vDm 644 {} "${pkgdir}/usr/include/${pkgname}/"{} \;
  find pluginterfaces \( -type f -iname "*.cpp" -or -iname "*.h" \) -exec install -vDm 644 {} "${pkgdir}/usr/include/${pkgname}/"{} \;
  find public.sdk -type f \( -iname "*.cpp" -or -iname "*.h" \) -exec install -vDm 644 {} "${pkgdir}/usr/include/${pkgname}/"{} \;
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 base/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.base.txt"
  install -vDm 644 public.sdk/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.public.sdk.txt"
}
