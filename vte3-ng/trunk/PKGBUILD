# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=vte3-ng
_basever=0.40.2
pkgver=$_basever.a
pkgrel=1
pkgdesc="Enhanced Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!emptydirs')
depends=('gtk3' 'vte-common')
makedepends=('intltool' 'gobject-introspection' 'gtk3' 'vala' 'gtk-doc' 'gperf')
url="https://github.com/thestinger/vte-ng"
source=(https://github.com/thestinger/vte-ng/archive/$pkgver.tar.gz)
sha256sums=('b35aa9a616d0a5f848dd1af2c66e8e46441c9703ae07bee53d5cdcb476c6414f')
provides=(vte3=$_basever vte3-select-text=$_basever)
conflicts=(vte3 vte3-select-text)
replaces=(vte3-select-text)

build() {
  cd "vte-ng-$pkgver"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --enable-gnome-pty-helper
  make
}

package() {
  cd "vte-ng-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir/usr/lib/vte/gnome-pty-helper"
  rm "$pkgdir/etc/profile.d/vte.sh"
}
