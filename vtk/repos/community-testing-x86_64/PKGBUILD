# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Ray Rashif <schiv at archlinux dot org>
# Contributor: Andrzej Giniewicz <gginiu at gmail dot com>
# Contributor: Thomas Dziedzic <gostrc at gmail>

pkgname=vtk
pkgver=8.0.1
_majorver=8.0
pkgrel=4
pkgdesc='A software system for 3D computer graphics, image processing, and visualization'
arch=('x86_64')
url='http://www.vtk.org/'
license=('BSD')
depends=('gcc-libs')
makedepends=('boost' 'cmake' 'doxygen' 'ffmpeg' 'gdal' 'glew' 'gnuplot'
             'hdf5' 'java-environment' 'jsoncpp' 'libharu'
             'mariadb' 'openmpi' 'proj' 'python2-autobahn' 'python2-matplotlib'
             'python2-mpi4py' 'python2-twisted' 'qt5-base' 'qt5-tools'
             'qt5-webkit' 'qt5-x11extras' 'tk' 'unixodbc' 'wget')
optdepends=('python2: python bindings'
            'java-runtime: java bindings'
            'tk: tcl bindings'
            'gnuplot: plotting tools'
            'graphviz: drawing tools'
            'python2-matplotlib: for Matplotlib rendering'
            'python2-twisted: for vtkWeb'
            'python2-autobahn: for vtkWeb'
            'python2-mpi4py: OpenMPI python support'
            'openmpi: OpenMPI support'
            'qt5-x11extras'
            'qt5-webkit: WebKit support'
            'ffmpeg'
            'gdal'
            'glew'
            'hdf5'
            'jsoncpp'
            'libharu'
            'mariadb'
            'proj'
            'unixodbc')
source=("http://www.vtk.org/files/release/${_majorver}/VTK-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${_majorver}/VTKData-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${_majorver}/VTKLargeData-${pkgver}.tar.gz")
options=(staticlibs)
sha512sums=('dd68b5218d3d94179446eb73015dce8ff7d42751acf425c10e2b8a7928ee3c3c217bc671af5c9e1b13bdc459ea1c3cd897a18aac189caffbf6bca6b46faac7c2'
            '3160123e6165c2b6bb474f957ab48f20db7c0820117f819e74816379a4ec493d4f1555b83860322727732e24d54968184b51969eae42bba6074fb10e51e77afa'
            'b5b412e346ec0f1b402b67b631afdc81ec4064d99515fc011e9834abed64931e62f2c9808f8374ddb46adcb963a100f3d23981ffa905b97f0cffb73116acf53b')

prepare() {
  rm -rf "build"
  mkdir "build"
}

build() {
  cd "build"

  # to help cmake find java
  export JAVA_HOME=/usr/lib/jvm/default

  # flags to enable using system libs
  local cmake_system_flags=""
  # TODO: try to use system provided XDMF2, XDMF3, NETCDF, GL2PS
  # VTK fails to compile with recent netcdf-cxx package, VTK should be ported to the latest API
  # VTK does not work with XDMF2 compiled from git. TODO: make vtk compatible with system XDMF library.
  # Note: VTK explicitly disables system GLEW dependency, it uses embedded sources with modifications
  # system gl2ps is not used because of http://www.vtk.org/Bug/view.php?id=16083
  for lib in EXPAT FREETYPE JPEG PNG TIFF ZLIB LIBXML2 OGGTHEORA TWISTED ZOPE SIX AUTOBAHN MPI4PY JSONCPP GLEW HDF HDF5 LIBPROJ4 LIBHARU; do
    cmake_system_flags+="-DVTK_USE_SYSTEM_${lib}:BOOL=ON "
  done

  local _tkver=$(echo 'puts $tcl_version' | tclsh)

  cmake \
    -Wno-dev \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DBUILD_DOCUMENTATION:BOOL=OFF \
    -DDOXYGEN_KEEP_TEMP:BOOL=ON \
    -DDOCUMENTATION_HTML_HELP:BOOL=OFF \
    -DDOCUMENTATION_HTML_TARZ:BOOL=OFF \
    -DBUILD_EXAMPLES:BOOL=ON \
    -DVTK_USE_FFMPEG_ENCODER:BOOL=ON \
    -DVTK_BUILD_ALL_MODULES:BOOL=ON \
    -DVTK_USE_LARGE_DATA:BOOL=ON \
    -DVTK_QT_VERSION:STRING="5" \
    -DVTK_WRAP_JAVA:BOOL=ON \
    -DVTK_WRAP_PYTHON:BOOL=ON \
    -DVTK_WRAP_TCL:BOOL=ON \
    -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
    -DVTK_PYTHON_VERSION="2" \
    -DVTK_CUSTOM_LIBRARY_SUFFIX="" \
    -DVTK_INSTALL_INCLUDE_DIR:PATH=include/vtk \
    -DVTK_INSTALL_TCL_DIR=/usr/lib/tcl${_tkver}/vtk/ \
    ${cmake_system_flags} \
    -DCMAKE_BUILD_TYPE=Release \
    "${srcdir}/VTK-${pkgver}"

  make
}

package() {
  cd "build"

  make DESTDIR="${pkgdir}" install
  # remove generated python files
  rm -rf "${pkgdir}/usr/lib/python2.7/site-packages/"{constantly,incremental}
  # remove xdmf cmake config file
  rm -rf "${pkgdir}/usr/XdmfConfig.cmake"

  # Move the vtk.jar to the arch-specific location
  install -dv "${pkgdir}/usr/share/java/vtk"
  mv -v "${pkgdir}/usr/lib/vtk.jar" "${pkgdir}/usr/share/java/vtk"
  rm -rf "${pkgdir}/usr/lib/vtk-${_majorver}/java"

  # Install license
  install -Dm644 "${srcdir}/VTK-${pkgver}/Copyright.txt" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # Fix path of QtDesigner plugin
  install -dv "${pkgdir}/usr/lib/qt"
  mv "${pkgdir}/usr/plugins" "${pkgdir}/usr/lib/qt/plugins"
}

