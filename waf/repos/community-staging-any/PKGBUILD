# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: XZS <d dot f dot fischer at web dot de>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=waf
pkgver=2.0.18
pkgrel=3
pkgdesc='General-purpose build system modelled after Scons'
url="http://waf.io/"
arch=('any')
license=('BSD')
depends=('python')
makedepends=('unzip')
provides=('python-waf')
source=("https://waf.io/${pkgname}-${pkgver}.tar.bz2"{,.asc}
        'building-waf.md')
sha512sums=('aa102922dd48bd1d2f39208ee84f91330a1a5993a3471667181e3e47817d4cf57b0ff9041c1d75b6648d279de6688c7564670cb76ca19da1bd412d1603389e0a'
            'SKIP'
            'fce4e44c3eff4b87165ad4c6856c028f90437af26ed0d2e7cdef825987a60ebe16afda3238813bc2aa4f4f39df2a705a9df870f93c85e9d755d5d1636f07af70')
validpgpkeys=('8AF22DE5A06822E3474F3C7049B4C67C05277AAA') # Thomas Nagy <tnagy@waf.io>

prepare() {
  local _py_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  cd "$pkgname-$pkgver"
  # make waf find its waflib
  sed -e "/test(i/ s|dirname|'python${_py_ver}/site-packages/waf'|" -i waf-light
  # license needs to be extracted:
  # https://gitlab.com/ita1024/waf/issues/2251
  head -n 30 waf | tail -n 28 | sed -e 's/# //g; s/"""//g' > LICENSE
}

build() {
  cd "$pkgname-$pkgver"
  ./waf-light \
    configure --prefix=/usr \
    build --make-waf

  # removing compiled in vendored library from executable
  sed -i '/^#==>$/,/^#<==$/d' waf
}

package() {
  local _py_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  cd "$pkgname-$pkgver"
  install -vDm 755 waf -t "$pkgdir/usr/bin"
  install -vDm 644 wscript -t "$pkgdir/usr/share/$pkgname"

  # installing, byte-compiling waflib and stripping pkgdir from it
  install -vDm 644 waflib/*.py \
    -t "${pkgdir}/usr/lib/python${_py_ver}/site-packages/waf/waflib"
  install -vDm 644 waflib/Tools/*.py \
    -t "${pkgdir}/usr/lib/python${_py_ver}/site-packages/waf/waflib/Tools"
  install -vDm 644 waflib/extras/*.py \
    -t "${pkgdir}/usr/lib/python${_py_ver}/site-packages/waf/waflib/extras"
  python -m compileall -d "/usr/lib/python${_py_ver}" \
    "${pkgdir}/usr/lib/python${_py_ver}"
  python -O -m compileall -d "/usr/lib/python${_py_ver}" \
    "${pkgdir}/usr/lib/python${_py_ver}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {README,../building-waf}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
