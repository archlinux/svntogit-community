# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=waf
pkgver=1.7.15
pkgrel=2
pkgdesc='General-purpose build system modelled after Scons'
url='http://code.google.com/p/waf/'
arch=('any')
license=('BSD')
depends=('python')
makedepends=('setconf')
provides=('python-waf')
options=('!emptydirs')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2")
sha256sums=('49780032ed2ac0e118030af329cbd5883d2b9cb9831fc632a3e7b2bfd99d3f1e')

prepare() {
  cd "$pkgname-$pkgver"

  # Extracting license
  head -n 30 waf | tail -n 25 > LICENSE
  # Python 3 fix
  sed -i '0,/env python/s//python3/' waf
}

build() {
  cd "$pkgname-$pkgver"

  ./waf-light configure --prefix=/usr
  ./waf-light --make-waf
}

package() {
  cd "$pkgname-$pkgver"

  ./waf-light install -f --destdir="$pkgdir" \
    --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8,eclipse'

  install -Dm755 waf "$pkgdir/usr/bin/waf"

  # Force the generation of .waf.admin files
  cd demos/c
  "$pkgdir/usr/bin/waf" configure build >& /dev/null
  cd ../..

  # Fix weird directory placement (FS#38216, FS#38300)
  mkdir -p "$pkgdir/usr/lib/waf"
  mv "$pkgdir/usr/bin/.waf3-$pkgver-"* "$pkgdir/usr/lib/waf/"
  setconf "$pkgdir/usr/bin/waf" base '"/usr/lib/waf"'

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
