# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wails
pkgver=2.0.0
pkgrel=1
pkgdesc="Create desktop apps using Go and Web Technologies"
arch=(x86_64)
url="https://github.com/wailsapp/wails"
license=(MIT)
depends=(glibc)
makedepends=(go webkit2gtk gtk3)
source=($pkgname-$pkgver.tar.gz::https://github.com/wailsapp/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('7d43a39b4c0b2bc48d95bb4eef1c4fbc7a517cd604858a7f77a19781d740051e18443f6e0612a0695820586c1af4048f5199cef2376c956a314f65a4c38656df')
b2sums=('531dd05cd32de7983aac0ca8ac01040092440094be2f4caabd6391965394ecff24fc6fecc59bfbcf7f446b05bcece500ce333d40a33fb7aa5f66c29f2312e54b')

prepare() {
  mkdir -vp $pkgname-$pkgver/build
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o build/ ./cmd/wails
}

check() {
  cd $pkgname-$pkgver
  go test
}

package() {
  install -vDm 755 $pkgname-$pkgver/build/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,CONTRIBUTORS,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
