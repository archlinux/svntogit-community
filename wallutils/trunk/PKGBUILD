# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=wallutils
pkgver=5.10.0
pkgrel=3
pkgdesc='Utilities for handling resolutions, wallpapers and timed wallpapers'
arch=(x86_64)
url='https://github.com/xyproto/wallutils'
license=(MIT)
depends=(libheif libx11 libxpm wayland)
makedepends=(git go libxcursor libxmu upx xbitmaps xorgproto)
optdepends=('feh: for setting the wallpaper for some window managers that runs under X')
source=("git+$url#commit=3e1caadf5c1a4c73565a643886766415118d7334") # tag: 5.10.0
options=(!strip !lto)
b2sums=('SKIP')

build() {
  make -C $pkgname
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname install
  find "$pkgdir/usr/bin/" -executable -type f -exec upx -q --no-progress {} >/dev/null \;
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
