# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=wallutils
pkgver=5.8.0
pkgrel=1
pkgdesc='Utilities for handling resolutions, wallpapers and timed wallpapers'
arch=(x86_64)
url='https://github.com/xyproto/wallutils'
license=(MIT)
depends=(libx11 wayland)
makedepends=(git gcc-go libxcursor libxmu xbitmaps xorgproto)
optdepends=('feh: for setting the wallpaper for some window managers for X')
source=("git+$url#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  sed -i 's,go build,export GOPATH="$$PWD/../.."; go get -d; go build -buildmode=pie -gccgoflags="-s -w $$LDFLAGS",g' $pkgname/Makefile
}

build() {
  make -C $pkgname
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname install
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
