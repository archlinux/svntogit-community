# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: cs-cam <me.at.camdaniel.com>

pkgname=wammu
pkgver=0.43
pkgrel=1
pkgdesc="A wxPython-based GUI for Gammu"
arch=('any')
url="http://wammu.eu/"
license=('GPL')
depends=('python2-gammu' 'wxpython')
optdepends=('gnome-bluetooth: Bluetooth support'
            'python2-pybluez: Bluetooth support')
source=(https://dl.cihar.com/$pkgname/v0/$pkgname-$pkgver.tar.xz)
sha256sums=('1595d120c7e18674538663d6471ce8fd5b975baf9682ab594f6303b1cf520796')


prepare() {
  cd "$pkgname-$pkgver"

  # python2 fix
  sed -i 's_/usr/bin/env python_/usr/bin/env python2_' setup.py
}

package() {
  cd "$pkgname-$pkgver"

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
