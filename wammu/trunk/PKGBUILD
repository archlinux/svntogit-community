# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: cs-cam <me.at.camdaniel.com>

pkgname=wammu
pkgver=0.41
pkgrel=1
pkgdesc="A wxPython-based GUI for Gammu"
arch=('any')
url="http://wammu.eu/"
license=('GPL')
depends=('python2-gammu' 'wxpython')
optdepends=('gnome-bluetooth: Bluetooth support'
            'python2-pybluez: Bluetooth support')
source=(http://dl.cihar.com/$pkgname/v0/$pkgname-$pkgver.tar.xz)
sha256sums=('a14dd535950d93552b744e0569fd683a5b3585f91e3f170f46f9bff00433f788')


build() {
  cd "$pkgname-$pkgver"

  # python2 fix
  sed -i 's_/usr/bin/env python_/usr/bin/env python2_' setup.py
}

package() {
  cd "$pkgname-$pkgver"

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
