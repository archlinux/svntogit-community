# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: pukyxd 
# Maintainer: Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=warmux
pkgver=11.04.1
pkgrel=5
pkgdesc="A clone of the worms game"
arch=('i686' 'x86_64')
url="http://www.wormux.org/"
license=('GPL')
makedepends=('pkgconfig' 'libxml++' 'sdl_image' 'sdl_gfx>=2.0.24' 'sdl_ttf' 'sdl_mixer' 'sdl_net' 'curl>=7.16.2' 'gcc-libs' 'libxml2')
replaces=('wormux')
source=(http://download.gna.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	include-zlib.patch
	gcc-fix.patch)
md5sums=('26ff65c43a9bb61a3f0529c98b943e35'
         'c8285955e880f80e3c68312715a3061c'
         'ab50d613d3fa3f71262b1d2fc71641b1')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver/.1/}"

  # libpng 1.5 fix
  patch -Np1 -i ../include-zlib.patch
  # gcc fix
  patch -Np1 -i ../gcc-fix.patch

  ./configure --prefix=/usr
  make
}

package() {
  depends=('libxml++' 'sdl_image' 'sdl_gfx>=2.0.19' 'sdl_ttf' 'sdl_mixer' 'sdl_net' 'curl>=7.16.2' 'gcc-libs' 'libxml2' "warmux-data>=${pkgver/.1/}")

  cd "${srcdir}/${pkgname}-${pkgver/.1/}"

  make DESTDIR="${pkgdir}" install
  rm -r ${pkgdir}/usr/share/warmux
}
