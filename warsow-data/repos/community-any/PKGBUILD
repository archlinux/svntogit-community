# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: Babets
# Contributor: Vain

pkgname=warsow-data
pkgver=1.03
pkgrel=1
pkgdesc="a free online multiplayer competitive FPS based on the Qfusion engine (data files)"
url="http://www.warsow.net/"
license=('custom:Warsow Content License')
arch=('any')
source=(http://www.warsow.net:1337/~warsow/1.02/warsow_1.02.tar.gz
        ftp://ftp.archlinux.org/other/community/$pkgname/warsow-basewsw-$pkgver.tar.xz{,.sig})
md5sums=('1f0aa20bb441d6734de2f6ff19b8cb2b'
         'a613495d9ee822cce9e385f5dd7fb332'
         'SKIP')

package() {
    # Create Destination Directories
    install -d $pkgdir/opt/warsow/

    # Move Data to Destination Directory
    cp -r $srcdir/warsow_1.02/basewsw $pkgdir/opt/warsow

    # Install Custom License: Warsow Content License
    install -Dm0644 warsow_1.02/docs/license.txt \
        $pkgdir/usr/share/licenses/${pkgname}/license.txt

    # Update to 1.03
    for file in warsow-basewsw-$pkgver/*; do
        install -m0644 $file "$pkgdir"/opt/warsow/basewsw/
    done
}
