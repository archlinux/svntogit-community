# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: Babets
# Contributor: Vain

pkgname=warsow
pkgver=1.51
pkgrel=1
pkgdesc="Free online multiplayer competitive FPS based on the Qfusion engine"
url="http://www.warsow.net/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('curl' 'libjpeg' 'libvorbis' 'libxinerama' 'libxxf86dga' 'libxxf86vm'
         'sdl' 'warsow-data' 'libxrandr' 'libpng' 'libtheora' 'freetype2' 'libxi')
makedepends=('mesa' 'openal' 'imagemagick' 'gendesk')
optdepends=('openal: for openal audio support')
source=('warsow.launcher'
        'wsw-server.launcher'
        'wswtv-server.launcher' \
        "http://update.warsow.net/mirror/warsow_${pkgver}_sdk.tar.gz")
_genericname=('First person shooter')
_comment=('Online Mulitiplayer Competitive FPS')
md5sums=('ec00081d81ad9802a8ca42fc2eac5498'
         'f73e10c26197178df71b941b10bf83d7'
         'd7e4a69835bbcf801e58307e9d6b951e'
         '0dc21fbc33cfe13b5f038cdd20139f5c')


prepare() {
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name "Warsow" --categories "Game;ActionGame"
}

build() {
  # Compile Warsow
  cd source/source/
  make
}

package() {
  cd source/source/

  # Create Destination Directories
  install -d $pkgdir/opt/warsow/

  # Move Compiled Data to Destination Directory
  cp -r release/* $pkgdir/opt/warsow
  find $pkgdir/opt/warsow -type d | xargs chmod 755 
  find $pkgdir/opt/warsow -type f | xargs chmod 755 # only executable files in there

  install -D -m 0755 $srcdir/warsow.launcher $pkgdir/usr/bin/warsow
  install -D -m 0755 $srcdir/wsw-server.launcher $pkgdir/usr/bin/wsw-server
  install -D -m 0755 $srcdir/wswtv-server.launcher $pkgdir/usr/bin/wswtv-server
  install -D -m 0644 $srcdir/warsow.desktop $pkgdir/usr/share/applications/warsow.desktop
  convert win32/warsow.ico $srcdir/warsow.png
  install -D -m 0644 $srcdir/warsow-0.png $pkgdir/usr/share/pixmaps/warsow.png
}

# vim: ts=2:sw=2
