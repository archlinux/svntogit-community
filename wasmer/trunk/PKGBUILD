# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=3.0.2
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi' 'libxkbcommon')
makedepends=('rust' 'cmake' 'llvm')
checkdepends=('lld' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/v${pkgver}.tar.gz")
sha512sums=('82e5b3f8d071f2f6fe57cfad13e31fd3e29b8ab1bccbebf1124624444e67ce265df4f645dbc4cffa96021f953a8fbaac96a976a928549a83471ee90691aa700e')
options=('staticlibs' '!lto')

build() {
  cd "$pkgname-$pkgver"
  make WASMER_INSTALL_PREFIX=/usr ENABLE_LLVM=1
}

check() {
  cd "$pkgname-$pkgver"
  make test-compilers test-examples
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir"/usr install
}

# vim:set ts=2 sw=2 et:
