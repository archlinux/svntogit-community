# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=2.2.1
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi' 'libxkbcommon')
makedepends=('rust' 'cmake' 'llvm11')
checkdepends=('lld' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/${pkgver}.tar.gz")
sha512sums=('c199a24520d063cf77179823bddba198d470e3f04ea0d21b0cd9f1154a8d777ff8af10e430a34190e7feacc89273d3da11aa1f79c0ed8e417f327a88057045b3')
options=('staticlibs' '!lto')

WASMER_INSTALL_PREFIX=/usr
ENABLE_LLVM=1

build() {
  cd "$pkgname-$pkgver"
  make all
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/usr make install
}

# vim:set ts=2 sw=2 et:
