# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=1.17.1
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('Apache')
source=("https://github.com/watchexec/watchexec/archive/refs/tags/cli-v${pkgver}.tar.gz")
sha256sums=('3bc82174729628010d29c85f2d2c61cc45cef5cc729f13153b1422c8f647d33f')

build() {
  cd "watchexec-cli-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "watchexec-cli-v$pkgver"
  cargo test --locked
}

package() {
  cd "watchexec-cli-v$pkgver"
  install -Dm644 "completions/zsh" "$pkgdir/usr/share/zsh/site-functions/_watchexec"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
