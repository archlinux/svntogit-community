# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=1.20.2
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
arch=('x86_64')
license=('Apache')
source=("https://github.com/watchexec/watchexec/archive/refs/tags/cli-v${pkgver}.tar.gz")
options=('!lto')
sha256sums=('62e2e722a84475436286010edff36c56c4c6fad9e76be4b35381877e93faeac8')

build() {
  cd "watchexec-cli-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "watchexec-cli-v$pkgver"
  cargo test --locked
}

package() {
  cd "watchexec-cli-v$pkgver"
  install -Dm644 "doc/watchexec.1" "$pkgdir/usr/share/man/man1/watchexec.1"
  gzip -9 "$pkgdir/usr/share/man/man1/watchexec.1"
  install -Dm644 "completions/zsh" "$pkgdir/usr/share/zsh/site-functions/_watchexec"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
