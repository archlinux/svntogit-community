# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Beej Jorgensen <beej@beej.us>

pkgname=wavemon
pkgver=0.8.1
pkgrel=1
pkgdesc='Ncurses-based monitoring application for wireless network devices'
url='http://eden-feed.erg.abdn.ac.uk/wavemon/'
arch=('x86_64')
license=('GPL3')
depends=('ncurses' 'libcap' 'libnl')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/uoaerg/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('6614e7591a55f76cc11391d1694ed8ad786adcf564c4cb1a8b52ce2b4eb591f2da239065ce274aa5e065d799479c65d3dabed3c228b851892f0a989b7ac36eb9')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e '/^CFLAGS=/d' -i configure.ac
  sed -r 's|(/share)|\1/doc|g' -i Makefile.in
  sed -r 's|\?=|=|g' -i Makefile.in
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
