# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=waybar
pkgver=0.9.1
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'gtk-layer-shell'
    'gtkmm3'
    'jsoncpp'
    'libjsoncpp.so'
    'libspdlog.so'
    'libdbusmenu-gtk3'
    'libpulse'
    'libmpdclient'
)
makedepends=(
    'cmake'
    'date'
    'meson'
    'scdoc'  # For generating manpages
    'sway'  # Support for workspace and window title modules
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz"
)
sha256sums=('97d61baced26835b49d9366a1316925fc5ec59c4ae9269d85c71c5375a061478')

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr --wrap-mode=nofallback build
    ninja -C build
}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
