# $Id$
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Mark Rosenstand <mark@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Chris Brannon <chris@the-brannons.com>

pkgname=webfs
pkgver=1.21
pkgrel=7
pkgdesc="Simple http server for mostly static content."
arch=(i686 x86_64)
url="http://linux.bytesex.org/misc/webfs.html"
license=("GPL")
depends=('mime-types' 'openssl')
backup=(etc/conf.d/webfsd)
source=(http://dl.bytesex.org/releases/webfs/webfs-${pkgver}.tar.gz
        webfsd.rc webfsd.conf webfs.patch)
md5sums=('6dc125fe160479404147e7bbfc781dbc' '3ae9fcdcf79b193aa88e386055e9ebd7'
         'b2c1ab041a82acd8391b06dc38d986be' '7294edcec2589df04bb775270d56536e')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -i ${srcdir}/webfs.patch
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make prefix=/usr DESTDIR=${pkgdir} install
	install -Dm755 ${srcdir}/webfsd.rc   ${pkgdir}/etc/rc.d/webfsd
	install -Dm644 ${srcdir}/webfsd.conf ${pkgdir}/etc/conf.d/webfsd
}
