# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=websocat
pkgver=1.10.1
pkgrel=2
pkgdesc='Command-line client for web sockets, like netcat/curl/socat for ws://'
url='https://github.com/vi/websocat/'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'openssl' 'gcc-libs')
makedepends=('cargo')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9b1b014246449e4b2314917642a28772409f925190715564739e4d76b87fad3adeaa7518ff9536c06844bbba2854923d0999eaf208bc7b28576016b6110b52b4')
b2sums=('44f4c1fd20cd32cdf86ae1e0590c3b6ab9743e37106ec42af032bf76df1655dad069d0106e4bae4377a57022a66844686ed40447f9f51ae7633b7499b7d669d3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # https://github.com/vi/websocat/issues/159
  mv Cargo.lock.freebsd Cargo.lock
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked --features=ssl
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked --features=ssl
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/websocat -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md doc.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
