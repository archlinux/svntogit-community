# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=websvn
pkgver=2.3.3
_xpkgpath=1380/49056
pkgrel=8
pkgdesc="subversion web interface"
arch=(any)
url="https://www.websvn.info"
license=('GPL')
depends=()
backup=(srv/http/websvn/.htaccess)
source=(http://websvn.tigris.org/files/documents/${_xpkgpath}/websvn-$pkgver.tar.gz
	https://arch.p5n.pp.ru/~sergej/dl/pear4websvn.tar.xz
	https://sources.debian.net/data/main/w/websvn/2.3.3-1.2+deb8u2/debian/patches/30_CVE-2016-2511.patch
	https://sources.debian.net/data/main/w/websvn/2.3.3-1.2+deb8u2/debian/patches/31_CVE-2016-1236.patch)
sha256sums=('67d5cb7ffb087f8a92e3dcad10f16612baac4d15a60d659aa8b6e06200ac8742'
            '94aa28ccff110555ee0b55dab2daf1b36b5449ec8b9e6b545439ab41923324e2'
            '164781875f08caf6863002076ac8b5cf210a621b628e4cc8a4605a838a8e5af9'
            'ce73c20506dcafad2fc3509218df50420980cbd26b034ba403d7446d8261c009')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf lib/pear
  cp -a "$srcdir"/pear lib/pear
  patch -p1 -i "$srcdir"/30_CVE-2016-2511.patch
  patch -p1 -i "$srcdir"/31_CVE-2016-1236.patch
}

package() {
  local _instdir="$pkgdir"/srv/http
  mkdir -p ${_instdir}
  cp -ra "$srcdir"/websvn-$pkgver ${_instdir}/websvn
  echo "deny from all" >${_instdir}/websvn/.htaccess
}
