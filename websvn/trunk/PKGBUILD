# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=websvn
pkgver=2.3.3
_xpkgpath=1380/49056
pkgrel=5
pkgdesc="subversion web interface"
arch=(any)
url="http://www.websvn.info"
license=('GPL')
depends=()
backup=(srv/http/websvn/.htaccess)
source=(http://websvn.tigris.org/files/documents/${_xpkgpath}/websvn-$pkgver.tar.gz
	http://arch.p5n.pp.ru/~sergej/dl/pear4websvn.tar.xz
	https://sources.debian.net/data/main/w/websvn/2.3.3-1.2+deb8u1/debian/patches/30_CVE-2016-2511.patch)
md5sums=('bc1821caf77a3225aa810e8f19400ea6'
         '03690912e6e4acf9d97fc0cc889a7ed7'
         '08f6ffdfd9e5ad1d0f87dfc5b11b0b40')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  rm -rf lib/pear
  cp -a $srcdir/pear lib/pear
  patch -p1 -i $srcdir/30_CVE-2016-2511.patch
}

package() {
  local _instdir=$pkgdir/srv/http
  mkdir -p ${_instdir}
  cp -ra $srcdir/websvn-$pkgver ${_instdir}/websvn
  echo "deny from all" >${_instdir}/websvn/.htaccess
}
