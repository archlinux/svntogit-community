# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jacobo Arvelo <unix4all@ya.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=wesnoth
pkgver=1.10.3
pkgrel=2
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.wesnoth.org/"
depends=('sdl_ttf' 'sdl_net' 'sdl_mixer' 'sdl_image' 'fribidi' 'boost-libs' 'pango' 'lua' "wesnoth-data" 'dbus-core' 'python2')
makedepends=('boost' 'cmake')
install=wesnoth.install
options=(!emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
        wesnoth.tmpfiles.conf
        wesnothd.rc.d
        boost_foreach.patch)
md5sums=('b25354c71d58f82c2c60cc12c6f09c36'
         'b8122f5054e3895c9c054e87460869dc'
         '85659b47d22dfdf4e4d046556973fc3e'
         '07e78c6ece06ed10119fc57ebc16ea9d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 < $srcdir/boost_foreach.patch

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DENABLE_OMP=ON \
      -DENABLE_TOOLS=ON \
      -DMANDIR=share/man \
      -DFIFO_DIR=/var/run/wesnothd
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  cd build
  make DESTDIR="$pkgdir" install

  rm -r $pkgdir/usr/share/applications
  rm -r $pkgdir/usr/share/doc
  rm -r $pkgdir/usr/share/pixmaps
  rm -r $pkgdir/usr/share/wesnoth

  install -Dm644 "$srcdir/wesnoth.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/wesnoth.conf"
  install -Dm755 "$srcdir/wesnothd.rc.d" "$pkgdir/etc/rc.d/wesnothd"
}
