# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jacobo Arvelo <unix4all@ya.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=wesnoth
pkgver=1.14.4
pkgrel=1
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('x86_64')
license=('GPL')
url="http://www.wesnoth.org/"
depends=('sdl2_ttf' 'sdl2_net' 'sdl2_mixer' 'sdl2_image' 'fribidi' 'boost-libs' 'pango' 'lua52' 'dbus' 'python2')
makedepends=('boost' 'cmake' 'git')
replaces=('wesnoth-data')
options=(!emptydirs)
source=("git://github.com/wesnoth/wesnoth.git#tag=${pkgver}"
        wesnothd.tmpfiles.conf
        wesnothd.service)
sha512sums=('SKIP'
            '6e776bf9d4e6f289b1dddbea03b0cc6972af9e926c8305fac823613b101d7f3bc40f30ab73708e9df355b28cc6459b138f3d8015528796ad94d3571d20dd54ed'
            '9d648c8bd13a574364b4dede4b1084a2fe1bf50a38ad04aa1a9f72f168915fc67d29b3c048f881a32f38a785fd4256d48c864346db08326e279237b28d5ca884')

build() {
  cd "$srcdir/$pkgname"

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DENABLE_OMP=ON \
      -DENABLE_TOOLS=ON \
      -DMANDIR=share/man \
      -DFIFO_DIR=/var/run/wesnothd
  make
}

package() {
  cd "$srcdir/$pkgname"

  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/wesnothd.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/wesnothd.conf"
  install -Dm644 "$srcdir/wesnothd.service" "$pkgdir/usr/lib/systemd/system/wesnothd.service"
}
