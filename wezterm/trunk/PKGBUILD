# Maintainer:  Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=wezterm
_pkgver=20210314-114017-04b7cedd
pkgver=20210314.114017.04b7cedd
pkgrel=1
pkgdesc="A GPU-accelerated cross-platform terminal emulator and multiplexer"
arch=('x86_64')
url="https://github.com/wez/wezterm"
license=('MIT')
depends=(
  'fontconfig'
  'hicolor-icon-theme'
  'wayland'
  'libx11'
  'libxkbcommon-x11'
  'xcb-util-keysyms'
  'xcb-util-wm'
  'xcb-util-image'
)
makedepends=('rust' 'cargo' 'cmake' 'git' 'pkgconf' 'python')
source=("git+$url.git#tag=$_pkgver")
sha256sums=('SKIP')

prepare() {
  cd "$pkgname"
  git submodule update --init --recursive
}

build() {
  cd "$pkgname"
	cargo build --release --locked
}

check() {
  cd "$pkgname"
  cargo test --release --locked
}

package() {
	cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-gui" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-mux-server" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/strip-ansi-escapes" -t "$pkgdir/usr/bin"
  install -Dm 644 "assets/icon/terminal.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/org.wezfurlong.$pkgname.png"
  install -Dm 644 "assets/$pkgname.desktop" "$pkgdir/usr/share/applications/org.wezfurlong.$pkgname.desktop"
  install -Dm 644 "assets/$pkgname.appdata.xml" "$pkgdir/usr/share/metainfo/org.wezfurlong.$pkgname.appdata.xml"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
