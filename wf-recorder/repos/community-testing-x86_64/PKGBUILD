# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Lennard Hofmann

pkgname=wf-recorder
pkgver=0.2.1
pkgrel=5
pkgdesc="Screen recorder for wlroots-based compositors such as sway"
arch=("x86_64")
url="https://github.com/ammen99/wf-recorder"
license=('MIT')
depends=(
	'libavcodec.so'
	'libavdevice.so'
	'libavformat.so'
	'libavutil.so'
	'libswresample.so'
	'libswscale.so'
	'ocl-icd'
	'wayland'
)
makedepends=(
	'meson'
	'opencl-headers'
	'scdoc'
	'wayland-protocols'
)
optdepends=(
	'slurp: Select a region to record'
)
options=(debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/ammen99/wf-recorder/archive/v$pkgver.tar.gz"
         ffmpeg5.patch)
sha256sums=('45cf04cf58cf241c22fa2fbb70481a3747ad33e6930e4bdba7b9cc7018789ad1'
            '8012b4f09d3a826d06e1ec880aabbd12068a6fa913fd4d577a88e25a09e17b64')

prepare() {
	# Fix build with FFmpeg 5
	patch -d $pkgname-$pkgver -p1 < ffmpeg5.patch
}

build() {
	meson "$pkgname-$pkgver" build \
		-Dman-pages=enabled \
		-Dopencl=enabled \
		--prefix=/usr \
		--buildtype=plain
	ninja -C build
}

package() {
	DESTDIR="$pkgdir/" ninja -C build install
	install -Dm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
