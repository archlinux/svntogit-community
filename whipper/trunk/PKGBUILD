# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Bruno Pagani <archange@archlinux.org>
# Contributor: Frederik "Freso" S. Olesen <archlinux@freso.dk>
# Contributor: Bastien Traverse <firstname at lastname dot email>
# Contributor: Samantha Baldwin <fuhsaz+git@cryptic.li>
# Contributor: Caleb Reach <jtxx000@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol "Kenji Takahashi" Woźniak <kenji.sx>
# Contributor: Mantas Mikulėnas <grawity@gmail.com>

pkgname=whipper
pkgver=0.10.0
pkgrel=1
pkgdesc="A Unix CD ripper aiming for accuracy over speed -- forked from morituri"
arch=('x86_64')
url="https://github.com/whipper-team/whipper"
license=('GPL3')
depends=(
  cdrdao                # for session, TOC, pregap, and ISRC extraction
  libcdio-paranoia      # for the actual ripping
  libsndfile            # for reading wav files (pulls in flac, also required for reading flac files)
  python-gobject
  python-musicbrainzngs # for metadata lookup
  python-mutagen        # for metadata handling
  python-pycdio         # for storing drive identification in config file
  python-requests
  python-ruamel-yaml    # for log output
  python-setuptools     # for plugin support
  sox                   # for track peak detection
)
checkdepends=(
  python-twisted
)
optdepends=('flac: For free lossless audio codec (FLAC) support')
makedepends=('python-setuptools-scm')
conflicts=('accuraterip-checksum')
provides=('accuraterip-checksum')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
        "c229c01a5821276de9ee275ccfebe99b51a1a8c8.patch")
sha512sums=('728ad98185aa2d29f4fb215a597136691bb2c3590b3cac4f659238f90a3b4328c377fafe830a725b655f050538739f404b3eca853db4c39001d1b9d721dc7fd8'
            '478cf26f36e77ad1fc9cb4f44995f807ce6b7c4810b426053a0439c5f7dab0dfbc32d027474a626af1911c58add1c9f295b41a4fb079dfed91b5c433a5d9da82')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -np1 < "${srcdir}/c229c01a5821276de9ee275ccfebe99b51a1a8c8.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  echo "Version: ${pkgver}" > PKG-INFO
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  # https://github.com/whipper-team/whipper/issues/515
  PYTHONPATH="build/lib.linux-${CARCH}-${python_version}/" python -m unittest discover || echo "Tests failed"
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}"/ --optimize=1 --skip-build
}
