# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands-data
pkgver=18
_realver=build18
pkgrel=1
pkgdesc="Widelands data files"
arch=('any')
url="http://widelands.org/"
license=('GPL')
makedepends=('cmake' 'boost' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'sdl_gfx' 'lua51' 'glew' 'python2')
source=("http://launchpad.net/widelands/${_realver}/build-${pkgver}/+download/widelands-$_realver-src.tar.bz2")
md5sums=('01efe8db8cd25ffeb4f82b8b4ab6652e')

build() {
    cd $srcdir/widelands-$_realver-src

    mkdir -p build/compile && cd build/compile

    cmake ../.. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_DATADIR=share/widelands \
        -DWL_INSTALL_BINDIR=bin
    make
}

package() {
    cd $srcdir/widelands-$_realver-src/build/compile

    make DESTDIR="$pkgdir" install

    rm -r "$pkgdir"/usr/{bin,share/widelands/{ChangeLog,COPYING,CREDITS,VERSION}}
}
