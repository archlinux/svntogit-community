# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=1.0
epoch=1
pkgrel=6
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('x86_64')
url="https://widelands.org/"
license=('GPL')
depends=('sdl2_mixer' 'sdl2_image' 'sdl2_net' 'sdl2_ttf' 'sdl2_gfx' 'lua' 'glew' 'python' 'dbus')
makedepends=('cmake' 'boost' 'mesa' 'ninja' 'git')
replaces=('widelands-data')
source=("$pkgname-$pkgver.tar.gz::https://github.com/widelands/widelands/archive/refs/tags/v${pkgver}.tar.gz"
        widelands-1.0-gcc12-werror.patch
        https://github.com/widelands/widelands/commit/316eaea209754368a57f445ea4dd016ecf8eded6.patch)
sha512sums=('1092750944a8669682097dfbd2b578cdb8fab377f32cb81ce8d85b9d9b2e8ae8c4c73500c0a89be649b360fd7e086e19595eba37cecb8d64679e46f7da13e46e'
            'a303972d15f2ad2fb895992358759f7a3006c81c875afbbbac4cf4ec76ca93d3ad93754b26eea2315bd9c82f2967912019759117c7004843d659d2d767c2f7ee'
            '1b236bf06173aeadd07a3b0074b5ec3e6577923e4c6595587504f73d2c729586f03cc5a8fd256a0cb98ed7f586deb09df534fc8300f15d3a259e64435838e317')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir"/widelands-1.0-gcc12-werror.patch # patch from Gentoo
  patch -Np1 -i "$srcdir"/316eaea209754368a57f445ea4dd016ecf8eded6.patch
}

build() {
  cd $pkgname-$pkgver

  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWL_INSTALL_BASEDIR=/usr \
      -DWL_INSTALL_DATADIR=/usr/share/widelands
  ninja -C build
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir" ninja -C build install

  # Fix wrong locations of some installed files (upstream bug)
  # See also FS#72240
  mkdir -p "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{widelands,wl_map_object_info,wl_map_info,wl_create_spritesheet} "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{VERSION,ChangeLog,CREDITS} "$pkgdir"/usr/share/widelands
  mv "$pkgdir"/share/* "$pkgdir"/usr/share/

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/COPYING "$pkgdir"/usr/share/licenses/$pkgname

}
