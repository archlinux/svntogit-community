# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=16
_realver=build16
pkgrel=3
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('i686' 'x86_64')
url="http://widelands.org/"
license=('GPL')
depends=('sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'sdl_gfx' 'ggz-client-libs' 'lua' 'glew' 'python2')
makedepends=('cmake' 'boost')
source=(http://launchpad.net/$pkgname/build16/$_realver/+download/$pkgname-$_realver-src.tar.bz2
        $pkgname.desktop
        $pkgname.png
        $pkgname.sh)
md5sums=('3d8c28e145b73c64d8ed1625319d25a2'
         '15820bf099fd6f16251fe70a75c534bb'
         '3dfda7e9ca76ca00dd98d745d0ceb328'
         '7cae50aba5ed0cd2cfeea79124637b46')

build() {
    cd $srcdir/$pkgname-$_realver-src

    mkdir -p build/compile && cd build/compile

    cmake ../.. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_DATADIR=share/$pkgname \
        -DWL_INSTALL_BINDIR=bin
    make
}

package() {
    cd $srcdir/$pkgname-$_realver-src/build/compile

    make DESTDIR="$pkgdir" install

    # Install bin, icon and desktop file
    #install -Dm 755 src/$pkgname $pkgdir/usr/share/$pkgname/$pkgname
    #install -Dm 755 $srcdir/$pkgname.sh $pkgdir/usr/bin/$pkgname
    install -Dm644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
    install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
