# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=19
_realver=build${pkgver}
pkgrel=13
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('x86_64')
url="http://widelands.org/"
license=('GPL')
depends=('sdl2_mixer' 'sdl2_image' 'sdl2_net' 'sdl2_ttf' 'sdl2_gfx' 'lua51' 'glew' 'python2' 'boost-libs')
makedepends=('cmake' 'boost' 'mesa')
replaces=('widelands-data')
source=("https://launchpad.net/widelands/${_realver}/build${pkgver}/+download/$pkgname-$_realver-src.tar.bz2"
        $pkgname.desktop
        $pkgname.png
        widelands-gcc7.patch)
sha512sums=('80d70542f30c47498c38c5066ecbfa094cfa16fa3c8c75a950420b334cc019766a90a9f715d326506ce425848da1d75fa6f87e33e3551019c0dd8a4ccf99d666'
            'ef7e54ffba11a2fe54d2cafc0a12fcbe5028e861b9b980f549317b159c8532ee62423af8724b05cfd344017097e022215fa750f4ca9afffa57703d434017d727'
            'f7db9d7584ec8431f2f1857baddeb32cd3ae182343e7f0369e61fb444514bd9c2e6a3931ae34f24cc994840dffbbe4451f33845798f3d582f41b31f451b261ac'
            'a60c6f0309579b167664cf0c1022e6c293dad328c2698793d3bc63e880bc3933db8c708494502e898016443d4ec0deac7bd4f80d201dc681098d8197af98193d')

prepare() {
    # Fix build with GCC 7 - don't turn warnings to errors
    patch -p0 -i widelands-gcc7.patch
}

build() {
    cd $srcdir/$pkgname-$_realver-src

    mkdir -p build/compile && cd build/compile

    # http://site.icu-project.org/download/61#TOC-Migration-Issues
    CXXFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

    cmake ../.. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_BASEDIR=/usr \
        -DWL_INSTALL_DATADIR=/usr/share/widelands
    make
}

package() {
    cd $srcdir/$pkgname-$_realver-src/build/compile

    make DESTDIR="$pkgdir" install

    mkdir -p $pkgdir/usr/bin
    mv $pkgdir/usr/{widelands,wl_map_object_info,wl_render_richtext,wl_map_info} $pkgdir/usr/bin
    mv $pkgdir/usr/{VERSION,ChangeLog,CREDITS} $pkgdir/usr/share/widelands

    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    mv $pkgdir/usr/COPYING $pkgdir/usr/share/licenses/$pkgname

    # Install bin, icon and desktop file
    install -Dm644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
    install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
