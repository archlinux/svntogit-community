# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Ian D, Scott <ian@perebruin.com>

pkgname=wikicurses
pkgver=1.4
pkgrel=2
pkgdesc="A simple curses interface for accessing Wikipedia."
arch=('any')
url="https://github.com/ids1024/wikicurses"
license=('MIT')
depends=('python' 'python-setuptools' 'python-urwid' 'python-beautifulsoup4' 'python-lxml')
backup=('etc/wikicurses.conf')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc")
sha256sums=('97ad9590747b99918aca9895fd105914508c9f143c81e77149b0e5dee5410148'
            'SKIP')
validpgpkeys=('098BF569A360E234CE2B861A4924E10E199B5959') # Ian D. Scott <ian@perebruin.com>

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py build
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 README.md "${pkgdir}"/usr/share/doc/${pkgname}/README.md
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
