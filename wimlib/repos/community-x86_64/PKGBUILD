# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.13.6
pkgrel=1
pkgdesc='A library and program to extract, create, and modify WIM files'
arch=('x86_64')
url='https://wimlib.net'
license=('custom')
depends=('openssl' 'fuse' 'libxml2' 'ntfs-3g')
checkdepends=('attr')
optdepends=(
  'cdrkit: for making ISO image of Windows PE'
  'mtools: for making disk image of Windows PE' 
  'syslinux: for making disk image of Windows PE'
  'cabextract: for extracting Windows PE from the WAIK'
)
provides=('libwim.so')
source=("https://wimlib.net/downloads/$pkgname-$pkgver.tar.gz")
sha512sums=('5aed00af2071f433a6f7c468d0f8c52fa8e202e69f5f5234976bf2f5f4e498a2bb30840c81287c445fa77ce5d8b91550a67b1c1959f7a0bcd3229a0c44fa0096')
b2sums=('ecf85b08f98a01b8f4af3db910bc09de467c775af33279a75aa4f03159d8e1a6107b379923cac152dd918514e2106059900d4603498721dd8b985910e6627a89')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --disable-static \
    --with-libcrypto \
    --with-fuse \
    --with-ntfs-3g \
    --prefix=/usr

  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # documentation
  install -Dm644 NEWS README -t "$pkgdir/usr/share/doc/$pkgname"

  # licenses
  install -Dm644 COPYING* -t "$pkgdir/usr/share/licenses/$pkgname"
}
