# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Allan McRae (allan <at> archlinux <dot> org)
# Contributor: Alexander 'bas' Brovikov (bas <at> it-core <dot> org)

pkgname=wine_gecko
pkgver=2.47
pkgrel=1
pkgdesc="Wine's built-in replacement for Microsoft's Internet Explorer"
arch=('x86_64')
url="http://wiki.winehq.org/Gecko"
license=('MPL')
depends=('wine')
source=(http://dl.winehq.org/wine/wine-gecko/$pkgver/wine_gecko-$pkgver-x86.msi)
source_x86_64=(http://dl.winehq.org/wine/wine-gecko/$pkgver/wine_gecko-$pkgver-x86_64.msi)
md5sums=('5ebc4ec71c92b3db3d84b334a1db385d')
md5sums_x86_64=('d93ac0d2e6aceafe9113a9918916df45')

package() {
  _geckodir="$pkgdir/usr/share/wine/gecko"

  install -Dm644 $pkgname-$pkgver-x86.msi "$_geckodir/$pkgname-$pkgver-x86.msi"
  if [[ $CARCH == x86_64 ]]; then
    install -m644 $pkgname-$pkgver-x86_64.msi "$_geckodir/"
  fi
}
