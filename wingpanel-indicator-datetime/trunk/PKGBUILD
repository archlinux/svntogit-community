# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-datetime
pkgver=2.3.0
pkgrel=1
pkgdesc='Date & Time indicator for Wingpanel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-datetime
license=(GPL3)
groups=(pantheon)
depends=(
  cairo
  evolution-data-server
  glib2
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
  libical
  libwingpanel-2.0.so
)
makedepends=(
  git
  meson
  vala
  wingpanel
)
_tag=d274dc6395e19a26cfe93893a162df55feff753e
source=(git+https://github.com/elementary/wingpanel-indicator-datetime.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-datetime
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-datetime build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
