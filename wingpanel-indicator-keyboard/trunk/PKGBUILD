# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-keyboard
pkgver=2.4.0
pkgrel=1
pkgdesc='Wingpanel Keyboard Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-keyboard
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  ibus
  libwingpanel-2.0.so
  libxml2
)
makedepends=(
  git
  meson
  vala
  wingpanel
)
_tag=fc7c3900890f08c460a1b0f3e9f751333df83812
source=(git+https://github.com/elementary/wingpanel-indicator-keyboard.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-keyboard
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-keyboard build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
