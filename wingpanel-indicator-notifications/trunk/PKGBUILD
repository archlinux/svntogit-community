# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-notifications
pkgver=6.0.4
pkgrel=2
pkgdesc='Notifications indicator for Wingpanel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-notifications
license=(GPL3)
groups=(pantheon)
depends=(
  dbus-glib
  glib2
  gtk3
  libdbus
  libgranite.so
  libhandy-1.so
  libwingpanel-2.0.so
  libwnck3
)
makedepends=(
  git
  meson
  vala
)
_tag=174a42914ccf31cc354fe54d4df81694e862553d
source=(git+https://github.com/elementary/wingpanel-indicator-notifications.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-notifications

  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
