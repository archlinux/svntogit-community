# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-session
pkgver=2.3.0
pkgrel=1
pkgdesc='Session indicator for Wingpanel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-session
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  gdk-pixbuf2
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  libwingpanel-2.0.so
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_tag=02f09e06d88d30ccc6674ec3c27f6602d64b490f
source=(git+https://github.com/elementary/wingpanel-indicator-session.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-session
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-session build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
