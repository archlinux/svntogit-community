# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=2.3.0
pkgrel=1
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter
)
makedepends=(
  git
  gala
  meson
  vala
)
provides=(libwingpanel-2.0.so)
source=(git+https://github.com/elementary/wingpanel.git#tag=b3e4f96183eb9245d28fdc466616902446b22e60)
sha256sums=(SKIP)

pkgver() {
  cd wingpanel

  git describe --tags
}

build() {
  arch-meson wingpanel build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
