# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=3.0.2
pkgrel=6
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel-2.0.so)
options=(debug)
_tag=34d11b48f7048c44e373b3e25165ea0d26de7a9c
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd wingpanel

  git describe --tags
}

prepare() {
  cd wingpanel

  # mutter 42
  git cherry-pick -n b5e8f8b044a49da4621fdf044dfe21b1213edcde
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
