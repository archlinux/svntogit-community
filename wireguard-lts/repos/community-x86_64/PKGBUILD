# Maintainer: Christian Hesse <mail@eworm.de>

pkgname='wireguard-lts'
pkgver=0.0.20190406
pkgrel=2
_linux_major=4
_linux_minor=19
arch=('x86_64')
url='https://www.wireguard.com/'
license=('GPL')
_linux_cur=${_linux_major}.${_linux_minor}
_linux_next=${_linux_major}.$((_linux_minor + 1))
makedepends=("linux-lts>=$_linux_cur" "linux-lts<$_linux_next"
             "linux-lts-headers>=$_linux_cur" "linux-lts-headers<$_linux_next"
             "wireguard-dkms>=$pkgver")

_extramodules="extramodules-${_linux_major}.${_linux_minor}-lts"

package_wireguard-lts() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Wireguard module for LTS Kernel'
  license=('GPL')
  depends=("linux-lts>=$_linux_cur" "linux-lts<$_linux_next")
  conflicts=('wireguard-dkms')
  provides=('WIREGUARD-MODULE')

  cd "/var/lib/dkms/wireguard/${pkgver}/$_kernver/$CARCH/module"
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m0644 *

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +
}
