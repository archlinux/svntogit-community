# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>
pkgname=('wireshark-cli' 'wireshark-common' 'wireshark-gtk' 'wireshark-qt')
pkgbase=wireshark
pkgver=2.0.3
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('c-ares' 'geoip' 'qt5-base' 'qt5-multimedia' 'gtk3' 'krb5' 'libpcap' 'bash'
             'gnutls' 'portaudio' 'lua52' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
options=(!emptydirs)
url='http://www.wireshark.org/'
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2
        do_not_use_svn_version.patch)
sha1sums=('9805d62b4fb108cc1e755d8c47f8c1e21eee8b73'
          '489a176a7b90f7ae6f0985510c0c9038de54d9d6')

prepare() {
  cd ${pkgbase}-${pkgver}

  # Avoid ugly "svn rev unknown from unknown" version string
  patch -p1 < "$srcdir"/do_not_use_svn_version.patch
}

build() {
  cd ${pkgbase}-${pkgver}

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-qt=5 \
      --with-gtk3=yes \
      --with-pcap \
      --with-zlib \
      --with-lua \
      --with-portaudio \
      --with-ssl \
      --with-krb5 \
      --with-c-ares \
      --with-geoip
  make all
}

package_wireshark-cli() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('c-ares' 'geoip' 'krb5' 'libgcrypt' 'libcap' 'libpcap' 'gnutls' 'glib2' 'lua52')
  install=wireshark.install
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" uninstall-local

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark" "${pkgdir}/usr/bin/wireshark-gtk"

  # Headers
  install -dm755 "${pkgdir}"/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes,wmem},wiretap,wsutil}

  install -m644 color.h config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes epan/wmem wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
  done
}

package_wireshark-common() {
  pkgdesc='Common files used by wireshark-gtk and wireshark-qt'
  depends=('hicolor-icon-theme' 'shared-mime-info' 'xdg-utils')
  install=wireshark-common.install

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install-data-local
  rm "${pkgdir}"/usr/share/applications/wireshark{,-gtk}.desktop
}

package_wireshark-gtk() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('desktop-file-utils' 'gtk3' 'portaudio' 'wireshark-cli' 'wireshark-common')
  replaces=(wireshark)
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}

  install -Dm755 .libs/wireshark-gtk "${pkgdir}/usr/bin/wireshark-gtk"
  install -Dm644 wireshark-gtk.desktop "${pkgdir}/usr/share/applications/wireshark-gtk.desktop"
}

package_wireshark-qt() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - Qt frontend'
  depends=('desktop-file-utils' 'qt5-base' 'qt5-multimedia' 'wireshark-cli' 'wireshark-common')

  cd ${pkgbase}-${pkgver}

  install -Dm755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
