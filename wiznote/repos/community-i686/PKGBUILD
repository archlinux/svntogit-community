# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Albert.Zhou <albert.zhou@wiz.cn>

pkgname=wiznote
pkgver=2.1.15
pkgrel=1
pkgdesc="Opensource cross-platform cloud based note-taking client"
arch=('i686' 'x86_64')
url="http://www.wiznote.com"
license=('GPL3' 'custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'qt5-base' 'qt5-webkit') # indirect: qt5-declarative qt5-sensors zlib glic gcc-libs
makedepends=('cmake' 'qt5-tools' 'git' 'boost')
install=wiznote.install
_wiznote_project_name="WizQTClient"
source=("git+https://github.com/WizTeam/${_wiznote_project_name}.git#branch=v$pkgver")
md5sums=('SKIP')

prepare() {
  rm $_wiznote_project_name/share/skins/default/Thumbs.db

  rm -rf build; mkdir build
}

build() {
  cd build

  cmake \
    -DWIZNOTE_USE_QT5=YES \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../$_wiznote_project_name
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
