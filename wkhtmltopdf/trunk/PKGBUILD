# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=wkhtmltopdf
pkgver=0.9.9
pkgrel=2
pkgdesc="Simple shell utility to convert html to pdf using the webkit rendering engine, and qt"
arch=('i686' 'x86_64')
url="http://code.google.com/p/wkhtmltopdf/"
license=('GPL3')
depends=('qtwebkit')
optdepends=('xorg-server: wkhtmltopdf needs X or Xvfb to operate')
source=(http://wkhtmltopdf.googlecode.com/files/$pkgname-$pkgver.tar.bz2)
sha1sums=('41f598c0103326e7c13101391447b0284b4ba3cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake wkhtmltopdf.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/usr" install

  # Generate and install man page
  install -d "$pkgdir/usr/share/man/man1"
  ./wkhtmltopdf --manpage >"$pkgdir/usr/share/man/man1/wkhtmltopdf.1"
}
