# $Id$
pkgname=wlc
pkgver=0.0.11
pkgrel=1
pkgdesc="Wayland Compositor Library"
url="https://github.com/Cloudef/wlc"
arch=("i686" "x86_64")
license=("MIT")
depends=(
	"libdrm" "pixman" "xcb-util-image" "xcb-util-wm"
	"wayland" "wayland-protocols" "libxkbcommon" "libinput" "libgl"
)
makedepends=("cmake")
source=(
	"https://github.com/Cloudef/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"
	"https://github.com/Cloudef/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2.asc"
)
sha256sums=(
	"c2061f5d6c627688ea8b822ac5f4d3ba4d407b3f8d79267677ce4011b6390d22"
	"SKIP"
)
validpgpkeys=(
	"C2AA3C3A6F9B1F932014879EF769BB961AF6D26A"
	"9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A"  # Drew DeVault <sway@cmpwn.com>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=Upstream \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

check() {
	cd build
	make test
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
