# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Dalius <dagis@takas.lt>

pkgname=wmctrl
pkgver=1.07
pkgrel=4
pkgdesc="Control your EWMH compliant window manager from command line"
url="http://tripie.sweb.cz/utils/wmctrl/"
arch=('x86_64')
license=('GPL')
depends=(glib2 libxmu)
source=(http://tripie.sweb.cz/utils/wmctrl/dist/$pkgname-$pkgver.tar.gz
        http://archive.debian.org/debian/pool/main/w/wmctrl/wmctrl_$pkgver-6.diff.gz)
sha256sums=('d78a1efdb62f18674298ad039c5cbdb1edb6e8e149bb3a8e3a01a4750aa3cca9'
            'c6560ba645652eab0b16d46c9dbd8e5c9881c1f9d3ac6fa3da2ee3c865b5e6a0')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/wmctrl_$pkgver-6.diff"
}

build() {
  cd $pkgname-$pkgver
  ./configure --bindir="$pkgdir/usr/bin" --mandir="$pkgdir/usr/share/man"
  make
}

package() {
  cd $pkgname-$pkgver
  make install
}
