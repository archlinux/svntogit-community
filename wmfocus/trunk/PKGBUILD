# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=wmfocus
pkgver=1.3.0
pkgrel=1
pkgdesc="Visually focus window by label"
url="https://github.com/svenstaro/wmfocus"
arch=("x86_64")
license=("MIT")
depends=("xcb-util-keysyms" "cairo" "libxkbcommon-x11")
makedepends=("rust" "python")
source=("$pkgname-$pkgver.tar.gz::https://github.com/svenstaro/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('5392420d2e55d42705be70b8ea5342ef7fb3dd6b4b8bf1ade17f8a51f2f2adcb65ed479aca97070301c38e8bbfdb01e67b6cd96df58c9caebd027ee1aec5677b')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo build --locked --release --features i3
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo test --locked --release --features i3
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
