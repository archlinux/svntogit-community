# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=workrave
pkgver=1.10.20
pkgrel=2
pkgdesc="Assist in the recovery and prevention of Repetitive Strain Injury (RSI)"
arch=('x86_64')
url="http://www.workrave.org/"
license=('GPL3')
depends=('gstreamer' 'gtkmm3' 'libsm' 'libxss')
makedepends=('autoconf-archive' 'intltool' 'gobject-introspection' 'libpulse' 'python2-cheetah'
             'gnome-panel' 'mate-panel' 'xfce4-panel')
optdepends=('gst-plugins-good: to play sound events'
            'gnome-panel: Applet for GNOME Panel'
            'mate-panel: Applet for MATE Panel'
            'xfce4-panel: Applet for Xfce4 Panel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rcaelers/workrave/archive/v${pkgver//./_}.tar.gz")
sha256sums=('a89c6e82d5bbbaae5e171100b87c4efd8316ab8a18d82b83714035e1116983ec')

prepare() {
  cd $pkgname-${pkgver//./_}
  ./autogen.sh
}

build() {
  cd $pkgname-${pkgver//./_}
  ./configure --prefix=/usr --sysconfdir=/etc  --localstatedir=/var \
              --libexecdir=/usr/lib/$pkgname PYTHON=python2
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-${pkgver//./_}
  make DESTDIR="$pkgdir" install
}
