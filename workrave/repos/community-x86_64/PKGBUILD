# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=workrave
pkgver=1.10.6
pkgrel=2
pkgdesc="a tool to help RSI"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://workrave.org"
depends=('libxtst' 'gtkmm3' 'gdome2' 'gstreamer' 'libsm' 'libice' 'libxss')
makedepends=('intltool' 'gobject-introspection' 'python2-cheetah')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/rcaelers/workrave/archive/v${pkgver//./_}.tar.gz)
md5sums=('6f9ee65b9d6878cac33e7d0a0e51bc38')

build() {
  cd $srcdir/workrave-${pkgver//./_}
  export PYTHON=python2
  ./autogen.sh
  ./configure --prefix=/usr --enable-dbus --sysconfdir=/etc --libexecdir=/usr/lib/workrave \
	--disable-pulse
  make
}

package() {
  cd $srcdir/workrave-${pkgver//./_}
  make DESTDIR=$pkgdir install
}
