# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=workrave
pkgver=1.10.45
pkgrel=1
pkgdesc="Assist in the recovery and prevention of Repetitive Strain Injury (RSI)"
arch=('x86_64')
url="https://workrave.org/"
license=('GPL3')
depends=('gstreamer' 'gtkmm3' 'libsm' 'libxss')
makedepends=('autoconf-archive' 'boost' 'intltool' 'gobject-introspection' 'libpulse' 'python-jinja'
             'gnome-panel' 'mate-panel' 'xfce4-panel')
optdepends=('gst-plugins-good: to play sound events'
            'gnome-panel: Applet for GNOME Panel'
            'mate-panel: Applet for MATE Panel'
            'xfce4-panel: Applet for Xfce4 Panel')
source=("https://github.com/rcaelers/workrave/releases/download/v${pkgver//./_}/$pkgname-v$pkgver.tar.gz")
sha256sums=('dafee5e7004086189c9c2962041b4d9e2b195c76dc8fa285e8ecbf84e8c7314d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc  --localstatedir=/var \
              --libexecdir=/usr/lib/$pkgname
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
