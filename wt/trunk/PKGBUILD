# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=3.2.3
_pkgver=3.2.3
pkgrel=5
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu')
makedepends=('boost' 'cmake>=2.8' 'postgresql-libs' 'zlib' 'fcgi' 'sqlite' 'graphicsmagick' 'pango' 'mysql++' 'qtbase')
optdepends=('openssl: for SSL support in built-in webserver'
	    'zlib: for compression in HTTP protocol'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'graphicsmagick: for raster image support (WRasterImage)'
	    'pango: for advanced font rendering (WRasterImage)'
	    'mysql++: for the hangman example'
	    'qt: for the Wt/Qt interopability example (wtwithqt)')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/$pkgname-${_pkgver}.tar.gz)
md5sums=('58922ba5d68104880adbe2e022132fb5')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}

  [[ -d build ]] && rm -r build
  mkdir -p build && cd build
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      ..
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}/build
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/share/cmake-2.8/Modules
  mv ${pkgdir}/usr/cmake/FindWt.cmake ${pkgdir}/usr/share/cmake-2.8/Modules/
  rm -r ${pkgdir}/usr/cmake
  rm -rf $pkgdir/var/run
}
