# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=3.1.11
pkgrel=1
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs>=1.36')
makedepends=('boost>=1.36' 'cmake>=2.8'
	     'postgresql-libs' 'zlib' 'fcgi' 'sqlite3' 'libharu' 'graphicsmagick' 'pango' 'mysql++' 'qt')
optdepends=('openssl: for SSL support in built-in webserver'
            'zlib: for compression in HTTP protocol'
            'fcgi: for FastCGI support'
            'postgresql-libs: for PostgreSQL Dbo support'
            'sqlite3: for Sqlite3 Dbo support'
            'libharu: for PDF generation (WPdfImage)'
            'graphicsmagick: for raster image support (WRasterImage)'
            'pango: for advanced font rendering (WRasterImage)'
            'mysql++: for the hangman example'
            'qt: for the Wt/Qt interopability example (wtwithqt)')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/$pkgname-${pkgver}.tar.gz)
md5sums=('0e1701db78a899d1f7e943098678cc7a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  mkdir -p build
  cd build
  cmake -DCONNECTOR_HTTP=ON -DCMAKE_INSTALL_PREFIX=/usr \
    -DWEBUSER=http -DWEBGROUP=http -DRUNDIR=/var/run/wt -DDEPLOYROOT=/var/www/wt \
    -DUSE_SYSTEM_SQLITE3=ON -DCMAKE_EXE_LINKER_FLAGS="-lboost_random" \
    -DDESTDIR=${pkgdir} -DWT_CMAKE_FINDER_INSTALL_DIR="share/cmake-2.8/Modules" ..
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/build
  make DESTDIR=${pkgdir} install
}
