# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=4.1.0
pkgrel=2
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu' 'graphicsmagick' 'pango' 'zlib' 'libgl' 'harfbuzz' 'qt5-base')
makedepends=('boost' 'cmake' 'postgresql-libs' 'fcgi' 'sqlite' 'mysql++' 'doxygen' 'graphviz' 'mesa' 'glu')
optdepends=('openssl: for SSL support in built-in webserver'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
        'mariadb-libs: for MySQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'mysql++: for the hangman example')
backup=('etc/wt/wt_config.xml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kdeforche/wt/archive/${pkgver}.tar.gz"
        5611d045c3e279c1101afdc07ab7ee1bdd5203e3.patch)
sha512sums=('99570bec1e4d7184dd236d50f6c05d93207548152695d66c6bafc82be3632985ca830b7393471efda429d2e5a08707cb9236961c50cc99fbd84120598bd09c31'
            '46564c8fd7c7faacdab423a72dfb624ab0422db4c6f3ab74098a802ab8914e2964514e9c27add49a7d6aba418e849585e7c883c4a83aa7e25333fd1d394b73c2')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver}

  patch -Np1 -i "$srcdir/"5611d045c3e279c1101afdc07ab7ee1bdd5203e3.patch

  mkdir "$srcdir"/${pkgname}-${pkgver}/build
}

build() {
  cd "$srcdir"/${pkgname}-${pkgver}/build

  cmake \
      -DCONNECTOR_HTTP=ON \
      -DWT_WRASTERIMAGE_IMPLEMENTATION=GraphicsMagick \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      -DBUILD_EXAMPLES=ON \
      ..
  VERBOSE=1 make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}/build

  make DESTDIR="$pkgdir" install
}
