# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=wvstreams
pkgver=4.6.1
pkgrel=18
pkgdesc="A network programming library written in C++"
arch=('x86_64')
url="https://web.archive.org/web/20110504183753/http://alumnit.ca:80/wiki/index.php?page=WvDial"
license=('LGPL')
depends=('zlib' 'pam' 'xplc' 'openssl-1.0' 'readline')
source=(https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/wvstreams/wvstreams-$pkgver.tar.gz
        wvstreams-4.6.1-glibc212.patch
        wvstreams-4.6.1-gcc47.patch
        wvstreams-4.6.1-gcc10.patch
        openssl-buildfix.patch
        gcc-6.patch)
sha256sums=('8403f5fbf83aa9ac0c6ce15d97fd85607488152aa84e007b7d0621b8ebc07633'
            '278c73140b963daa8aae85dfbba4917e2963e72c187bbb97b6252a5a70f54bc2'
            '7b83dad8e3931686962823a7008af0034a430f35e1eb617f5c8137141e14ccdd'
            '25aa17989936479a095837a354e0f0727f7a6fa393967a86f2e67e310a72e944'
            '2deb3a5aaacd1d2c4e2ce24cdb8182f99ff0cfc68eb1315dc59a0ec01bc09278'
            '1f750516825dc8c3ef7de980b90dadf22c5132eab828a5dd634fa2a88624b6d9')
options=('!makeflags')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p0 -i "$srcdir/wvstreams-4.6.1-glibc212.patch"
  patch -p0 -i "$srcdir/wvstreams-4.6.1-gcc47.patch"
  patch -p1 -i "$srcdir/openssl-buildfix.patch"
  patch -p1 -i "$srcdir/gcc-6.patch"
  patch -p1 -i "$srcdir/wvstreams-4.6.1-gcc10.patch" # Gentoo patch
}

build() {
  cd ${pkgname}-${pkgver}

  CPPFLAGS+=" -I/usr/include/openssl-1.0" \
  LDFLAGS+=" -L/usr/lib/openssl-1.0" \
  ./configure --prefix=/usr \
     --sbin=/usr/bin \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --without-dbus \
     --with-pam \
     --with-openssl \
     --without-tcl \
     --without-qt
  make COPTS="$CFLAGS -ffat-lto-objects -fPIC" CXXOPTS="$CXXFLAGS -ffat-lto-objects -fPIC -fpermissive -fno-tree-dce -fno-optimize-sibling-calls" VERBOSE=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # FS#15974
  # --without-dbus still installs the pkg-config file
  rm "${pkgdir}/usr/lib/pkgconfig/libwvdbus.pc"
  # --without-qt still installs the pkg-config file
  rm "${pkgdir}/usr/lib/pkgconfig/libwvqt.pc"
}
