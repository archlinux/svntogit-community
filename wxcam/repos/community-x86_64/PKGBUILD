# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=wxcam
pkgver=1.1
pkgrel=10
arch=('i686' 'x86_64')
license=(GPL)
pkgdesc="Webcam application for linux"
url="http://wxcam.sourceforge.net"
depends=('mjpegtools' 'wxgtk3' 'xvidcore' 'v4l-utils' 'alsa-lib' 'cimg' 'ffmpeg')
source=(wxcam.tar.gz::"http://wxcam.cvs.sourceforge.net/viewvc/wxcam/wxcam/?view=tar"
        gtk3-fix.patch)
md5sums=('SKIP'
         'e48a79b8e444e719672336b40d09f275')

prepare() {
    cd "$srcdir"/$pkgname

    # Build with wxgtk3
    patch -Np1 -i ../gtk3-fix.patch
    sed -i "s/wx-config /wx-config-gtk3 /" nbproject/*

    sed -i "s/^DEFAULTCONF.*$/DEFAULTCONF=Release/g" nbproject/Makefile-impl.mk
}

build() {
    cd "$srcdir"/$pkgname
    make
}

package() {
    cd "$srcdir"/$pkgname
    install -Dm755 bin/wxcam "$pkgdir"/usr/bin/wxcam
}
