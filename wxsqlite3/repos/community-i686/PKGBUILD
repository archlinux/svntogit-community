# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: napa3um <napa3um@gmail.com>
# Contributor: Scott Furry <scott.wl.furry@gmail.com>

pkgname=wxsqlite3
pkgver=3.5.5
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('i686' 'x86_64')
url="http://utelle.github.io/wxsqlite3"
license=('custom:wxWindows')
depends=('wxgtk3')
source=($pkgname-$pkgver.tar.gz::https://github.com/utelle/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('303aba23d5d3349335f6a9e56a35f0c55641b4af9c330f7074def71384e49303')

build() {
  cd $pkgname-$pkgver

  autoreconf
  ./configure --with-wx-config=/usr/bin/wx-config-gtk3 --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# license
  install -Dm644 LICENCE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
