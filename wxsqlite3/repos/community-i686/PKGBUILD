# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: napa3um <napa3um@gmail.com>

pkgname=wxsqlite3
pkgver=3.2.1
pkgrel=2
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('i686' 'x86_64')
url="http://wxcode.sourceforge.net/components/wxsqlite3/"
license=('custom:wxWindows')
depends=('wxgtk' 'sqlite3')
source=("http://downloads.sourceforge.net/wxcode/$pkgname-$pkgver.tar.gz")
md5sums=('2ac2508d05901a8a8e9de9915ed7a7f9')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 LICENCE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
