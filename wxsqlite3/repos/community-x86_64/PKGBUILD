# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: napa3um <napa3um@gmail.com>
# Contributor: Scott Furry <scott.wl.furry@gmail.com>

pkgname=wxsqlite3
pkgver=3.5.9
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('x86_64')
url="http://utelle.github.io/wxsqlite3"
license=('custom:wxWindows')
depends=('wxgtk3')
source=($pkgname-$pkgver.tar.gz::https://github.com/utelle/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('42cd64d978496d81e2e875ecdeed63abc2513c2eb810b570e6c84247a055d026')

build() {
  cd $pkgname-$pkgver

  autoreconf
  ./configure --with-wx-config=/usr/bin/wx-config-gtk3 --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
