# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: napa3um <napa3um@gmail.com>
# Contributor: Scott Furry <scott.wl.furry@gmail.com>

pkgname=wxsqlite3
pkgver=4.8.2
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('x86_64')
url="https://utelle.github.io/wxsqlite3"
license=('custom:LGPL-3.0+ with WxWindows-exception-3.1')
depends=('wxwidgets-gtk3')
source=($pkgname-$pkgver.tar.gz::https://github.com/utelle/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('8992dff294e6f1bed72cf15b7d5e6f91266778f899e7dd68511b8fd09389bfd2')

build() {
  cd $pkgname-$pkgver

  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# license
  install -Dm644 LICENCE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENCE
}
