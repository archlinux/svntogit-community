# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Leonidas Spyropoulos <artafinde at gmail com>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Joshua Stiefer <facedelajunk@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=wxsvg
pkgver=1.5.23
pkgrel=1
pkgdesc='C++ library to create, manipulate and render SVG files'
arch=('x86_64')
url='http://wxsvg.sourceforge.net/'
license=('custom:wxWindows')
depends=('ffmpeg' 'libexif' 'wxgtk3')
makedepends=('webkit2gtk')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('3f07361facc7d18cc19a940dd73c78142ed5eec38c14d993a85bf0c061dcabb9')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/libwxsvg_la_LDFLAGS = /libwxsvg_la_LDFLAGS = $(LDFLAGS) /' src/Makefile.am
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
