# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Chris Mosher <cmosher01@gmail.com>

pkgname=xa
pkgver=2.3.12
pkgrel=1
pkgdesc='Cross-assembler for the 6502 microprocessor'
arch=(x86_64)
url='https://www.floodgap.com/retrotech/xa/'
license=(GPL2)
makedepends=(setconf)
source=("https://www.floodgap.com/retrotech/xa/dists/$pkgname-$pkgver.tar.gz")
b2sums=('8165d2173d9270948fa4177420d5cd373e975101ab4f2f6bd00f38ae212b2f4574f0af17a2d6338d4d87fba6d3653522cebc8c55c2a8cd1ede26da6e69741d1b')

prepare() {
  cd $pkgname-$pkgver
  setconf Makefile CFLAGS="$CFLAGS -fPIC -Wl,-z,relro,-z,now"
  setconf Makefile LDFLAGS="$LDFLAGS -z,relro,-z,now"
}

build() {
  make -C $pkgname-$pkgver
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir/usr" install
}
