# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapp
pkgver=2.0.0
pkgrel=2
pkgdesc="Common library for X-Apps project"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('libdbusmenu-gtk3' 'libgnomekbd')
optdepends=('python: for mate-desktop status applet')
makedepends=('meson' 'samurai' 'gobject-introspection' 'python-gobject' 'vala')
provides=('xapps')
conflicts=('xapps')
replaces=('xapps')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "https://github.com/linuxmint/xapp/pull/117/commits/8fb391b5d8f081d6fa1ed4a7d9f56e175aae31ff.patch")
sha256sums=('cc33dea39cc1092e77280ba290b94adf991125d922b285dc20c5a62cc41dac29'
            'e5e0e3462746d2c967d136a5561de72fb67b9882d644fad30d516512eb8f5255')
b2sums=('64159030705a8f94c4ebebc361a78b34ede83ff21e8ef66fe0445d0ff69f1d8546ccbe19625bad1ce9101fa6c4654ef3706ca9b0fc006f3b934562f388e96814'
        '785f78e9f0dd51ae70388d09de26d760a530a6859f78995197fc9eacd1b4c483c3570b2e716ad27c9133f6b2f5e835507ba203359ed81069fc9b4e5c09ef8995')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # do not install file in Debian-specific location that doesn't work anywhere else
    # https://github.com/linuxmint/xapp/pull/117
    patch -p1 -i ../8fb391b5d8f081d6fa1ed4a7d9f56e175aae31ff.patch

}

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib \
          --buildtype=plain \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install

    # rm useless scripts
    rm -r "${pkgdir}"/usr/bin

    # byte-compile python modules since meson does not implement autotools'
    # py-compile.
    # This is kind of ugly but removes traces of the build root.
    while read -rd '' _file; do
        _destdir="$(dirname "${_file#${pkgdir}}")"
        python -m compileall -d "${_destdir}" "${_file}"
        python -O -m compileall -d "${_destdir}" "${_file}"
    done < <(find "${pkgdir}"/usr/lib/python3* -name '*.py' -print0)
}
