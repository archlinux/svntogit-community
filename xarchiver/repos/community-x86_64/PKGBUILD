# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=xarchiver
pkgver=0.5.3
pkgrel=3
pkgdesc="GTK+ frontend to various command line archivers"
arch=('i686' 'x86_64')
url="http://xarchiver.sourceforge.net/"
license=('GPL')
depends=('gtk2' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('zip: ZIP support'
            'unzip: ZIP support'
            'p7zip: 7z support'
            'arj: ARJ support'
            'lzop: LZOP support'
            'cpio: RPM support'
            'unrar: RAR support')
install=xarchiver.install
source=(http://downloads.sourceforge.net/xarchiver/xarchiver-$pkgver.tar.bz2
        xarchiver-0.5.3-fix-rpm-support.patch
        xarchiver-0.5.3-fix-double-escaping.patch
        xarchiver-0.5.3-fix-password-protected.patch
        xarchiver-0.5.3-add-mime-types.patch
        rar5-withespace.patch)
md5sums=('fd390bbd2df76a5f8a007bdeae82d4aa'
         '812b93339f5e3332621f3c5abebfe277'
         '6178d7ab679b761469c880a8db991907'
         '35ab96d98521a0a36f3e9e9ec0969107'
         'c42ea22910c33205916079b8be653324'
         '77a4aead93f0b2c9cf44647101a85742')

prepare() {
  cd $pkgname-$pkgver

  # Fix RPM support
  patch -Np1 -i ../xarchiver-0.5.3-fix-rpm-support.patch

  # Fix error when trying to create archives with spaces
  patch -Np1 -i ../xarchiver-0.5.3-fix-double-escaping.patch

  # Fix segfault when handling password protected files
  patch -Np1 -i ../xarchiver-0.5.3-fix-password-protected.patch

  # Add more MIME types in the desktop file
  patch -Np1 -i ../xarchiver-0.5.3-add-mime-types.patch

  # Fix RAR5 files containing white space
  patch -Np1 -i ../rar5-withespace.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
