# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=xboard
pkgver=4.5.1
pkgrel=1
pkgdesc="A graphical user interfaces for chess"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/xboard/"
license=('GPL3')
depends=('libxaw' 'gnuchess' 'texinfo' 'xdg-utils')
install=${pkgname}.install
source=(http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        'xboard.desktop')
md5sums=('704cd7be87aada1f24c33e616b3ec2b5'
         '1d9d4a7864465a703fe4a215e83f4806')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/fairymax/gnuchess/' xboard.h
  ./configure --prefix=/usr --mandir=/usr/share/man --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
