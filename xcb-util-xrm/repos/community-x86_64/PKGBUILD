# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomazau
# Contributor: Ingo BÃ¼rk <admin at airblader dot de>

pkgname=xcb-util-xrm
pkgver=1.3
pkgrel=1
pkgdesc='XCB utility functions for the X resource manager'
url='https://github.com/Airblader/xcb-util-xrm'
arch=('x86_64')
license=('MIT')
depends=('xcb-util')
makedepends=('xorg-util-macros' 'libx11')
source=(https://github.com/Airblader/xcb-util-xrm/releases/download/v${pkgver}/xcb-util-xrm-${pkgver}.tar.gz)
sha256sums=('0129f74c327ae65e2f4ad4002f300b4f02c9aff78c00997f1f1c5a430f922f34')
sha512sums=('d8a427ed6d1f1568ce58db9b89284e4fafcc7b7929c31bccf70e5ccd91f3f6cb9c87ff25c4e64d95b0c6215cfecde6c0ee2b3a18759817aea3169ba87602c5de')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
