# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=xchm
pkgver=1.23
pkgrel=7
pkgdesc="A cross-platform GUI front-end to chmlib"
arch=('x86_64')
url="http://xchm.sourceforge.net/"
license=('GPL')
depends=('chmlib' 'wxgtk3')
source=("https://downloads.sourceforge.net/sourceforge/xchm/${pkgname}-${pkgver}.tar.gz"
        xchm.desktop
        xchm-wx3.0-compat.patch)
md5sums=('486d029bd81071a2d04e7181909b1602'
         '629a10fc2bac9e6ca47daa66512f33b4'
         '80fa0eec2acb73860ccadff21aa5c71c')

prepare() {
  cd $pkgname-$pkgver
# Fix build with WxGTK 3 (Debian)
  patch -p1 -i ../xchm-wx3.0-compat.patch
}

build() {
  cd ${pkgname}-${pkgver}

  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/xchm.desktop "${pkgdir}/usr/share/applications/xchm.desktop"
}
