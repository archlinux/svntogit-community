# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=xchm
pkgver=1.23
pkgrel=7
pkgdesc="Cross-platform GUI front-end to chmlib"
arch=('x86_64')
url="http://xchm.sourceforge.net/"
license=('GPL')
depends=('chmlib' 'wxgtk3')
source=("https://downloads.sourceforge.net/xchm/$pkgname-$pkgver.tar.gz"
        xchm.desktop
        xchm-wx3.0-compat.patch)
sha256sums=('8f8f5c60954f340e50f1613913eaca6ff489c10ca36b2570b360d8ccba77c062'
            '5c3a47762361f2088eddff29c89e2217c9d063aee8ef152fe3ec9dd9deb8046f'
            'f98c3fa2a82f66ae627015388a9f0e671e280c2b9a1b6007f78a7b3ce013138f')

prepare() {
  cd $pkgname-$pkgver
  # Fix build with WxGTK 3 (Debian)
  patch -Np1 -i ../xchm-wx3.0-compat.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
}
