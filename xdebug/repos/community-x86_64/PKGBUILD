# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=2.6.0beta1
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=("https://www.xdebug.org/files/$pkgname-${pkgver,,}.tgz"
	'xdebug.ini')
sha256sums=('49de661e1e18cbbd739fc9fb7a014a36f97a84d2c4a89417c358dd258340527d'
            '7c66883dc2ade69069ef84e30188b25630748aa9c8b0dd123727c00505421205')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd "$srcdir"/$pkgname-$pkgver/debugclient
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/debugclient
  make DESTDIR="$pkgdir" install

  cd "$srcdir"/$pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -D -m 644 "$srcdir"/xdebug.ini "$pkgdir"/etc/php/conf.d/xdebug.ini
}
