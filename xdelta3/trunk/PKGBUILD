# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>

pkgname=xdelta3
pkgver=3.0.11
pkgrel=1
pkgdesc='Diff utility which works with binary files'
arch=('x86_64')
url='http://xdelta.org/'
license=('GPL')
depends=('xz')
source=("https://github.com/jmacd/xdelta-devel/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.sign})
sha1sums=('3758290f34f33d8c56d1d4ef033044effb317c39'
          'SKIP')
validpgpkeys=('B56315DB06EC0C6FAC7C60E22B9ED4DF0ABFB111')

build() {
  cd "$pkgname-$pkgver"
  
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"

  ./$pkgname test
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: github.com/jmacd/xdelta
# vim:set ts=2 sw=2 et:
