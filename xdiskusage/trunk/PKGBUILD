# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=xdiskusage
pkgver=1.48
pkgrel=6
pkgdesc="xdiskusage is a user-friendly program to show you what is using up all your disk space"
arch=('i686' 'x86_64')
depends=('fltk')
makedepends=('libxi')
license=('GPL')
url="http://xdiskusage.sourceforge.net"
options=(!emptydirs)
source=(http://xdiskusage.sourceforge.net/$pkgname-$pkgver.tgz
	'stdin-is-null.patch')
md5sums=('a902aa9d73761ade98256c3cd5c1f533'
	'0181b66b44e382e44c91602d8de9a7b6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/-lGL -lGLU/-lXft/' makeinclude.in

  patch -Np1 -i "$srcdir/stdin-is-null.patch"

  [ $CARCH == "x86_64" ] && patch xdiskusage.C <<EOF
991c991
<   switch ((int)v) {
---
>   switch ((long long)v) {
1004c1004
<   int n = (int)v;
---
>   int n = (long long)v;
EOF

  ./configure --prefix=/usr
  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
  LDFLAGS=-lXi make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir/usr" mandir="$pkgdir/usr/share/man" install
  chmod 0644 $pkgdir/usr/share/man/man1/*
}
