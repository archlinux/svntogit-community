# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Rttommy <rttommy@gmail.com>

pkgname=xdotool
pkgver=3.20150503.1
pkgrel=1
pkgdesc="Command-line X11 automation tool"
arch=('i686' 'x86_64')
url="http://www.semicomplete.com/projects/xdotool/"
license=('BSD')
depends=('libxtst' 'libxinerama' 'libxkbcommon')
source=(https://github.com/jordansissel/xdotool/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e8326883bd5e91bede7336cbee186e6e9143f40b3fb61c84afc9bb31b87e96d1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make WITHOUT_RPATH_FIX=1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" INSTALLMAN="$pkgdir/usr/share/man" install

  # Remove execute bit from header file
  chmod -x "$pkgdir/usr/include/xdo.h"

  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}

# vim:set ts=2 sw=2 et:
