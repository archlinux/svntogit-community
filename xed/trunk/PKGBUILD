# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=3.2.2
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
groups=('x-apps')
depends=('gspell' 'gtksourceview4' 'libpeas' 'xapps')
makedepends=('meson' 'samurai' 'gobject-introspection' 'intltool' 'itstool')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('7f0a1a8abc85da66980354aa033c1215eb7bf4e23e71d14b6ae9a28956352632')
b2sums=('38481f68ba5279b7e1af361a783700d301b3e8d830c66b78e178283ae9e804ad2632ad6942d6367d02a1c084cfd69d3eb8df8702f0a7611ab94046e7ec9d7e47')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/${pkgname} \
          --buildtype=plain \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
