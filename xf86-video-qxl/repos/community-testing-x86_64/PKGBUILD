# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Yonathan Dossow
# Contributor: Ansgar Taflinski <ataflinski@uni-koblenz.de>

pkgname=xf86-video-qxl
pkgver=0.1.5
pkgrel=8
pkgdesc='Xorg X11 qxl video driver'
arch=('x86_64')
url='https://www.x.org'
license=('MIT')
groups=('xorg-drivers')
depends=('spice')
optdepends=('python2: for Xspice')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'spice-protocol'
	'xorgproto' 'git' 'libcacard')
conflicts=('xf86-video-qxl-git' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25')
validpgpkeys=('94A9F75661F77A6168649B23A9D8C21429AC6C82')
source=("https://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('b18682e04503c6326f7bf7190f3ee50a3d4d69758a2a3cc9af102a6b3f114c92'
            'SKIP')
sha512sums=('7510b2d037b3e978df6063b29e2406f3d1270695a239f29fdaec9b1dc65a30ab10cb959f15eb336f78e93aa708d41c64c5ea43803958feffc64542229605b782'
            'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# this is python 2.x
	sed -i '1c #!/usr/bin/python2' scripts/Xspice
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

        # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
        # With them, module fail to load with undefined symbol.
        # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
        export CFLAGS=${CFLAGS/-fno-plt}
        export CXXFLAGS=${CXXFLAGS/-fno-plt}
        export LDFLAGS=${LDFLAGS/,-z,now}

	autoreconf -fi
	./configure \
		--enable-xspice \
		--prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" install

	install -v -D -m0755 scripts/Xspice "${pkgdir}"/usr/bin/Xspice
}
