# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Yonathan Dossow
# Contributor: Ansgar Taflinski <ataflinski@uni-koblenz.de>

pkgname=xf86-video-qxl
# https://gitlab.freedesktop.org/xorg/driver/xf86-video-qxl/-/commits/master
_commit=52e975263fe88105d151297768c7ac675ed94122 # master 2022-04-06
pkgver=0.1.5.r23.g52e9752
pkgrel=2
pkgdesc='Xorg X11 qxl video driver'
arch=('x86_64')
url='https://www.x.org'
license=('MIT')
groups=('xorg-drivers')
depends=('spice' 'systemd-libs' 'libxfont2')
optdepends=('python: for Xspice')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=25.2' 'spice-protocol'
    'xorgproto' 'git' 'libcacard')
conflicts=('xf86-video-qxl-git' 'X-ABI-VIDEODRV_VERSION<25' 'X-ABI-VIDEODRV_VERSION>=26')
validpgpkeys=('94A9F75661F77A6168649B23A9D8C21429AC6C82')
source=(#"https://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        "git+https://gitlab.freedesktop.org/xorg/driver/xf86-video-qxl#commit=$_commit"
)
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^xf86-video-qxl-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}/${pkgname}" #-${pkgver}"

    # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
    # With them, module fail to load with undefined symbol.
    # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
    export CFLAGS=${CFLAGS/-fno-plt}
    export CXXFLAGS=${CXXFLAGS/-fno-plt}
    export LDFLAGS=${LDFLAGS/,-z,now}

    autoreconf -fi
    ./configure --enable-xspice --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}" #-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    install -v -Dm0755 scripts/Xspice "${pkgdir}"/usr/bin/Xspice
    install -Dm644 "${srcdir}"/${pkgname}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
