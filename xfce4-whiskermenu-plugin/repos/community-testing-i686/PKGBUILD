# $Id: PKGBUILD 123401 2014-12-03 12:28:05Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=1.5.1
pkgrel=2
pkgdesc='Alternate menu for Xfce4'
arch=('x86_64' 'i686')
url="http://gottcode.org/xfce4-whiskermenu-plugin/"
groups=('xfce4-goodies')
license=('GPL2')
depends=('xfce4-panel')
makedepends=('cmake')
install="$pkgname.install"
source=("http://gottcode.org/$pkgname/$pkgname-$pkgver-src.tar.bz2")
sha256sums=('1c7650562322867513370ab575536b67aa3712329bb60a0c91a0a70e23a8945a')

build() {
  cd "$pkgname-$pkgver"

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  make -C "$pkgname-$pkgver" install DESTDIR="$pkgdir"
}

# getver ok
# vim:set ts=2 sw=2 et:
