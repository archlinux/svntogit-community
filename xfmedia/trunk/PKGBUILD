# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Tom Killian <tomk@runbox.com>
# Contributor: Spider.007 <archPackage@spider007.net>

pkgname=xfmedia
pkgver=0.9.2
pkgrel=12
pkgdesc="xfce media player"
arch=('i686' 'x86_64')
license=('GPL')
url="http://spuriousinterrupt.org/projects/xfmedia/"
depends=('xine-lib' 'taglib' 'exo' 'libxtst' 'libxss' 'libxfcegui4' 'dbus-glib')
makedepends=('intltool' 'libxt')
install=xfmedia.install
source=(http://spuriousinterrupt.org/files/$pkgname/$pkgname-$pkgver.tar.bz2
	xfmedia-dbus-0.6-support.patch
	xfmedia-empty-prev-next-fix.patch
	xfmedia-exo-0.6.patch
	xfmedia-xine-1.2.0.patch)
md5sums=('6eb8bd1f67201f829e0f45e733c02bd5'
         '8f2aa12eea584de65d8ed28179e56b51'
         '650f8cb9e2ddd9a8a2a03748b8757110'
         'f5e17fcdf2056dcc8ca8aae8600c0132'
         '934603c7d3a1a0a66edb847bcb930df1')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -Np1 -i ../xfmedia-dbus-0.6-support.patch
  patch -Np1 -i ../xfmedia-empty-prev-next-fix.patch
  patch -Np1 -i ../xfmedia-exo-0.6.patch
  patch -Np1 -i ../xfmedia-xine-1.2.0.patch
  LDFLAGS="$LDFLAGS -lXext -lm -lgmodule-2.0" ./configure \
    --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
