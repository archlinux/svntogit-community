# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.7.8
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport."
arch=('i686' 'x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL')
depends=('ffmpeg' 'ffmpeg-compat' 'imlib2' 'jack' 'liblo>=0.28' 'libxpm' 'libxv' 'portmidi')
makedepends=('qt4')
optdepends=('qt4: for the GUI (qjadeo)')
backup=('etc/xjadeorc')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
	"qjadeo.desktop")
md5sums=('b93f9971436d0dda36867d16b15dc0dd'
         '315e2ab44ce3edf4068ff6db48942908')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-qtgui \
              --with-qt4prefix=/usr/lib/qt4
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # rc file
  install -Dm644 doc/xjadeorc "$pkgdir/etc/xjadeorc"

  # desktop file
  install -Dm644 ../qjadeo.desktop \
    "$pkgdir/usr/share/applications/qjadeo.desktop"

  # icon
  install -Dm644 doc/xjadeo.png \
    "$pkgdir/usr/share/pixmaps/qjadeo.png"
}
