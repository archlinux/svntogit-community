# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.9
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport"
arch=('x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL2')
depends=('ffmpeg' 'imlib2' 'liblo' 'libltc' 'libxpm' 'portmidi')
makedepends=('gendesk' 'glu')
backup=('etc/xjadeorc')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('66a810e8c276600d5557c856e9206d11f4bed868d8e7ac490dcbdbafe95a90cc1b42d38fd73ccdeb2b685c9eafc0fc4b736f2a7174645bdd6706970ab2c16774')

prepare() {
  cd "${pkgname}-${pkgver}"
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # rc file
  install -vDm 644 doc/xjadeorc -t "${pkgdir}/etc/"

  # desktop file
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"

  # icon
  install -vDm 644 "src/${pkgname}/icons/xjadeoH128.png" \
    "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 {AUTHORS,ChangeLog,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
