# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.11
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport"
arch=(x86_64)
url="https://github.com/x42/xjadeo"
license=(GPL2)
depends=(gcc-libs glibc imlib2 libglvnd libx11 libxpm libxv)
makedepends=(alsa-lib ffmpeg gendesk git glu jack liblo libltc portmidi)
backup=(etc/xjadeorc)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/x42/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('77d58ca8f847425180fe91c1e20d21e542b25a97a00ea03a3812ac96f46b85f4786dae88442cbe0e4b615239849d49073487caf0f82c0bb84ab203e1eea439b4')
b2sums=('92569b90a8ff6f5636e4267312595e572cc103176b3b7ffaa25ca0b47c64e5692279855af67b3b1f834e734c3cd6e09360f5811e9f29d255d127caee2c99540b')

prepare() {
  cd $pkgname-$pkgver
  gendesk -n \
          --pkgname $pkgname \
          --pkgdesc "$pkgdesc" \
          --categories "AudioVideo;Audio"
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  depends+=(libasound.so libavcodec.so libavformat.so libavutil.so
  libfreetype.so libjack.so liblo.so libltc.so libportmidi.so libswscale.so)

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # config
  install -vDm 644 doc/xjadeorc -t "$pkgdir/etc/"
  # desktop file
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # icon
  install -vDm 644 src/$pkgname/icons/xjadeoH128.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
