# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>

pkgname=xkeycaps
pkgver=2.46
pkgrel=7
arch=('x86_64')
pkgdesc='A graphical front-end to xmodmap'
url="https://www.jwz.org/xkeycaps/"
license=('custom')
depends=('libxaw')
makedepends=('imake' 'xbitmaps')
source=(https://www.jwz.org/$pkgname/$pkgname-$pkgver.tar.Z
	license.txt)
sha256sums=('40da137109e0ef0b687a72b71a1ef0b3d08a10749fdda05fc16c52b4cbed763e'
            '5cb75d666cab06c0e107dd057847fe9df0ef5483b532d32c5cc4b9bec8886671')
DLAGENTS=('https::/usr/bin/curl --user-agent Mozilla\ Firefox -fLC - --retry 3 --retry-delay 3 -o %o %u')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  xmkmf
  make MANPATH=/usr/share/man
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" MANPATH=/usr/share/man install.man
  install -Dm644 ../license.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
