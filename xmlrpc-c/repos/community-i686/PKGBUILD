# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

pkgname=xmlrpc-c
pkgver=1.36.00
pkgrel=1
epoch=1
pkgdesc='XML-RPC for C and C++'
arch=('x86_64' 'i686')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2')
makedepends=('libtool' 'setconf')
options=('!makeflags' '!emptydirs')
source=('svn://svn.code.sf.net/p/xmlrpc-c/code/advanced#revision=2545')
sha1sums=('SKIP')

build() {
  cd "$srcdir/advanced"

  [ "$CARCH" != "i686" ] && export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-libwww-client \
    --disable-wininet-client \
    --enable-abyss \
    --enable-cplusplus
  mkdir include/curl
  touch include/curl/types.h
  make CFLAGS_PERSONAL="$CFLAGS"
  make CFLAGS_PERSONAL="$CFLAGS" -C tools
}

package() {
  cd "$srcdir/advanced"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C tools install
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # Remove references to build directory
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" BLDDIR ''
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" ABS_SRCDIR ''
}

# vim:set ts=2 sw=2 et:
