# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

pkgname=xmlrpc-c
# Using the Stable version
pkgver=1.43.03
pkgrel=2
epoch=1
pkgdesc='XML-RPC for C and C++'
arch=('x86_64' 'i686')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2')
makedepends=('libtool' 'setconf' 'clang' 'subversion')
options=('!makeflags' '!emptydirs')
source=("svn://svn.code.sf.net/p/xmlrpc-c/code/stable")
sha1sums=('SKIP')

build() {
  cd stable

  [ "$CARCH" != "i686" ] && export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-libwww-client \
    --disable-wininet-client \
    --enable-abyss \
    --enable-cplusplus
  mkdir -p include/curl
  touch include/curl/types.h

  setconf config.mk CC clang
  setconf config.mk CXX clang++

  make CFLAGS_PERSONAL="$CFLAGS" CXXFLAGS_PERSONAL="$CXXFLAGS"
  make -C tools CFLAGS_PERSONAL="$CFLAGS" CXXFLAGS_PERSONAL="$CXXFLAGS"
}

package() {
  cd stable

  make DESTDIR="$pkgdir" install
  make -C tools DESTDIR="$pkgdir" install
  install -Dm 644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # Remove references to build directory
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" BLDDIR ''
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" ABS_SRCDIR ''
}

# getver: -u 2 xmlrpc-c.sourceforge.net/change_stable.html
# vim:set ts=2 sw=2 et:
