# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

pkgname=xmlrpc-c
pkgver=1.38.02
pkgrel=1
epoch=1
pkgdesc='XML-RPC for C and C++'
arch=('x86_64' 'i686')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2')
makedepends=('libtool' 'setconf' 'python')
options=('!makeflags' '!emptydirs')
source=('svn://svn.code.sf.net/p/xmlrpc-c/code/advanced')
sha1sums=('SKIP')

pkgver() {
  python -c 'print("%01d.%02d.%02d" % tuple(map(int, (x.rsplit(" ",1)[1] for x in open("advanced/version.mk").read().split("\n")[-4:-1]))))' 
}

build() {
  cd advanced

  [ "$CARCH" != "i686" ] && export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-libwww-client \
    --disable-wininet-client \
    --enable-abyss \
    --enable-cplusplus
  mkdir include/curl
  touch include/curl/types.h
  make CFLAGS_PERSONAL="$CFLAGS"
  make -C tools CFLAGS_PERSONAL="$CFLAGS"
}

package() {
  cd advanced

  make DESTDIR="$pkgdir" install
  make -C tools DESTDIR="$pkgdir" install
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # Remove references to build directory
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" BLDDIR ''
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" ABS_SRCDIR ''
}

# vim:set ts=2 sw=2 et:
