# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Kritoke <kritoke@nospam.gmail.com>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=xmms-imms
pkgver=3.1.0_rc10
pkgrel=1
pkgdesc="An intelligent playlist plug-in for XMMS that tracks your listening patterns and dynamically adapts to your taste."
arch=(i686 x86_64)
license=("GPL")
url="http://imms.luminal.org/"
depends=('xmms' 'pcre' 'sqlite' 'taglib' 'glib2' 'sox' 'torch' 'libxss' 'fftw')
replaces=('imms')
source=(http://imms.googlecode.com/files/imms-${pkgver/_/-}.tar.bz2)
md5sums=('61302249e9638779b3241987ab62676d')

build() {
  cd $srcdir/imms-${pkgver/_/-}

  if [ $NOEXTRACT -ne 1 ]; then
    autoheader
    aclocal
    autoconf
    CXXFLAGS="$CXXFLAGS -DDEBUG=0" ./configure --prefix=/usr --disable-immsremote
  fi

  make
}

package() {
  cd $srcdir/imms-${pkgver/_/-}

  mkdir -p $pkgdir/usr/bin
  make prefix=$pkgdir/usr DESTDIR=$pkgdir/usr/lib/xmms/General \
	 BMPDESTDIR=$pkgdir/usr/lib/bmp/General install
}
