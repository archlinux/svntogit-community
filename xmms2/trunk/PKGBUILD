# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xmms2
pkgver=0.7DrNo
pkgrel=9
pkgdesc="complete rewrite of the popular music player"
arch=('i686' 'x86_64')
url="http://xmms2.org/"
license=('LGPL')
backup=('etc/conf.d/xmms2d.conf')
depends=('sqlite3' 'libmad')
makedepends=('alsa-lib' 'boost' 'curl' 'faad2' 'ffmpeg' 'fftw' 'flac' 'glib' # avahi
	'jack' 'libao' 'libmms' 'libmpcdec' 'libofa' 'libsamplerate' 'libshout'
	'libvorbis' 'libxml2' 'mpg123' 'oss' 'perl' 'pulseaudio' 'pyrex'
	'ruby' 'smbclient' 'speex' 'wavpack' 'libmodplug')
optdepends=('alsa-lib: ALSA audio output'
	    'avahi: announce xmms2d via bonjour/mDNS/zeroconf'
	    'boost: C++ language bindings'
	    'curl: play HTTP streams'
	    'faad2: AAC support'
	    'ffmpeg: WMA, avcodec & avformat support'
	    'fftw: calculations for visualization'
	    'flac: FLAC support'
	    'jack: JACK audio output'
	    'libao: liboa audio output'
	    'libofa: MusicDNS fingerprinting'
	    'libmms: play MMS streams'
	    'libmodplug: to play MOD files'
	    'libmpcdec: Musepack support'
	    'libsamplerate: vocoder support'
	    'libshout: Icecast audio output'
	    'libvorbis: Ogg Vorbis support'
	    'libxml2: XSPF and podcast support'
	    'mpg123: alternative MP3 plugin'
	    'oss: OSS audio output'
	    'perl: Perl language bindings'
	    'pulseaudio: PulseAudio audio output'
	    'pyrex: Python language bindings'
	    'ruby: Ruby language bindings'
#	    'sidplay: to play a Commodore 64 music files AKA zaks'
# we do not have sidplay 2 series in repos; also it's
# somehow connected with resid i guess(not in repos also)
	    'smbclient: direct CIFS/SMB access'
	    'speex: Speex support'
	    'wavpack: WavPack support')
conflicts=('xmms2-devel')
install=xmms2.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	xmms2d.conf
	xmms2d.rc
	xmms2-helpers.patch
	xmms2-ruby-1.9.patch
	vorbis-albumart.patch)
md5sums=('60e50b591078acb6a85cd83de0f2b077'
         'af13c937bf3c86b77ae6820107aab9b8'
         '893371ba4dc7b428322e44329683211f'
         'eddb800ebcc495b8574044a110a34add'
         '017eff634d755c98dd037cd41aecd652'
         'c5a16da0c5a6ebe5e7f58afb6edfa87e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 < ${srcdir}/xmms2-helpers.patch
# patch -Np1 < ${srcdir}/xmms2-ruby-1.9.patch
#  sed -i -e 's#avcodec.h#libavcodec/avcodec.h#' src/plugins/avcodec/avcodec.c

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' waf

  patch -p1 <$srcdir/vorbis-albumart.patch

  ./waf configure --prefix=/usr \
	--with-optionals=python,cli,launcher,xmmsclient++,xmmsclient++-glib,perl,ruby,nycli
  ./waf build
  ./waf --destdir=${pkgdir} install

  install -D -m 0755 ../xmms2d.rc ${pkgdir}/etc/rc.d/xmms2d
  install -D -m 0644 ../xmms2d.conf ${pkgdir}/etc/conf.d/xmms2d.conf
}
