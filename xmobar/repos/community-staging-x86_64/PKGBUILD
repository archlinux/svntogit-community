# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar
pkgver=0.32
pkgrel=41
pkgdesc='Minimalistic Text Based Status Bar'
url='https://hackage.haskell.org/package/xmobar'
license=('BSD')
arch=('x86_64')
depends=('libxft' 'libxinerama' 'libxrandr' 'libxpm' 'ghc-libs' 'haskell-x11'
         'haskell-x11-xft' 'haskell-utf8-string' 'haskell-network-uri'
         'haskell-hinotify' 'haskell-stm' 'haskell-parsec' 'haskell-parsec-numbers'
         'haskell-mtl' 'haskell-regex-base' 'haskell-regex-compat'
         'haskell-http' 'haskell-dbus' 'haskell-libmpd' 'haskell-iwlib'
         'wireless_tools' 'haskell-text' 'haskell-async' 'haskell-aeson'
         'haskell-timezone-olson' 'haskell-timezone-series' 'alsa-lib'
         'haskell-extensible-exceptions' 'haskell-http-conduit'
         'haskell-http-types' 'haskell-alsa-core' 'haskell-alsa-mixer')
makedepends=('ghc' 'haskell-hspec' 'haskell-temporary')
source=(https://github.com/jaor/xmobar/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8d2543152b050460bf48514aaa981e6d508cabb7688b848a3b3528cfcecacb76')
sha512sums=('fa9b7c5d568b1c8e731b525736d8cf6a630def93a75884fcf3adc68e5527b31b61ef96df3019befafb83fa080703865ba511750deeb09d8ab70582ccf433b1f6')

prepare() {
  cd xmobar-$pkgver
  sed -e 's/&& <.*,/,/g' -e 's/==.*,/,/g' -i xmobar.cabal
}

build() {
  cd xmobar-${pkgver}

  _flags=(with_xft with_utf8 with_inotify with_iwlib with_mpd with_alsa
          with_datezone with_mpris with_dbus with_xpm with_threaded
          with_rtsopts with_weather)

  runhaskell setup configure -O \
    --enable-shared \
    --enable-executable-dynamic \
    --disable-library-vanilla \
    --prefix=/usr \
    --dynlibdir=/usr/lib \
    --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    --flags="${_flags[*]}" \
    --enable-tests
  runhaskell setup build
}

check() {
  cd xmobar-${pkgver}
  runhaskell setup test
}

package() {
  cd xmobar-${pkgver}
  runhaskell setup copy --destdir="${pkgdir}"
  install -Dm 644 license -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
