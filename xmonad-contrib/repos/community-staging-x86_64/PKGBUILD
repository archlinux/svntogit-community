# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: orbisvicis <gmail.com>

pkgname=xmonad-contrib
pkgver=0.12
pkgrel=2
pkgdesc="Add-ons for xmonad"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.10.3' 'xmonad=0.12' 'sh' 'haskell-x11=1.6.1.2' 'haskell-x11-xft=0.3.1' 'haskell-utf8-string' 'haskell-random=1.1' 'haskell-old-time')
install='xmonad-contrib.install'
options=('staticlibs')
source=("https://github.com/xmonad/xmonad-contrib/archive/v${pkgver}.tar.gz")

build() {
  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup.lhs configure --ghc --enable-shared --enable-split-objs --prefix=/usr -fuse_xft \
             --libsubdir=\$compiler/site-local/\$pkgid --docdir=/usr/share/doc/${pkgname}
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$pkgname"

  install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
md5sums=('5538c34c020946016cc31b49594c7d4b')
