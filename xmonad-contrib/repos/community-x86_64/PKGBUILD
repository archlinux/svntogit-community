# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: orbisvicis <gmail.com>

pkgname=xmonad-contrib
pkgver=0.13.20180518
_commit=d3d0818e9b9ef6e270b1b0e8c0e6ac683274da80
pkgrel=1
pkgdesc='Add-ons for xmonad'
url='http://xmonad.org/'
arch=('x86_64')
license=('BSD')
depends=('ghc-libs' 'xmonad' 'sh' 'haskell-x11' 'haskell-x11-xft' 'haskell-utf8-string' 'haskell-random'
         'haskell-old-time')
makedepends=('ghc')
source=(${pkgname}-${_commit}.tar.gz::https://github.com/xmonad/xmonad-contrib/archive/${_commit}.tar.gz)
sha512sums=('5efeb94294a50ab35b8eee255edadbc788b941f504f7a6b0317344cabc8b29ce370c83f56b935fb63cbe4176aa8100bed91e867666ec48bb68df80c703f9edde')

prepare() {
  mv $pkgname-{$_commit,$pkgver}
  sed -i -e '/semigroups/d' -e 's/utf8-string,/utf8-string/' $pkgname-$pkgver/$pkgname.cabal
}

build() {
  cd ${pkgname}-${pkgver}

  runhaskell Setup.lhs configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
             --prefix=/usr -fuse_xft --libsubdir=\$compiler/site-local/\$pkgid \
             --docdir=/usr/share/doc/${pkgname}
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup.lhs copy --destdir="${pkgdir}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
