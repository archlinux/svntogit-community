# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.13
pkgrel=8
pkgdesc="Lightweight X11 tiled window manager written in Haskell"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc' 'haskell-x11' 'sh' 'haskell-mtl' 'haskell-utf8-string'
         'haskell-extensible-exceptions' 'haskell-data-default' 'haskell-setlocale')
makedepends=('gendesk')
optdepends=('xorg-xmessage: for displaying visual error messages')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmonad/xmonad/archive/v${pkgver}.tar.gz"
        "dynamic-compilation.patch"
        "xmonad.svg")

sha512sums=('c23daf4c187514001383e10586fc13624fd02029bc3d1b33ec6e79abfaaa14847196c1c54096734ce970391674425d614b485139a7201e0daaab4431dc7fa5d6'
            'abc598ebc4b55917186cd4d549fc970f53905f820d394ada9a092c08d8833050ad18b23e2f4fd7f281487da2db5b0ca058600e995ac97b18d3be4c62071562d6'
            '31f0ceb724a9931aeda75ab706c8908b5d8341af4df50ab1d749814baacb748ad3622601cd99ac03bf936b698fcd854b23d1541e1772306e3337a13a79b110b6')

prepare() {
  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "${srcdir}/dynamic-compilation.patch"
}

build() {
  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"

  runhaskell Setup.lhs copy --destdir=$pkgdir

  # Requires pandoc, regex-posix, Pretty and cabal
  #runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE
  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg
  install -D -m644 $srcdir/xmonad.desktop $pkgdir/usr/share/xsessions/xmonad.desktop
}
