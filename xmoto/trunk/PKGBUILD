# $Id: $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca> 
# Contributor: Denis (dtonator@gmail.com)

pkgname=xmoto
pkgver=0.5.9
pkgrel=1
pkgdesc="A challenging 2D motocross platform game, where physics play an important role."
arch=('i686' 'x86_64')
url="http://xmoto.tuxfamily.org"
license=('GPL')
depends=('bzip2' 'libjpeg' 'libpng' 'lua' 'sdl_mixer' 'ode=0.11.1' 'curl' 
	 'mesa' 'sqlite3' 'sdl_ttf' 'desktop-file-utils' 'sdl_net' 'libxdg-basedir' 'libxml++')
install=xmoto.install
source=("http://download.tuxfamily.org/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        'xmoto-0.5.9-libpng15.patch')
sha1sums=('07757accce78151dc8873bef8270df0e56196772'
          '27f52bd30772f67898e0bfd8c1e12478a4848192')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}/xmoto-0.5.9-libpng15.patch"

  # build and install 
  ./configure LDFLAGS="-L/usr/lib" --prefix=/usr --disable-sdltest

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} mangdir=/usr/share/man/man6 install

  # install desktop file
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/extra/xmoto.desktop \
        ${pkgdir}/usr/share/applications/xmoto.desktop

  # install icon for desktop file
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/extra/xmoto.xpm \
        ${pkgdir}/usr/share/pixmaps/xmoto.xpm
}
