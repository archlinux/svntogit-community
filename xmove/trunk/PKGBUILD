# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Benjamin Andresen <benny AT klapmuetz DOT org>

pkgname=xmove
pkgver=2.0beta2
pkgrel=6
pkgdesc="A tool to move X windows to different X servers, something like a GNU screen for X."
arch=('x86_64')
url="http://code.google.com/p/archlinux-stuff/"
license=('custom')
depends=('libx11')
makedepends=('imake')
source=(http://archlinux-stuff.googlecode.com/files/xmove-$pkgver.tar.gz)
md5sums=('32b8db5bdbe71b8604e4f89c9eb2975b')

build() {
  cd "$srcdir"/xmove-$pkgver/xmove
  xmkmf
  echo xmove.man:: >> Makefile
  make

  cd "$srcdir"/xmove-$pkgver/xmovectrl
  xmkmf
  echo xmovectrl.man:: >> Makefile
  make

  chmod 755 "$srcdir"/xmove-$pkgver/man/man1
}

package() {
  mkdir -p "$pkgdir"/usr/share/man
  cp -r "$srcdir"/xmove-$pkgver/man/man1 "$pkgdir"/usr/share/man/
  install -D -m0755 "$srcdir"/xmove-$pkgver/xmove/xmove "$pkgdir"/usr/bin/xmove
  install -D -m0755 "$srcdir"/xmove-$pkgver/xmovectrl/xmovectrl "$pkgdir"/usr/bin/xmovectrl
  install -D -m0644 "$srcdir"/xmove-$pkgver/xmove.copyright "$pkgdir"/usr/share/licenses/xmove/COPYRIGHT
}
