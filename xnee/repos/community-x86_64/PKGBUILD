# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=xnee
pkgver=3.17
pkgrel=1
pkgdesc="A program to record, distribute, and replay X (X11) protocol data"
arch=('i686' 'x86_64')
url="http://xnee.wordpress.com/"
license=('GPL3')
depends=('gtk2' 'libxtst')
makedepends=('texinfo')
optdepends=('xosd: feedback support')
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz
        gnee.desktop)
sha256sums=('c76d1c8b9deff2e52721e474cb69cf092c0d72212c916c6dacc94e33618c8638'
            'de42d419a46083e39a9b6d60c045c1cd68deb793cc5166a86df81dff608387c1')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-gui --enable-lib --disable-static --disable-static-programs
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#.desktop file
  install -Dm644 "${srcdir}"/gnee.desktop "${pkgdir}"/usr/share/applications/gnee.desktop
}
