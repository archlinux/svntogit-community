# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=xnoise
pkgver=0.2.21
pkgrel=4
pkgdesc="Media player with a slick GUI, great speed and lots of features."
arch=('i686' 'x86_64')
url="http://www.xnoise-media-player.com/"
license=('GPL2')
depends=('desktop-file-utils' 'gst-plugins-base-libs' 'gst-plugins-good' 'gtk3' 'libsoup' 'sqlite' 'libtaginfo>=0.2' 'librsvg')
optdepends=('gst-plugins-base: Basic media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
	    'gst-libav: Extra media codecs'
	    'gvfs: Needed for fetching album art')
makedepends=('vala' 'intltool' 'git' 'gnome-common')
install=$pkgname.install
source=("git+https://bitbucket.org/shuerhaaken/xnoise.git#tag=$pkgver"
        xnoise-ListStore.patch
        xnoise-compilation-fix.patch)
sha512sums=('SKIP'
            'b47297bda23fc4dcd22bf44a84ad2c5e3b5ed0d648ef78a3bbe4de4d481c82e8ab0c52193dc8c4373e74cd389537faf66bd434cd8d695eeff4b2820c67bf1423'
            'f2d11322d61465894f59e89cf55582c6f669ed2e8bc300b30a1a0f6f81eb587a0021ebeafabb0cee23ea883450066d96609d5dfc67b7eafc7dbddb55609a0775')

prepare() {
  cd $pkgname
  # Patches from openSUSE. Thanks!
  patch -p1 -i ../xnoise-ListStore.patch
  patch -p1 -i ../xnoise-compilation-fix.patch
}

build() {
  cd $pkgname
  
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  
  # Remove obsolete files
  rm -rf "$pkgdir/usr/include" "$pkgdir/usr/share/vala"
}
