# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=xnoise
pkgver=0.2.20
pkgrel=1
pkgdesc="Media player with a slick GUI, great speed and lots of features."
arch=('i686' 'x86_64')
url="http://www.xnoise-media-player.com/"
license=('GPL2')
depends=('desktop-file-utils' 'gnome-icon-theme' 'gst-plugins-base-libs' 'gst-plugins-good' 'gtk3' 'libsoup' 'sqlite' 'libtaginfo>=0.2' 'librsvg')
optdepends=('gst-plugins-base: Basic media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
	    'gst-libav: Extra media codecs'
	    'gvfs: Needed for fetching album art')
makedepends=('vala' 'intltool')
install=$pkgname.install
source=("https://bitbucket.org/shuerhaaken/xnoise/downloads/$pkgname-$pkgver.tar.gz")

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  
  # Remove obsolete files
  rm -rf "$pkgdir/usr/include" "$pkgdir/usr/share/vala"
}

sha512sums=('d2bcf7c8fa542ef9703a831583ab90e1a03f00a0740b068966cb8bf5c76475832cf04eb55b9f14ac1c5aa50536d8e3965ac3e0d22503af44e1af8e7f908d2390')
