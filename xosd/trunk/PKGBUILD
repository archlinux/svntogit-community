# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=xosd
pkgver=2.2.14
pkgrel=8
pkgdesc="Displays text on your screen. On-Screen-Display-libs for some tools"
arch=('x86_64')
license=('GPL2')
url="http://sourceforge.net/projects/libxosd/"
depends=('libxt' 'libxinerama' 'sh')
source=("https://downloads.sourceforge.net/libxosd/${pkgname}-${pkgver}.tar.gz"
	"https://ftp.de.debian.org/debian/pool/main/x/xosd/xosd_$pkgver-2.debian.tar.gz")
sha256sums=('4f1fa96f4895baee6a3cba40f188a0da23bbac9d68e8c326d749e084143cb508'
            '82673c6d4fa7644ae7707f6fc35799cbbf38b02852d375b38faeb2a0690c0ca5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <"$srcdir"/debian/patches/25_fix_mangapges.diff
  patch -p1 <"$srcdir"/debian/patches/328676.diff
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  sed -i 's/AC_DEFUN(AM_PATH_LIBXOSD,/AC_DEFUN([AM_PATH_LIBXOSD],/' \
    "$pkgdir/usr/share/aclocal/libxosd.m4"
}
