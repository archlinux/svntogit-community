# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=xosd
pkgver=2.2.14
pkgrel=8
pkgdesc="Displays text on your screen. On-Screen-Display-libs for some tools"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://sourceforge.net/projects/libxosd/"
depends=('libxt' 'libxinerama' 'sh')
source=("http://downloads.sourceforge.net/libxosd/${pkgname}-${pkgver}.tar.gz"
	"http://ftp.de.debian.org/debian/pool/main/x/xosd/xosd_$pkgver-2.debian.tar.gz")
md5sums=('4b349fe930e4eee2f504d6c02673e24d'
         'cd3c37f4e4bbfba6f3ffd7ec43256cc2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <$srcdir/debian/patches/25_fix_mangapges.diff
  patch -p1 <$srcdir/debian/patches/328676.diff
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  sed -i 's/AC_DEFUN(AM_PATH_LIBXOSD,/AC_DEFUN([AM_PATH_LIBXOSD],/' \
    "$pkgdir/usr/share/aclocal/libxosd.m4"
}
