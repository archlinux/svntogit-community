# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=xpad
pkgver=5.0.0
pkgrel=1
pkgdesc="Stickies-like notes program"
arch=(i686 x86_64)
depends=('gtk3' 'libsm' 'librsvg' 'gtksourceview3')
makedepends=('intltool')
url="https://launchpad.net/xpad"
license=('GPL')
validpgpkeys=('3930646349B9DD3886D9087392BF2FCCA6F2322B'
	      '8450BEE19B722715FFDB18C6683EAA45951FC552')
source=(https://launchpad.net/xpad/trunk/${pkgver}/+download/xpad-$pkgver.tar.bz2{,.asc})
sha256sums=('6b5673c68faad4afb44aea0ff5d72c7b5d2aef0d00a89980a27ca56f5e9fd10b'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install helpdir=/usr/share/xpad/help
}
