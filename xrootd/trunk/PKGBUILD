# Maintainer: Konstantin Gizdov < arch at kge dot pw >
# Contributor: Jan Ka≈°par < jan.kaspar at gmail dot com >
# Contributor: Alex Pearce < alex at alexpearce dot me >
pkgname=xrootd
pkgdesc="Software framework for fast, low latency, scalable and fault tolerant data access."
pkgver=5.0.1
pkgrel=1
arch=('x86_64')
url="https://xrootd.slac.stanford.edu/"
license=('LGPL3')
depends=('ceph' 'curl' 'json-c' 'libxml2' 'openssl' 'perl' 'python' 'systemd')
makedepends=('cmake' 'git')
checkdepends=('cppunit')
source=(
    "${pkgname}-${pkgver}::git+https://github.com/${pkgname}/${pkgname}.git#tag=v${pkgver}"
    'cxx17.patch'
    'add_missing_header.patch'
)
sha256sums=('SKIP'
            '828de400b8fef40a80b9eadb8198fdfe7c991515a466045b699425df2f681817'
            '68eac594d8d3b15c1e4d8522c4422a26c9a51c9206d3a88fedf00d4e9599d455')

get_pyver () {
    python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare () {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    # source tarballs don't seem to contain all submodules anymore
    git submodule update --init --recursive
    patch -Np1 -i "${srcdir}/cxx17.patch"
    patch -Np1 -i "${srcdir}/add_missing_header.patch"
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"

    # configure
    mkdir -p "${srcdir}/build"
    cd "${srcdir}/build"

    CFLAGS="${CFLAGS}" \
    CXXFLAGS="${CXXFLAGS}" \
    LDFLAGS="${LDFLAGS}" \
    cmake -DCMAKE_BUILD_TYPE:STRING=Release \
          -DCMAKE_INSTALL_LIBDIR:PATH=lib \
          -DCMAKE_INSTALL_PREFIX:PATH=/usr \
          -DCMAKE_CXX_STANDARD="17" \
          -DENABLE_TESTS=1 \
          -DXRD_PYTHON_REQ_VERSION=$(get_pyver) \
          "${srcdir}/${pkgbase}-${pkgver}"

    # build
    make
}

check() {
    cd "${srcdir}/build/tests"

    # Check has significantly changed, to-be-updated
    # ./common/text-runner ./XrdCephTests/libXrdCephTests.so "All Tests"

    ##
    # This requires a running XRootD server with multiIP DNS forwarder and local disk servers
    # only run this if you have configured the env correctly,
    # examples in https://github.com/xrootd/xrootd-test-suite
    # sample environment can be configured like so:
    # export XRDTEST_MAINSERVERURL=metaman.xrd.test
    # or export XRDTEST_MAINSERVERURL=http://xrootd.cern.ch/
    # export XRDTEST_DISKSERVERURL=srv1.xrd.test
    # or export XRDTEST_DISKSERVERURL=http://xrootd.cern.ch/
    # export XRDTEST_DATAPATH=/tests/test-files/
    # export XRDTEST_LOCALFILE=/data/a048e67f-4397-4bb8-85eb-8d7e40d90763.dat
    # or export XRDTEST_LOCALFILE=/tmp/accwe.root
    # export XRDTEST_REMOTEFILE=${XRDTEST_MAINSERVERURL}${XRDTEST_DATAPATH}/a048e67f-4397-4bb8-85eb-8d7e40d90763.dat
    # or export XRDTEST_MULTIIPSERVERURL=multiip.xrd.test
    # ./common/text-runner ./XrdClTests/libXrdClTests.so "All Tests"
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
    # drop unneeded test bin & lib
    rm "${pkgdir}/usr/bin/text-runner"
    rm "${pkgdir}/usr/lib/libXrdClTests.so"
    install -d "${pkgdir}/usr/lib/cmake/XRootD"
    mv "${pkgdir}/usr/share/xrootd/cmake/XRootDConfig.cmake" "${pkgdir}/usr/lib/cmake/XRootD"/
    rm -rf "${pkgdir}/usr/share/xrootd/cmake"
    ln -s "/usr/lib/cmake" "${pkgdir}/usr/share/xrootd/cmake"
}
