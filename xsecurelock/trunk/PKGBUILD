# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Johannes Wienke <languitar@semipol.de>
pkgname=xsecurelock
pkgver=1.4.0
pkgrel=1
pkgdesc="X11 screen lock utility with security in mind"
arch=('x86_64')
url="https://github.com/google/xsecurelock"
license=('APACHE')
depends=('pam' 'libxcomposite' 'libxmu' 'libxft' 'libxrandr' 'libxss')
optdepends=('mplayer: for the saver_mplayer module'\
            'mpv: for the saver_mpv module'\
            'imagemagick: for the auth_pamtester module'\
            'pamtester: for the auth_pamtester module'\
            'xorg-xset: for the saver_blank module'\
            'xscreensaver: for the saver_xscreensaver module')
source=("https://github.com/google/xsecurelock/archive/v${pkgver}.tar.gz")
sha512sums=('f882099750d769a83331c1d3b521e84d4f2592089fb0f2fdcb21c34911e0ccf1fb62dc46ebfa63195f63f51c33520731961eb1ee597b9bfad635f4ab279a3c7c')

build() {
    cd "${pkgname}-${pkgver}"
    sh autogen.sh
    ./configure --prefix=/usr \
                --libexecdir=/usr/lib \
                --with-pam-service-name=system-auth
    make GIT_VERSION="${pkgver}"
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
