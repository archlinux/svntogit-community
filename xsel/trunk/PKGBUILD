# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xsel
pkgver=1.2.0.20160929
_commit=aa7f57eed805adb09e9c59c8ea841870e8206b81
pkgrel=1
pkgdesc="XSel is a command-line program for getting and setting the contents of the X selection"
arch=(x86_64)
url="http://www.vergenet.net/~conrad/software/xsel/"
license=("custom")
depends=('libx11')
makedepends=('libxt' 'git')
#source=(http://www.vergenet.net/~conrad/software/xsel/download/$pkgname-$pkgver.tar.gz)
source=("git://github.com/kfish/xsel.git#commit=${_commit}")
md5sums=('SKIP')

build() {
  cd "$srcdir"/$pkgname
  touch README
  [ -x configure ] && ./configure --prefix=/usr || ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/share/licenses/xsel/
  install -D -m0644 COPYING "$pkgdir"/usr/share/licenses/xsel/COPYING
}
