# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Ray Rashif <schivmeister@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xsensors
pkgver=0.70
pkgrel=2
pkgdesc="X11 interface to lm_sensors"
arch=('i686' 'x86_64')
url="http://www.linuxhardware.org/xsensors/"
license=('GPL')
depends=('gtk2' 'lm_sensors')
changelog=$pkgname.changelog
source=(http://www.linuxhardware.org/xsensors/$pkgname-$pkgver.tar.gz
	fix-deprecated-GTK_WIDGET_STATE-call.diff
	xsensors.desktop)
md5sums=('4f8fb83cfd03c0cc34967a73c6021531'
         'e5ac8e58f0dd27501234692587c01b76'
         'fe1369d2b61eed1a25002e2247617357')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np1 -i ${srcdir}/fix-deprecated-GTK_WIDGET_STATE-call.diff

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
  install -D ${srcdir}/xsensors.desktop \
    ${pkgdir}/usr/share/applications/xsensors.desktop
}
