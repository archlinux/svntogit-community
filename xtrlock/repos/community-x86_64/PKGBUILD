# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.9
pkgrel=1
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=('x86_64')
license=('GPL3')
depends=('libx11')
source=(http://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.xz)
sha256sums=('e7a78ed1e15a4926edf3be51439d49a0045b6dfd8c8ddacf119fa232669731eb')
sha512sums=('236b56b510614dfe3c1b8fa5b6738836fac6e21de5a242c2ebc943b2fd903370a8661a90d2a747ae4f66219512db11df5b30a3b5aa88af1ee4caf3507fd35ad5')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.noimake \
    CFLAGS="${CFLAGS} -DSHADOW_PWD" \
    LDLIBS="-lX11 -lcrypt" \
    xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock -t "${pkgdir}/usr/bin"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:
