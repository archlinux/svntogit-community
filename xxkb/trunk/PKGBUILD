# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xxkb
pkgver=1.11
pkgrel=4
pkgdesc="keyboard layout switcher/indicator"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/xxkb/"
license=('PerlArtistic')
depends=('libxpm')
makedepends=('imake')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-$pkgver-src.tar.gz"
    	XXkb
    	en22.xpm
    	ru22.xpm)
md5sums=('c2e5c7f3285033f0e26b358ef95a7c89'
         'f181b7a017d3d2524ecf10638317d341'
         '649c6d378fe12853d457b768bd273dc5'
         'f1692f0be5fc697b3b792d0098bdb42f')

build() {
  cd "${srcdir}"/${pkgname}
  xmkmf
  make PROJECTROOT=/usr PIXMAPDIR=/usr/share/$pkgname LOCAL_LIBRARIES='-lXext -lXt -lXpm'
}

package() {
  cd "${srcdir}"/${pkgname}
  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/$pkgname
  install -d $pkgdir/usr/man/man1
  install -d $pkgdir/usr/share/X11/app-defaults

  install -m 0755 xxkb $pkgdir/usr/bin
  install -m 0644 ../XXkb $pkgdir/usr/share/X11/app-defaults/XXkb
  install -m 0644 pixmaps/*.xpm $pkgdir/usr/share/$pkgname
  install -m 0644 $pkgname.man $pkgdir/usr/man/man1/$pkgname.1
  install -m 0644 ../*.xpm $pkgdir/usr/share/$pkgname/

  mv $pkgdir/usr/man $pkgdir/usr/share/
}
