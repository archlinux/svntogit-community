# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.14.1
pkgrel=1
pkgdesc='Yet another Elegant Go Interpreter'
arch=('x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('17d87196fa895dae6511a91041dbd992535db5c2a55bff36f084e11d91573d4b62b105f341efd98910c01def1bde6f2df43f5e6e8cf3a2a8dc72ef79d7fdd790')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build -v ./cmd/yaegi
}

# disabling tests...
#check() {
#  cd "${pkgname}-${pkgver}"
#  sed -i 's|"-race", ||' ./cmd/yaegi/yaegi_test.go
#  GOROOT="/usr/lib/go" go test -v ./...
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
