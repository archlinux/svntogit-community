# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: das-ich <das-ich@yandex.ru>

pkgname=yagf
pkgver=0.9.3.2
pkgrel=1
pkgdesc="Qt4-based GUI for Cuneiform and Tesseract OCRs"
arch=(i686 x86_64)
url="http://symmetrica.net/cuneiform-linux/yagf-en.html"
license=('GPL')
depends=('qt4' 'aspell')
optdepends=('cuneiform: recognition tool'
	    'tesseract: recognition tool'
	    'xsane: acquire images from a scanner into YAGF directly')
makedepends=('cmake')
install=yagf.install
source=(http://symmetrica.net/cuneiform-linux/${pkgname}-${pkgver}.tar.gz)
md5sums=('f36a0f318e331652a441f7751e06590d')

build() {
  cd "${srcdir}/${pkgname}-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd "${srcdir}/${pkgname}-$pkgver"
  make DESTDIR=${pkgdir} install
  [ -d $pkgdir/usr/lib64 ] && mv $pkgdir/usr/lib64 $pkgdir/usr/lib || true
}
