# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: das-ich <das-ich@yandex.ru>

pkgname=yagf
pkgver=0.9.5
pkgrel=3
pkgdesc="Qt4-based GUI for Cuneiform and Tesseract OCRs"
arch=(i686 x86_64)
url="http://symmetrica.net/cuneiform-linux/yagf-en.html"
license=('GPL')
depends=('qt4' 'aspell')
optdepends=('cuneiform: recognition tool'
	    'tesseract: recognition tool'
	    'xsane: acquire images from a scanner into YAGF directly')
makedepends=('cmake')
#source=(https://yagf.googlecode.com/archive/yagf-$pkgver.tar.gz)
source=(http://downloads.sourceforge.net/project/yagf-ocr/yagf-$pkgver.tar.gz)
md5sums=('cc01d671fafbfa71fad3d95591ccca37')

prepare() {
  cd `find ${srcdir}/ -type d -name "${pkgname}*"`
  cp src/images/{A,a}lign.png
  sed -i '1,1i#include <unistd.h>' src/mainform.cpp
}

build() {
  cd `find ${srcdir}/ -type d -name "${pkgname}*"`
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd `find ${srcdir}/ -type d -name "${pkgname}*"`
  make DESTDIR=${pkgdir} install
  [ -d $pkgdir/usr/lib64 ] && mv $pkgdir/usr/lib64 $pkgdir/usr/lib || true
}
