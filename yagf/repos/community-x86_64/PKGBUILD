# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: das-ich <das-ich@yandex.ru>

pkgname=yagf
pkgver=0.9.4
pkgrel=2
pkgdesc="Qt4-based GUI for Cuneiform and Tesseract OCRs"
arch=(i686 x86_64)
url="http://symmetrica.net/cuneiform-linux/yagf-en.html"
license=('GPL')
depends=('qt4' 'aspell')
optdepends=('cuneiform: recognition tool'
	    'tesseract: recognition tool'
	    'xsane: acquire images from a scanner into YAGF directly')
makedepends=('cmake')
install=yagf.install
source=(https://yagf.googlecode.com/archive/yagf-$pkgver.tar.gz)
md5sums=('8a14cffa63c4e5492962b777cbb5f96b')

prepare() {
  cd `find ${srcdir}/ -type d -name "${pkgname}*"`
  cp src/images/{A,a}lign.png
  sed -i '1,1i#include <unistd.h>' src/mainform.cpp
}

build() {
  cd `find ${srcdir}/ -type d -name "${pkgname}*"`
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd `find ${srcdir}/ -type d -name "${pkgname}*"`
  make DESTDIR=${pkgdir} install
  [ -d $pkgdir/usr/lib64 ] && mv $pkgdir/usr/lib64 $pkgdir/usr/lib || true
}
