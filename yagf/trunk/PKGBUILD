# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: das-ich <das-ich@yandex.ru>

pkgname=yagf
pkgver=0.9.2
pkgrel=2
pkgdesc="QT4-based GUI for Cuneiform and Tesseract OCRs"
arch=(i686 x86_64)
url="http://symmetrica.net/cuneiform-linux/yagf-en.html"
license=('GPL')
depends=('qt>=4.3' 'aspell')
optdepends=('cuneiform: recognition tool'
	    'tesseract: recognition tool'
	    'xsane: acquire images from a scanner into YAGF directly')
makedepends=('cmake')
install=yagf.install
source=(http://symmetrica.net/cuneiform-linux/${pkgname}-${pkgver}.tar.gz)
md5sums=('13592e52006d20f71c0094aafb13ed0e')

build() {
  cd "${srcdir}/${pkgname}-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd "${srcdir}/${pkgname}-$pkgver"
  make DESTDIR=${pkgdir} install
  [ -d $pkgdir/usr/lib64 ] && mv $pkgdir/usr/lib64 $pkgdir/usr/lib || true
}
