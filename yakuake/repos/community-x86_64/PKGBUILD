# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: leeghoofd <abcdefg@solcon.nl>

pkgname=yakuake
pkgver=3.0.3
pkgrel=1
pkgdesc="A drop-down terminal emulator based on KDE konsole technology"
arch=('i686' 'x86_64')
url='http://yakuake.kde.org/'
license=('GPL')
depends=('konsole' 'knewstuff' 'kwayland' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig})
md5sums=('f85eb541a76061012fe815e573f441f3'
         'SKIP')
validpgpkeys=('DA6B31C4B98A877DBB2EF9A292220328C632316E') # Eike Hein <hein@kde.org>

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
