# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: leeghoofd <abcdefg@solcon.nl>

pkgname=yakuake
pkgver=3.0.4
pkgrel=2
pkgdesc="A drop-down terminal emulator based on KDE konsole technology"
arch=('x86_64')
url='http://yakuake.kde.org/'
license=('GPL')
depends=('konsole' 'knewstuff' 'kwayland' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python')
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig}
        yakuake-wayland1.patch::"https://cgit.kde.org/yakuake.git/patch/?id=7534df21"
        yakuake-wayland2.patch::"https://cgit.kde.org/yakuake.git/patch/?id=3cdc1d06"
        yakuake-wayland3.patch::"https://cgit.kde.org/yakuake.git/patch/?id=5c555b3d")
sha256sums=('10e2b57c508e2ec5bfd09eac38c4c16a1ac4b93b394b3ce3120b7d8859b861e0'
            'SKIP'
            '0f5c7d10be914cf3ce5fc57cf635cf5ce6905c67ba5c439b306b9cbcf5914f46'
            '359aa9a723cb962a2511e16344fb92100c4c67ebde1276df4858526c9f85c2ef'
            '978fd631f7a02612a47360fd384c45b052fb445ad45d463af8e81e34f2a7ccd8')
validpgpkeys=('DA6B31C4B98A877DBB2EF9A292220328C632316E') # Eike Hein <hein@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../yakuake-wayland1.patch # Fix positioning on wayland
  patch -p1 -i ../yakuake-wayland2.patch
  patch -p1 -i ../yakuake-wayland3.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
