# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Markus Martin <markus@archwyrm.net>

pkgname=yaml-cpp
pkgver=0.5.3
pkgrel=3
pkgdesc="YAML parser and emitter in C++, written around the YAML 1.2 spec"
url="https://github.com/jbeder/yaml-cpp"
arch=('i686' 'x86_64')
license=('MIT')
depends=('gcc-libs')
makedepends=('boost' 'cmake')
source=(https://github.com/jbeder/yaml-cpp/archive/release-${pkgver}.tar.gz
        500db60f899ae6845039d4eca503133d0db81dbd.patch
        1f4b6d5c855b59e849b7228090981c520928c2a4.patch)
sha512sums=('5ed15fee3c6455c08e6bd8f74256b230f274ef18f8e144491e940640e41626517c7eaaf4a1f380c4179066a2a757c8a0f61878df9dc3caa15e37c4954be47fe0'
            '8ffd7dd7deaf3eef8ce5eefac73d4a453c86f852cd93579b0e3edcd3b174fd52b2a8a6ef37e5588a3d3f85dde3bdf53bc4b385e2b24c4e46a8cd00af75161ae0'
            'e71948c84d3912c14484fc4dd4efa4f2402bb0fda98b4102fe8ce5af9cf1676ad1ccf49a33411559433f271610db4fa44f5cf3ed998cb0bcf330ee7f83ac335e')

prepare() {
    cd $srcdir/$pkgname-release-$pkgver
    patch -Np1 < $srcdir/500db60f899ae6845039d4eca503133d0db81dbd.patch
    patch -Np1 < $srcdir/1f4b6d5c855b59e849b7228090981c520928c2a4.patch
}

build() {
    cd $srcdir/$pkgname-release-$pkgver

    cmake . -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON
    make
}

package() {
    cd $srcdir/$pkgname-release-$pkgver

    make DESTDIR=$pkgdir install

    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
