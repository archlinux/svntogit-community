# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Markus Martin <markus@archwyrm.net>

pkgname=yaml-cpp
pkgver=0.5.2
pkgrel=5
pkgdesc="YAML parser and emitter in C++, written around the YAML 1.2 spec"
url="https://github.com/jbeder/yaml-cpp"
arch=('i686' 'x86_64')
license=('MIT')
depends=('gcc-libs')
makedepends=('boost' 'cmake')
source=(https://github.com/jbeder/yaml-cpp/archive/release-${pkgver}.tar.gz)
md5sums=('2728af8a15e2b2c407730c45b99b274b')

build() {
    cd $srcdir/$pkgname-release-$pkgver

    cmake . -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON
    make
}

package() {
    cd $srcdir/$pkgname-release-$pkgver

    make DESTDIR=$pkgdir install

    install -Dm644 yaml-cpp-config.cmake $pkgdir/usr/lib/cmake/${pkgname}/yaml-cpp-config.cmake
    install -Dm644 yaml-cpp-config-version.cmake $pkgdir/usr/lib/cmake/${pkgname}/yaml-cpp-config-version.cmake

    install -Dm644 license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
