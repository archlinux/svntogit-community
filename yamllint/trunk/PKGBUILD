# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yamllint
pkgver=1.28.0
pkgrel=1
pkgdesc='Linter for YAML files'
arch=('any')
url=https://github.com/adrienverge/yamllint
license=('GPL3')
depends=('python-pathspec' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-nose')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('9e3d8ddd16d0583214c5fdffe806c9344086721f107435f68bad990e5a88826b')
b2sums=('6c93b7f45cab49159f6a36aa24ac303455432734fcb90f75cf80551eb2c5a78c9695e76f968aa7e22ad976accd0257567d85d7ac24474695dbe3e4dcf7f6eea3')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/setuptools/d' setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m unittest discover tests
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
