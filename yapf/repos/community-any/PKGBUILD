# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("$pkgbase" "python2-$pkgbase")
pkgver=0.6.3
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/google/${pkgbase}.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a $pkgbase{,-py2}
}

build() {
  cd "${srcdir}/${pkgbase}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgbase}"
  nosetests3

  cd "${srcdir}/${pkgbase}-py2"
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd $pkgbase
  python setup.py install -O1 --root="$pkgdir/"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd $pkgbase-py2
  python2 setup.py install -O1 --root="$pkgdir/"
  mv "$pkgdir/usr/bin/yapf" "$pkgdir/usr/bin/yapf2"
}

# vim:set ts=2 sw=2 et:
