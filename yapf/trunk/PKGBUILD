# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("yapf" "python2-yapf")
pkgver=0.8.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/google/yapf.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a yapf{,-py2}
}

build() {
  cd "$srcdir"/yapf
  python setup.py build

  cd "$srcdir"/yapf-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/yapf
  nosetests3

  cd "$srcdir"/yapf-py2
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd yapf
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd yapf-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/yapf{,2}
}

# vim:set ts=2 sw=2 et:
