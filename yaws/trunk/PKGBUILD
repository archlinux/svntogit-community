# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Patrick Smits <mail@patricksmits.net>

pkgname=yaws
pkgver=2.0.4
pkgrel=2
pkgdesc='Web server/framework written in Erlang'
arch=('x86_64' 'i686')
url='http://yaws.hyber.org/'
license=('BSD')
depends=('pam' 'erlang-nox')
backup=('etc/yaws/yaws.conf')
options=('!emptydirs')
install="$pkgname.install"
source=("http://yaws.hyber.org/download/$pkgname-$pkgver.tar.gz" # no https
        "$pkgname.service")
sha256sums=('da6677c315aadc7c64c970ef74eaa29f61eba886c7d30c61806651ac38c1e6c5'
            '3628a0f6fdacc91c216cb782e3f3da638958cca6eee34a0a1e692482659109db')

prepare() {
  cd "$pkgname-$pkgver"

  # use python2
  for f in cgi.yaws cgi-bin/foo.py; do
    sed -i 's:n/python:n/python2:' "www/$f"
  done

  # don't treat warnings as errors just now
  sed -i 's/-Werror//g' include.mk
}

build() {
  cd "$pkgname-$pkgver"

  export wwwdir=/srv/http/yaws
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/lib/erlang/lib"
  ln -s /usr/lib/yaws "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver"
  install -Dm644 "$srcdir/$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # path fixes
  cp -ru examples/include "$pkgdir/usr/lib/yaws-$pkgver/examples/"
  mv "$pkgdir/srv/http/yaws/"* "$pkgdir/srv/http/www" || true
  rmdir "$pkgdir/srv/http/yaws" || true
  mv "$pkgdir/srv/http/www" "$pkgdir/srv/http/yaws" || true
}

# vim:set ts=2 sw=2 et:
