# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=yder
pkgver=1.4.18
pkgrel=1
pkgdesc='Logging library for C applications'
arch=(x86_64)
url=https://github.com/babelouest/yder
license=(LGPL2.1)
depends=(
  glibc
  liborcania.so
  libsystemd.so
)
makedepends=(
  cmake
  doxygen
  git
  graphviz
  ninja
  systemd
)
provides=(libyder.so)
_tag=2d52b510dae2512944f567ba6d9a88938f83cad0
source=(git+https://github.com/babelouest/yder.git#tag=${_tag})
validpgpkeys=(8405B02FCC28EF9744C8F253FE82139440BD22B9) # Nicolas Mora <nicolas@babelouest.org>
b2sums=(SKIP)

pkgver() {
  cd yder
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S yder -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_YDER_DOCUMENTATION=ON
  ninja -C build doc
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -dm 755 "${pkgdir}"/usr/share/doc
  cp -dr --no-preserve=ownership yder/doc/html "${pkgdir}"/usr/share/doc/yder/
}

# vim: ts=2 sw=2 et:
