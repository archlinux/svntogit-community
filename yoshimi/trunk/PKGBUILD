# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.1.2.2
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=(x86_64)
url="https://yoshimi.github.io/"
license=(GPL2)
groups=(pro-audio)
depends=(cairo gcc-libs glibc zlib)
makedepends=(alsa-lib cmake fftw fltk jack lv2 mxml ncurses readline)
checkdepends=(kxstudio-lv2-extensions lv2lint)
optdepends=('lv2-host: for LV2 plugin')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
)
sha512sums=('14e9dc19e9654d4178004adfc930ca74522ab9db6711aaf2780e57930d6426cb28fd51f3d8d9b7478864e6a3e17d737512dd42cb9cdceeefe168ba803e71edaf')
b2sums=('45da49df7bea0919d08f66fa37ddf21483ac3074aa7fcb57f1a55137ab9f29545dc2121041b7c0fb33d86cffa0608a5fda5a963a79ce54c33cc4fc90ab5e38ba')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  cp -v "build/LV2_Plugin/${pkgname}_lv2.so" "src/LV2_Plugin/"
  lv2lint -Mpack -I src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v "src/LV2_Plugin/${pkgname}_lv2.so"
}

package() {
  depends+=(libasound.so libfftw3f.so libfltk.so libfltk_images.so libjack.so
  libmxml.so libncursesw.so libreadline.so)

  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
