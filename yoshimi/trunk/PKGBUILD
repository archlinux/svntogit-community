# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.2.0
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=(x86_64)
url="https://yoshimi.github.io/"
license=(GPL2)
groups=(pro-audio)
depends=(cairo gcc-libs glibc zlib)
makedepends=(alsa-lib cmake fftw fltk jack lv2 mxml ncurses readline)
checkdepends=(kxstudio-lv2-extensions lv2lint)
optdepends=('lv2-host: for LV2 plugin')
# plugin exposes symbols globally if built with LTO:
# https://github.com/Yoshimi/yoshimi/issues/164
options=(!lto debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz)
sha512sums=('b2d21732d1dd476b0217c10012d654f038250a822517fd4cd4abd85d931c62f3a416cc39438c119bbd1b11b5f40a55735ff8316cc513204abb380b33fbc37231')
b2sums=('9ed0976f70c1eea6a17be12e55bc3e01bb304614d0cf91d26f90ad676306baf46967406f7adf576e9758ae7ff103fee4d02584141d3afe99e4b67ff71ef01b18')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build\
        -S $pkgname-$pkgver/src
  make VERBOSE=1 -C build
}

check() {
  cp -v build/LV2_Plugin/${pkgname}_lv2.so $pkgname-$pkgver/src/LV2_Plugin/
  lv2lint -Mpack -I $pkgname-$pkgver/src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v $pkgname-$pkgver/src/LV2_Plugin/${pkgname}_lv2.so
}

package() {
  depends+=(libasound.so libfftw3f.so libfltk.so libfltk_images.so libjack.so
  libmxml.so libncursesw.so libreadline.so)

  make DESTDIR="$pkgdir" VERBOSE=1 install -C build
  install -vDm 644 $pkgname-$pkgver/{Changelog,Dependencies,README.txt,Yoshimi_Helpers} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
