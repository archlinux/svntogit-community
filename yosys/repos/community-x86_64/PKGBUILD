# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Beno√Æt Allard <benoit dot allard at gmx dot de>
# Contributor: killruana <killruana@gmail.com>

pkgname=yosys
pkgver=0.12
pkgrel=5
pkgdesc='A framework for RTL synthesis'
arch=('x86_64')
url='http://www.clifford.at/yosys/'
license=('custom:ISC')
depends=('abc' 'bash' 'boost-libs' 'tcl' 'libffi' 'python' 'protobuf')
makedepends=('boost')
checkdepends=('iverilog')
optdepends=('graphviz: Schematics display support'
            'xdot: Display netlists')
options=('!lto') # getting SIGSEGV when running the tests with LTO
source=("https://github.com/cliffordwolf/yosys/archive/$pkgname-$pkgver.tar.gz")
sha512sums=('df91ea75ae08c7c7e134cfa6284c4e9349e6f85f2df32e4710a571176d5e1a334a6e1e77d52bf573686d33b405559e40af1a8d42cbd4e1f95f0e3b4e212e0b06')

_make() {
    make \
        PREFIX="/usr" \
        CONFIG=gcc \
        ENABLE_TCL=1 \
        ENABLE_ABC=1 \
        ENABLE_GLOB=1 \
        ENABLE_PLUGINS=1 \
        ENABLE_LIBYOSYS=1 \
        ENABLE_PROTOBUF=1 \
        ENABLE_ZLIB=1 \
        ENABLE_PYOSYS=1 \
        ABCEXTERNAL=abc \
        BOOST_PYTHON_LIB="-lpython3.10 -lboost_python310" $@
}

build() {
    cd $pkgname-$pkgname-$pkgver
    _make
}

check() {
    cd $pkgname-$pkgname-$pkgver
    _make test
}

package() {
    cd $pkgname-$pkgname-$pkgver
    _make DESTDIR="$pkgdir" PYTHON_PREFIX="$pkgdir/usr" install

    install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
