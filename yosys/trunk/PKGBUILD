# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Beno√Æt Allard <benoit dot allard at gmx dot de>
# Contributor: killruana <killruana@gmail.com>

pkgname=yosys
pkgver=0.9
_abccommit=2ddc57d8760d94e86699be39a628178cff8154f8
pkgrel=5
pkgdesc='A framework for RTL synthesis'
arch=('x86_64')
url='http://www.clifford.at/yosys/'
license=('custom:ISC')
depends=('bash' 'tcl' 'libffi' 'python' 'protobuf')
optdepends=('graphviz: Schematics display support'
            'xdot: Display netlists')
source=("https://github.com/cliffordwolf/yosys/archive/$pkgname-$pkgver.tar.gz"
        abc-$_abccommit.tar.gz::https://github.com/berkeley-abc/abc/archive/$_abccommit.tar.gz)
sha512sums=('6ae0f822ca0e3ec749773dae89e948488b698aaa149e73f04604dd899ac403a3073645f0e27382801f5f1a9483d87ef4066fde5dc797181eff06684de7a5b138'
            '5e5c9b887b89ae40a7b82f6d3c5b28bc0c79375fd2178598d5ddcf281c98ff6d61c73c06bc74cfad1be140722d14ea8deabae6c1c9e6c3cd641b37e6f0b7e805')

prepare() {
    ln -s "$srcdir"/abc-$_abccommit $pkgname-$pkgname-$pkgver/abc
}

_make() {
    make \
        PREFIX="/usr" \
        ABCREV=default \
        CONFIG=gcc \
        ENABLE_TCL=1 \
        ENABLE_ABC=1 \
        ENABLE_GLOB=1 \
        ENABLE_PLUGINS=1 \
        ENABLE_LIBYOSYS=1 \
        ENABLE_PROTOBUF=1 \
        ENABLE_ZLIB=1 \
        ENABLE_PYOSYS=0 $@
}

build() {
    cd $pkgname-$pkgname-$pkgver

    _make
}

package() {
    cd $pkgname-$pkgname-$pkgver
    _make DESTDIR="$pkgdir" install

    install -D -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
