# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

pkgname=youtube-viewer
epoch=1
pkgver=3.5.2
pkgrel=1
pkgdesc='Command line utility for viewing YouTube videos'
arch=('any')
url='https://github.com/trizen/youtube-viewer'
license=('GPL3')
depends=('perl' 'perl-data-dump' 'perl-json' 'perl-lwp-protocol-https' 'perl-libwww')
makedepends=('perl-module-build')
optdepends=('perl-json-xs: faster JSON to HASH conversion'
            'perl-text-charwidth: print results in a fixed-width format'
            'perl-term-readline-gnu: better STDIN support'
            'youtube-dl: play videos with encrypted signatures'
            'wget: download videos with wget'
            'vlc: play videos with VLC'
            'mpv: play videos with MPV'
            'gtk2-perl: GTK GUI'
            'perl-file-sharedir: GTK GUI')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trizen/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('0c0170c2870692a38bc6953142164f200e768e25531c701fb03897b47a57e927')

build() {
    cd "$pkgname-$pkgver"
    perl Build.PL --installdirs vendor --gtk
}

check(){
    cd "$pkgname-$pkgver"
    ./Build test
}

package() {
    cd "$pkgname-$pkgver"
    ./Build install --destdir "$pkgdir"
    rm -r "$pkgdir"/usr/lib

    mkdir "$pkgdir"/usr/share/{applications,pixmaps}
    mv "$pkgdir"/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/gtk-youtube-viewer.desktop \
        "$pkgdir"/usr/share/applications/gtk-youtube-viewer.desktop
    cp "$pkgdir"/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/icons/gtk-youtube-viewer.png \
        "$pkgdir"/usr/share/pixmaps/gtk-youtube-viewer.png
}
