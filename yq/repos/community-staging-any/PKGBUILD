# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=yq
pkgver=2.10.1
pkgrel=2
pkgdesc="Command-line YAML/XML processor - jq wrapper for YAML/XML documents"
url="https://github.com/kislyuk/yq"
depends=('jq' 'python-setuptools' 'python-yaml' 'python-xmltodict'
         'python-argcomplete')
checkdepends=('python-toml')
optdepends=('python-toml')
license=('Apache')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('7fd38d454a924810117b7b87e519b4e91be654631827f806a95723ca04218fe3a7ece84a5a88787fe4b2d9acd60da74d6a7131950724f339b4e1bf3d0e35c792')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
