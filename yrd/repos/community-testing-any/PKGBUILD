# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>

pkgname=yrd
pkgver=0.5.3
pkgrel=2
pkgdesc='cjdns swiss army knife'
url='https://github.com/kpcyrd/yrd'
license=('GPL3')
arch=('any')
depends=('python-setuptools' 'python-argh' 'python-requests' 'cjdns')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kpcyrd/yrd/archive/v${pkgver}.tar.gz
        yrd.tmpfiles.conf)
sha512sums=('c76ab91d59c97d096adf15fb2c3e00f76102fa9a7715911348e2adb56fb818e5ceaed299027645deb9836f3d4811bdf47618fc0e96adb352cf05ff9406957096'
            '191fdf5687886357c01b72b2b9037045ca24d0ee888dc2bf6b2748a769eeb9a481eb01a9807f8f32b783d0970229f18d1264e937c2dd44b282ac764c49450cca')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build

  install -Dm 644 doc/yrd.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -Dm 644 "${srcdir}/${pkgname}.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/yrd.conf"
  install -Dm 644 init/yrd.service -t "${pkgdir}/usr/lib/systemd/system"
}

# vim: ts=2 sw=2 et:
