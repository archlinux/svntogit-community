# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname='yubikey-manager'
pkgver=0.5.0
pkgrel=1
pkgdesc='Python library and command line tool for configuring a YubiKey'
arch=('any')
url='https://developers.yubico.com/yubikey-manager/'
license=('BSD')
depends=('python' 'python-click' 'python-cryptography' 'python-pyopenssl' 'python-pyscard'
         'python-pyusb' 'python-six' 'ccid' 'libu2f-host' 'yubikey-personalization')
makedepends=('python-setuptools')
validpgpkeys=('8D0B4EBA9345254BCEC0E843514F078FF4AB24C3'  # Dag Heyman <dag@yubico.com>
              '57A9DEED4C6D962A923BB691816F3ED99921835E') # Emil Lundberg (Software Developer) <emil@yubico.com>
source=("https://developers.yubico.com/$pkgname/Releases/yubikey-manager-$pkgver.tar.gz"{,.sig})
sha256sums=('c4a7c6f8af3c7f719a0c1dfcbe12440667c60137f74188ff8d4c5506bd631118'
            'SKIP')

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	python setup.py install --root="${pkgdir}/" --optimize=1

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

