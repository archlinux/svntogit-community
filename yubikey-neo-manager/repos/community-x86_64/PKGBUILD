# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-neo-manager
pkgver=1.3.0
pkgrel=2
pkgdesc="Cross platform personalization tool for the YubiKey NEO"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubikey-neo-manager"
license=('BSD')
depends=('yubikey-personalization' 'libykneomgr' 'libu2f-host' 'python2-pyside' 'python2-crypto' 'qtwebkit')
makedepends=('git' 'python2-setuptools' 'python2-pyside-tools' 'imagemagick')
install=yubikey-neo-manager.install
source=("git://github.com/Yubico/yubikey-neo-manager.git#tag=yubikey-neo-manager-${pkgver}")
sha256sums=('SKIP')

package() {
	cd yubikey-neo-manager/

	python2 setup.py qt_resources
	python2 setup.py install --root="${pkgdir}/" --optimize=1

	install -D -m0644 resources/neoman-large.png "${pkgdir}/usr/share/icons/hicolor/128x128/neoman.png"
	for SIZE in 16 24 32 48 64 96; do
		convert -scale ${SIZE} \
			resources/neoman-large.png \
			${srcdir}/neoman.png
		install -D -m0644 ${srcdir}/neoman.png "${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/neoman.png"
	done

	install -D -m0644 resources/neoman.desktop "${pkgdir}/usr/share/applications/neoman.desktop"

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/yubikey-neo-manager/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/yubikey-neo-manager/README"
}

