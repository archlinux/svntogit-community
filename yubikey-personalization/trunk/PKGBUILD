# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-personalization
_shortname=ykpers
pkgver=1.19.0
pkgrel=2
pkgdesc='Yubico YubiKey Personalization library and tool'
arch=('x86_64')
url='https://github.com/Yubico/yubikey-personalization'
license=('BSD')
depends=('libusb' 'json-c' 'yubico-c-client')
# We need systemd to be installed for udev version check
makedepends=('systemd')
provides=('ykpers')
conflicts=('ykpers')
validpgpkeys=('0A3B0262BCA1705307D5FF06BCA00FD4B2168C0A') # Klas Lindfors <klas@yubico.com>
source=("https://developers.yubico.com/${pkgname}/Releases/${_shortname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('05e3447656d52a2cd4092f1df2e99e1289414515644ae10daaa1be851811996904188039e3c8362059b56a43df37cba4396928dc5c546d4506f796b20e4ceabe'
            'SKIP')

build() {
	cd "${_shortname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--with-udevrulesdir=/usr/lib/udev/rules.d/
	make
}

check() {
	cd "${_shortname}-${pkgver}"

	make check
}

package() {
	cd "${_shortname}-${pkgver}"

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/yubikey-personalization/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/yubikey-personalization/README"
	make DESTDIR="${pkgdir}/" install
}

