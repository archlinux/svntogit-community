# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-ps
pkgver=0.2.6
pkgrel=1

pkgdesc="Adds ps support to zathura by using the libspectre library"
url="https://pwmt.org/projects/zathura-ps"
arch=('x86_64')
license=('custom')

depends=('libspectre' 'zathura' 'desktop-file-utils')
makedepends=('meson' 'ninja')

source=(zathura-ps-$pkgver.tar.gz::https://github.com/pwmt/zathura-ps/archive/$pkgver.tar.gz)

sha1sums=('30e8443a29819a6bc9e49cc1d63a6efc025af4a9')

build(){
  cd zathura-ps-$pkgver
  arch-meson build
  ninja -C build
}

package(){
  DESTDIR="$pkgdir" ninja -C zathura-ps-$pkgver/build install
  install -Dm0644 zathura-ps-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
