# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.1.2
pkgrel=2
pkgdesc="a document viewer"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/"
license=('custom')
depends=('girara-gtk2' 'cairo' 'sqlite')
makedepends=('docutils')
optdepends=('zathura-djvu' 'zathura-pdf-poppler' 'zathura-ps')
install=zathura.install
source=(http://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.gz
	bash-completion)
md5sums=('c018b0385a392adb87b465b0f759aaf9'
         'cac20c37f0e77ba62a8138788f4ccabb')

build() {
  cd $srcdir/zathura-$pkgver
  make ZATHURA_GTK_VERSION=2
}

package() {
  cd $srcdir/zathura-$pkgver
  make install DESTDIR=$pkgdir ZATHURA_GTK_VERSION=2
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 $srcdir/bash-completion $pkgdir/usr/share/bash-completion/completions/zathura
}
