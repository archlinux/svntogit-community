# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.5.2
pkgrel=1

pkgdesc="Minimalistic document viewer"
url="https://pwmt.org/projects/zathura/"
arch=('x86_64')
license=('custom')

makedepends=('python-sphinx' 'texlive-bin' 'meson' 'ninja' 'appstream-glib' 'check')
depends=('girara>=0.2.7' 'sqlite' 'desktop-file-utils' 'file' 'libsynctex' 'file')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support'
            'zathura-cb: Comic book support')

source=(zathura-$pkgver.tar.gz::https://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.xz)

sha256sums=('c64ba7cb9facf2b1499b9dc929b6736c72c69f8062eed4f2940556c852256194')

build() {
  cd zathura-$pkgver
  arch-meson build

  cd build
  ninja
}

check() {
  cd zathura-$pkgver/build

  ninja test
}

package() {
  cd zathura-$pkgver/build
  DESTDIR="$pkgdir" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
