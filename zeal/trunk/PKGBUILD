# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Oleg Shparber <trollixx+aur@gmail.com>
# Contributor: Thomas Weißschuh <thomas t-8ch de>

pkgname=zeal
epoch=1
pkgver=0.4.0
pkgrel=1
pkgdesc='An offline API documentation browser'
arch=('x86_64')
url='https://zealdocs.org/'
license=(GPL3)
depends=(qt5-webkit hicolor-icon-theme desktop-file-utils libarchive
         qt5-x11extras)
source=(zeal-${pkgver}.tar.gz::https://github.com/zealdocs/zeal/archive/v${pkgver}.tar.gz
        0001-Revert-doc-Add-Carbon-ad-to-the-welcome-page.patch)
sha256sums=('1f6b50026036923ae0cfbf3f4eb1066ee2fdaf0425d4c321203e0dd9506823fd'
            '6b0a5642be3bd6b36f02973a0b28c15334803cf93ec70a5d18601852a974dac1')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/0001-Revert-doc-Add-Carbon-ad-to-the-welcome-page.patch"
}

build() {
  cd $pkgname-$pkgver
  QT_SELECT=5 qmake CONFIG+=force_debug_info
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
