# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=zeromq
pkgver=4.3.4
_cppver=4.8.0
pkgrel=2
pkgdesc="Fast messaging system built on sockets. C and C++ bindings. aka 0MQ, ZMQ."
arch=('x86_64')
url="http://www.zeromq.org"
license=('LGPL')
depends=('gcc-libs' 'util-linux' 'libsodium' 'libpgm')
makedepends=('asciidoc' 'xmlto')
provides=('libzmq.so')
options=('staticlibs')
# "http://download.zeromq.org/$pkgname-$pkgver.tar.gz"
source=("https://github.com/zeromq/libzmq/releases/download/v$pkgver/zeromq-$pkgver.tar.gz"
        "zmq.hpp.$_cppver.tgz::https://github.com/zeromq/cppzmq/archive/v$_cppver.tar.gz")
        #"zmq.hpp.$pkgver::https://raw.githubusercontent.com/zeromq/cppzmq/b0e6d4b/zmq.hpp")
sha256sums=('c593001a89f5a85dd2ddf564805deb860e02471171b3f204944857336295c3e5'
            '4c07600e3ef311e24d9cf1917122067b48bd447cb7de86c5d23a29826a204649')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # Needed for new libsodium
  sed -i 's/libzmq_werror="yes"/libzmq_werror="no"/' configure

  sed -i 's/openpgm-5.1/openpgm-5.3/' configure configure.ac
  sed -i 's/CXXFLAGS="-Wno-long-long/CXXFLAGS="-Wno-unused-function -Wno-long-long/' configure
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure prefix=/usr --with-pgm --with-libsodium \
    --with-documentation --enable-static --disable-Werror
  make -Wno-unused-function -Wnoerror=unused-function
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  # this will fail if zmq.hpp is out of date
  LANG=C gcc -c ../cppzmq-$_cppver/zmq.hpp -L ./.libs/ -I ./include/ -o ./test.o
  rm -f test.o

  if [[ $CARCH == 'i686' ]]; then
    return 0
  fi
  make -k check
}


package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  cd "$srcdir/cppzmq-$_cppver"
  install -Dm644 zmq.hpp "$pkgdir/usr/include/zmq.hpp"
  install -Dm644 zmq_addon.hpp "$pkgdir/usr/include/zmq_addon.hpp"
}

