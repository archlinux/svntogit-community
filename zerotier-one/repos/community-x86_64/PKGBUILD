# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.10.2
pkgrel=1
pkgdesc='Creates virtual Ethernet networks of almost unlimited size.'
arch=('x86_64')
url='https://www.zerotier.com/'
license=('custom:BSL')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/$pkgver.tar.gz")
sha512sums=('4876285c323b9c0a44230dfec99798a338d5ec110815f88cbde27ccc53b3f0b1a53a00e3a935fcb336f6de0f8c3a7f49ceed7e7299a2b64e47ad42c5494c7238')

prepare() {
  cd ZeroTierOne-$pkgver
  sed -i 's/sbin/bin/' make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver
  make
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
