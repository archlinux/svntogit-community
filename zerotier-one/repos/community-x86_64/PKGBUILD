# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.2.4
pkgrel=3
pkgdesc="Creates virtual Ethernet networks of almost unlimited size."
arch=('x86_64')
url="https://www.zerotier.com/index.html"
license=('GPL3')
depends=('gcc-libs' 'http-parser' 'libnatpmp' 'miniupnpc')
makedepends=('ruby-ronn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/$pkgver.tar.gz"
        https://raw.githubusercontent.com/leleobhz/archlinux-zerotier-one/master/miniupnpc-system.patch)
sha512sums=('82adb110208d24ae2745e3839810afcac87955de050ebfe0517a7dc2a875881dafd40c1b16a041742d8c4d0f6513abcc71d6ea3e06c2fb89b47be2630a500363'
            '49c24fe0c850875e81cc602f3a15ec7583bfa2862772d9cb9e4d0bcc272b11464526d02ba7b1b0a29c931847092e455340ce30ce48e02e96d8d40199d44f1abc')

prepare() {
  cd ZeroTierOne-$pkgver
  patch -p1 -i ../miniupnpc-system.patch
  sed -i 's/sbin/bin/' make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver
  make
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
