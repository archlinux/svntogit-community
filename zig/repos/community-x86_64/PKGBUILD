# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Marc Tiehuis <marctiehuis@gmail.com>

pkgname=zig
pkgver=0.8.1
pkgrel=2
pkgdesc='a general-purpose programming language and toolchain for maintaining robust, optimal, and reusable software'
arch=('x86_64')
url='https://ziglang.org/'
license=('MIT')
depends=('clang' 'llvm-libs' 'lld')
makedepends=('cmake' 'llvm')
checkdepends=('lib32-glibc')
source=("https://ziglang.org/download/$pkgver/zig-$pkgver.tar.xz"
        "llvm13.patch"
        "resolve_DNS.patch")
sha256sums=('8c428e14a0a89cb7a15a6768424a37442292858cdb695e2eb503fa3c7bf47f1a'
            '93acc71c79cdceeb5ef6f643f1b34b5bb764b899dbd8af14f37bc9aa40fc2a7d'
            'c2b7e709dcc229f0e58e2529db900bcfb9a1cfb73da765c5c76ab1e6d0a333cc')

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 < ../llvm13.patch
    # https://github.com/ziglang/zig/issues/6898
    patch -p1 < ../resolve_DNS.patch
}

build() {
    cd "$pkgname-$pkgver"

    # The zig CMakeLists uses build type Debug if not set
    # override it back to None so makepkg env vars are respected
    cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DZIG_PREFER_CLANG_CPP_DYLIB=ON \
        .
    make -C build
}

check() {
    cd "$pkgname-$pkgver/build"

    ./zig build test
}

package() {
    cd "$pkgname-$pkgver"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    make -C build DESTDIR="$pkgdir" install
}
