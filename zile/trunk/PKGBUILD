# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=zile
pkgver=2.4.11
pkgrel=3
pkgdesc="A small, fast, and powerful Emacs clone"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/zile/"
license=('GPL3')
depends=('acl' 'gc' 'ncurses')
makedepends=('help2man')
source=(http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha1sums=('ad2efb80031c3a406f8f83ac5d400a38bc297434'
          'SKIP')
validpgpkeys=('92978852A62FA5E285B2A17468089F7380EE4A00')  # Reuben Thomas

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
