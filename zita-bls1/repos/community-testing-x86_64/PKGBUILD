# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-bls1
pkgver=0.1.0
pkgrel=3
pkgdesc="Implementation of a Blumlein Shuffler to convert binaural stereo signals into a form suitable for reproduction on convential stereo speakers."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'clthreads' 'clxclient' 'jack' 'zita-convolver')
makedepends=()
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a1b4bd7a7ca56afb627e9f095e5e1f67415a52ae316bdc6ce0eced191830f53eaf5ba3402a44fbbb97529e29a0b007d1f0f8e79c89c863207216bca1fb76435c')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip march=native
  sed -e '/native/d' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../doc/html/* \
    -vDm644 ../{AUTHORS,README}
}
