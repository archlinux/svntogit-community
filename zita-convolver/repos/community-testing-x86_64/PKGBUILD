# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: orbisvicis

pkgname=zita-convolver
pkgver=3.1.0
pkgrel=3
pkgdesc="A fast, partitioned convolution engine library."
arch=('i686' 'x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
depends=('gcc-libs' 'fftw')
source=("${url}downloads/zita-convolver-$pkgver.tar.bz2")
md5sums=('7e264d0fb0d8ea277cdb4e33d764c68a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/libs"

  # FS#36248 - strip -march=native
  sed -i '/native/d' Makefile

  # prevent ldconfig call
  sed -i '/ldconfig/d' Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver/libs"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/libs"
  make SUFFIX="" PREFIX=/usr DESTDIR="$pkgdir/" install

  # missing softlink
  ln -s lib$pkgname.so.$pkgver \
    "$pkgdir/usr/lib/lib$pkgname.so.${pkgver%%.*}"
}
