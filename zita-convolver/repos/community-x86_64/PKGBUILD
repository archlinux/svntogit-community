# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: orbisvicis

pkgname=zita-convolver
pkgver=3.1.0
pkgrel=6
pkgdesc="A fast, partitioned convolution engine library."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
depends=('fftw')
source=("${url}downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('0003d4fffcc07b3e67d54996a63db35d537d97b26003e06afbeed19fba551d552d4d5a808a86b5c600f5254816c6b26db49f48e13bfa3c77d93d61956c479eb9')

prepare() {
  cd "${pkgname}-${pkgver}/libs"

  # FS#36248 - strip -march=native
  sed -i '/native/d' Makefile

  # prevent ldconfig call
  sed -i '/ldconfig/d' Makefile
}

build() {
  cd "${pkgname}-${pkgver}/libs"
  make
}

package() {
  cd "${pkgname}-${pkgver}/libs"
  make SUFFIX="" PREFIX=/usr DESTDIR="${pkgdir}/" install

  # missing softlink
  ln -s "lib${pkgname}.so.${pkgver}" \
    "$pkgdir/usr/lib/lib${pkgname}.so.${pkgver%%.*}"

  # docs
  cd ..
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
