# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-rev1
pkgver=0.2.1
pkgrel=2
pkgdesc="A reworked jack version of the reverb originally developed for Aeolus."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-rev1-doc/quickguide.html"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'clxclient' 'clthreads' 'jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('82d3106d9542c6ffff5b8249a3e9e0b3c210690e619e182c0521316d280d3cb4829e7ee624d577e09309ff5941bcc4a96113c50d4e40b886cc2f9eb11c43c3bd')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip march=native
  sed -i '/native/d' Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make DESTDIR="$pkgdir/" PREFIX=/usr install
  # docs
  install -t "$pkgdir/usr/share/doc/$pkgname" \
    -vDm644 ../{AUTHORS,README,doc/*}
}
