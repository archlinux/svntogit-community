# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=zn_poly
pkgver=0.9
pkgrel=3
pkgdesc="Polynomial arithmetic over Z/nZ"
arch=('x86_64')
url="http://web.maths.unsw.edu.au/~davidharvey/code/zn_poly/"
license=('GPL')
depends=('gmp')
makedepends=('python2')
source=("http://web.maths.unsw.edu.au/~davidharvey/code/zn_poly/releases/$pkgname-$pkgver.tar.gz")
md5sums=('0eeaae2524addf558de94bfbc914d22e')

build() {
  cd $pkgname-$pkgver

  python2 makemakefile.py --cflags="$CFLAGS -fPIC" --prefix=/usr --ntl-prefix=/usr --gmp-prefix=/usr --flint-prefix=/usr > makefile
  make

# build shared library
  make libzn_poly.so
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir"/usr/{include/zn_poly,lib}
  cp libzn_poly.so "$pkgdir"/usr/lib
  cp include/*.h "$pkgdir"/usr/include/zn_poly
}

