# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=znc
pkgver=1.0
pkgrel=4
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://en.znc.in/wiki/index.php/ZNC'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libsasl')
makedepends=('tcl' 'python' 'perl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')
install=$pkgname.install
source=("http://znc.in/releases/$pkgname-$pkgver.tar.gz")
md5sums=('23807ca830c27392cccb6774f542df6e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-cyrus \
    --enable-python \
    --enable-perl \
    --enable-tcl \
    --with-systemdsystemunitdir=yes
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
