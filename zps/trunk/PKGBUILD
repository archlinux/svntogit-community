# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=zps
pkgver=1.2.7
pkgrel=1
pkgdesc="A small utility for listing and cleaning up zombie processes"
arch=('x86_64')
url="https://github.com/orhun/zps"
license=('GPL3')
depends=('glibc')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f65c9d197822cc86d436afb22defaa8061fcb97079192634b8fd60b7990e814d224516fb86d55708cdf1aafd7cc25ac5d273b76b5b5f0308695a33ad92923429')

build() {
  cd "$pkgname-$pkgver"
  mkdir -p build && cd build/
  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install
  install -Dm 644 "../README.md" -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 "../man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 "../.application/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
}

# vim: ts=2 sw=2 et:
