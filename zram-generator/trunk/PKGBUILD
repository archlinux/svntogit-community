# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergey Shatunov <me@prok.pw>

pkgname=zram-generator
pkgver=0.3.2
pkgrel=1
pkgdesc="Systemd unit generator for zram devices"
arch=("x86_64")
url="https://github.com/systemd/zram-generator"
license=('MIT')
depends=("systemd")
makedepends=('rust' 'ruby-ronn-ng')
source=("https://github.com/systemd/zram-generator/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('9c014ca1d1c92a31dd827641ab3c413aa36672e48b39e105e195796b3b3ae40a')

build() {
  cd zram-generator-$pkgver
  make CARGOFLAGS="--target-dir=target" build man
}

check() {
  cd zram-generator-$pkgver
  make CARGOFLAGS="--target-dir=target" check
}

package() {
  cd zram-generator-$pkgver

  make CARGOFLAGS="--target-dir=target" DESTDIR="$pkgdir" install
  install -Dpm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
