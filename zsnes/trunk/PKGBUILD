# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Frank Tetzel <frank.tetzel@mailbox.tu-dresden.de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes
pkgver=2.0.9
pkgrel=2
pkgdesc='Super Nintendo emulator'
url='https://github.com/xyproto/zsnes'
arch=(x86_64)
license=(GPL2)
makedepends=(gendesk git mesa nasm)
depends=(lib32-libpng lib32-opengl-driver lib32-sdl)
optdepends=('lib32-alsa-lib: sound support for ALSA'
            'lib32-libpulse: sound support for Pulse Audio')
source=("git+$url#commit=09bc1b15e0b6ab8320e86a9399afc2984a9336f3") # tag: 2.0.9
b2sums=('SKIP')

prepare() {
  gendesk -f -n --pkgname $pkgname --pkgdesc "$pkgdesc"
}

build() {
  export CFLAGS=${CFLAGS/-fcf-protection/}
  export CXXFLAGS=${CXXFLAGS/-fcf-protection/}
  make -C $pkgname
}

package() {
  install -Dm644 zsnes.desktop "$pkgdir/usr/share/applications/zsnes.desktop"
  install -Dm644 zsnes/icons/64x64x32.png "$pkgdir/usr/share/pixmaps/zsnes.png"
  DESTDIR="$pkgdir" make -C $pkgname install
}
