# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan FÃ¶rberg <johan@forberg.se>

pkgname=zstd
pkgver=1.3.1
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm'
arch=('x86_64')
url='http://www.zstd.net/'
license=('BSD', 'GPL2')
depends=('zlib' 'xz')
makedepends=('gtest')
source=("https://github.com/facebook/zstd/archive/v${pkgver}.tar.gz")
sha1sums=('bf944afb80907bc2c0ade0806d2558aa5b657f09')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
    make zstdmt
    make -C contrib/pzstd
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make PREFIX="/usr" DESTDIR="$pkgdir/" install
    install -D -m755 zstdmt "$pkgdir/usr/bin/zstdmt"
    install -D -m755 contrib/pzstd/pzstd "$pkgdir/usr/bin/pzstd"
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
