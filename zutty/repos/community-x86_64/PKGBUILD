# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=zutty
pkgver=0.11
pkgrel=2
pkgdesc='Terminal emulator'
arch=(x86_64)
url='https://tomscii.sig7.se/zutty'
license=(GPL3)
depends=(freetype2 libglvnd libxmu xorg-fonts-misc)
makedepends=(git python)
source=('git+https://github.com/tomszilagyi/zutty#commit=369cf95e242324a1a310ff2a888a5a135771a073') # tag: 0.11
b2sums=('SKIP')

prepare() {
  # FS#70372
  sed -i 's,-march=native,,g;s,-mtune=native,,g' $pkgname/wscript
}

build() {
  cd $pkgname
  python waf configure --prefix=/usr
  python waf -v build
}

package() {
  cd $pkgname
  python waf --destdir="$pkgdir" install
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -v doc/* "$pkgdir/usr/share/doc/$pkgname"
}
