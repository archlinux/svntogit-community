# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=zynaddsubfx
pkgver=2.4.1
pkgrel=1
pkgdesc="An opensource software synthesizer capable of making a countless number of instruments"
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL')
depends=('fltk' 'fftw' 'jack')
makedepends=('mxml')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/ZynAddSubFX-${pkgver}.tar.bz2 \
        zynaddsubfx.desktop Makefile.inc.patch)
md5sums=('59eb69ce24d6f8c605f8ba43958d0526'
         '892bf54cfc691ed07d66e4e061b5a2d0'
         '654fc59abdfeac7fd39673fa87093fe5')

build() {
  cd ${srcdir}/ZynAddSubFX-${pkgver}/src

  patch -p0 < ${srcdir}/Makefile.inc.patch

  make
  install -v -D -m 0755 zynaddsubfx $pkgdir/usr/bin/zynaddsubfx

  cd ${srcdir}/ZynAddSubFX-${pkgver}/ExternalPrograms/Spliter
  make
  install -v -m 0755 spliter ${pkgdir}/usr/bin/spliter
  install -v -D -m 0644 readme.txt ${pkgdir}/usr/share/${pkgname}/spliter-readme.txt

  cd ${srcdir}/ZynAddSubFX-${pkgver}/ExternalPrograms/Controller
  make
  install -v -m 0755 controller ${pkgdir}/usr/bin/controller

  cd ${srcdir}/ZynAddSubFX-${pkgver}
  cp -r banks ${pkgdir}/usr/share/${pkgname}/
  cp -r examples ${pkgdir}/usr/share/${pkgname}/
  install -v -m 0644 *.txt ${pkgdir}/usr/share/${pkgname}/

  install -v -D -m 0644 src/zynaddsubfx_icon.svg ${pkgdir}/usr/share/pixmaps/zynaddsubfx.svg
  install -v -D -m 0644 ${srcdir}/zynaddsubfx.desktop ${pkgdir}/usr/share/applications/zynaddsubfx.desktop
}
