# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: DonVla <donvla@users.sourceforge.net>

_p=ZynAddSubFX
pkgname=zynaddsubfx
pkgver=2.4.1
pkgrel=3
pkgdesc="An opensource software synthesizer capable of making a countless number of instruments."
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL')
depends=('fltk' 'fftw' 'lash')
makedepends=('mxml' 'cmake')
options=('!emptydirs')
source=("http://switch.dl.sourceforge.net/sourceforge/$pkgname/$_p-$pkgver.tar.bz2"
        "$pkgname.desktop" "$pkgname.svg"
        "http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/media-sound/zynaddsubfx/files/zynaddsubfx-2.4.1-fltk.patch"
        "http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/media-sound/zynaddsubfx/files/zynaddsubfx-2.4.1-fltk13.patch")
md5sums=('59eb69ce24d6f8c605f8ba43958d0526'
         '5da6735ee59fdfa21f171fdc4d6c80cb'
         '6f7e9c3ce3947088a10c99c46a65431f'
         'eb95d339ff5deac8c6b54965f04a8c60'
         '49464a33ab9c4250520eda0df7705663')

build() {
  cd "$srcdir/$_p-$pkgver"

  # patches
  patch -p1 -i "$srcdir/$pkgname-$pkgver-fltk.patch"
  patch -p1 -i "$srcdir/$pkgname-$pkgver-fltk13.patch"

  [ -d build ] || mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DOutputModule=jack -DGuiModule=fltk ..
  make

  # build external programs
  cd ../ExternalPrograms/Spliter && make
  cd ../Controller && sed -i "s|Box\.h|Box\.H|" ControllerUI.fl && make
}

package() {
  cd "$srcdir/$_p-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  # install external programs
  install -Dm755 ../ExternalPrograms/Spliter/spliter "$pkgdir/usr/bin/spliter"
  install -Dm755 ../ExternalPrograms/Controller/controller "$pkgdir/usr/bin/controller"
  install -Dm644 ../ExternalPrograms/Spliter/readme.txt "$pkgdir/usr/share/doc/$pkgname/SPLITER.txt"

  # banks and examples
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a ../banks "$pkgdir/usr/share/$pkgname"
  cp -a ../examples "$pkgdir/usr/share/$pkgname"

  # desktop file and icon
  install -Dm644 "$srcdir/$pkgname.svg" "$pkgdir/usr/share/pixmaps/$pkgname.svg"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
